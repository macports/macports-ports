# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:filetype=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           cmake 1.1
PortGroup           active_variants 1.1
PortGroup           github 1.0

name                gr-iqbalance
categories          science comms
homepage            http://sdr.osmocom.org/trac/wiki/GrOsmoSDR
license             GPL-3
platforms           darwin macosx
maintainers         {michaelld @michaelld} {ra1nb0w @ra1nb0w} openmaintainer
description         gr-iqbalance provides I/Q balancing blocks for GNU Radio
long_description    ${description}. This port is kept up with the \
    gr-iqbal GIT 'master' branch, which is typically updated monthly.

# use C++11
compiler.cxx_standard 2011

if {${subport} eq ${name}} {

    github.setup  osmocom gr-iqbal 0.38.2 v
    checksums     rmd160  e6dc5ca7e8587131a7826f77f4c55a5e4e929d17 \
                  sha256  7a133cb0329b4695d959455638fdb603e3e027cabdc51ec721e501b50cb5facc \
                  size    106418
    revision      0

    # move from date to version
    epoch         1

    conflicts     gr37-iqbalance

    depends_lib-append \
        path:lib/libgnuradio-runtime.dylib:gnuradio

    set python_versions { 3.6 3.7 3.8 }
    set default_python_variant +python37

}

subport gr37-iqbalance {

    name          gr37-iqbalance

    github.setup  osmocom gr-iqbal fc67c92f0a739b2c998689f4bda155eb14700a78
    version       20191111
    checksums     rmd160  aeb13e2182f03e9c304e25c938bcf46679435db0 \
                  sha256  60ebcc9a563090ce49a13be6acc2dd5b6b41cacc17bb48fb6ca1cbadadac9ded \
                  size    105936
    revision      0
    git.branch    gr3.7
    github.livecheck.branch gr3.7

    conflicts     gr-iqbalance

    depends_lib-append \
        port:gnuradio37

    set python_versions { 2.7 }
    set default_python_variant +python27

}

# Define the available variants
foreach py_ver ${python_versions} {
    set py_ver_no_dot [join [split ${py_ver} "."] ""]
    set variant_line {variant python${py_ver_no_dot} description "Build with python ${py_ver} support"}
    foreach py_over ${python_versions} {
        if { ${py_ver} == ${py_over} } { continue }
        set py_over_no_dot [join [split ${py_over} "."] ""]
        append variant_line " conflicts python${py_over_no_dot}"
    }
    append variant_line { { } }
    eval $variant_line
    if {[variant_isset python${py_ver_no_dot}]} {
        if {${default_python_variant} != "+python${py_ver_no_dot}"} {
            set default_python_variant ""
        }
    }
}

# set default python variant if not selected
if {${default_python_variant} != ""} {
    default_variants-append "${default_python_variant}"
}

# If a python variant is enabled, activate it
set active_python_version ""
set active_python_version_no_dot ""
foreach py_ver ${python_versions} {
    set py_ver_no_dot [join [split ${py_ver} "."] ""]
    if {[variant_isset python${py_ver_no_dot}]} {
        set active_python_version        ${py_ver}
        set active_python_version_no_dot ${py_ver_no_dot}
    }
}

depends_build-append \
    port:pkgconfig \
    port:cppunit

depends_lib-append \
    port:boost \
    port:libosmo-dsp \
    port:fftw-3-single \
    port:python${active_python_version_no_dot}

# specify the Python version to use
set python_framework_dir ${frameworks_dir}/Python.framework/Versions/${active_python_version}
configure.args-append \
    -DPYTHON_EXECUTABLE=${python_framework_dir}/bin/python${active_python_version} \
    -DPYTHON_INCLUDE_DIR=${python_framework_dir}/Headers \
    -DPYTHON_LIBRARY=${python_framework_dir}/Python \
    -DGR_PYTHON_DIR=${python_framework_dir}/lib/python${active_python_version}/site-packages

configure.args-append \
    -DDOXYGEN_DOT_EXECUTABLE= \
    -DDOXYGEN_EXECUTABLE= \
    -DCMAKE_MODULES_DIR=share/cmake \
    -DSWIG_EXECUTABLE= \
    -DCMAKE_CXX_STANDARD=11

variant docs description "Install ${name} documentation" {

    depends_build-append \
        port:doxygen \
        path:bin/dot:graphviz

    configure.args-delete \
        -DDOXYGEN_DOT_EXECUTABLE= \
        -DDOXYGEN_EXECUTABLE=

    configure.args-append \
        -DDOXYGEN_DOT_EXECUTABLE=${prefix}/bin/dot \
        -DDOXYGEN_EXECUTABLE=${prefix}/bin/doxygen

}

variant swig description "Install ${name} with support for SWIG-base Python bindings" {

    # require gnuradio to also have this variant
    require_active_variants \
        path:lib/libgnuradio-runtime.dylib:gnuradio swig

    configure.args-delete \
        -DSWIG_EXECUTABLE=

    if {${subport} eq "gr37-iqbalance"} {
        depends_build-append \
            port:swig3-python

        configure.args-append \
            -DSWIG_EXECUTABLE=${prefix}/bin/swig3
    } else {
        depends_build-append \
            port:swig-python
    }

}

default_variants +docs +swig
