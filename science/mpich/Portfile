# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           compiler_blacklist_versions 1.0
PortGroup           select 1.0
PortGroup           compilers 1.0
PortGroup           muniversal 1.0

name                mpich
version             3.2.1
# Note multiple revisions in Portfile (for various sub-ports to reduce builds)
revision            0

license             BSD
categories          science parallel net
platforms           darwin
maintainers         {eborisch @eborisch} \
                    {sean @seanfarley} \
                    openmaintainer

description         Message Passing Interface (MPI) Library

long_description    MPICH is a high-performance and widely portable\
                    implementation of the Message Passing Interface (MPI) \
                    standard (MPI-1, MPI-2 and MPI-3). The goals of MPICH are:\
                    (1) to provide an MPI implementation that efficiently\
                    supports different computation and communication platforms\
                    including commodity clusters (desktop systems,\
                    shared-memory systems, multicore architectures), high-speed\
                    networks (10 Gigabit Ethernet, InfiniBand, Myrinet,\
                    Quadrics) and proprietary high-end computing systems (Blue\
                    Gene, Cray) and (2) to enable cutting-edge research in MPI\
                    through an easy-to-extend modular framework for other\
                    derived implementations.

homepage            http://www.mpich.org/
master_sites        ${homepage}static/downloads/${version}

checksums \
    rmd160  b830ba3f5cb5401c03898ad9abf87c60d5cf1e50 \
    sha256  5db53bf2edfaa2238eb6a0a5bc3d2c2ccbfbb1badd79b664a1a919d2ce2330f1 \
    size    11908154

livecheck.type      regex
livecheck.regex     {href=.([0-9.p]+)/}
livecheck.url       ${homepage}/static/downloads/

if {[string first "-devel" $subport] > 0} {
    name                mpich-devel
    version             3.3rc1
    revision            0
    # To see 3.2 as newer than 3.2rcX
    epoch               2

    ## Nightly versions. Disabled when alpha / beta / rc versions available.
    #set tag             g658ff0d9
    #distname            mpich-master-v${version}-${tag}
    #master_sites        ${homepage}static/downloads/nightly/master/mpich
    #livecheck.regex \
        mpich-master-v(\[0-9.\]+-\[0-9\]+)-\[0-9a-z\]+${extract.suffix}
    #livecheck.url       ${master_sites}

    ## Alpha / rc versions (When available)
    distname            mpich-${version}
    master_sites        ${homepage}static/downloads/${version}
    # nightly snapshots have not been updated for a while so let's manually
    # check them for now
    livecheck.type      none
    # livecheck.regex     {href=.([0-9.abrcp]+)/}
    checksums \
        rmd160  40c558dd598b2bbf8772925722a52c1eb0ffdd41 \
        sha256  4a83f1f3b28d1bcd2f6dc9f91c87cf74583dc5f29cfef172015844b9ec75e9f2 \
        size    27216970

    # Currently getting spurious crashes on MPI_Finalize() with 3.2.1
    configure.args-append   --enable-nemesis-shm-collectives
}

# As MPICH creates compiler wrappers, there are lots of
# variants for what compiler the user would like to wrap.

# Subport names and corresponding configure.compiler value
array set clist {
    clang   {clang}
    clang37 {macports-clang-3.7}
    clang39 {macports-clang-3.9}
    clang40 {macports-clang-4.0}
    clang50 {macports-clang-5.0}
    clang60 {macports-clang-6.0}
    clang70 {macports-clang-7.0}
    gcc44   {macports-gcc-4.4}
    gcc48   {macports-gcc-4.8}
    gcc49   {macports-gcc-4.9}
    gcc5    {macports-gcc-5}
    gcc6    {macports-gcc-6}
    gcc7    {macports-gcc-7}
    gcc8    {macports-gcc-8}
}

foreach key [array name clist] {
    subport ${name}-${key} {}
    subport ${name}-devel-${key} {}
}
subport ${name}-default {}
subport ${name}-devel {}
subport ${name}-devel-default {}

# Linker for Apple clang version 421.11.66 segfaults
# See https://trac.macports.org/ticket/36654#comment:9
compiler.blacklist-append   {clang >= 421.11.66 < 425.0.24}

configure.args      --disable-dependency-tracking    \
                    --disable-fortran                \
                    --disable-silent-rules           \
                    --enable-base-cache              \
                    --enable-cache                   \
                    --enable-cxx                     \
                    --enable-fast=O2                 \
                    --enable-shared                  \
                    --enable-versioning              \
                    --with-device=ch3:nemesis        \
                    --with-pm=hydra                  \
                    --with-thread-package=posix      \
                    "F90FLAGS='' F90=''"

variant threads description {Build with full thread support} {
    configure.args-append   --enable-threads=multiple
}

platform darwin {
    configure.args-append   --enable-timer-type=mach_absolute_time
}

# We're making compiler wrappers here... don't default to -O2 for wrappers.
# Actual library code is compiled with -O2 via --enable-fast=O2 configure arg
configure.optflags-delete   -O2 -Os
configure.cppflags-delete   -I${prefix}/include
configure.ldflags-delete    -L${prefix}/lib

if {${subport} != ${name}} {
    set cname                   [lindex [split ${subport} -] end]
    if {${cname} == "default"} {
        set cname                   mp
    }

    # As we are making wrappers, we depend on the compilers to exist. The
    # compilers group already does this for gcc, but not clangXX.  This adds
    # clang-X.X to the depends_lib (not just depends_build)
    if {[regexp {clang\d} ${cname}] == 1} {
        set cport_name          [regsub {(\d)(\d)} ${cname} {-\1.\2}]
        depends_lib-append      port:${cport_name}
    }

    if {[lsearch -exact {mp llvm clang} ${cname}] != -1} {
        # Force local builds with Xcode-provided compilers (avoid issues with
        # different Xcode versions on buildbot and user machines)
        archive_sites
    }

    depends_build-append        port:pkgconfig
    depends_lib-append          port:libxml2
    depends_run-append          port:mpi_select port:mpi-doc
    select.group                mpi
    select.file                 ${filespath}/${name}-${cname}

    # Prevent "ccache" and "distcc" from being baked into the wrapper scripts.
    configure.ccache            no
    configure.distcc            no

    # don't need to livecheck all the subports, just the port
    livecheck.type              none

    if {[string first "-default" $subport] < 0} {
        configure.compiler      [lindex $clist($cname) 0]
        append long_description "\\n\\nTHIS SUBPORT WRAPS ${cname}'s C/C++"
    } else {
        append long_description \
"\\n\\nTHIS SUBPORT WRAPS MACPORTS' DEFAULT COMPILER FOR C/C++"
    }

    configure.args-append   \
        --libdir=${prefix}/lib/${name}-${cname} \
        --sysconfdir=${prefix}/etc/${name}-${cname} \
        --program-suffix=-${name}-${cname} \
        --includedir=${prefix}/include/${name}-${cname}

    # upstream report:
    #    https://lists.mpich.org/pipermail/discuss/2017-November/005350.html
    if {[string first "-devel" $subport] < 0} {
        patchfiles-append patch-pkt_CTS_handler.diff
    }

    # avoid dependency on port:bash
    configure.env-append \
        BASH_SHELL=/bin/bash

    # setting xFLAGS resulting in mpicc, etc. always using these flags
    # setting MPICHLIB_xFLAGS only uses them for building the library
    foreach env_var {CFLAGS CPPFLAGS CXXFLAGS FFLAGS FCFLAGS} {
        configure.args-append \
            MPICHLIB_${env_var}=\"\$${env_var}\" \
            ${env_var}=""
    }

    # remove LDFLAGS mpicc, etc. and pkgconfig files
    post-build {
        if {[variant_isset universal]} {
            set archs ${configure.universal_archs}
        } else {
            set archs ${build_arch}
        }
        foreach arch ${archs} {
            if {[variant_isset universal]} {
                set dir ${worksrcpath}-${arch}
            } else {
                set dir ${worksrcpath}
            }
            foreach f [glob \
                           ${dir}/src/env/*.bash \
                           ${dir}/src/env/*.sh \
                           ${dir}/src/packaging/pkgconfig/*.pc] {
                reinplace \
                    "s| ${configure.ldflags}||g" \
                    ${f}
                reinplace \
                    "s| -arch ${arch}||g" \
                    ${f}
            }
        }
    }

    # Fortran headers do not understand preprocessors commands like __LP64__
    # Fortran mod files can not be merged for different architectures
    # create a architecture specific folder for the non-build_arch architecture
    if {[variant_isset universal]} {
        patchfiles-append patch-universal_fortran.diff
        post-patch {
            if {${build_arch} eq "ppc" || ${build_arch} eq "ppc64"} {
                set arch32 "ppc"
                set arch64 "ppc64"
            } else {
                set arch32 "i386"
                set arch64 "x86_64"
            }
            reinplace \
                "s|__MACPORTS_32_BIT_ARCH__|${arch32}|g" \
                ${worksrcpath}/src/env/mpifort.bash.in
            reinplace \
                "s|__MACPORTS_64_BIT_ARCH__|${arch64}|g" \
                ${worksrcpath}/src/env/mpifort.bash.in
            reinplace \
                "s|__MACPORTS_BUILD_ARCH__|${build_arch}|g" \
                ${worksrcpath}/src/env/mpifort.bash.in
        }
        merger-post-destroot {
            foreach arch ${configure.universal_archs} {
                set incdir  ${destroot}-${arch}${prefix}/include/${name}-${cname}
                if {${arch} ne ${build_arch}} {
                    set archinc ${incdir}/${arch}
                    xinstall -d -m 0755 ${archinc}
                    foreach f [glob -nocomplain -directory ${incdir} *.mod mpif.h] {
                        move ${f} ${archinc}
                    }
                }
            }
        }
    }

    post-destroot {
        if {[string first "-devel" $subport] > 0} {
            # Supply our own -devel manpages (might be materially different /
            # new)
            move ${destroot}${prefix}/share/man \
                ${destroot}${prefix}/share/doc/mpich/
            move ${destroot}${prefix}/share/doc/mpich \
                ${destroot}${prefix}/share/doc/${name}-${cname}
            system -W ${destroot}${prefix}/share/doc/${name}-${cname}/man \
                "/usr/bin/find . -type f -print0 | /usr/bin/xargs -0 -P ${build.jobs} /usr/bin/gzip -v"
        } else {
            # This version doesn't supply doc/manpages
            if {[file isdirectory ${destroot}${prefix}/share/man]} {
                delete ${destroot}${prefix}/share/man
            }
            if {[file isdirectory ${destroot}${prefix}/share/doc]} {
                delete ${destroot}${prefix}/share/doc
            }
        }
    }

    if { [string first gcc $cname] == 0 } {
        append long_description " AND FORTRAN COMPILERS"
        revision                6
        configure.args-delete   --disable-fortran
        configure.args-append   --enable-fortran
        select.file             ${filespath}/${name}-${cname}-fortran
        variant fortran description {
            Stub : fortran always enabled for gcc-based version
        } {}
        default_variants-append     +fortran


        if {[ variant_isset tuned ] && ${os.major} >= 14} {
            if {![ file exists ${prefix}/bin/clang ]} {
                pre-configure {
                    error "

    **********************************************
    A MacPorts-provided clang must be installed
    and 'select'-ed to compile mpich-*gcc* +tuned!
    (Such that ${prefix}/bin/clang exists.)
    **********************************************
"
                }

            }
            # Use clang assembler
            configure.cflags-append     -Wa,-q
            configure.cxxflags-append   -Wa,-q
        }
    } else {
        append long_description " (AND THE FORTRAN COMPILER SELECTED BY THE VARIANT, IF ANY)"
        revision                5

        compilers.choose   fc f77 f90
        compilers.setup    default_fortran

        if {[fortran_variant_isset]} {
            configure.args-delete   --disable-fortran
            configure.args-append   --enable-fortran
            select.file             ${filespath}/${name}-${cname}-fortran

            # at least currently, the Fortran variants gccX and g95 match the name of the ports
            depends_lib-append      port:[fortran_variant_name]

            if {[variant_isset g95]} {
                configure.args-append lt_cv_ld_force_load=no
            }
        }
    }

    set devnotes ""

    if {[string first "-devel" $subport] > 0} {
        set devnotes "
|
| To see the -devel MAN pages, add:
|   ${prefix}/share/doc/${name}-${cname}/man
| to the start of your MANPATH environment variable."
    }

notes "

+--- MPICH Usage note ----
| The mpicc wrapper (and friends) are installed as:
|
|   ${prefix}/bin/mpicc-${name}-${cname} (likewise mpicxx, ...)
|
| To make ${subport}'s wrappers the default (what you get when
| you execute 'mpicc' etc.) please run:
|
|   sudo port select --set mpi [file tail ${select.file}]${devnotes}
+-------------------------

"

    if {[variant_isset gcc48] ||
        [variant_isset gcc49] ||
        [variant_isset gcc5] ||
        [variant_isset gcc6] ||
        [variant_isset gcc7] } {
        set DEFAULT_MSG "
NOTE: Default fortran changed to +gcc8; consider switching variants to enable
pre-built packages for ${subport} by running:
  \"sudo port clean ${subport} && sudo port upgrade ${subport} +gcc8-gcc<N>\"
"
        notes-append    ${DEFAULT_MSG}

        pre-fetch {
            ui_msg ${DEFAULT_MSG}
            system "sleep 3"
            ui_msg "Continuing with local build."
        }
    }

    variant gforker description {
        Use gforker process manager instead of the default hydra
    } {
        configure.args-append   --with-pm=gforker
        configure.args-delete   --with-pm=hydra
    }

    variant tuned description {Build with more optimizations} {
        configure.args-delete   --enable-fast=O2
        configure.args-append   --enable-fast=all \
                                MPICHLIB_CFLAGS='-fomit-frame-pointer -O2'
    }
} else {
    depends_lib-append      port:$name-default
    distfiles
    patchfiles
    supported_archs         noarch
    use_configure           no
    build {}
    destroot {
        set docdir          ${destroot}${prefix}/share/doc/${name}
        xinstall -d ${docdir}
        system "echo $name is a stub port > ${docdir}/README"
    }
}
