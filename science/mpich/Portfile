# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           compilers 1.0
PortGroup           mpiutil 1.0

#===============================================================================
#
# *** IMPORTANT NOTE ***
#
# When making logic changes to this port, PLEASE review port 'openmpi' to see
# if the same changes should be applied. While the subports and variants aren't
# exactly the same between the two - and things like configure arguments
# certainly differ, as they're different code bases - much of the core logic
# is very similar. (And often identical.)
#
# Please help us avoid divergent MPI ports, which cause serious migraines.
#
#===============================================================================

# make sure to keep in sync with mpi-doc
name                mpich
version             4.2.2
revision            0

license             BSD
categories          science parallel net
platforms           darwin
maintainers         {eborisch @eborisch} \
                    {mascguy @mascguy}

description         Message Passing Interface (MPI) Library

long_description    MPICH is a high-performance and widely portable\
                    implementation of the Message Passing Interface (MPI) \
                    standard (MPI-1, MPI-2 and MPI-3). The goals of MPICH are:\
                    (1) to provide an MPI implementation that efficiently\
                    supports different computation and communication platforms\
                    including commodity clusters (desktop systems,\
                    shared-memory systems, multicore architectures), high-speed\
                    networks (10 Gigabit Ethernet, InfiniBand, Myrinet,\
                    Quadrics) and proprietary high-end computing systems (Blue\
                    Gene, Cray) and (2) to enable cutting-edge research in MPI\
                    through an easy-to-extend modular framework for other\
                    derived implementations.

homepage            https://www.mpich.org/
master_sites        ${homepage}static/downloads/${version}

checksums           rmd160  158f1c23a1f646838bf87d4b2920c752d7f9d3ef \
                    sha256  883f5bb3aeabf627cb8492ca02a03b191d09836bbe0f599d8508351179781d41 \
                    size    40241352

# Disable livecheck for all subports; only enabled for main port, at end of portfile
livecheck.type      none

#-------------------------------------------------------------------------------
# Target Compiler Logic
#-------------------------------------------------------------------------------

# As MPICH creates compiler wrappers, there are lots of
# variants for what compiler the user would like to wrap.

# Please update the mpi portgroup whenever clist is changed.

# Subport names and corresponding configure.compiler value
set clist [dict create]
set clist_unsupported [list]
set clist_obsolete [list]

# Compilers supported across-the-board
dict set clist gcc7    {macports-gcc-7}
dict set clist gcc10   {macports-gcc-10}
dict set clist gcc11   {macports-gcc-11}
dict set clist gcc12   {macports-gcc-12}
dict set clist gcc13   {macports-gcc-13}
dict set clist gcc14   {macports-gcc-14}
dict set clist clang11 {macports-clang-11}
dict set clist clang12 {macports-clang-12}
dict set clist clang13 {macports-clang-13}
dict set clist clang14 {macports-clang-14}
dict set clist clang15 {macports-clang-15}
dict set clist clang16 {macports-clang-16}
dict set clist clang17 {macports-clang-17}
dict set clist clang18 {macports-clang-18}

# https://www.mpich.org/supported-compilers
compiler.blacklist-append   *gcc-4.*

# Only enable default (gcc), and Xcode clang, for MacOS 10.7 and later
dict set clist default {}
dict set clist fortran {}
dict set clist lib {}

if { ${os.major} >= 11 } {
    dict set clist clang   {clang}
} else {
    lappend clist_unsupported \
        default clang
}

# Clang 9 and 10 not supported on ARM
if { ${os.arch} eq "arm" } {
    lappend clist_unsupported \
        clang90 clang10
} else {
    dict set clist clang90 {macports-clang-9.0}
    dict set clist clang10 {macports-clang-10}
}

# GCC 9 only supported for macOS 10.6 through 10.10
if { ${os.major} >= 10 && ${os.major} <= 14 } {
    dict set clist gcc9  {macports-gcc-9}
} else {
    lappend clist_unsupported \
        gcc9
}

#-------------------------------------------------------------------------------
# Subport Creation/Validation
#-------------------------------------------------------------------------------

set cname \
    [lindex [split ${subport} -] end]

mpiutil_add_subports \
    ${name} ${subport} \
    ${clist} ${clist_unsupported} ${clist_obsolete}

set subport_enabled \
    [mpiutil_validate_subport \
        ${name} ${subport} ${cname} \
        ${clist} ${clist_unsupported} ${clist_obsolete} \
    ]

#-------------------------------------------------------------------------------
# General Subport Logic
#-------------------------------------------------------------------------------

if {${subport} eq "mpich-lib"} {
    PortGroup select 1.0
    PortGroup muniversal 1.0
    PortGroup legacysupport 1.1

    set sub_prefix          ${prefix}/libexec/mpich-lib
    set cname "mp"

    # Add various dependencies: build, lib, and run
    mpiutil_add_depends \
        ${subport} ${cname}

    # Determine whether buildbot binaries should be used, and disable if necessary
    mpiutil_set_binary_eligibility \
        ${subport} ${cname}

    depends_build-append        port:pkgconfig
    depends_lib-append          port:libxml2

    # mpich-lib (C and Fortran code; c++ is disabled) are built with the
    # "default" GCC compiler now.
    configure.compiler $cdb(${compilers.gcc_default},compiler)

    # ch4 is hanging in finalize https://github.com/pmodels/mpich/issues/6584
    # always use ch3 for now.

    configure.args  --disable-collalgo-tests         \
                    --disable-cxx                    \
                    --disable-dependency-tracking    \
                    --disable-silent-rules           \
                    --enable-base-cache              \
                    --enable-cache                   \
                    --enable-f08                     \
                    --enable-fast=O2                 \
                    --enable-fortran                 \
                    --enable-shared                  \
                    --enable-timer-type=mach_absolute_time \
                    --enable-versioning              \
                    --with-device=ch4:ofi:tcp        \
                    --with-hwloc-prefix=${prefix}    \
                    --with-pm=hydra                  \
                    --with-thread-package=posix

    if {${os.platform} eq "darwin" && ${os.major} < 12} {
        # MPICH requires OpenCL version 1.2, which was not introduced until OS X Mountain Lion
        configure.args-append \
                    --enable-opencl=no

        # scandir doesn't have const flag on third argument pre < 10.8
        configure.cflags-append -Wno-incompatible-pointer-types
    }

    patchfiles-append \
                    patch-no_qmkshrobj.diff \
                    patch-mpich-darwin-powerpc.diff \
                    patch-mpi-h.diff

    post-patch {
        reinplace \
            "s/commons_use_dylibs_works=yes/commons_use_dylibs_works=no/" \
            configure
    }

    platform darwin powerpc {
        # libfabric calls for atomicops, on PPC at least
        configure.ldflags-append \
                    -latomic
    }

    # We're making compiler wrappers here... don't default to -O2 for wrappers.
    # Actual library code is compiled with -O2 via --enable-fast=O2 configure arg
    configure.optflags-delete   -O2 -Os
    configure.cppflags-delete   -I${prefix}/include
    configure.ldflags-delete    -L${prefix}/lib
    configure.pre_args          --prefix=${sub_prefix}

    select.group                mpi
    select.file                 ${filespath}/portselect/${name}-lib-fortran

    # Prevent "ccache" and "distcc" from being baked into the wrapper scripts.
    configure.ccache            no
    configure.distcc            no

    long_description-append \
        "\n\nTHIS SUBPORT WRAPS MACPORTS' DEFAULT COMPILER FOR C/C++"

    # avoid dependency on port:bash
    configure.env-append \
        BASH_SHELL=/bin/bash

    # setting xFLAGS resulting in mpicc, etc. always using these flags
    # setting MPICHLIB_xFLAGS only uses them for building the library
    foreach env_var {CFLAGS CPPFLAGS CXXFLAGS FFLAGS FCFLAGS} {
        configure.args-append \
            MPICHLIB_${env_var}=\"\$${env_var}\" \
            ${env_var}=""
    }

    # remove LDFLAGS mpicc, etc. and pkgconfig files
    post-build {
        foreach arch [get_canonical_archs] {
            if {[variant_exists universal] && [variant_isset universal]} {
                set dir ${worksrcpath}-${arch}
            } else {
                set dir ${worksrcpath}
            }
            foreach f [glob \
                           ${dir}/src/env/*.bash \
                           ${dir}/src/env/*.sh \
                           ${dir}/src/packaging/pkgconfig/*.pc] {
                reinplace \
                    "s| ${configure.ldflags}||g" \
                    ${f}
                reinplace \
                    "s| -arch ${arch}||g" \
                    ${f}
            }
        }
    }

    post-destroot {
        # mpiexec expects to find hydra_pmi_proxy "next to" it. We have
        # symlinks pointing to it, leaving it with no ${0%/*}/hydra_mpi_proxy.
        # Use a wrapper script to update the path of $0 so things work.
        system -W ${destroot}${sub_prefix}/bin \
            "echo '#!/bin/bash' > mpiexec-wrap &&
             echo 'exec ${sub_prefix}/bin/mpiexec \"\$@\"' >> mpiexec-wrap &&
             chmod 755 mpiexec-wrap"

        # Since we don't depends_lib on the gcc compiler for mpich-lib, disable
        # the compiler wrappers when called without MPICH_** env. vars.
        # Individual mpich-_compiler_ subports provide working wrapers
        # with appropriate compiler dependencies.
        system -W ${destroot}${sub_prefix}/bin \
            "for f in mpic* mpif*; do \[ -f \"\$f\" \] && sed -i '' -E 's^(.+)=\"${prefix}.*^\\1=/usr/bin/false^' \"\$f\"; done"
        
             
        # This version doesn't supply doc/manpages
        if {[file isdirectory ${destroot}${sub_prefix}/share/man]} {
            delete ${destroot}${sub_prefix}/share/man
        }
        if {[file isdirectory ${destroot}${sub_prefix}/share/doc]} {
            delete ${destroot}${sub_prefix}/share/doc
        }
    }

    #---------------------------------------------------------------------------
    # Fortran Support
    #---------------------------------------------------------------------------

    set fortran_enabled no

    long_description-append "(AND THE FORTRAN COMPILER SELECTED BY THE VARIANT, IF ANY)"

    compilers.allow_arguments_mismatch yes
    compilers.choose   fc f77 f90
    compilers.setup    default_fortran

    if {[variant_isset g95]} {
        configure.args-append lt_cv_ld_force_load=no
    }

    # avoid
    #     Python 3 is required to generate F90 bindings but not found!
    set python_branch       3.12
    set python_version      [string map {. {}} ${python_branch}]
    depends_build-append    port:python${python_version}
    depends_skip_archcheck-append   port:python${python_version}
    configure.python        ${prefix}/bin/python${python_branch}

    #---------------------------------------------------------------------------
    # Universal Build Support
    #---------------------------------------------------------------------------

    if {![info exists universal_possible]} {
        set universal_possible [expr {${os.universal_supported} && [llength ${configure.universal_archs}] >= 2}]
    }
    # Fortran headers do not understand preprocessors commands like __LP64__
    # Fortran mod files can not be merged for different architectures
    # create a architecture specific folder for the non-build_arch architecture
    if {${universal_possible} && [variant_isset universal]} {
        patchfiles-append patch-universal_fortran.diff
        post-patch {
            if {${build_arch} eq "ppc" || ${build_arch} eq "ppc64"} {
                set arch32 "ppc"
                set arch64 "ppc64"
            } else {
                set arch32 "i386"
                set arch64 "x86_64"
            }
            reinplace \
                "s|__MACPORTS_32_BIT_ARCH__|${arch32}|g" \
                ${worksrcpath}/src/env/mpifort.bash.in
            reinplace \
                "s|__MACPORTS_64_BIT_ARCH__|${arch64}|g" \
                ${worksrcpath}/src/env/mpifort.bash.in
            reinplace \
                "s|__MACPORTS_BUILD_ARCH__|${build_arch}|g" \
                ${worksrcpath}/src/env/mpifort.bash.in
        }
        merger-post-destroot {
            foreach arch ${configure.universal_archs} {
                set incdir  ${destroot}-${arch}${sub_prefix}/include
                if {${arch} ne ${build_arch}} {
                    set archinc ${incdir}/${arch}
                    xinstall -d -m 0755 ${archinc}
                    foreach f [glob -nocomplain -directory ${incdir} *.mod mpif.h] {
                        move ${f} ${archinc}
                    }
                }
            }
        }
    }

    #---------------------------------------------------------------------------
    # Variants
    #---------------------------------------------------------------------------

    variant threads description {Build with full thread support} {
        configure.args-append   --enable-threads=multiple
    }

    variant gforker description {Use gforker process manager instead of the default hydra} {
        configure.args-replace  --with-pm=hydra \
                                --with-pm=gforker
    }

    variant native description {Build for local machine} {
        configure.args-replace      --enable-fast=O2 \
                                    --enable-fast=all
        # Note these need to be above the following foreach
        if {${build_arch} in [list ppc ppc64]} {
            configure.cflags-append     -mtune=native
            configure.cxxflags-append   -mtune=native
        } else {
            configure.cflags-append     -march=native
            configure.cxxflags-append   -march=native
        }
    }

    #---------------------------------------------------------------------------
    # Notes
    #---------------------------------------------------------------------------

    mpiutil_add_notes \
        ${name} ${subport} ${cname} ${select.file}

} elseif {${subport_enabled}} {
    PortGroup select 1.0

    if {${subport} eq "mpich-default"} {
        set sub_prefix          ${prefix}/libexec/mpich-mp
    } elseif {${subport} eq "mpich-fortran"} {
        configure.compiler $cdb(${compilers.gcc_default},compiler)
        depends_lib-append port:$cdb(${compilers.gcc_default},depends)
        set sub_prefix          ${prefix}/libexec/mpich-fortran
    } else {
        set compiler            [dict get ${clist} ${cname}]
        configure.compiler      [lindex ${compiler} 0]
        unset compiler

        if {[regexp {clang([0-9]+)} ${subport} comp cver]} {
            depends_lib-append port:clang-${cver}
        } elseif {[regexp {gcc([0-9]+)} ${subport} comp]} {
            depends_lib-append port:${comp}
        }
        set sub_prefix          ${prefix}/libexec/${subport}
    }

    set def_prefix          ${prefix}/libexec/mpich-lib

    long_description-append \
        "\n\nThis subport uses compilers from ${cname} for mpicc, mpicxx, ...\n"

    select.group                mpi

    variant fortran description { Include fortran wrappers: mpich-default mush  } {}

    default_variants +fortran

    if {[variant_isset fortran]} {
        depends_lib-append          path:libexec/mpich-lib/bin/mpif77-mpich-mp:${name}-lib
        select.file                 ${filespath}/portselect/${subport}-fortran
    } else {
        depends_lib-append          port:${name}-lib
        select.file                 ${filespath}/portselect/${subport}
    }

    # We use the libraries that come from mpich-default, and provide shim
    # wrappers to call the requested compiler for mpicc, mpicxx, ...

    fetch {}
    checksum {}
    extract {}
    patch {}
    use_configure no
    build {}
    test {}

    destroot {
        xinstall -d ${destroot}${sub_prefix}/bin

        set xlist [dict create]
        dict set xlist mpicc    "MPICH_CC   ${configure.cc}"
        dict set xlist mpicxx   "MPICH_CXX  ${configure.cxx}"
        dict set xlist mpic++   "MPICH_CXX  ${configure.cxx}"

        if {[variant_isset fortran]} {
            dict set xlist mpif77   "MPICH_FC   ${configure.fc}"
            dict set xlist mpif90   "MPICH_FC   ${configure.fc}"
            dict set xlist mpifort  "MPICH_FC   ${configure.fc}"
        }

        # Build wrapper scripts that set the required compiler env. var.
        dict for {exname info} ${xlist} {
            system -W ${destroot}${sub_prefix}/bin \
                "echo '#!/bin/bash' > ${exname} &&
                 echo '[lindex ${info} 0]=\"[lindex ${info} 1]\" exec ${def_prefix}/bin/${exname} \"\$@\"' >> ${exname}"
        }
        system -W ${destroot}${sub_prefix}/bin "chmod a+x mpi*"

        system -W ${destroot}${sub_prefix} "for x in ${def_prefix}/*; do \[ ! -e \${x##*/} \] && ln -s \$x; done"
        system -W ${destroot}${sub_prefix}/bin "for x in ${def_prefix}/bin/*; do
          \[ \${x/mpif/} == \${x} \] && \[ \${x/wrap/} == \${x} \] && \[ ! -e \${x##*/} \] && ln -s \$x;
          done"

    }


    #---------------------------------------------------------------------------
    # Notes
    #---------------------------------------------------------------------------
    mpiutil_add_notes ${name} ${subport} ${cname} ${select.file}

} elseif {${subport} eq ${name}} {
    PortGroup               stub 1.0

    depends_lib-append      port:${name}-default

    livecheck.type          regex
    livecheck.regex         {href=.([0-9.p]+)/}
    livecheck.url           ${homepage}/static/downloads/
}
