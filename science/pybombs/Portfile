# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem            1.0
PortGroup             python 1.0
PortGroup             github 1.0

name                  pybombs

set pythons_suffixes {27 35 36 37 38 39}
set default_py_ver    38

categories            science python
license               GPL-3
platforms             darwin
supported_archs       noarch
maintainers           {michaelld @michaelld}

description           Python Build Overlay Managed Bundle System

long_description      PyBOMBS (Python Build Overlay Managed Bundle System) is the GNU Radio install management system for resolving dependencies and pulling in out-of-tree projects from CGRAN (Comprehensive GNU Radio Archive Network).

subport ${name}-devel {}

if {${subport} eq ${name}} {

    github.setup    pybombs pybombs 2.3.4 v
    checksums       rmd160  0348e35724946dfccb26c2202d54f3a71603a1b4 \
                    sha256  87a89a4bef14b1c067cd48c58a15287d7110f2e70e28200efaa7f29f4c2573fe \
                    size    101309

    worksrcdir      gnuradio-pybombs-4923226
    conflicts       ${name}-devel

    # fix the version to be more explicit
    post-patch {
        reinplace "s@${version}@${version} MacPorts@g" ${worksrcpath}/pybombs/__init__.py
    }

} else {

    github.setup    pybombs pybombs 09afa1e664dda38d016368887d99f94726c148ae
    version         20201113-[string range ${github.version} 0 7]
    checksums       rmd160  aa2946c55a93f57f87a252f0b03ea3533e7cc1a2 \
                    sha256  5b848b205314c2eefc227e7f9f45c889d031af7adfe5b945f4fe291e61defdd3 \
                    size    102859
    revision        0

    worksrcdir      gnuradio-${github.project}-[string range ${github.version} 0 6]
    long_description    ${long_description} \
        This port is kept up with the ${name} GIT 'master' branch, is typically updated weekly to monthly.
    conflicts       ${name}

    # fix the version to be very explicit
    post-patch {
        set mps "git-[string range ${github.version} 0 7] MacPorts"
        reinplace "/__version__/s@\\\"\\\(.*\\\)*\\\"@\\\"\\1-${mps}\\\"@g" \
            ${worksrcpath}/pybombs/__init__.py
    }
}

# declare +python* variants

foreach this_py_version_no_dot ${pythons_suffixes} {
    set ndx [lsearch -exact ${pythons_suffixes} ${this_py_version_no_dot}]
    set this_conflicts_no_dot [lreplace ${pythons_suffixes} ${ndx} ${ndx}]
    set this_conflicts [join ${this_conflicts_no_dot} " python"]
    set py_version_with_dot [join [split ${this_py_version_no_dot} ""] "."]
    set this_description "Use Python ${py_version_with_dot}"
    variant python${this_py_version_no_dot} \
        conflicts {*}python${this_conflicts} \
        description ${this_description} "
            python.default_version ${this_py_version_no_dot}
            depends_build-append \
                port:py${this_py_version_no_dot}-setuptools
            depends_lib-append \
                port:py${this_py_version_no_dot}-future \
                port:py${this_py_version_no_dot}-requests \
                port:py${this_py_version_no_dot}-six \
                port:py${this_py_version_no_dot}-ruamel-yaml
    "
}

# set default, if no variants selected

set PY_VER_NO_DOT 0
foreach this_py_version_no_dot ${pythons_suffixes} {
    if {[variant_isset python${this_py_version_no_dot}]} {
        set PY_VER_NO_DOT ${this_py_version_no_dot}
        break
    }
}

if {${PY_VER_NO_DOT} == 0} {
    default_variants +python${default_py_ver}
}

# make sure the default variant is not deselected alone

set PY_VER_NO_DOT 0
foreach this_py_version_no_dot ${pythons_suffixes} {
    if {[variant_isset python${this_py_version_no_dot}]} {
        set PY_VER_NO_DOT ${this_py_version_no_dot}
        break
    }
}

if {${PY_VER_NO_DOT} == 0} {
    ui_error "\n\nYou must select a Python variant; you cannot select -python${default_py_ver} alone.\n"
    return -code error "Invalid variant selection."
}
