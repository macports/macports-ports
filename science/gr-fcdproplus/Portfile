# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0
PortGroup           cmake 1.0
PortGroup           github 1.0
PortGroup           active_variants 1.1

github.setup        dl1ksv gr-fcdproplus f82beea355c25449856b37a799b0a95141b98201
version             20141011
revision            1
checksums           rmd160 d7b5a0a221c4ef87c9269ee68e07587a8f7ea884 \
                    sha256 517d2dfd5b9f649d2c6fba3b2b35eef6f13d0ae4fef00ff9892efe143fa978a6

categories          science comms
maintainers         michaelld openmaintainer
description         Implements a funcube dongle pro+ source in GNU Radio.
long_description    ${description}
license             GPL-3
platforms           darwin

depends_build-append port:pkgconfig
depends_lib-append	port:boost \
                    port:doxygen \
                    port:libusb \
                    port:swig

# allow gr-osmosdr to work with both gnuradio and gnuradio-devel ...

depends_lib-append  path:lib/libgnuradio-runtime.dylib:gnuradio

# ... but not with gnuradio-legacy or gnuradio-next

pre-fetch {
    if {![catch {set installed [lindex [registry_active gnuradio-legacy] 0]}]} {
        # gnuradio-legacy is installed; this version of gr-osmosdr does not work with gnuradio-legacy
        ui_msg "\nError: ${name} requires the gnuradio or gnuradio-devel port, and will not work with the gnuradio-legacy port.  deactivate gnuradio-legacy, and then install or activate gnuradio or gnuradio-devel.\n"
        return -code error "Invalid port dependency: gnuradio-legacy"
    }
    if {![catch {set installed [lindex [registry_active gnuradio-next] 0]}]} {
        # gnuradio-next is installed; this version of gr-osmosdr does not work with gnuradio-next
        ui_msg "\nError: ${name} requires the gnuradio or gnuradio-devel port, and will not work with the gnuradio-next port.  deactivate gnuradio-next, and then install or activate gnuradio or gnuradio-devel.\n"
        return -code error "Invalid port dependency: gnuradio-next"
    }
}

# do VPATH build

post-extract        { file mkdir ${workpath}/build }
configure.dir       ${workpath}/build
build.dir           ${workpath}/build

# remove top-level library path, such that internal libraries are used
# instead of any already-installed ones.

configure.ldflags-delete -L${prefix}/lib

# set last configure argument to the reletive path
# to the top-level cmake source

configure.post_args ../${worksrcdir}

# be verbose when building, for debugging purposes

build.post_args VERBOSE=1

# set Python variants

set pythons_suffixes {26 27}
global chosen_python_suffix
set chosen_python_suffix ""

set pythons_ports {}
foreach s ${pythons_suffixes} {
    lappend pythons_ports python${s}
}

foreach s ${pythons_suffixes} {
    set p python${s}
    set v [join [split ${s} ""] "."]
    set i [lsearch -exact ${pythons_ports} ${p}]
    set c [lreplace ${pythons_ports} ${i} ${i}]
    eval [subst {
        variant ${p} description "Build UHD for Python ${v}" \
            conflicts ${c} {

            set chosen_python_suffix ${s}

            # require gnuradio to also have this Python variant

            require_active_variants \
                path:lib/libgnuradio-runtime.dylib:gnuradio ${p}

            depends_lib-append \
                port:${p}

            # specify that Python version to use
            configure.args-append \
                -DPYTHON_EXECUTABLE=${prefix}/bin/python${v} \
                -DPYTHON_INCLUDE_DIR=${prefix}/Library/Frameworks/Python.framework/Versions/${v}/Headers \
                -DPYTHON_LIBRARY=${prefix}/Library/Frameworks/Python.framework/Versions/${v}/Python \
                -DGR_PYTHON_DIR=${frameworks_dir}/Python.framework/Versions/${v}/lib/python${v}/site-packages
        }
    }]
}

# if no python variant is set, default to 2.7
if {![variant_isset python26] &&
    ![variant_isset python27]} {
    default_variants +python27
}

# make sure -python27 was not used alone
if {![variant_isset python26] &&
    ![variant_isset python27]} {
    return -code error "Exactly one Python variant must be selected."
}
