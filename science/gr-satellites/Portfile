# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:filetype=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           cmake 1.1
PortGroup           github 1.0

name                gr-satellites
categories          science comms
platforms           darwin macosx
license             GPL-3
maintainers         {ra1nb0w @ra1nb0w} {michaelld @michaelld} openmaintainer
description         GNU Radio decoders for several Amateur satellites.
long_description    ${description}

# use C++11
compiler.cxx_standard 2011

if {${subport} eq ${name}} {

    github.setup    daniestevez gr-satellites 2.3.1 v
    checksums       rmd160  f6d8949cc7f6f9c879786e91004a1ffe7fff9859 \
                    sha256  94bbd1951d0078a3bd46e258cb789d5517c1d9c1c0acabcce1b2163ab7d9afff \
                    size    714627
    revision        0

    depends_lib-append \
        path:lib/libgnuradio-runtime.dylib:gnuradio

    conflicts gr37-satellites

    set python_versions { 3.6 3.7 3.8 }
    set default_python_variant +python37

    github.livecheck.regex  {([0-9.]+)}

}

subport gr37-satellites {

    name gr37-satellites

    github.setup    daniestevez gr-satellites 1.8.3 v
    checksums       rmd160  ddd2a6a6bfaa1cd86b22a875e91601ba4fb1d1e4 \
                    sha256  5df3e8f4d258655c2ffb3792d20c25bc81b1936fbd269ebd2dfb653fb663a0ed \
                    size    760212
    revision        0

    conflicts gr-satellites

    set python_versions { 2.7 }
    set default_python_variant +python27

    depends_lib-append \
        port:gnuradio37

    github.livecheck.regex  {(1.[0-9.]+)}

}

# Define the available variants
foreach py_ver ${python_versions} {
    set py_ver_no_dot [join [split ${py_ver} "."] ""]
    set variant_line {variant python${py_ver_no_dot} description "Build with python ${py_ver} support"}
    foreach py_over ${python_versions} {
        if { ${py_ver} == ${py_over} } { continue }
        set py_over_no_dot [join [split ${py_over} "."] ""]
        append variant_line " conflicts python${py_over_no_dot}"
    }
    append variant_line { { } }
    eval $variant_line
    if {[variant_isset python${py_ver_no_dot}]} {
        if {${default_python_variant} != "+python${py_ver_no_dot}"} {
            set default_python_variant ""
        }
    }
}

# set default python variant if not selected
if {${default_python_variant} != ""} {
    default_variants-append "${default_python_variant}"
}

# If a python variant is enabled, activate it
set active_python_version ""
set active_python_version_no_dot ""
foreach py_ver ${python_versions} {
    set py_ver_no_dot [join [split ${py_ver} "."] ""]
    if {[variant_isset python${py_ver_no_dot}]} {
        set active_python_version        ${py_ver}
        set active_python_version_no_dot ${py_ver_no_dot}
    }
}

depends_build-append \
    port:cppunit \
    port:pkgconfig \
    port:swig-python

depends_lib-append \
    port:boost \
    port:libfec \
    port:python${active_python_version_no_dot}

depends_run-append \
    port:py${active_python_version_no_dot}-construct \
    port:py${active_python_version_no_dot}-requests

# specify the Python version to use
set python_framework_dir ${frameworks_dir}/Python.framework/Versions/${active_python_version}
configure.args-append \
    -DPYTHON_EXECUTABLE=${python_framework_dir}/bin/python${active_python_version} \
    -DPYTHON_INCLUDE_DIR=${python_framework_dir}/Headers \
    -DPYTHON_LIBRARY=${python_framework_dir}/Python \
    -DGR_PYTHON_DIR=${python_framework_dir}/lib/python${active_python_version}/site-packages

configure.args-append \
    -DDOXYGEN_DOT_EXECUTABLE= \
    -DDOXYGEN_EXECUTABLE= \
    -DCMAKE_MODULES_DIR=share/cmake

variant docs description "Install ${name} documentation" {

    depends_build-append \
        port:doxygen \
        path:bin/dot:graphviz

    configure.args-delete \
        -DDOXYGEN_DOT_EXECUTABLE= \
        -DDOXYGEN_EXECUTABLE=

    configure.args-append \
        -DDOXYGEN_DOT_EXECUTABLE=${prefix}/bin/dot \
        -DDOXYGEN_EXECUTABLE=${prefix}/bin/doxygen

}

default_variants +docs

post-destroot {
    # copy GNU Radio examples
    xinstall -m 755 -d ${destroot}${prefix}/share/gnuradio/examples/satellites
    file copy {*}[glob ${worksrcpath}/examples/*] \
        ${destroot}${prefix}/share/gnuradio/examples/satellites
}

