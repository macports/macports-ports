# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0
PortGroup           cmake 1.0
PortGroup           github 1.0
PortGroup           active_variants 1.1

github.setup        bistromath gr-air-modes 9891907e97b6bb46447aeffd2e619df35074443e
version             20140928
revision            1
checksums           rmd160 6d3e324fb444d424630c35f42fa78cf77bd6c6bc \
                    sha256 b2bd0187e3803cbed61164d4436dc0681a3d887a3cfade5b8d3c08b9f5888d37

# allow gr-air-modes to work with both gnuradio and gnuradio-devel ...

depends_lib-append  path:lib/libgnuradio-runtime.dylib:gnuradio

# ... but not with gnuradio-legacy or gnuradio-next

pre-fetch {
    if {![catch {set installed [lindex [registry_active gnuradio-legacy] 0]}]} {
        # gnuradio-legacy is installed; this version of gr-air-modes does not work with gnuradio-legacy
        ui_msg "\nError: ${name} requires the gnuradio or gnuradio-devel port, and will not work with the gnuradio-legacy port.  deactivate gnuradio-legacy, and then install or activate gnuradio or gnuradio-devel.\n"
        return -code error "Invalid port dependency: gnuradio-legacy"
    }
    if {![catch {set installed [lindex [registry_active gnuradio-next] 0]}]} {
        # gnuradio-next is installed; this version of gr-air-modes does not work with gnuradio-next
        ui_msg "\nError: ${name} requires the gnuradio or gnuradio-devel port, and will not work with the gnuradio-next port.  deactivate gnuradio-next, and then install or activate gnuradio or gnuradio-devel.\n"
            return -code error "Invalid port dependency: gnuradio-next"
    }
}

categories          science comms
maintainers         michaelld openmaintainer
description         Provides augmented functionality (blocks, GRC definitions, apps, etc) for GNU Radio.
long_description    ${description}
license             GPL-3
platforms           darwin

# disable parallel build for now

use_parallel_build  no

depends_build-append port:pkgconfig
depends_lib-append	port:boost port:cppzmq

# Requires qwt >= 5.2, so not the "qwt" port.  Also requires
# py*-pyqwt which does not compile with qwt >= 6.0; so -have- to
# use qwt52 for now.

depends_lib-append  port:qwt52

# do VPATH build

post-extract        { file mkdir ${workpath}/build }
configure.dir       ${workpath}/build
build.dir           ${workpath}/build

# temporary patch to fix CMake stuff on OSX

patchfiles-append   patch-CMakeLists.txt.diff

# temporary patch to fix Python handling

patchfiles-append   patch-cmake_Modules_GrPython.cmake.diff

# temporary patch to fix use of pyuic

patchfiles-append   patch-cmake_Modules_FindPyQt4.cmake.diff

# remove top-level library path, such that internal libraries are used
# instead of any already-installed ones.

configure.ldflags-delete -L${prefix}/lib

# set last configure argument to the reletive path
# to the top-level cmake source

configure.post_args ../${worksrcdir}

# be verbose when building, for debugging purposes

build.post_args VERBOSE=1

# set Python variants

set pythons_suffixes {26 27}
global chosen_python_suffix
set chosen_python_suffix ""

set pythons_ports {}
foreach s ${pythons_suffixes} {
    lappend pythons_ports python${s}
}

foreach s ${pythons_suffixes} {
    set p python${s}
    set v [join [split ${s} ""] "."]
    set i [lsearch -exact ${pythons_ports} ${p}]
    set c [lreplace ${pythons_ports} ${i} ${i}]
    eval [subst {
        variant ${p} description "Build ${name} for Python ${v}" \
            conflicts ${c} {

            set chosen_python_suffix ${s}

            # require gnuradio to also have this Python variant
            require_active_variants \
                path:lib/libgnuradio-runtime.dylib:gnuradio ${p}

            depends_lib-append \
                port:${p}

            # pyzmq is not checked for at configure, but is
            # required for runtime; so use depends_run.

            depends_run-append \
                port:py${s}-zmq

            # pyqt4 is checked for at configure, then required for
            # runtime; so use depends_lib to get both.  Can use
            # either py*-pyqt4 or py*-pyqt4-devel.

            depends_lib-append \
                path:share/py${s}-sip/PyQt4:py${s}-pyqt4

            # specify that Python version to use
            configure.args-append \
                -DPYTHON_EXECUTABLE=${prefix}/bin/python${v} \
                -DPYTHON_INCLUDE_DIR=${prefix}/Library/Frameworks/Python.framework/Versions/${v}/Headers \
                -DPYTHON_LIBRARY=${prefix}/Library/Frameworks/Python.framework/Versions/${v}/Python \
                -DGR_PYTHON_DIR=${frameworks_dir}/Python.framework/Versions/${v}/lib/python${v}/site-packages \
                -DPYUIC4_EXECUTABLE=${prefix}/bin/pyuic4-${v}
        }
    }]
}

# if no python variant is set, default to 2.7
if {![variant_isset python26] &&
    ![variant_isset python27]} {
    default_variants +python27
}

# make sure -python27 was not used alone
if {![variant_isset python26] &&
    ![variant_isset python27]} {
    return -code error "Exactly one Python variant must be selected."
}
