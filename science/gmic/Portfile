# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

PortGroup           conflicts_build 1.0
if {${subport} eq "gmic-clib"} {
    PortGroup       makefile        1.0
} else {
    PortGroup       cmake           1.1
}
if {${subport} ne "gmic" && ${subport} ne "gmic-clib"} {
    PortGroup       qt5             1.0
    PortGroup       active_variants 1.1
}

name                gmic
version             3.0.1
revision            0
license             CeCILL
categories          science graphics
maintainers         {mps @Schamschula} openmaintainer

description         G'MIC is an interpreter of image processing macros

long_description    G'MIC stands for GREYC's Magic Image Converter. It is \
                    focused on the design of possibly complex pipelines for \
                    converting, manipulating, filtering and visualizing \
                    generic 1d/2d/3d multi-spectral image datasets. This \
                    includes classical color images, but also more complex \
                    data as image sequences or 3D volumetric images. G'MIC \
                    has been designed with portability in mind and runs on \
                    different platforms (Windows, Unix, Mac OS X).

homepage            https://gmic.eu
master_sites        https://gmic.eu/files/source/

# gmic will pick up include files from opencv, opencv3 dods not conflict
conflicts_build     opencv

distfiles           ${name}_${version}${extract.suffix}

checksums           rmd160  81977505d1bbc126548a3a0ce509a05badc8edaf \
                    sha256  6cc20a20e3ab53ce485ccf6e044a30141b3d62cf7743b83bb04906ff29453035 \
                    size    10457923

depends_lib         port:curl \
                    path:lib/libavcodec.dylib:ffmpeg \
                    port:fftw-3 \
                    port:GraphicsMagick \
                    port:ilmbase \
                    port:libheif \
                    path:include/turbojpeg.h:libjpeg-turbo \
                    port:libpng \
                    port:openexr \
                    port:tiff \
                    port:zlib

compiler.cxx_standard           2011

# Uses more memory per compile process than we can accommodate on our
# automated build system. See https://github.com/dtschump/gmic/issues/325
use_parallel_build              no

subport ${name}-qt {
    description-append          (GUI application)

    configure.args-append       -DGMIC_QT_HOST=none
}

subport ${name}-gimp {
    description-append          (GIMP plug-in).

    configure.args-append       -DGMIC_QT_HOST=gimp
    depends_lib-append          path:lib/pkgconfig/gimp-2.0.pc:gimp2
}

subport ${name}-clib {
    description-append          (C library).

    patchfiles                  patch-src-Makefile.diff
}

if {${subport} ne "${name}-clib"} {
    patchfiles                  patch-CMakeLists.txt.diff \
                                patch-cmake-FindCImg.cmake.diff
}

if {${subport} eq ${name}} {
    revision                    1

    description-append          (C++ library and stand-alone binary).

    depends_build-append        port:pkgconfig

    # CMakeLists.txt assumes variables CMAKE_CXX_FLAGS_${cmake.build_type}
    #    (CMAKE_CXX_FLAGS_DEBUG and CMAKE_CXX_FLAGS_RELEASE)
    if {[variant_isset debug]} {
        cmake.build_type        Debug
    } else {
        cmake.build_type        Release
    }

    post-extract {
        copy ${filespath}/FindHEIF.cmake ${worksrcpath}/cmake/
    }

    patchfiles-append           patch-src-gmic_stdlib.gmic.diff

    configure.args-append       -DCUSTOM_CFLAGS=ON \
                                -DENABLE_OPENMP=OFF \
                                -DENABLE_X=OFF

    variant opencv4 description {compile with OpenCV support} {
        configure.args-append   -DENABLE_OPENCV=ON
        configure.ldflags-append \
                                -L${prefix}/lib/opencv4
        configure.pkg_config_path-append \
                                ${prefix}/lib/opencv4/pkgconfig

        depends_lib-append      path:lib/opencv4/libopencv_core.dylib:opencv4
    }

    variant x11 {
        configure.args-replace  -DENABLE_X=OFF \
                                -DENABLE_X=ON

        depends_lib-append      port:xorg-libX11 \
                                port:xorg-libXext \
                                port:xorg-libXcursor \
                                port:xorg-libice \
                                port:xorg-libsm
    }
    default_variants-append     +x11

    livecheck.url               https://gmic.eu/files/source/
    livecheck.regex             ${name}_(\[0-9.\]+)${extract.suffix}
} elseif {${subport} eq "${name}-clib"} {
    # see SOVERSION in ${worksrcpath}/CMakeLists.txt
    set soversion               1

    depends_build-append        port:pkgconfig

    build.dir                   ${worksrcpath}/src
    build.target                libc

    makefile.override-append    PREFIX
    makefile.prefix_name        USR

    variant x11 {
        depends_lib-append      port:xorg-libX11
    }
    default_variants-append     +x11

    pre-build {
        # see ${worksrcpath}/src/Makefile
        if {[variant_isset x11]} {
            set x11_cflags     "-Dcimg_display=1 [exec ${prefix}/bin/pkg-config --cflags x11]"
            set x11_libs       "[exec ${prefix}/bin/pkg-config --libs x11] -lpthread"
        } else {
            set x11_cflags     "-Dcimg_display=0"
            set x11_libs       "-lpthread"
        }
        build.args-append       X11_CFLAGS="${x11_cflags}" \
                                X11_LIBS="${x11_libs}"
    }
    build.args-append           NO_STDLIB=Yes \
                                OPT_CFLAGS="" \
                                SOVERSION=${soversion}

    destroot {
        xinstall -m 0644 ${worksrcpath}/src/gmic_libc.h                 ${destroot}${prefix}/include
        xinstall -m 0644 ${worksrcpath}/src/libcgmic.a                  ${destroot}${prefix}/lib
        xinstall -m 0755 ${worksrcpath}/src/libcgmic.${soversion}.dylib ${destroot}${prefix}/lib
        ln       -s      libcgmic.${soversion}.dylib                    ${destroot}${prefix}/lib/libcgmic.dylib
    }

    livecheck.type  none
} else {
    # CMakeLists.txt assumes variables CMAKE_CXX_FLAGS_${cmake.build_type}
    #    (CMAKE_CXX_FLAGS_DEBUG and CMAKE_CXX_FLAGS_RELEASE)
    if {[variant_isset debug]} {
        cmake.build_type        Debug
    } else {
        cmake.build_type        Release
    }

    cmake.source_dir            ${worksrcpath}/gmic-qt

    configure.args-append       -DENABLE_DYNAMIC_LINKING=ON \
                                -DENABLE_LTO=OFF \
                                -DENABLE_OPENMP=OFF \
                                -DGMIC_PATH=${worksrcpath}/src

    qt5.depends_build_component qttools

    depends_lib-append          port:${name} \
                                port:xorg-libX11 \
                                port:xorg-libXext \
                                port:xorg-libice \
                                port:xorg-libsm

    require_active_variants     ${name} x11

    livecheck.type  none
}
