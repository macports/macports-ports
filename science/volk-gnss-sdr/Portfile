# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:filetype=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

PortGroup           cmake 1.0
PortGroup           github 1.0
PortGroup           active_variants 1.1

name                volk-gnss-sdr
maintainers         {michaelld @michaelld} {gmail.com:carles.fernandez @carlesfernandez} openmaintainer
description         Volk modules for GNSS-SDR
categories          science
license             GPL-3
platforms           darwin

dist_subdir         gnss-sdr

# Requires C++14
compiler.cxx_standard 2014

if {${subport} eq "volk-gnss-sdr"} {

    long_description    ${description}: \
        This port is kept up with the VOLK-GNSS-SDR release, which is typically updated every few months.

    github.setup        gnss-sdr gnss-sdr 0.0.12 v
    revision            2
    checksums           rmd160 1b3a38340c00632cc49ace1fb7fa326d95a20ca8 \
                        sha256 3de8a4b6939be9bf4f25ffd9aa52cc5b92dbbbd454184e54cd1b9290c132f5f1 \
                        size   3766952

    conflicts           volk-gnss-sdr-devel

}

subport volk-gnss-sdr-devel {
    long_description    ${description}: \
        This port is kept up with the VOLK-GNSS-SDR GIT next branch, which is typically updated daily to weekly. This version of VOLK-GNSS-SDR generally contains fixes and new features that will be incorporated in an upcoming release.

    name      volk-gnss-sdr-devel
    github.setup gnss-sdr gnss-sdr 0c4bdfac48f6674bd0993965a91cfcc3e84e5930
    version   20200628-[string range ${github.version} 0 7]
    checksums rmd160 bf58cefb7ab09d324063442e7a4093bb1a67f81a \
              sha256 b197ceb46e1bbc56006b1e1726f02c226dcbd73acf5315fadfb2b4e6af5a7b86 \
              size   3940345
    revision  0

    conflicts volk-gnss-sdr

    # overload the github livecheck branch

    github.livecheck.branch next

}

# This port can be removed on Dec 4, 2020.
subport volk-gnss-sdr-next {
    replaced_by volk-gnss-sdr-devel
    PortGroup obsolete 1.0

    name      volk-gnss-sdr-next
    version   20191204
    revision  0
}

# override github PortGroup homepage setting

homepage            https://gnss-sdr.org

depends_build-append port:pkgconfig

# can use either the Volk release or devel port.
# the Volk API is required, not the ABI.

depends_lib-append \
    path:lib/libvolk.dylib:volk \
    port:orc \
    port:boost

# specify the Python dependencies; these are checked for at configure,
# then used for building, but not at runtime.

set pythons_suffixes {27 36 37 38}

set pythons_ports {}
foreach s ${pythons_suffixes} {
    lappend pythons_ports python${s}
}

proc python_dir {} {
    global pythons_suffixes
    foreach s ${pythons_suffixes} {
        if {[variant_isset python${s}]} {
            set p python[string index ${s} 0].[string index ${s} 1]
            return [file normalize [exec ${p} -c "import sys; print(sys.prefix)"]/lib/${p}/site-packages]
        }
    }
    error "Python support not enabled."
}

foreach s ${pythons_suffixes} {
    set p python${s}
    set v [string index ${s} 0].[string index ${s} 1]
    set i [lsearch -exact ${pythons_ports} ${p}]
    set c [lreplace ${pythons_ports} ${i} ${i}]
    variant ${p} description "Build Volk using Python ${v}" conflicts {*}${c} "
    # required Python
    depends_lib-append \
        port:${p}
    depends_build-append \
        port:py${s}-six \
        port:py${s}-mako
    # specify the Python version to use
    configure.args-append \
        -DPYTHON_EXECUTABLE=${frameworks_dir}/Python.framework/Versions/${v}/bin/python${v} \
        -DVOLK_PYTHON_DIR=${frameworks_dir}/Python.framework/Versions/${v}/lib/python${v}/site-packages
    "
}

if {![variant_isset debug]} {
    set selected_python python37
    foreach s ${pythons_suffixes} {
        if {[variant_isset python${s}]} {
            set selected_python python${s}
        }
    }
    default_variants +${selected_python}
}

# do VPATH (out of source tree) build

cmake.out_of_source yes

# set work directory

worksrcdir ${worksrcdir}/src/algorithms/libs/volk_gnsssdr_module/volk_gnsssdr

# remove top-level library path, such that internal libraries are used
# instead of any already-installed ones.

configure.ldflags-delete -L${prefix}/lib

# specify where to install cmake files

configure.args-append \
    -DCMAKE_MODULES_DIR=${prefix}/share/cmake

test.run yes
