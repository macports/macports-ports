# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem        1.0
PortGroup         perl5 1.0

name              gnucash
conflicts         gnucash gnucash-devel
conflicts-delete  ${subport}
version           2.6.20
perl5.branches    5.26
categories        gnome x11
license           GPL-2+
maintainers       {dports @drkp} openmaintainer
platforms         darwin

set branch        [join [lrange [split ${version} .] 0 1] .]

description       a personal and small-business financial-accounting software
long_description  GnuCash is a personal and small-business \
                  financial-accounting software, freely licensed under the \
                  GNU GPL.  Designed to be easy to use, yet powerful and \
                  flexible, GnuCash allows you to track bank accounts, \
                  stocks, income and expenses. As quick and intuitive to \
                  use as a checkbook register, it is based on professional \
                  accounting principles to ensure balanced books and \
                  accurate reports.

homepage          http://www.gnucash.org/
master_sites      sourceforge \
                  http://www.gnucash.org/pub/gnucash/sources/stable/

use_bzip2         yes

checksums           rmd160  cce754a8530c109e7724f1b935d4cf0bc5aca105 \
                    sha256  44baf7d0133b8bdc9fcb819ee4360afaca2f03a1a254c0221d02e23f35c93025

# We'll install gnucash's libraries in a separate subdir of
# ${prefix}/lib, because gnucash sets DYLD_LIBRARY_PATH at runtime in
# order to dynamically load these directories, and including all
# MacPorts libraries in DYLD_LIBRARY_PATH can cause problems.
# guile18 modules are also installed in a separate subdirectory.
# See https://trac.macports.org/ticket/52494.
set gnclibdir     ${prefix}/lib/gnucash

if {$subport eq $name} {
    patchfiles        patch-configure.ac.diff \
                      patch-src_bin_environment.in.diff \
                      patch-src_core-utils_gnc-path.c.diff

    post-patch {
        reinplace "s|@@GUILEPATH@@|${prefix}/lib/guile18|" ${worksrcpath}/src/bin/environment.in
        reinplace "s|@@LIBDIR@@|${gnclibdir}|" ${worksrcpath}/src/bin/environment.in
        reinplace "s|@@LIBDIR@@|${gnclibdir}|" ${worksrcpath}/src/core-utils/gnc-path.c
        # gnc-fq-dump needs to be patched to use MacPorts perl.
        # (autoconf takes care of the other perl scripts.)
        reinplace "s|^#!/usr/bin/perl|#!${perl5.bin}|" ${worksrcpath}/src/quotes/gnc-fq-dump
    }
} elseif {$subport eq "gnucash-devel"} {
    patchfiles        patch-devel-src_bin_environment.in.diff \
                      patch-devel-src_core-utils_gnc-path.c.diff

    post-patch {
        reinplace "s|@@GUILEPATH@@|${prefix}/lib/guile18|" ${worksrcpath}/gnucash/environment.in
        reinplace "s|@@LIBDIR@@|${gnclibdir}|" ${worksrcpath}/gnucash/environment.in
        reinplace "s|@@LIBDIR@@|${gnclibdir}|" ${worksrcpath}/libgnucash/core-utils/gnc-path.c
        # gnc-fq-dump needs to be patched to use MacPorts perl.
        # (autoconf takes care of the other perl scripts.)
        reinplace "s|^#!/usr/bin/perl|#!${perl5.bin}|" ${worksrcpath}/libgnucash/quotes/gnc-fq-dump
    }
}

post-patch {

    # patch source files to include guile18 header instead of guile.
    # (Note that gnucash's autoconf scripts prefer guile18 over guile
    # if both are installed.)
    fs-traverse f ${worksrcpath} {
        if [regexp {\.(c|h|cpp|hpp)$} ${f}] {
            reinplace -q -locale C "s|<libguile\\.h>|<libguile18.h>|g" ${f}
            reinplace -q -locale C "s|<libguile/|<libguile18/|g" ${f}
        }
    }
}

depends_build     port:pkgconfig \
                  port:intltool \
                  port:autoconf \
                  port:automake \
                  port:libtool

depends_lib       port:guile18 \
                  port:libgnomecanvas \
                  path:lib/pkgconfig/webkit-1.0.pc:webkit-gtk \
                  port:goffice08 \
                  port:p${perl5.major}-datemanip \
                  port:p${perl5.major}-finance-quote

# yelp currently requires X11 to build so disable dependency if building
# with +quartz
#
# TODO: add +x11 +quartz variants to distinguish one build from the other
# since they have different binary dependencies.  Otherwise, rev-upgrade
# errors may occur

if {![variant_isset quartz]} {
    depends_run port:yelp
}

platform darwin {
    if {${configure.cxx_stdlib} eq "libstdc++"} {
       depends_lib-delete path:lib/pkgconfig/webkit-1.0.pc:webkit-gtk
       depends_lib-append path:lib/pkgconfig/webkit-1.0.pc:webkit-gtk-2.0
    }
}

# aqbanking is not universal
universal_variant no

# reconfigure using upstream autogen.sh for intltool 0.51 compatibility

pre-configure {
    xinstall -m 755 ${filespath}/autogen.sh ${worksrcpath}
    system "cd ${worksrcpath} && ./autogen.sh"
    if {[variant_isset python27]} {
        # Fix python exec_prefix
        reinplace "s|\\(PYTHON_EXEC_PREFIX=\\).*$|\\1${frameworks_dir}/Python.framework/Versions/2.7/|" \
            ${worksrcpath}/configure
        reinplace "s|\\(PYTHON_PREFIX=\\).*$|\\1${frameworks_dir}/Python.framework/Versions/2.7/|" \
            ${worksrcpath}/configure
    }
}

configure.args    --disable-dependency-tracking \
                  --disable-aqbanking \
                  --disable-ofx \
                  --disable-dbi \
                  --with-guile=1.8

configure.args-append   --libdir=${gnclibdir}

configure.perl    ${perl5.bin}
configure.env \
    GUILE=${prefix}/bin/guile18 \
    GUILE_CONFIG=${prefix}/bin/guile18-config \
    GUILE_TOOLS=${prefix}/bin/guile18-tools

variant hbci description {AqBanking/HBCI support} {
    depends_lib-append  path:lib/libaqbanking.dylib:aqbanking5-gtk
    configure.args-delete   --disable-aqbanking
    configure.args-append   --enable-aqbanking
}

variant ofx description {OFX support} {
    depends_lib-append  port:libofx
    configure.args-delete   --disable-ofx
    configure.args-append   --enable-ofx
}

variant dbi description {Adds dbi backend support} {
    depends_lib-append  port:libdbi \
                        port:libdbi-drivers
    configure.args-delete --disable-dbi
    configure.args-append --enable-dbi
    configure.args-append --with-dbi-dbd-dir=${prefix}/lib/dbd
    notes-append "gnucash DBI backend support requires the libdbi-drivers port to be built with support for the desired databases. sqlite3 is available by default; for MySQL or PostgreSQL support, ensure that the corresponding variant is selected for libdbi-drivers."
}

default_variants +ofx +hbci

variant python27 description {Install Python bindings for Python 2.7} {
    depends_lib-append  port:python27
    set python_bindir ${frameworks_dir}/Python.framework/Versions/2.7/bin/
    configure.args-append --enable-python \
                          PYTHON_EXTRA_LDFLAGS=\"\`${python_bindir}/python2.7-config --ldflags\`\" \
                          PYTHON_CPPFLAGS=\"\`${python_bindir}/python2.7-config --cflags\`\"
    configure.python    ${python_bindir}/python2.7
}

post-activate {
    system "${prefix}/bin/gtk-update-icon-cache -f -t ${prefix}/share/icons/hicolor"
    system "${prefix}/bin/glib-compile-schemas ${prefix}/share/glib-2.0/schemas"
}

livecheck.regex "/${name} .*/(\[0-9\]+\\.\[0-9\]*\[02468\]\\.\[0-9\]+)/"



subport gnucash-devel {
    version           2.7.2
    revision          0

    checksums           rmd160  6227bc00052b67ca7c609f56ef1b772ce64448fc \
                        sha256  baa6bae13918da62be58fe09d904ccf38893607e57dae4eba53db4ff96ab61b3

    patchfiles        patch-devel-src_bin_environment.in.diff \
                      patch-devel-src_core-utils_gnc-path.c.diff

    depends_build-append port:gtest
    depends_lib-delete port:libgnomecanvas \
                       path:lib/pkgconfig/webkit-1.0.pc:webkit-gtk \
                       port:goffice08
    depends_lib-append port:boost \
                       port:gtk3 \
                       path:lib/pkgconfig/webkit2gtk-4.0.pc:webkit2-gtk

    configure.args-delete --with-html-engine=webkit
    configure.args-append --with-gtest-root=${prefix}/src/googletest \
                          --with-gmock-root=${prefix}/src/googlemock
    
    post-patch {
        reinplace "s|-lboost_filesystem|-lboost_filesystem-mt|g" \
            ${worksrcpath}/libgnucash/core-utils/Makefile.am
        reinplace "s|-lboost_regex|-lboost_regex-mt|g" \
            ${worksrcpath}/libgnucash/backend/dbi/Makefile.am \
            ${worksrcpath}/libgnucash/backend/dbi/test/Makefile.am \
            ${worksrcpath}/libgnucash/engine/Makefile.am \
            ${worksrcpath}/gnucash/import-export/csv-imp/Makefile.am
        reinplace "s|-lboost_locale|-lboost_locale-mt|g" \
            ${worksrcpath}/gnucash/import-export/csv-imp/Makefile.am

        # Drop in a patched version of glibconfig.h via
        # https://git.gnome.org/browse/gtk-osx/plain/patches/glib-gint64-long-long.patch
        # because gnucash incorrectly assumes int64_t and gint64 are
        # the same size. See https://trac.macports.org/ticket/55446
        file copy ${filespath}/glibconfig.h ${worksrcpath}/
    }

    master_sites      sourceforge:${name} \
                      http://www.gnucash.org/pub/gnucash/sources/unstable/${branch}.x/

    livecheck.regex "/${name} .*/(\[0-9\]+\\.\[0-9\]*\[0-9\]\\.\[0-9\]+)/"
}
