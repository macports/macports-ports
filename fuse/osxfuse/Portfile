# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0

name                osxfuse
version             2.6.4
categories          fuse devel
platforms           macosx
license             BSD APSL
maintainers         dports openmaintainer

description         A FUSE-Compliant File System Implementation Mechanism \
                    for Mac OS X
long_description    FUSE for OS X implements a mechanism that makes it possible to implement \
                    a fully functional file system in a user-space program on Mac OS X. It \
                    aims to be API-compliant with the FUSE (File-system in USErspace) \
                    mechanism that originated on Linux.  Therefore, many existing FUSE file \
                    systems become readily usable on Mac OS X. This port provides the \
                    user-space library and header files for building filesystems.

homepage            http://osxfuse.github.io/

# We want to match the supported arch
set kernel_arch [exec uname -m]
switch ${kernel_arch} {
    i386 -
    x86_64 {
        supported_archs ${kernel_arch}
    }
    default {
        supported_archs i386 x86_64
    }
}

distfiles
set mp.dist {
    osxfuse     7b0d8d3   {rmd160 05a8896c7cd738966da1f3f18e8a17723b140dff sha256 bba529834f61fe79be00b4a6298f40a86dd8170f91979c4eeaf9378a1ce067cd}
    kext        c59029c   {rmd160 a19f4d8b0f4a4660b614e1716f21db749677444c sha256 77205fce0f289c3473d3621ca612cb33d86ebf4ea932712661c9527f91013bae}
    framework   9fdde8a   {rmd160 4124b3e9c396ee41dfce35d480caff41936b8000 sha256 9dd705e05de0b346000853c08c143a15d73673684afcc9bbcf55e6c71b33fb87}
    prefpane    8c3ea24   {rmd160 08376d5848025074b4a4feee3d5ed99bc456dfeb sha256 a6a4c5397d7faf9a3f068ddf409e8afc19292a03db0b094bbd144d5c64757118}
    fuse        98b9960   {rmd160 589cbe0c819caded56dc4be64727580e1400fd9e sha256 f60205e19c2971228b78d180dc65cda2a6271a567229acdeb19567fcbcebb641}
    support     1213520   {rmd160 1f774cd0d5273c02716a533463c1d374c793dfc1 sha256 57ccf15e451c132874664c00990c2df12d0dced32ba2cfe6bc6c1be863b663a9}
}

depends_build       port:autoconf \
                    port:automake \
                    port:libtool

set mp.osxfuse_rev  [lindex ${mp.dist} [expr [lsearch ${mp.dist} osxfuse] + 1]]
worksrcdir          osxfuse-osxfuse-${mp.osxfuse_rev}

foreach { mp.comp mp.rev mp.chksum } ${mp.dist} {
    set f ${mp.comp}-${mp.rev}.tar.gz
    master_sites-append https://github.com/osxfuse/${mp.comp}/tarball/${mp.rev}/:${mp.comp}
    distfiles-append    ${f}:${mp.comp}
    checksums-append    ${f}
    foreach {type chksum} ${mp.chksum} {
        checksums-append ${type} ${chksum}
    }
}

post-extract {
    foreach { mp.comp mp.rev mp.chksum } ${mp.dist} {
        if {${mp.comp} ne "osxfuse"} {
            # Replace existing empty directory if it exists
            file delete ${workpath}/${worksrcdir}/${mp.comp}
            move ${workpath}/osxfuse-${mp.comp}-${mp.rev} ${workpath}/${worksrcdir}/${mp.comp}
        }
    }
}

patchfiles          patch-buildsystem.diff \
                    patch-kext-location.diff

post-patch {
    # Perhaps the ports sandbox does not let us write to /tmp.
    # For a quick and temporary fix use this reinplace.
    file mkdir ${workpath}/tmp
    reinplace "s,@@TMP@@,${workpath}/tmp,g" ${worksrcpath}/build.sh

    # Only build the archs we want, not the hardcoded universal archs
    reinplace -E "s,@@ARCHS@@,[get_canonical_archs],g" ${worksrcpath}/build.sh

    # Inject the destroot path as the buildsystem tries to write to the prefix directly
    reinplace -E "s,@@DESTROOT@@,${destroot},g" ${worksrcpath}/build.sh

    # Correct the location of the kext
    reinplace -E "s,@@PREFIX@@,${prefix},g" ${worksrcpath}/kext/common/fuse_param.h
}

use_configure   no

build.cmd       ./build.sh
# Yes, they really named this target homebrew
build.target    -t homebrew
build.args      -f ${prefix}

# The build step already does everything
destroot {}

# Move filesystem bundle into place
post-destroot {
    # Set proper permissions
    fs-traverse f ${destroot}${prefix}/Library {
        file attributes $f -owner root -group wheel
    }
    # Enable setuid on helper binary
    file attributes ${destroot}${prefix}/Library/Filesystems/osxfusefs.fs/Support/load_osxfusefs -permissions 04755
}
destroot.violate_mtree yes

notes {
    When upgrading, unmount all FUSE filesystems and then unload the kernel extension.
    Unloading can be done via: sudo kextunload -b com.github.osxfuse.filesystems.osxfusefs
    Alternativley (or if this fails), just reboot your computer now.
}

# Could probably be supported by setting ARCHS correctly above
universal_variant no
