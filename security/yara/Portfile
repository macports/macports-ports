# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               github 1.0

# Need strnlen() and strndup()
PortGroup               legacysupport 1.0
legacysupport.newest_darwin_requires_legacy 10

github.setup            VirusTotal yara 4.0.5 v
revision                0

categories              security
license                 GPL-2+
platforms               darwin
maintainers             nomaintainer

description             Malware identification and classification tool
long_description \
    YARA is a tool aimed at helping malware researchers to identify and \
    classify malware samples.

checksums               rmd160  3f86fd4e72c9655c60b32459c47e541703d483ef \
                        sha256  274a53e581060ccb16228c709df005c6bec1b1ec8f183e0ceb2a5cba729a273b \
                        size    890468

if {${subport} eq ${name}} {
    depends_build       port:autoconf \
                        port:automake \
                        port:libtool \
                        port:pkgconfig

    depends_lib         port:libmagic \
                        port:jansson \
                        path:lib/libssl.dylib:openssl \
                        port:pcre \
                        port:protobuf-c

    patchfiles          patch-loop_declarations.diff

    pre-configure {
        reinplace "s|/usr/local/opt/openssl/include|${prefix}/include/openssl|" ${worksrcpath}/configure.ac
        reinplace "s|/usr/local/opt/openssl/lib|${prefix}/lib|" ${worksrcpath}/configure.ac
        system -W ${worksrcpath} "./bootstrap.sh"
    }
    # If previous yara is installed, this build will fail since
    # /opt/local/include is before ./libyara/include
    # so swap CPPFLAGS and CFLAGS
    # Remove this upon the next release after verifying it is not needed.
    post-configure {
            reinplace "s|\$(CPPFLAGS) \$(AM_CFLAGS) \$(CFLAGS)|\$(CFLAGS) \$(AM_CFLAGS) \$(CPPFLAGS)|" ${worksrcpath}/Makefile
            reinplace "s|\$(DEFAULT_INCLUDES) \$(INCLUDES) \$(AM_CPPFLAGS) \$(CPPFLAGS)|\$(DEFAULT_INCLUDES) \$(INCLUDES) \$(AM_CPPFLAGS) \$(CFLAGS)|" ${worksrcpath}/Makefile
            reinplace "s|\t\$(AM_CFLAGS) \$(CFLAGS)|\t\$(AM_CFLAGS) \$(CPPFLAGS)|" ${worksrcpath}/Makefile
    }

    configure.cflags-append -std=gnu89
    configure.args-append   --with-crypto \
                            --disable-silent-rules \
                            --enable-cuckoo \
                            --enable-magic

    test.run            yes
    test.target         check
}

set python.versions {27 35 36 37 38 39}

foreach v ${python.versions} {
    set python.version         ${v}
    set python.branch          [string range ${python.version} 0 end-1].[string index ${python.version} end]
    set python.bin             ${prefix}/bin/python${python.branch}
    set python.prefix          ${frameworks_dir}/Python.framework/Versions/${python.branch}

    subport py${python.version}-${name} {
        github.setup            VirusTotal yara-python 4.0.4 v
        revision                0
        categories              security python
        description             ${name} bindings for python ${python.branch}
        long_description        Malware identification and classification tool. \
                                    This subport provides bindings for python ${python.branch}

        checksums               rmd160  44ecc51f2069b7b3da9ceece0f9dca31bcaff831 \
                                sha256  9e955b7b6c5146e8684fb31b32cb077c59d099e67bbe1889ddaeef6e435439ff \
                                size    32318

        depends_build-append    port:py${python.version}-setuptools
        depends_lib-append      port:${name} \
                                port:python${python.version}

        use_configure           no

        build.cmd               ${python.bin} setup.py --no-user-cfg
        build.target            build --dynamic-linking

        destroot.cmd            ${python.bin} setup.py --no-user-cfg
        destroot.destdir        --prefix=${python.prefix} --root=${destroot}

        pre-test {
            test.env            PYTHONPATH=[glob -nocomplain ${worksrcpath}/build/lib*]
        }

        test.run                yes
        test.cmd                ${python.bin} tests.py
        test.target
    }
}
