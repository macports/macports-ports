# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           python 1.0
PortGroup           github 1.0

github.setup        ronaldoussoren pyobjc 7.3 v
revision            2

checksums           rmd160  31d8aa7291bb396f60541e8e2376d19dbf84619c \
                    sha256  64a35c96b27dfb106b918d5c2a5e5a2c07478e89e5a4f09e8298873c76be3441 \
                    size    12911754

name                py-pyobjc
categories-append   devel
license             MIT
maintainers         openmaintainer {danchr @danchr}
platforms           darwin

description         bidirectional bridge between python and Objective C
long_description    The PyObjC project aims to provide a bridge between \
                    the Python and Objective-C programming languages. The \
                    bridge is intended to be fully bidirectional, allowing \
                    the Python programmer to take full advantage of the \
                    power provided by various Objective-C based toolkits \
                    and the Objective-C programmer transparent access to \
                    Python based functionality.
homepage            https://pyobjc.readthedocs.io

python.versions     36 37 38 39 310

if {${os.major} < 10} {
    depends_lib
    depends_build
    pre-fetch {
        ui_error "${subport} @${version} requires Mac OS X 10.7 or later"
        return -code error "incompatible Mac OS X version"
    }

} elseif {${name} ne ${subport}} {
    depends_lib-append \
                    port:py${python.version}-setuptools
    depends_build   port:py${python.version}-sphinx

    # the patches below suppresses cleaning on incremental builds, as
    # MacPorts handles that itself, and disables a sphinx extension
    # not in MacPorts
    patchfiles-append \
                    pr-367.diff \
                    patch-docs-conf.py.diff \
                    patch-pyobjc-core-setup.py.diff \
                    patch-setup-no-werror.diff \
                    patch-setup-macosx11.diff \
                    patch-setuptools-distutils.diff

    # use system libffi on Catalina and Big Sur until upstream merges
    # and releases Apple's changes
    # https://trac.macports.org/ticket/62475
    if {${os.major} < 19} {
        depends_lib-append \
                    port:libffi
    } else {
        # this patch causes the build to fail hard should any module
        # fail to build, rather than just stop; as this is currently
        # the case for many older systems, restrict it to recent
        # platforms
        patchfiles-append \
                    patch-install.py.diff
    }

    post-patch {
        reinplace \
            "s/sphinx-build /sphinx-build-${python.branch} /g" \
            ${worksrcpath}/docs/Makefile

        ln -s install.py ${worksrcpath}/setup.py

        # see comment above
        if {${os.major} < 19} {
            # force using both our libffi _and_ its headers
            reinplace \
                "s,/usr/include/ffi,${prefix}/include,g" \
                ${worksrcpath}/pyobjc-core/setup.py

            reinplace \
                "s,<ffi/ffi.h>,<ffi.h>,g" \
                ${worksrcpath}/pyobjc-core/Modules/objc/libffi_support.h
        } else {
            # force linking against system libffi by path, so that we
            # bypass CPython's dependency on it
            reinplace \
                "s,-lffi,${configure.sdkroot}/usr/lib/libffi.tbd,g" \
                ${worksrcpath}/pyobjc-core/setup.py
        }
    }

    if {${configure.sdkroot} eq ""} {
        set sdkroot "/"
    } else {
        set sdkroot ${configure.sdkroot}
    }

    build.env-append    SDKROOT=${sdkroot}
    destroot.env-append SDKROOT=${sdkroot}

    # build the documentation
    post-build {
        system -W ${worksrcpath}/docs "make -j${build.jobs} html"
    }

    post-destroot {
        xinstall -m 644 -W ${worksrcpath}/pyobjc-core HISTORIC.txt Install.txt \
            License.txt README.txt \
            ${destroot}${prefix}/share/doc/${subport}
        copy ${worksrcpath}/docs/_build/html ${destroot}${prefix}/share/doc/${subport}/html
    }
}
