# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               python 1.0

# meta-version; bump whenever underlying variants are updated. Needed to
# support portindex (variants can't have different versions.)
version                 8
# Needed for change to meta-versioning
epoch                   1
name                    py-clang
python.versions         37 38 39 310
license                 NCSA
maintainers             {eborisch @eborisch} \
                        openmaintainer
description             Python bindings for clang
long_description        ${description}
homepage \
    https://github.com/llvm/llvm-project/tree/main/clang/bindings/python

supported_archs         noarch

livecheck.url           https://api.github.com/repos/llvm/llvm-project/git/refs/tags
# Update this to the most recent clang supported
livecheck.version       14.0.6
livecheck.regex         {llvmorg-([\d.]+)\"}

if {${name} ne ${subport}} {
    # Share llvm's downloads
    dist_subdir             llvm
    use_xz                  yes

    checksums \
     cfe-3.7.1.src.tar.xz \
      rmd160  185b0f75970bc50682766a21794440578db87b5d \
      sha256  56e2164c7c2a1772d5ed2a3e57485ff73ff06c97dff12edbeea1acc4412b0674 \
     clang-14.0.6.src.tar.xz \
      rmd160  0e08ef630f183fd654247aeeb10d8fafee5e0814 \
      sha256  2b5847b6a63118b9efe5c85548363c81ffe096b66c3b3675e953e26342ae4031 \
     clang-13.0.0.src.tar.xz \
      rmd160  41d60ff0bfa3dbd9e4f04461db1750c2d9f50ac2 \
      sha256  5d611cbb06cfb6626be46eb2f23d003b2b80f40182898daa54b1c4e8b5b9e17e

    depends_build-append    port:py${python.version}-setuptools

    # Keeping 37 around for old systems; otherwise two latest releases.
    set clanglist       {37    14     13}
    set clangvlist      {3.7.1 14.0.6 13.0.0}

    foreach cvnum $clanglist {
        # Explictly use (and depend on) the libclang we select during install
        set cvidx [lsearch $clanglist $cvnum]
        set clang_version [lindex $clangvlist $cvidx]
        set cflist {}
        foreach v [lreplace $clanglist $cvidx $cvidx] {lappend cflist clang$v}
        if {${cvnum} < 90 && ${cvnum} >= 37} {
            set diststub cfe
        } else {
            set diststub clang
        }
        # Once clang reaches version 370, this will need to be revisited...
        if {${cvnum} >= 37} {
            set cvstr [string index $cvnum 0].[string index $cvnum 1]
        } else {
            set cvstr ${cvnum}
        }
        set substr \
          "library_path = '${prefix}/libexec/llvm-${cvstr}/lib'"
        variant clang${cvnum} description {
               Use clang${cvnum}'s libclang
        } conflicts {*}${cflist} "
            master_sites        https://releases.llvm.org/${clang_version} \
                                https://github.com/llvm/llvm-project/releases/download/llvmorg-${clang_version}
            depends_lib-append  port:clang-${cvstr}
            distfiles           ${diststub}-${clang_version}.src.tar.xz
            worksrcdir          ${diststub}-${clang_version}.src/bindings/python
            post-patch {
                reinplace {s^library_path = None^$substr^} clang/cindex.py
                reinplace {s/version=.*/version=\"${clang_version}\",/} setup.py
            }
            livecheck.version   ${clang_version}
            livecheck.regex \
                \"llvmorg-([string range $clang_version 0 1]\\\\.\\\[0-9.\\\]+)\\\"\"
        "
    }

    test.run            yes
    test.cmd            ${python.bin}
    test.target         -m unittest discover -v

    if {![variant_isset clang37] &&
        ![variant_isset clang13] &&
        ![variant_isset clang14]} {
        default_variants +clang14
    }

    pre-extract {
        # Will never hit this when installing from packages, which is OK, as
        # they will have the default variant set above.
        if {![variant_isset clang37] &&
            ![variant_isset clang13] &&
            ![variant_isset clang14]} {
            ui_error "At least one of the clangNN variants must be active."
            return -code error "Unsupported (no) variants selected."
        }
    }

    post-extract {
        file copy ${filespath}/setup.py ${worksrcpath}/
    }

    post-destroot {
        set DOCDIR ${destroot}${prefix}/share/doc/${subport}
        xinstall -d ${DOCDIR}
        file copy ${worksrcpath}/README.txt ${DOCDIR}
    }

    notes {
        The path to the selected libclang variant has been recorded in the\
        installed package. It can be overridden with:\
        cindex.set_library_(path|file)().
    }
}
