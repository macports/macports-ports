# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem                                         1.0
PortGroup           python                         1.0
PortGroup           github                         1.0
PortGroup           compilers                      1.0
PortGroup           compiler_blacklist_versions    1.0
PortGroup           xcode_workaround               1.0

name                py-tensorflow
version             2.6.0
revision            0
github.setup        tensorflow tensorflow ${version} v
platforms           darwin

license             Apache-2
maintainers         {emcrisostomo @emcrisostomo} openmaintainer

description         TensorFlow helps the tensors flow
long_description    {*}${description}

homepage            https://www.tensorflow.org/

github.livecheck.regex  {([0-9.]+)}

checksums           rmd160  5a7cf86116fb585f08ed119dd758709e2a675b2e \
                    sha256  3acc1fbb93013f4a8686b0eeb05c8884b35db3480a052b10f49ff07da44d6fd9 \
                    size    59094790

# Not yet ready for arm
supported_archs     x86_64

set min_darwin 16
if { ${os.major} < ${min_darwin} } {
    known_fail yes
    # https://trac.macports.org/ticket/58518
    pre-fetch {
        ui_error "${name} ${version} requires Darwin ${min_darwin} or newer."
        return -code error "incompatible macOS version"
    }
}

compiler.cxx_standard 2014
compiler.blacklist-append {clang < 1200.0.32.27} {macports-clang-[4-9].0}

# https://trac.macports.org/ticket/59192
xcode_workaround.type                  avoid_xcode_compiler
xcode_workaround.os_versions           18 19
xcode_workaround.fixed_xcode_version   11.4

# Supported python versions
python.versions  37 38 39

if {${name} ne ${subport}} {

    PortGroup bazel 1.0

    # See https://www.tensorflow.org/install/source#macos for tested bazel versions.
    bazel.version   3.7

    # TF versions 1 and 2 cannot be installed at once
    conflicts py${python.version}-tensorflow1

    # Conflicts with tensorflow-macos
    if { (${build_arch} eq {arm64} && (${python.version} eq 38 || ${python.version} eq 39))
        || (${build_arch} eq {x86_64} && ${python.version} eq 38) } {
        conflicts-append \
                    py${python.version}-tensorflow-macos \
                    py${python.version}-tensorflow_macos
    }

    variant mkl description {Enable Intel Math Kernel Library support} {
        bazel.extra_build_opts-append --config=mkl
        depends_lib-append port:py${python.version}-mkl
    }
    # Enable MKL by default on 10.15 and newer.
    #if {${os.major} >= 19} {
    #    default_variants-append +mkl
    #}

    depends_build-append \
        port:py${python.version}-future \
        port:py${python.version}-pip \
        port:py${python.version}-mock \
        port:cctools

    depends_lib-append \
        port:py${python.version}-absl \
        port:py${python.version}-astor \
        port:py${python.version}-astunparse \
        port:py${python.version}-flatbuffers \
        port:py${python.version}-gast \
        port:py${python.version}-google-pasta \
        port:py${python.version}-grpcio \
        port:py${python.version}-keras \
        port:py${python.version}-numpy \
        port:py${python.version}-opt_einsum \
        port:py${python.version}-protobuf3 \
        port:py${python.version}-six \
        port:py${python.version}-tensorboard \
        port:py${python.version}-tensorflow_estimator \
        port:py${python.version}-termcolor \
        port:py${python.version}-wheel \
        port:py${python.version}-wrapt

    use_configure yes
    # Options passed to configure script via env vars
    bazel::add_to_envs PYTHON_LIB_PATH=${python.prefix}/lib/python${python.branch}/site-packages
    bazel::add_to_envs PYTHON_BIN_PATH=${prefix}/bin/python${python.branch}

    # Use correct python in bazel build
    bazel.python_version ${python.branch}

    bazel.build_target     "//tensorflow/tools/pip_package:build_pip_package"
    bazel.post_build_cmd   ./bazel-bin/tensorflow/tools/pip_package/build_pip_package [list $workpath]
    bazel.extra_build_opts-append --config=opt

    #bazel.limit_build_jobs no

    destroot.cmd  pip-${python.branch}
    destroot.args           \
        --ignore-installed  \
        --no-cache-dir      \
        --no-dependencies   \
        --root ${destroot}  \
        ${workpath}/tensorflow-*.whl
    destroot.post_args

    post-destroot {
        file delete ${destroot}${python.prefix}/bin/tensorboard
        file delete ${destroot}${prefix}/bin/tensorboard-${python.branch}
    }

    livecheck.type  none

    # If tensorflow-macos is available for this OS + python version advertise it
    if { ${os.major} > 19 && ${python.branch} == 3.8 } {
        notes "
A hardware accelerated MacOS optimised port of tensorflow from Apple
is available as an alternative to ${subport}. To use run :-
    sudo port uninstall -f ${subport} ${subport}-addons
    sudo port install ${subport}-macos
"
    }

}
