# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem                                         1.0
PortGroup           python                         1.0
PortGroup           cxx11                          1.1
PortGroup           java                           1.0
PortGroup           github                         1.0
PortGroup           compilers                      1.0
PortGroup           xcodeversion                   1.0
PortGroup           compiler_blacklist_versions    1.0

name                py-tensorflow
version             1.14.0
revision            1
github.setup        tensorflow tensorflow ${version} v
platforms           darwin
supported_archs     x86_64

license             Apache-2
maintainers         {emcrisostomo @emcrisostomo} {jonesc @cjones051073} openmaintainer

description         TensorFlow helps the tensors flow
long_description    ${description}

homepage            https://www.tensorflow.org/

github.livecheck.regex  {([0-9.]+)}

checksums           rmd160  c89d7a99933fb2cac9e9d8e694c6def88c2dfa9c \
                    sha256  fab2eb90de8dc1fc6e8e95cbc66c3aa7b92c28e3df4191017cb2901c8d3f023a \
                    size    41341410

compiler.blacklist-append {clang < 840}

patch.pre_args      -p1

# Required java version
java.version        11
# JDK port to install if required java not found
java.fallback       openjdk11

# The oldest Xcode version to use default Xcode compiler
# Note setting here should be in sync with that in bazel
set tf_min_xcode    9.0

if {${os.major} < 16} {
    # Work around for issues with clock_gettime(CLOCK_REALTIME, &ts);
    # https://stackoverflow.com/questions/5167269/clock-gettime-alternative-in-mac-os-x
    patchfiles-append CLOCK_REALTIME-Older-OSX.patch
}

variant native description {Build from source for best native platform support} { }

# Support python versions
python.versions        27 35 36 37
python.default_version 37

if {${name} ne ${subport}} {

    depends_build-append \
        port:py${python.version}-pip \
        port:bazel25 \
        port:cctools

    depends_lib-append \
        port:py${python.version}-wheel \
        port:py${python.version}-tensorboard \
        port:py${python.version}-six \
        port:py${python.version}-protobuf3 \
        port:py${python.version}-numpy \
        port:py${python.version}-mock \
        port:py${python.version}-gast \
        port:py${python.version}-astor \
        port:py${python.version}-termcolor \
        port:py${python.version}-keras \
        port:py${python.version}-absl \
        port:py${python.version}-future
    if {${python.version} < 34} {
        depends_lib-append \
            port:py${python.version}-backports.weakref \
            port:py${python.version}-enum34
    }

    use_configure yes
    # Options passed to configure script via env vars
    configure.env-append PYTHON_LIB_PATH=${python.prefix}/lib/python${python.branch}/site-packages
    configure.env-append PYTHON_BIN_PATH=${prefix}/bin/python${python.branch}
    configure.env-append JAVA_HOME=${java.home}
    build.env-append     TMPDIR=${workpath}/tmp
    build.env-append     JAVA_HOME=${java.home}
    set base_march "-march=ivybridge"
    if {![variant_isset native]} {
        configure.env-append CC_OPT_FLAGS=${base_march}
        build.env-append     CC_OPT_FLAGS=${base_march}
        notes "This version is built based on a base architecture for convenience, 
              which may not be optimized for your system. To build a version 
              customized for your machine, use the +native variant"
    }
    # Disable Xcode detection on older OSes, as we want the
    # MP provided commandline utilities to be used instead.
    if {[vercmp ${xcodeversion} ${tf_min_xcode}] < 0} {
        configure.env-append BAZEL_USE_CPP_ONLY_TOOLCHAIN=1
        build.env-append     BAZEL_USE_CPP_ONLY_TOOLCHAIN=1
    }
    # Remove all arguments
    configure.args
    configure.pre_args
    # configure command
    configure.cmd ./configure
    # Patch configuration
    pre-configure {
        # enforce correct build settings
        foreach f [ exec find ${worksrcpath} -name "*.tpl" -or -name "*.bzl" -or -name "CROSSTOOL" -or -name "configure.py" ] {
            foreach cmd {ar nm strip libtool ld objdump} {
                reinplace -q "s|/usr/bin/${cmd}|${prefix}/bin/${cmd}|g" ${f}
            }
            reinplace -q "s|/usr/bin/clang|\"${configure.cc}\"|g"   ${f}
            reinplace -q "s|\"clang\"|\"${configure.cc}\"|g"        ${f}
            reinplace -q "s| clang | ${configure.cc} |g"            ${f}
            reinplace -q "s|/usr/local/include|${prefix}/include|g" ${f}
            reinplace -q "s|std=c++0x|std=c++11|g"                  ${f}
        }
        # If not native build, make sure not used...
        if {![variant_isset native]} {
            foreach f [ exec find ${worksrcpath} -name "configure" -or -name "configure.py" -or -name "CMakeLists.txt" -or -name "Makefile" -or -name "*.sh" ] {
                reinplace -q "s|-march=native|${base_march}|g" ${f}
            }
        }
    }

    build {
        # Build using the wonderful bazel build system ...
        set tf_bazel_cmd "bazel --max_idle_secs=60 --output_user_root=${workpath}"
        set tf_bazel_build_opts "-s -c opt --config=opt"
        # hack to try and transfer MP c, c++ and ld options to bazel...
        foreach opt [list {*}${configure.cflags} ] {
            set tf_bazel_build_opts "${tf_bazel_build_opts} --conlyopt '${opt}'"
        }
        foreach opt [list {*}${configure.cxxflags} ] {
            set tf_bazel_build_opts "${tf_bazel_build_opts} --cxxopt '${opt}'"
        }
        foreach opt [list {*}${configure.ldflags} ] {
            set tf_bazel_build_opts "${tf_bazel_build_opts} --linkopt '${opt}'"
        }
        if {[vercmp ${xcodeversion} ${tf_min_xcode}] < 0} {
            set tf_bazel_build_opts "${tf_bazel_build_opts} --action_env CC=${configure.cc}"
            set tf_bazel_cmd "BAZEL_USE_CPP_ONLY_TOOLCHAIN=1 ${tf_bazel_cmd}"
        }
        if {![variant_isset native]} {
            set tf_bazel_build_opts "${tf_bazel_build_opts} --copt=${base_march}"
            set tf_bazel_cmd "CC_OPT_FLAGS=${base_march} ${tf_bazel_cmd}"
        }
        system -W ${worksrcpath} "${tf_bazel_cmd} build ${tf_bazel_build_opts} //tensorflow/tools/pip_package:build_pip_package"
        # Build the python wheel
        system -W ${worksrcpath} "./bazel-bin/tensorflow/tools/pip_package/build_pip_package ${workpath}"
        # Clean up
        system -W ${worksrcpath} "${tf_bazel_cmd} clean"
    }

    set macos_minor_v [expr [lindex [split ${os.version} "."] 0] - 4]
    set install_whlfile ${workpath}/tensorflow-${version}-cp${python.version}-cp${python.version}m-macosx_10_${macos_minor_v}_x86_64.whl

    destroot.cmd  pip-${python.branch}
    destroot.args           \
        --upgrade           \
        --force-reinstall   \
        --no-cache-dir      \
        --no-dependencies   \
        --root ${destroot}  \
        ${install_whlfile}
    destroot.post_args

    post-destroot {
        file delete ${destroot}${python.prefix}/bin/tensorboard
        file delete ${destroot}${prefix}/bin/tensorboard-${python.branch}
    }

    livecheck.type  none

}
