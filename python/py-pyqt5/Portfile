# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               python 1.0

name                    py-pyqt5
python.rootname         PyQt5
# we the next bump check --allow-sip-warnings if needed
version                 5.14.1
revision                0
categories-append       devel
platforms               darwin
maintainers             nomaintainer
description             PyQt5 is a set of Python bindings for the Qt5 toolkit
long_description        ${description}. The bindings \
                        are implemented as a set of Python modules and contain over 620 classes.
homepage                https://www.riverbankcomputing.com/software/pyqt/intro
license                 GPL-3
checksums               rmd160  3cb6fbdc9e83dffe2972125c56afe858a6e1f66c \
                        sha256  2f230f2dbd767099de7a0cb915abdf0cbc3256a0b5bb910eb09b99117db7a65b \
                        size    3241571

python.versions 27 35 36 37 38
subport "${name}-common" {}

foreach py_ver ${python.versions} {
    subport "py${py_ver}-pyqt5-webengine" {}
    subport "py${py_ver}-pyqt5-chart" {}
}

if {${subport} eq "${name}-common"} {
    description-append          (.sip sourcefiles)
    long_description-append     This port contains the .sip source files.
    depends_lib
    use_configure               no
    universal_variant           no
    build                       {}
    destroot {
        xinstall -m 755 -d ${destroot}${prefix}/share/sip/PyQt5
        # copy all relevant directories from ${worksrcpath}/sip into the destination.
        # This is exactly what the regular build system does, except that here we
        # use the same location as used on Linux, not the one hidden in the Python
        # framework that is apparently the default on Mac.
        file copy {*}[glob -directory ${worksrcpath}/sip/ Q*] ${destroot}${prefix}/share/sip/PyQt5
        file copy ${worksrcpath}/sip/Enginio ${destroot}${prefix}/share/sip/PyQt5
    }

} elseif {[string first "webengine" ${subport}] != -1 || [string first "chart" ${subport}] != -1} {
    PortGroup           qmake5 1.0

    compiler.cxx_standard 2011
    depends_lib-append  port:py${python.version}-pyqt5
    use_configure       yes
    configure.pre_args
    configure.cmd       "${python.bin} configure.py"
    qt5.spec_cmd        --spec=
    configure.args-append \
                        -q ${qt_qmake_cmd} \
                        --verbose \
                        --sip=${prefix}/bin/sip-${python.branch} \
                        --no-qsci-api \
                        --no-dist-info

    build.cmd           make
    build.target        all
    destroot.cmd        ${build.cmd}
    destroot.destdir    DESTDIR=${destroot}

    if {[string first "webengine" ${subport}] != -1} {
        python.rootname     PyQtWebEngine
        version             5.14.0
        revision            0
        description         PyQt5 Webengine bindings
        long_description    ${description}
        checksums           rmd160  00981c8d95d1fb76208c0c089eda7ffb5746c586 \
                            sha256  e11595051f8bfbfa49175d899b2c8c2eea3a3deac4141edf4db68c3555221c92 \
                            size    47794

        qt5.depends_component \
                            qtwebengine
    } elseif {[string first "chart" ${subport}] != -1} {
        python.rootname     PyQtChart
        version             5.14.0
        revision            0
        description         PyQt5 Charts bindings
        long_description    ${description}
        checksums           rmd160  d415605d1680bc7b276754f3d281a3c2b958b94a \
                            sha256  f9004861441becab7a4a48e834da14c3976e4c03e5513c93e005d5df36085046 \
                            size    68193

        qt5.depends_component \
                            qtcharts
    }

} elseif {${name} ne ${subport}} {
    PortGroup           qmake5 1.0

    compiler.cxx_standard 2011
    # pull in the Qt5 meta-port
    qt5.depends_component \
                        qtscript \
                        qt3d \
                        qt5
    depends_lib-append  port:py${python.version}-sip \
                        port:dbus-python${python.version}

    if {${python.version} == 27} {
        depends_lib-append port:py27-enum34
    }

    use_configure       yes
    configure.pre_args
    configure.cmd       "${python.bin} configure.py"
    qt5.spec_cmd        --spec=
    configure.args-append \
                        -q ${qt_qmake_cmd} \
                        --verbose \
                        --confirm-license \
                        --sip=${prefix}/bin/sip-${python.branch} \
                        --dbus=${python.include}/dbus-1.0 \
                        --designer-plugindir=${qt_plugins_dir}/designer/Py${python.version}Qt5 \
                        --qml-plugindir=${qt_plugins_dir}/Py${python.version}Qt5 \
                        --no-qsci-api \
                        --disable=QtWebKit \
                        --disable=QtWebKitWidgets \
                        --no-dist-info \
                        --allow-sip-warnings

    # using --dbus means the compiler will find dbus-python.h but not
    # the DBus headers themselves
    # do not clear include directories just because --dbus is given
    patchfiles-append   patch-dbus_includes.diff
    # PyQt5 insists on using a private copy of sip, but the default sip seems to work just fine
    # (actually, the insistence appears to be on sip using a private module directory;
    # see the online pyqt5 build-from-source instructions).
    patchfiles-append   patch-use-default-sip.diff

    build.cmd           make
    build.target        all
    destroot.cmd        ${build.cmd}
    destroot.destdir    DESTDIR=${destroot}

    variant debug description "Build debug libraries" {
        configure.cmd-append --debug
    }

    variant scintilla description {Build the PyQt API for QScintilla} {
        depends_lib-append      port:qscintilla-qt5
        configure.args-replace  --no-qsci-api \
                                --qsci-api

        post-destroot {
            # move the Qsci API file to a Python specific name
            # it is generated during the configure step and differs only
            # (and slightly) for python 2.x vs. python 3.x .
            # Figure out a way to install only a single copy...
            move ${destroot}${qt_data_dir}/qsci/api/python/PyQt5.api \
                ${destroot}${qt_data_dir}/qsci/api/python/PyQt5-Python${python.branch}.api
        }
    }

    # variant is obsolete since py-pyqt5 version 5.12.1, remove after March 26, 2020
    variant webengine description {Build QtWebEngine module (obsolete, use the py-pyqt5-webengine port)} {
        notes "
        The +webengine variant is obsolete because PyQtWebEngine is not included anymore in the PyQt5 bindings.\
        If you need PyQtWebEngine, please install the py-pyqt5-webengine port.
        "
    }

    variant webkit description {Build QtWebKit module} {
        qt5.depends_component   qtwebkit
        configure.args-delete   --disable=QtWebKit \
                                --disable=QtWebKitWidgets
    }

    variant graceful description {Don't abort (crash) on Python errors} {
        patchfiles-append       patch-no-abort-on-python-errors.diff
    }
}
