# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0
PortGroup           python 1.0

name                youtube-dl

if {${subport} eq ${name}} {
    github.setup    ytdl-org ${subport} 2021.12.17
    revision        0
    checksums       rmd160  beca02d231a287488cecffe4c98651b4d6537b9d \
                    sha256  9f3b99c8b778455165b4525f21505e86c7ff565f3ac319e19733d810194135df \
                    size    3332299

    homepage        https://youtube-dl.org

    set symlinks [list \
        etc/bash_completion.d/${subport}.bash-completion \
        etc/fish/completions/${subport}.fish \
        share/man/man1/${subport}.1 \
        share/zsh/site-functions/_${subport} \
    ]

    post-destroot {
        # https://github.com/ytdl-org/youtube-dl/issues/9231
        xinstall -d ${destroot}${python.prefix}/share/zsh/site-functions
        xinstall -m 0644 ${worksrcpath}/youtube-dl.zsh \
            ${destroot}${python.prefix}/share/zsh/site-functions/_youtube-dl
    }

    livecheck.type  regex
    livecheck.url   ${homepage}/latest/version
    livecheck.regex {^([0-9.]+)$}
}

subport yt-dlp {
    github.setup    yt-dlp ${subport} 2021.12.27
    revision        0
    checksums       rmd160  d1a8369ba25d1f33ce886136dd82e8fafe51d5a9 \
                    sha256  3492c344b1b71ac13c73a53dbb3bc504f7ebaa325c10807ad905428e74a6d630 \
                    size    4221035

    dist_subdir     ${subport}/${version}
    distname        ${subport}

    set symlinks [list \
        share/bash-completion/completions/${subport} \
        share/fish/vendor_completions.d/${subport}.fish \
        share/man/man1/${subport}.1 \
        share/zsh/site-functions/_${subport} \
    ]
}

categories          net
platforms           darwin
maintainers         {ryandesign @ryandesign}
supported_archs     noarch
license             public-domain
installs_libs       no

description         command-line program to download videos from YouTube.com and other sites
long_description    ${name} is a small {*}${description}.

github.tarball_from releases

worksrcdir          ${name}

patchfiles-append   ${subport}-config-location.patch \
                    ${subport}-update.patch

post-patch {
    reinplace -W ${worksrcpath} "s|@PREFIX@|${prefix}|g" \
            README.md \
            ${subport}.1 \
            [string map {- _} ${subport}]/options.py
}

post-destroot {
    foreach f ${symlinks} {
        xinstall -d ${destroot}${prefix}/[file dirname ${f}]
        ln -s ${python.prefix}/${f} ${destroot}${prefix}/${f}
    }

    set docdir ${prefix}/share/doc/${subport}
    xinstall -d ${destroot}${docdir}
    xinstall -m 0644 -W ${worksrcpath} LICENSE README.md ${destroot}${docdir}
}

variant ffmpeg description {Add ffmpeg dependency, used to extract audio} {
    depends_run-append      path:bin/AtomicParsley:AtomicParsley \
                            path:bin/ffmpeg:ffmpeg
}

default_variants    +ffmpeg

variant python36 conflicts python37 python38 python39 description {Use Python 3.6} {}
variant python37 conflicts python36 python38 python39 description {Use Python 3.7} {}
variant python38 conflicts python36 python37 python39 description {Use Python 3.8} {}
variant python39 conflicts python36 python37 python38 description {Use Python 3.9} {}

if {![variant_isset python36] && ![variant_isset python37] && ![variant_isset python38] && ![variant_isset python39]} {
    default_variants +python39
}

if {[variant_isset python36]} {
    python.default_version  36
} elseif {[variant_isset python37]} {
    python.default_version  37
} elseif {[variant_isset python38]} {
    python.default_version  38
} elseif {[variant_isset python39]} {
    python.default_version  39
}

# WARNING: hlsnative has detected features it does not support, extraction will be delegated to ffmpeg
depends_lib-append          port:py${python.version}-pycryptodome
