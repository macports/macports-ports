PortSystem          1.0
PortGroup           github 1.0

github.setup        prometheus node_exporter 1.0.1 v
github.tarball_from archive

description         Machine-metric exporter for the Prometheus monitoring \
                    system.

long_description    The Prometheus Node Exporter can expose metrics that \
                    Prometheus can scrape, including a wide variety of \
                    system, hardware- and kernel-related metrics.

homepage            https://prometheus.io/docs/guides/node-exporter

platforms           darwin
categories          net
license             apache

maintainers         {gmail.com:herby.gillot @herbygillot} \
                    openmaintainer

depends_build       port:promu \
                    port:go

build.env           GOPATH=${workpath} \
                    PATH=$env(PATH):${workpath}/bin

build.target        build

use_configure       no
installs_libs       no
use_parallel_build  no

checksums \
  rmd160    c1bf34bc93f44db6fefc47a0fac86cd03de5b049 \
  sha256    a841bf3e236376840be9e1d8e6c4a38196be6f3957b0982d1c7970a5e416b0ad \
  size      2792033

set svc_name        prometheus-node-exporter
set prom_user       prometheus
set ne_doc_dir      ${prefix}/share/doc/${svc_name}
set ne_share_dir    ${prefix}/share/${svc_name}
set ne_log_dir      ${prefix}/var/log/${svc_name}
set ne_log_file     ${ne_log_dir}/${name}.log

add_users           ${prom_user} \
                    group=${prom_user} \
                    realname=Prometheus

# promu will fail if it cannot capture the current user using the USER
# environment variable.  So if USER is not currently set in the environment,
# we'll set it here for purposes of the build.
if {! [info exists env(USER)]} {
    build.env-append "USER=${macportsuser}"
}

post-extract {
    # Install promu
    xinstall -d ${workpath}/bin
    ln -s ${prefix}/bin/promu ${workpath}/bin/

    copy  ${filespath}/org.macports.${name}.plist \
          ${workpath}/org.macports.${name}.plist

    reinplace "s|@NAME@|${name}|g" \
        ${workpath}/org.macports.${name}.plist

    reinplace "s|@USER@|${prom_user}|g" \
        ${workpath}/org.macports.${name}.plist

    reinplace "s|@GROUP@|${prom_user}|g" \
        ${workpath}/org.macports.${name}.plist

    reinplace "s|@PREFIX@|${prefix}|g" \
        ${workpath}/org.macports.${name}.plist

    reinplace "s|@LOGFILE@|${ne_log_file}|g" \
        ${workpath}/org.macports.${name}.plist

}

destroot {
    xinstall -m 755 ${worksrcpath}/${name} ${destroot}${prefix}/bin/${name}

    xinstall -d -m 755 ${destroot}${ne_doc_dir}
    xinstall -d -m 755 ${destroot}${ne_share_dir}
    xinstall -d -m 755 -o ${prom_user} -g ${prom_user} ${destroot}${ne_log_dir}

    touch ${destroot}${ne_log_file}

    file attributes ${destroot}${ne_log_file} \
        -owner ${prom_user} -group ${prom_user}

    foreach _dir {examples text_collector_examples} {
        copy ${worksrcpath}/${_dir} ${destroot}${ne_share_dir}/
    }

    copy {*}[glob ${worksrcpath}/docs/*] ${destroot}${ne_doc_dir}/

    xinstall -d -m 755 \
        ${destroot}${prefix}/etc/LaunchDaemons/org.macports.${name}

    xinstall -m 0644 -o root -W ${workpath} org.macports.${name}.plist \
        ${destroot}${prefix}/etc/LaunchDaemons/org.macports.${name}

    xinstall -d -m 755 ${destroot}/Library/LaunchDaemons

    ln -s ${prefix}/etc/LaunchDaemons/org.macports.${name}/org.macports.${name}.plist \
        ${destroot}/Library/LaunchDaemons/org.macports.${name}.plist
}

notes "
To enable the Prometheus Node Exporter service, use `port load`, as follows:

\$ sudo port load ${name}

When enabled, by default, the service will be available at http://localhost:9100

...and by default will also log to:

  ${ne_log_file}

"

github.livecheck.regex {([0-9.]+)}
