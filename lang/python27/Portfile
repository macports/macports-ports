# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           select 1.0
PortGroup           clang_dependency 1.0

name                python27
epoch               2
# Remember to keep py27-tkinter and py27-gdbm's versions sync'd with this
version             2.7.16
revision            2

set major           [lindex [split $version .] 0]
set branch          [join [lrange [split ${version} .] 0 1] .]
categories          lang
license             PSF
platforms           darwin

maintainers         {jmr @jmroot} openmaintainer

description         An interpreted, object-oriented programming language
long_description    Python is an interpreted, interactive, object-oriented \
                    programming language.

homepage            https://www.python.org/
master_sites        ${homepage}ftp/python/${version}/

distname            Python-${version}
use_xz              yes

checksums           md5 30157d85a2c0479c09ea2cbe61f2aaf5 \
                    rmd160 2e4ab325a9c9edf1687b6a5969cdafeb3cc954da \
                    sha256 f222ef602647eecb6853681156d32de4450a2c39f4de93bd5b20235f2e660ed7

patchfiles          patch-Makefile.pre.in.diff \
                    patch-setup.py.diff \
                    patch-Lib-cgi.py.diff \
                    patch-Lib-ctypes-macholib-dyld.py.diff \
                    patch-configure.diff \
                    patch-libedit.diff \
                    enable-loadable-sqlite-extensions.patch \
                    lchmod.patch

depends_build       port:pkgconfig
depends_lib         port:bzip2 \
                    port:db48 \
                    port:expat \
                    port:gettext \
                    port:libedit \
                    port:libffi \
                    port:ncurses \
                    path:lib/libssl.dylib:openssl \
                    port:sqlite3 \
                    port:zlib
depends_run         port:python_select \
                    port:python2_select

set my_prefix ${prefix}
set my_applications ${applications_dir}
set my_frameworks   ${frameworks_dir}

# This port is used by clang-3.4 to bootstrap libcxx
subport ${name}-bootstrap {
    set my_prefix ${prefix}/libexec/libcxx-bootstrap
    configure.pre_args  --prefix=${my_prefix}
    set my_frameworks      ${my_prefix}/Library/Frameworks
    set my_applications    ${my_prefix}/Applications
    configure.cppflags-prepend   -I${my_prefix}/include
    configure.ldflags-prepend    -L${my_prefix}/lib
    configure.env-append    PATH=${my_prefix}/bin:$::env(PATH)
    configure.cxx_stdlib
    compiler.whitelist      clang llvm-gcc-4.2 gcc-4.2 apple-gcc-4.2
    patchfiles-delete       patch-libedit.diff
    depends_build-replace   port:pkgconfig port:pkgconfig-bootstrap
    depends_lib-replace     port:gettext port:gettext-bootstrap \
                            port:ncurses port:ncurses-bootstrap
    depends_lib-delete      port:db48 \
                            port:libedit \
                            port:sqlite3
    # no need to be 'port select'able
    depends_run
    use_xz              no
    depends_extract     port:xz-bootstrap
    extract.suffix      .tar.xz
    extract.cmd         ${my_prefix}/bin/xz
}
# Also needed by later clangs.
if {${os.platform} eq "darwin" && ${os.major} < 11 && ${cxx_stdlib} eq "libc++"} {
    clang_dependency.extra_versions 3.7
}

configure.args      --enable-framework=${my_frameworks} \
                    --enable-ipv6 \
                    --with-system-expat \
                    --with-system-ffi

configure.cppflags-append -I${my_prefix}/include/db48
configure.ldflags-append -L${my_prefix}/lib/db48

configure.ccache    no

post-patch {
    reinplace "s|@@PREFIX@@|${my_prefix}|g" \
        ${worksrcpath}/Lib/cgi.py \
        ${worksrcpath}/Lib/ctypes/macholib/dyld.py
    reinplace "s|@@APPLICATIONS_DIR@@|${my_applications}|" \
        ${worksrcpath}/configure
}

post-configure {
    reinplace "s;/* #undef PY_FORMAT_LONG_LONG */;#define PY_FORMAT_LONG_LONG \"ll\";" ${worksrcpath}/pyconfig.h
}

build.target        all

test.run            yes
test.target         test

destroot.target     frameworkinstall maninstall

if {$subport eq $name} {
set pythonNoDot python[string map {. {}} $branch]
select.entries      [list python python-$pythonNoDot $pythonNoDot] \
                    [list python2 python2-$pythonNoDot $pythonNoDot]

notes "
To make this the default Python or Python 2 (i.e., the version run by\
the 'python' or 'python2' commands), run one or both of:

    sudo port select --set python $pythonNoDot
    sudo port select --set python2 $pythonNoDot
"
}

platform darwin {
    set framewpath ${my_frameworks}/Python.framework
    set framewdir  ${framewpath}/Versions/${branch}

    post-patch {
        if {![file exists /usr/lib/libSystemStubs.a]} {
            reinplace s/-lSystemStubs//g ${worksrcpath}/configure
        }
    }

    post-build {
        set buildlibdir [lindex [glob -directory ${worksrcpath}/build lib.*-*-*-${branch}] 0]
        # change LINKFORSHARED to support dependents that incorrectly use it
        system -W ${buildlibdir} "awk -F : \
            \"/'LINKFORSHARED'/ {printf \\\"%s: '-L${framewdir}/lib/python${branch}/config\
            -lpython${branch} -ldl -framework CoreFoundation',\\n\\\", \\\$1; next} {print}\"\
            _sysconfigdata.py > _sysconfigdata.py.new"
        file rename -force ${buildlibdir}/_sysconfigdata.py.new \
            ${buildlibdir}/_sysconfigdata.py

        # remove -arch flags from the config
        reinplace -E {s|-arch [a-z0-9_]+||g} \
            ${buildlibdir}/_sysconfigdata.py
    }

    post-destroot {
        foreach dir { Headers Resources Python Versions/Current } {
            file delete ${destroot}${framewpath}/${dir}
        }

        xinstall -d ${destroot}${my_prefix}/share/man/man1 \
                    ${destroot}${my_prefix}/lib/pkgconfig
        
        ln -s ${framewdir}/share/man/man1/python${branch}.1 ${destroot}${my_prefix}/share/man/man1/
        ln -s ${framewdir}/Python ${destroot}${my_prefix}/lib/libpython${branch}.dylib
        ln -s ${framewdir}/lib/pkgconfig/python-${branch}.pc ${destroot}${my_prefix}/lib/pkgconfig/

        # Without this, LINKFORSHARED is set to
        # ... $(PYTHONFRAMEWORKDIR)/Versions/$(VERSION)/$(PYTHONFRAMEWORK)
        # (this becomes Python.framework/Versions/2.7/Python) which doesn't
        # quite work (see ticket #15099); instead we mirror the behavior of
        # `python-config --ldflags` here.
        reinplace {s|^\(LINKFORSHARED=\).*$|\1 -L$(LIBPL) -lpython$(VERSION) $(LIBS) $(SYSLIBS)|} \
            ${destroot}${framewdir}/lib/python${branch}/config/Makefile

        # remove -arch flags from the config
        reinplace -E {s|-arch [a-z0-9_]+||g} \
            ${destroot}${framewdir}/lib/python${branch}/config/Makefile
    }
}

post-destroot {
    foreach unversionedFile {2to3 2to3-2 idle idle2 pydoc pydoc2 python python2 python-config python2-config pythonw pythonw2 smtpd.py smtpd2.py} {
        delete ${destroot}${my_prefix}/bin/${unversionedFile}
    }
}

variant universal {
    patchfiles-append patch-configure-universal.diff
    post-patch {
        reinplace \
            "s|__UNIVERSAL_ARCHFLAGS__|${configure.universal_cflags}|" \
            ${worksrcpath}/configure
    }
    if {${configure.sdkroot} ne ""} {
        configure.args-append   --enable-universalsdk=${configure.sdkroot}
    } else {
        configure.args-append   --enable-universalsdk=/
    }
}

variant ucs4 description {Enable support for UCS4 (breaks ABI)} {
    configure.args-append   --enable-unicode=ucs4
    notes-append "The ucs4 variant changes Python's ABI, breaking binary\
    compatibility with extension modules. It is strongly recommended to\
    configure MacPorts to build all ports from source when using it. Adding\
    or removing this variant on an existing installation will mean that all\
    ports that install Python extension modules need to be reinstalled. This\
    variant also breaks compatibility with native Mac frameworks used by\
    modules such as PyObjC."
}

variant optimizations description {Compile with LTO and PGO. Build time greatly increased} {
    configure.args-append   --enable-optimizations
}

livecheck.type          regex
livecheck.url           ${homepage}downloads/
livecheck.regex         Python (${branch}(?:\\.\\d+)*)
