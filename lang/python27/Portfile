# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0
PortGroup           select 1.0

name                python27
epoch               2
# Remember to keep py27-tkinter and py27-gdbm's versions sync'd with this
version             2.7.5
revision            1

set major           [lindex [split $version .] 0]
set branch          [join [lrange [split ${version} .] 0 1] .]
categories          lang
license             PSF
platforms           darwin

maintainers         jwa openmaintainer

description         An interpreted, object-oriented programming language
long_description    Python is an interpreted, interactive, object-oriented \
                    programming language.

homepage            http://www.python.org/
master_sites        ${homepage}ftp/python/${version}/

distname            Python-${version}
use_xz				yes

checksums           md5 5eea8462f69ab1369d32f9c4cd6272ab \
                    rmd160 baa7ad7d82cb4dc0a70b6a7aded43b7b78b0067e \
                    sha256 f33c4cab167dc69e10962e1cebf1c0768e2d0e8575648130c20e6bda84551db1

patchfiles          patch-Makefile.pre.in.diff \
                    patch-setup.py.diff \
                    patch-setup.py-disabled_modules.diff \
                    patch-Lib-cgi.py.diff \
                    patch-Lib-ctypes-macholib-dyld.py.diff \
                    patch-configure.diff \
                    patch-libedit.diff

depends_lib         port:gettext \
                    port:zlib \
                    port:openssl \
                    port:sqlite3 \
                    port:db46 \
                    port:ncurses \
                    port:bzip2 \
                    port:libedit
depends_run         port:python_select

configure.args      --enable-framework=${frameworks_dir} \
                    --enable-ipv6

configure.cppflags-append -I${prefix}/include/db46
configure.ldflags-append -L${prefix}/lib/db46

configure.ccache    no

# signed integer overflow is normally undefined as per the C spec, but python
# needs it to be defined as twos-complement
configure.cflags-append -fwrapv

post-patch {
    reinplace "s|@@PREFIX@@|${prefix}|g" \
        ${worksrcpath}/Lib/cgi.py \
        ${worksrcpath}/Lib/ctypes/macholib/dyld.py
    reinplace "s|@@APPLICATIONS_DIR@@|${applications_dir}|" \
        ${worksrcpath}/configure

    # See https://trac.macports.org/changeset/37861
    reinplace "s|xargs -0 rm -r|/usr/bin/xargs -0 /bin/rm -r|g" \
        ${worksrcpath}/Mac/PythonLauncher/Makefile.in
}

build.target        all

test.run            yes
test.target         test

destroot.target     frameworkinstall maninstall

# ensure that correct compiler is used
build.args-append   MAKE="${build.cmd} CC=${configure.cc}"
destroot.args-append MAKE="${destroot.cmd} CC=${configure.cc}"

select.group        python
select.file         ${filespath}/python[string map {. {}} ${branch}]

notes "
To make python ${branch} the default (i.e. the version you get when you run\
'python'), please run:

sudo port select --set ${select.group} [file tail ${select.file}]
"

post-destroot {
    set framewpath ${frameworks_dir}/Python.framework
    set framewdir  ${framewpath}/Versions/${branch}

    foreach dir { Headers Resources Python Versions/Current } {
        file delete ${destroot}${framewpath}/${dir}
    }

    ln -s ${framewdir}/share/man/man1/python${branch}.1 ${destroot}${prefix}/share/man/man1/
    ln -s ${framewdir}/Python ${destroot}${prefix}/lib/libpython${branch}.dylib

    foreach unversionedFile {2to3 2to3-2 idle idle2 pydoc pydoc2 python python2 python-config python2-config pythonw pythonw2 smtpd.py smtpd2.py} {
        delete ${destroot}${prefix}/bin/${unversionedFile}
    }

    # remove -arch flags from the config
    reinplace -E {s|-arch [a-z0-9_]+||g} \
        ${destroot}${framewdir}/lib/python${branch}/config/Makefile \
        ${destroot}${framewdir}/lib/python${branch}/_sysconfigdata.py
}

platform darwin {
    post-patch {
		if {![file exists /usr/lib/libSystemStubs.a]} {
			reinplace s/-lSystemStubs//g ${worksrcpath}/configure
		}
    }
}

variant universal {
    # This patch will need updating when 2.7.6 is released, as ARCH_RUN_32BIT
    # will once again be used for something. See ticket #39270.
    patchfiles-append patch-configure-universal.diff
    post-patch {
        reinplace \
            "s|__UNIVERSAL_ARCHFLAGS__|${configure.universal_cflags}|" \
            ${worksrcpath}/configure
    }
    if {${configure.sdkroot} != ""} {
        configure.args-append   --enable-universalsdk=${configure.sdkroot}
    } else {
        configure.args-append   --enable-universalsdk=/
    }
}

variant ucs4 description {Enable support for UCS4} {
    configure.args-append   --enable-unicode=ucs4
}

livecheck.type          regex
livecheck.url           ${homepage}download/releases/
livecheck.regex         (${branch}(?:\\.\\d+)*)
