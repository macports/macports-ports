# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

name                lua
conflicts           lua50
version             5.3.6
revision            0
categories          lang
license             MIT
platforms           darwin linux
maintainers         nomaintainer
description         powerful, lightweight programming language
long_description    Lua is a powerful, light-weight programming language designed for \
                    extending applications. Lua is also frequently used as a general-purpose, \
                    standalone language.

homepage            https://www.lua.org/
master_sites        ${homepage}ftp/

checksums           rmd160  cc715df991bccaec75a022404902200d984b002e \
                    sha256  fc5fd69bb8736323f026672b1b7235da613d7177e72558893a0bdcd320466d60 \
                    size    303770

patchfiles          patch-Makefile.diff \
                    patch-src-Makefile.diff \
                    patch-src-luaconf.h.diff

# TODO: also add lua50 here
subport             lua52 {
    version         5.2.4
    revision        4
    checksums       rmd160  3bad2da85f354132da6a53b45882006f0a451b5c \
                    sha256  b9e2e4aad6789b3b63a056d442f7b39f0ecfca3ae0f1fc0ae4e9614401b69f4b \
                    size    252651

    patchfiles      patch-Makefile-52.diff \
                    patch-src-Makefile-52.diff \
                    patch-src-luaconf.h-52.diff

    # this is needed only on lua52, to fix build in 10.4
    # this patch is already in lua53+ and works on all tested systems
    patchfiles-append \
        patch-src-liolib-tiger.diff

}

subport             lua51 {
    version         5.1.5
    revision        6
    checksums       rmd160  b0c2234675335a8bf158a125b6778cf800c754a0 \
                    sha256  2640fc56a795f29d28ef15e13c34a47e223960b0240e8cb0a82d9b0738695333 \
                    size    221213

    patchfiles      patch-Makefile-51.diff \
                    patch-src-Makefile-51.diff \
                    patch-src-luaconf.h-51.diff
}

set branch          [join [lrange [split ${version} .] 0 1] .]

depends_lib         port:readline
build.target        macosx

platform linux {
    # With lua 5.4, replace with linux-readline
    build.target    linux
}

test.run            yes
test.env            DYLD_LIBRARY_PATH=./lib

post-extract {
    xinstall -m 0644 -W ${filespath} COPYRIGHT ${worksrcpath}

    if {$subport eq $name} {
        xinstall -m 0644 -W ${filespath} lua.pc ${worksrcpath}
    } else {
        xinstall -m 0644 ${filespath}/lua-branched.pc ${worksrcpath}/lua.pc
    }
}

post-patch {
    reinplace "s|@PLATFORM@|${os.platform}|g" ${worksrcpath}/Makefile ${worksrcpath}/src/Makefile
    reinplace "s|@PREFIX@|${prefix}|g"   ${worksrcpath}/lua.pc ${worksrcpath}/Makefile ${worksrcpath}/src/Makefile ${worksrcpath}/src/luaconf.h
    reinplace "s|@VERSION@|${version}|g" ${worksrcpath}/lua.pc ${worksrcpath}/src/Makefile
    reinplace "s|@BRANCH@|${branch}|g"   ${worksrcpath}/lua.pc ${worksrcpath}/Makefile ${worksrcpath}/src/Makefile

    # See: https://trac.macports.org/ticket/53708
    if {$subport eq $name} {
        reinplace "s|@VERSION@|${version}|g" ${worksrcpath}/Makefile
    }
}

configure {
    reinplace -E "/^MYLDFLAGS=/s|\$| ${configure.ldflags} [get_canonical_archflags]|" ${worksrcpath}/src/Makefile
    reinplace -E "/^CFLAGS=/s|\$| ${configure.cflags} ${configure.cppflags} [get_canonical_archflags]|" ${worksrcpath}/src/Makefile
    reinplace "s|CC= .*\$|CC= ${configure.cc}|" ${worksrcpath}/src/Makefile
}

use_parallel_build  no

destroot.target install INSTALL_TOP=${destroot}${prefix}
post-destroot {
    # documentation
    set docdir ${prefix}/share/doc/${subport}
    xinstall -d -m 0755 ${destroot}${docdir}/html
    xinstall -m 0644 -W ${worksrcpath} COPYRIGHT README ${destroot}${docdir}
    foreach html [glob ${worksrcpath}/doc/*.html ${worksrcpath}/doc/*.css ${worksrcpath}/doc/*.gif ${worksrcpath}/doc/*.png] {
        xinstall -m 0644 ${html} ${destroot}${docdir}/html
    }

    if {$subport eq $name} {
        xinstall -W ${worksrcpath} -m 0644 lua.pc ${destroot}${prefix}/lib/pkgconfig/
    } else {
        #  version executable and man pages files
        foreach f [glob ${destroot}${prefix}/bin/*] {
            move $f ${f}${branch}
        }
        foreach f [glob ${destroot}${prefix}/share/man/man1/*] {
            # Executables are in the form luaBRANCH rather than lua-BRANCH
            move $f [file rootname $f]${branch}[file extension $f]
        }

        #  install versioned pkgconfig file
        xinstall -d -m 0755 ${destroot}${prefix}/lib/pkgconfig
        xinstall -m 0644 ${worksrcpath}/lua.pc ${destroot}${prefix}/lib/pkgconfig/lua-${branch}.pc
    }
}

livecheck.type  regex
livecheck.url   ${master_sites}
# Main lua port livecheck shouldn't be limited by the branch
if {$subport eq $name} {
    livecheck.regex  {lua-(\d+(?:\.\d+)*)}
} else {
    livecheck.regex  lua-(${branch}\.\[0-9]+)
}
