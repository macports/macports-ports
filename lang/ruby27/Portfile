# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           select 1.0
PortGroup           openssl 1.0

name                ruby27
version             2.7.7
revision            1

categories          lang ruby
maintainers         {kimuraw @kimuraw} \
                    {fwright.net:fw @fhgwright} \
                    openmaintainer
platforms           darwin

description         Powerful and clean object-oriented scripting language
long_description    Ruby is the interpreted scripting language for quick \
                    and easy object-oriented programming. It has many \
                    features to process text files and to do system \
                    management tasks (as in Perl). It is simple, \
                    straight-forward, extensible, and portable.

homepage            https://www.ruby-lang.org/
license             {Ruby BSD}

master_sites        ruby:2.7
use_bzip2           yes
distname            ruby-${version}
dist_subdir         ruby27

checksums           md5 63c55e75150251d3ba092b662138e4af \
                    rmd160 6a4fdf0292259124e6a6a51ddd8b8aa1cbdc0615 \
                    sha1 dfcd86b459a9d4bbdf2d4eb82ad3476cb9820892 \
                    sha256 cf800820c9e69cdd31a8cdab920391f74ed935db2397a905afabd48961913658 \
                    size 14850886

# ruby/openssl does not support openssl-3
openssl.branch      1.1

depends_lib         port:readline \
                    port:zlib \
                    port:libyaml \
                    port:libffi \
                    port:gdbm
depends_run         port:ruby_select
depends_build       port:pkgconfig
depends_skip_archcheck pkgconfig

select.group        ruby
select.file         ${filespath}/ruby27

configure.args      --enable-shared \
                    --enable-install-static-library \
                    --disable-install-doc \
                    --mandir="${prefix}/share/man" \
                    --enable-pthread \
                    --without-gmp \
                    --with-opt-dir="${prefix}" \
                    --program-suffix=2.7 \
                    --with-rubylibprefix="${prefix}/lib/ruby2.7" \
                    --with-openssl-dir=[openssl::install_area] \
                    --without-baseruby

# Patches derived from MacPorts-enhanced GitHub fork at
#   github.com/fhgwright/ruby
# These replace and enhance the former topic-specific patch, and can be
# seen more readably in the fork.

# patch-sources.diff: fixes for various build problems.
# This diff is from v2_7_7 vs. macports-2_7_7r1.
#
patchfiles-append   patch-sources.diff

# patch-generated.diff: additional patches to generated files, which don't
# exist in the upstream git sources, and which must be patched after the
# source patches so that make doesn't regard them as outdated.
# At present, the only patched generated file is 'configure', to avoid
# the need to rerun autoconf, whose output has reproducibility issues.
# This diff is from tarball-2_7_7 vs. macports-tarball-2_7_7r1.
#
patchfiles-append   patch-generated.diff

# Using a patch instead of reinplace for the 'gem' versioning fix
# would also be more readable and more trackable.  A commit version of this
# already exists in the fork, in the macports-reinplace-2_7 branch.

# [NOTE] workaround for mismatch of sdk versions on macOS 11.x,
# such as MacOSX11.0.sdk (buildbot) <=> MacOSX11.1.sdk (user's Mac).
#
# - ruby27 @2.7.2: pre-built archive for Big Sur contains hardcoded path to 11.0 SDK
#   https://trac.macports.org/ticket/61899
# - rb27-nokogiri @1.10.4 : Failed to compile on Big Sur (Apple Silicon)
#   https://trac.macports.org/ticket/61827
post-build {
    if {${os.major} >= 20} {
        # ruby saves its compile options (CFLAGS, LDFLAGS, ..) in "rbconfig.rb" at build,
        # and uses the options when compiling binary extensions.

        # rewrite MacOSX11.1.sdk -> MacOSX.sdk in rbconfig.rb
        file copy ${worksrcpath}/rbconfig.rb ${worksrcpath}/rbconfig.rb.orig
        reinplace -E -q {s|(/MacOSX)[0-9\.]+(\.sdk[\'\"]?[[:blank:]])|\1\2|g} \
        ${worksrcpath}/rbconfig.rb \
            ${worksrcpath}/ruby-2.7.pc
        reinplace -E -q {s|(/MacOSX)[0-9\.]+(\.sdk[\'\"]?)$|\1\2|g} \
            ${worksrcpath}/rbconfig.rb \
            ${worksrcpath}/ruby-2.7.pc
    }
}

# Add the architecture flag as required
if {[info exists build_arch] && ${build_arch} != ""} {
    configure.args-append "--with-arch=${build_arch}"
}

post-patch {
    # rewrite "gem" to "gem2.7"
    # def gem_command
    #   ENV["GEM_COMMAND"] ? ENV["GEM_COMMAND"] : "gem"
    # end
    reinplace -E "s/(\"GEM_COMMAND\". : )(\"gem\")/\\1\"gem2.7\"/g" \
        ${worksrcpath}/lib/bundler/gem_helper.rb

    # Ensure that generated files have clearly later mtimes than sources,
    # to avoid gratuitous rebuilds by make.
    # This is already nominally true based on the patching order, but
    # one-second resolution on timestamps may cover it up.
    #
    # Increment the mtime(s) by one second.
    foreach genfile [list configure] {
        set genfull ${worksrcpath}/${genfile}
        file mtime ${genfull} [expr [file mtime ${genfull}] + 1]
    }
}

# KNOWN BUG:  If a previous install of ruby27 is already active, destroot
# fails in trace mode, apparently due to a blocked access to the *installed*
# library, though this is not listed in the trace-mode report.  This bug
# goes back at least as far as 2.7.4_3 (earlier versions are harder to
# test due to the openssl issue).

post-destroot {
    foreach type {site vendor} {
        set libdir ${destroot}${prefix}/lib/ruby2.7/${type}_ruby/2.7.0
        xinstall -m 0755 -d ${libdir}
        foreach subdir [exec find ${libdir} -type d -empty] {
            destroot.keepdirs-append ${subdir}
        }
    }

    # install destination of commands from port:rb27-*
    xinstall -m 0755 -d ${destroot}${prefix}/libexec/ruby2.7
    destroot.keepdirs-append ${destroot}${prefix}/libexec/ruby2.7
}

notes-append "
To make this the default Ruby (i.e., the version run by\
the 'ruby', 'gem' or 'bundle' commands), run:
    sudo port select --set ruby $name
"

variant doc description "Install rdoc indexes and C API documents" {
        configure.args-delete   --disable-install-doc
}

variant gmp description "use gmp" {
        configure.args-delete   --without-gmp
        depends_lib-append      port:gmp
}

variant jemalloc description "use jemalloc" {
        configure.args-delete   --without-jemalloc
        configure.args-append   --with-jemalloc
        depends_lib-append      port:jemalloc
}

variant universal {
        # Disable parallel builds for Universal case
        # See: https://trac.macports.org/ticket/24240
        use_parallel_build      no

        # use ruby built-in universal mechanism.
        configure.args-append   --with-arch=[join ${universal_archs} ,]
        # clear macports' universal flags
        configure.universal_cflags
        configure.universal_cppflags
        configure.universal_cxxflags
        configure.universal_ldflags
}

variant relative description "Enable relative loading of libraries to allow for relocation of binaries." {
        #enable relative loading
        configure.args-append  --enable-load-relative
}

# legacy systems support
platform darwin {

    if {${os.major} < 11} {
        depends_build-append    port:gmake
        build.cmd               ${prefix}/bin/gmake
        configure.args-append   --disable-dtrace
        configure.cflags-append -std=c99
    }

    if {${os.major} == 8} {
        configure.cppflags-append -DCPU_SUBTYPE_MASK=0xff000000
    }
}

test.run        yes
test.target     check

livecheck.type  regex
livecheck.url   https://cache.ruby-lang.org/pub/ruby/2.7/
livecheck.regex ruby-(2\\.7\\.\\d+)[quotemeta ${extract.suffix}]
