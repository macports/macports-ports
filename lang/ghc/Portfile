# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           gpg_verify 1.0

name                ghc
version             8.10.1
revision            0
categories          lang haskell
maintainers         {ieee.org:s.t.smith @essandess} openmaintainer
license             BSD
platforms           darwin
supported_archs     x86_64

description The Glorious Glasgow Haskell Compilation System
long_description    \
        The Glasgow Haskell Compiler is a robust,       \
        fully-featured, optimising compiler and interactive \
        environment for Haskell 98, GHC compiles Haskell to \
        either native code or C.  It implements numerous    \
        experimental language extensions to Haskell 98,     \
        for example: concurrency, a foreign language interface, \
        multi-parameter type classes, scoped type variables,    \
        existential and universal quantification, unboxed   \
        types, exceptions, weak pointers, and so on.        \
        GHC comes with a generational garbage collector,    \
        and a space and time profiler.

homepage            http://haskell.org/${name}

# Bootstrapped ghc is the default build
variant prebuilt \
    description {Do not bootstrap ghc; install the pre-built binary.} {}

# https://www.haskell.org/ghcup/
# https://gitlab.haskell.org/haskell/ghcup/blob/master/README.md#manual-install
use_xz              yes
master_sites        https://downloads.haskell.org/~${name}/${version}
distfiles           ${distname}-x86_64-apple-darwin${extract.suffix} \
                    ${distname}-testsuite${extract.suffix}

checksums           ${distname}-x86_64-apple-darwin${extract.suffix} \
                    rmd160  5798ed5082418e20603ea022228a3a1015253848 \
                    sha256  65b1ca361093de4804a7e40b3e68178e1ef720f84f743641ec8d95e56a45b3a8 \
                    size    192889416 \
                    ${distname}-testsuite${extract.suffix} \
                    rmd160  225d1aba26458102330a730b96ae4500cf00d0bc \
                    sha256  37cf67b04bcf00ae78914e4612db9d959bc5bd455a27ea5f6461100137c0b800 \
                    size    2091988

gpg_verify.use_gpg_verification \
                    yes

if {[option gpg_verify.use_gpg_verification]} {
    distfiles-append \
                    ${distname}-x86_64-apple-darwin${extract.suffix}.sig \
                    ${distname}-testsuite${extract.suffix}.sig
    checksums-append \
                    ${distname}-x86_64-apple-darwin${extract.suffix}.sig \
                    size    586 \
                    ${distname}-testsuite${extract.suffix}.sig \
                    size    586

    post-checksum {
        # check GPG signature: https://www.haskell.org/ghc/download_ghc_8_6_5.html
        set gpg_keyid 97db64ad

        gpg_verify.verify_gpg_signature \
            ${filespath}/keyid-${gpg_keyid}.txt \
            ${distpath}/${distname}-x86_64-apple-darwin${extract.suffix}.sig \
            ${distpath}/${distname}-x86_64-apple-darwin${extract.suffix}

        gpg_verify.verify_gpg_signature \
            ${filespath}/keyid-${gpg_keyid}.txt \
            ${distpath}/${distname}-testsuite${extract.suffix}.sig \
            ${distpath}/${distname}-testsuite${extract.suffix}
    }
}

extract.only        ${distname}-x86_64-apple-darwin${extract.suffix} \
                    ${distname}-testsuite${extract.suffix}

# use these to specify python versions, python3 required
set python3_version 3.8
set python3_version_nickname \
                    [join [lrange [split ${python3_version} .] 0 1] {}]

set srcpath ${workpath}/src

# Note: ghc has DESTDIR; however, specifying --prefix=${prefix} installs
# binaries into ${prefix}, not DESTDIR.  Work around this by setting
# --prefix=${destroot}${prefix} and not setting DESTDIR

if { [variant_isset "prebuilt"] } {
    set bootstrap_dir ${destroot}${prefix}
} else {
    set bootstrap_dir ${workpath}/bootstrap

    distfiles-append \
                    ${distname}-src${extract.suffix}
    checksums-append \
                    ${distname}-src${extract.suffix} \
                    rmd160  99cd5da02c02364bdfb9b171f14012bea32b843a \
                    sha256  4e3b07f83a266b3198310f19f71e371ebce97c769b14f0d688f4cbf2a2a1edf5 \
                    size    19781652

    depends_build-append \
                    port:alex \
                    port:autoconf \
                    port:automake \
                    port:happy \
                    port:HsColour \
                    port:python${python3_version_nickname} \
                    port:py${python3_version_nickname}-sphinx \
                    port:texlive \
                    port:texlive-fonts-extra \
                    port:texlive-fonts-recommended \
                    port:texlive-latex-extra

    pre-configure {
        xinstall -m 0755 -d ${bootstrap_dir}
    }

    # add the bootstrap binary and the sphinx-build binary to PATH
    configure.env-append \
        GHC=${bootstrap_dir}/bin/ghc \
        "PATH=$env(PATH):${bootstrap_dir}/bin:${frameworks_dir}/Python.framework/Versions/${python3_version}/bin"

    # overwrite `build.env`
    build.env \
        "PATH=$env(PATH):${bootstrap_dir}/bin:${frameworks_dir}/Python.framework/Versions/${python3_version}/bin"

    pre-build {
        # test: move extracted testsuite directory to src
        xinstall -m 0755 -d ${srcpath}/${distname}
        move        ${test.dir} ${srcpath}/${distname}/testsuite
        test.dir    ${srcpath}/${distname}/testsuite
    }

    post-build {
        # bootstrap build/destroot from ${distname}-src${extract.suffix}

        use_parallel_build yes

        # extract
        extract.dir  ${srcpath}
        extract.only ${distname}-src${extract.suffix}
        portextract::extract_main

        # fix DejaVu texlive fonts names, e.g. "\setmonofont{DejaVu Sans Mono}"
        foreach f "\
            ${srcpath}/${distname}/libraries/Cabal/Cabal/doc/conf.py \
            ${srcpath}/${distname}/docs/users_guide/conf.py \
            " {
            reinplace -E "s|(\{DejaVu\[\[:alnum:\]_\]*)\[\[:space:\]\]+|\\1|g" \
                ${f}
            reinplace -E "s|(\{DejaVu\[\[:alnum:\]_\]*)\[\[:space:\]\]+|\\1|g" \
                ${f}
            reinplace -E "s|\{(DejaVu\[\[:alnum:\]_\]*)\}|\{\\1.ttf\}|g" \
                ${f}
        }
        # fix python3 executable name
        foreach f "\
            ${srcpath}/${distname}/boot \
            ${srcpath}/${distname}/utils/checkUniques/check-uniques.py \
            " {
            reinplace -E "s|^(#!/usr/bin/env python)3|\\1${python3_version}|" \
                ${f}
        }

        # see https://trac.macports.org/wiki/UsingTheRightCompiler
        configure.env-append CC=${configure.cc}
        build.env-append     CC=${configure.cc} \
                             CXX=${configure.cxx}
        system -W ${srcpath}/${distname} "patch -p0 < ${filespath}/patch-gmp-ghc.mk.diff"

        system -W ${srcpath}/${distname} \
            "export PATH=$env(PATH):${bootstrap_dir}/bin:${frameworks_dir}/Python.framework/Versions/${python3_version}/bin ;\
            ./boot"

        # configure
        # install the final compiler to destroot
        configure.pre_args \
            --prefix=${destroot}${prefix}
        configure.dir ${srcpath}/${distname}
        system -W ${srcpath}/${distname} \
            "export ${configure.env};\
            ${configure.cmd} ${configure.pre_args}"

        # build and destroot the second stage: `make && make install`
        system -W ${srcpath}/${distname} \
           "export ${build.env};\
           ${build.cmd} [portbuild::build_getjobsarg]"
        system -W ${srcpath}/${distname} \
           "export ${build.env};\
           ${build.cmd} ${destroot.pre_args}"
    }
}

# install the initial bootstrap compiler to destroot
configure.pre_args  --prefix=${bootstrap_dir}

# the PATH environment must provide the bootstrap path
# note: this command does not change the destroot PATH environment, so export
# PATH explicitly in the necessary system command below
build.env-append \
    "PATH=$env(PATH):${bootstrap_dir}/bin"

# Note: `make install` in bootstrap build cannot use a parallel build
use_parallel_build  no

build.target        install

destroot {
    xinstall -W ${filespath} -m 0644 ./ghci.conf ${destroot}${prefix}/etc
}

post-destroot {
    # delete any destroot path appearing in text files
    fs-traverse f ${destroot}${prefix} {
        if {[file isfile ${f}]} {
            if {[string match "text/*" [lindex [exec /usr/bin/file --mime-type ${f}] end]]} {
                reinplace -q "s|${destroot}||g" ${f}
            }
        }
    }
}

post-activate {
    system "${prefix}/bin/ghc-pkg recache"
}

test.run            yes
test.env-append     "PATH=$env(PATH):${bootstrap_dir}/bin"
test.dir            ${worksrcpath}/testsuite
test.target         test

notes "The GHC User Manual is available at:

    file://${prefix}/share/doc/${distname}/html/index.html
    ${prefix}/share/doc/${distname}/users_guide.pdf

Copy/edit ${prefix}/etc/ghci.conf to your directory ~/.ghc
for a user-specific startup configuration."

# livecheck broken on downloads.haskell.org
# See: https://lists.macports.org/pipermail/macports-dev/2019-December/041581.html
#livecheck.type      regex
#livecheck.url       https://downloads.haskell.org/~${name}
#livecheck.regex     (\\d+(?:\\.\\d+){1,3})
