# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           select 1.0

name                ruby26
version             2.6.8
revision            1

categories          lang ruby
maintainers         {kimuraw @kimuraw} openmaintainer
platforms           darwin

description         Powerful and clean object-oriented scripting language
long_description    Ruby is the interpreted scripting language for quick \
                    and easy object-oriented programming. It has many \
                    features to process text files and to do system \
                    management tasks (as in Perl). It is simple, \
                    straight-forward, extensible, and portable.

homepage            https://www.ruby-lang.org/
license             {Ruby BSD}

master_sites        ruby:2.6
use_bzip2           yes
distname            ruby-${version}
dist_subdir         ruby26

checksums           md5 c53761123d17e929cfe248f50429bcab \
                    rmd160 734337856050cb238e0b81280364b614ee02b56d \
                    sha1 7d38cacb6a0779f04b9f19f94406da97e95bbec4 \
                    sha256 dac96ca6df8bab5a6fc7778907f42498037f8ce05b63d20779dce3163e9fafe6

use_parallel_build  no

depends_lib         port:readline \
                    path:lib/libssl.dylib:openssl \
                    port:zlib \
                    port:libyaml \
                    port:libffi \
                    port:gdbm
depends_run         port:ruby_select
depends_build       port:pkgconfig
depends_skip_archcheck pkgconfig

select.group        ruby
select.file         ${filespath}/ruby26

configure.args      --enable-shared \
                    --enable-install-static-library \
                    --disable-install-doc \
                    --mandir="${prefix}/share/man" \
                    --enable-pthread \
                    --without-gmp \
                    --with-opt-dir="${prefix}" \
                    --program-suffix=2.6 \
                    --with-rubylibprefix="${prefix}/lib/ruby2.6"

# patch-configure_cxx11.diff: fix "invalid suffix on literal" with C++11
#                             from RUBY_ARCH and RUBY_PLATFORM in config.h
#                             https://trac.macports.org/ticket/58255
patchfiles          patch-configure_cxx11.diff
# Fix test failure at ext/fiddle
patchfiles-append   patch-test-fiddle-helper.rb.diff
# avoid build error with apple clang 12 (implicit declaration of function)
# https://bugs.ruby-lang.org/issues/17777
patchfiles-append   patch-ruby_issue_17777.diff

# [NOTE] workaround for mismatch of sdk versions on macOS 11.x,
# such as MacOSX11.0.sdk (buildbot) <=> MacOSX11.1.sdk (user's Mac).
# (see ruby27)
post-build {
    if {${os.major} >= 20} {
        # ruby saves its' compile options (CFLAGS, LDFLAGS, ..) in "rbconfig.rb" at build,
        # and uses the options when complile binary extensions.

        # rewrite MacOSX11.1.sdk -> MacOSX.sdk in rbconfig.rb
        file copy ${worksrcpath}/rbconfig.rb ${worksrcpath}/rbconfig.rb.orig
        reinplace -E -q {s|(/MacOSX)[0-9\.]+(\.sdk[\'\"]?[[:blank:]])|\1\2|g} \
		    ${worksrcpath}/rbconfig.rb \
		    ${worksrcpath}/ruby-2.6.pc
        reinplace -E -q {s|(/MacOSX)[0-9\.]+(\.sdk[\'\"]?)$|\1\2|g} \
		    ${worksrcpath}/rbconfig.rb \
		    ${worksrcpath}/ruby-2.6.pc
    }
}

# Add the architecture flag as required
if {[info exists build_arch] && ${build_arch} != ""} {
    configure.args-append "--with-arch=${build_arch}"
}

post-patch {
    # (see rb-bundler) sh "gem build ..." -> sh "gem2.6 build ..."
    reinplace -E "s/(gem) (build|install|push)/gem2.6 \\2/g" \
        ${worksrcpath}/lib/bundler/gem_helper.rb
}

post-destroot {
    foreach type {site vendor} {
        set libdir ${destroot}${prefix}/lib/ruby2.6/${type}_ruby/2.6.0
        xinstall -m 0755 -d ${libdir}
        foreach subdir [exec find ${libdir} -type d -empty] {
            destroot.keepdirs-append ${subdir}
        }
    }

    # install destination of commands from port:rb26-*
    xinstall -m 0755 -d ${destroot}${prefix}/libexec/ruby2.6
    destroot.keepdirs-append ${destroot}${prefix}/libexec/ruby2.6
}

variant doc description "Install rdoc indexes and C API documents" {
        configure.args-delete   --disable-install-doc
}

variant gmp description "use gmp" {
        configure.args-delete   --without-gmp
        depends_lib-append      port:gmp
}

variant jemalloc description "use jemalloc" {
        configure.args-delete   --without-jemalloc
        configure.args-append   --with-jemalloc
        depends_lib-append      port:jemalloc
}

variant universal {
        # use ruby built-in universal mechanism.
        configure.args-append   --with-arch=[join ${universal_archs} ,]
        # clear macports' universal flags
        configure.universal_cflags
        configure.universal_cppflags
        configure.universal_cxxflags
        configure.universal_ldflags
}

variant relative description "Enable relative loading of libraries to allow for relocation of binaries." {
        #enable relative loading
        configure.args-append  --enable-load-relative
}

# legacy systems suport
platform darwin {

    # Build requires 10.8 SDK when targeting 10.7
    # see https://trac.macports.org/ticket/57986
    if {${os.major} == 11} {
        set ten_eight_sdkpath ${developer_dir}/Platforms/MacOSX.platform/Developer/SDKs/MacOSX10.8.sdk
        if {[file exists $ten_eight_sdkpath]} {
            configure.sdkroot $ten_eight_sdkpath
        } else {
            pre-fetch {
                error "Building $name @${version} on Mac OS X 10.7 requires the MacOSX10.8.sdk to be present in ${developer_dir}/Platforms/MacOSX.platform/Developer/SDKs"
            }
        }
    }

    if {${os.major} < 11} {
        depends_build-append    port:gmake
        build.cmd               ${prefix}/bin/gmake
        configure.args-append   --disable-dtrace
        configure.cflags-append -std=c99
    }

    if {${os.major} == 8} {
        configure.cppflags-append -DCPU_SUBTYPE_MASK=0xff000000
    }
}


livecheck.type  regex
livecheck.url   https://cache.ruby-lang.org/pub/ruby/2.6/
livecheck.regex ruby-(2\\.6\\.\\d+)[quotemeta ${extract.suffix}]
