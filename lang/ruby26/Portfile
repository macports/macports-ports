# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           select 1.0
PortGroup           openssl 1.0

name                ruby26
version             2.6.9
revision            0

categories          lang ruby
maintainers         {kimuraw @kimuraw} openmaintainer
platforms           darwin

description         Powerful and clean object-oriented scripting language
long_description    Ruby is the interpreted scripting language for quick \
                    and easy object-oriented programming. It has many \
                    features to process text files and to do system \
                    management tasks (as in Perl). It is simple, \
                    straightforward, extensible, and portable.

homepage            https://www.ruby-lang.org/
license             {Ruby BSD}

master_sites        ruby:2.6
use_bzip2           yes
distname            ruby-${version}
dist_subdir         ruby26

checksums           md5 ddc24495adaf215c8ee40630b4a55041 \
                    rmd160 33f71ce3eec012bb56c56f23776b4b125d854032 \
                    sha1 a482c36645e7ff4596c6aca2cf96d15481fcfc5e \
                    sha256 a0639060c4519572e51828eb742f09dd40f154c820f6007246de7a2090e3ee45 \
                    size 14137792

# ruby/openssl does not support openssl-3
openssl.branch      1.1

depends_lib         port:readline \
                    port:zlib \
                    port:libyaml \
                    port:libffi \
                    port:gdbm
depends_run         port:ruby_select
depends_build       port:pkgconfig
depends_skip_archcheck pkgconfig

select.group        ruby
select.file         ${filespath}/ruby26

configure.args      --enable-shared \
                    --enable-install-static-library \
                    --disable-install-doc \
                    --mandir="${prefix}/share/man" \
                    --enable-pthread \
                    --without-gmp \
                    --with-opt-dir="${prefix}" \
                    --program-suffix=2.6 \
                    --with-rubylibprefix="${prefix}/lib/ruby2.6" \
                    --with-openssl-include=[openssl::include_dir] \
                    --with-openssl-lib=[openssl::lib_dir]

# patch-configure_cxx11.diff: fix "invalid suffix on literal" with C++11
#                             from RUBY_ARCH and RUBY_PLATFORM in config.h
#                             https://trac.macports.org/ticket/58255
# Note that this patches the output of autoconf, rather than the input, but
# at present, rerunning autonconf produces a broken configure.
patchfiles          patch-configure_cxx11.diff
# Fix test failure at ext/fiddle
patchfiles-append   patch-test-fiddle-helper.rb.diff
# avoid build error with apple clang 12 (implicit declaration of function)
# https://bugs.ruby-lang.org/issues/17777
patchfiles-append   patch-ruby_issue_17777.diff
# Fix build on 10.4
patchfiles-append   patch-tiger.diff
# Fix build on < 10.6
patchfiles-append   patch-osversions.diff
# Fix build with pre-C99 compilers and some locale settings
patchfiles-append   patch-c99check.diff
# Fix ext/openssl uses wrong pkgconfig (#63845)
patchfiles-append   patch-ext-openssl-extconf.rb.diff

# [NOTE] workaround for mismatch of sdk versions on macOS 11.x,
# such as MacOSX11.0.sdk (buildbot) <=> MacOSX11.1.sdk (user's Mac).
# (see ruby27)
post-build {
    if {${os.major} >= 20} {
        # ruby saves its' compile options (CFLAGS, LDFLAGS, ..) in "rbconfig.rb" at build,
        # and uses the options when complile binary extensions.

        # rewrite MacOSX11.1.sdk -> MacOSX.sdk in rbconfig.rb
        file copy ${worksrcpath}/rbconfig.rb ${worksrcpath}/rbconfig.rb.orig
        reinplace -E -q {s|(/MacOSX)[0-9\.]+(\.sdk[\'\"]?[[:blank:]])|\1\2|g} \
            ${worksrcpath}/rbconfig.rb \
            ${worksrcpath}/ruby-2.6.pc
        reinplace -E -q {s|(/MacOSX)[0-9\.]+(\.sdk[\'\"]?)$|\1\2|g} \
            ${worksrcpath}/rbconfig.rb \
            ${worksrcpath}/ruby-2.6.pc
    }
}

# Add the architecture flag as required
if {[info exists build_arch] && ${build_arch} != ""} {
    configure.args-append "--with-arch=${build_arch}"
}

post-patch {
    # (see rb-bundler) sh "gem build ..." -> sh "gem2.6 build ..."
    reinplace -E "s/(gem) (build|install|push)/gem2.6 \\2/g" \
        ${worksrcpath}/lib/bundler/gem_helper.rb
}

post-destroot {
    foreach type {site vendor} {
        set libdir ${destroot}${prefix}/lib/ruby2.6/${type}_ruby/2.6.0
        xinstall -m 0755 -d ${libdir}
        foreach subdir [exec find ${libdir} -type d -empty] {
            destroot.keepdirs-append ${subdir}
        }
    }

    # install destination of commands from port:rb26-*
    xinstall -m 0755 -d ${destroot}${prefix}/libexec/ruby2.6
    destroot.keepdirs-append ${destroot}${prefix}/libexec/ruby2.6
}

notes-append "
To make this the default Ruby (i.e., the version run by\
the 'ruby', 'gem' or 'bundle' commands), run:
    sudo port select --set ruby $name
"

variant doc description "Install rdoc indexes and C API documents" {
        configure.args-delete   --disable-install-doc
}

variant gmp description "use gmp" {
        configure.args-delete   --without-gmp
        depends_lib-append      port:gmp
}

variant jemalloc description "use jemalloc" {
        configure.args-delete   --without-jemalloc
        configure.args-append   --with-jemalloc
        depends_lib-append      port:jemalloc
}

variant universal {
        # Disable parallel builds for Universal case
        # See: https://trac.macports.org/ticket/24240
        use_parallel_build      no

        # use ruby built-in universal mechanism.
        configure.args-append   --with-arch=[join ${universal_archs} ,]
        # clear macports' universal flags
        configure.universal_cflags
        configure.universal_cppflags
        configure.universal_cxxflags
        configure.universal_ldflags
}

variant relative description "Enable relative loading of libraries to allow for relocation of binaries." {
        #enable relative loading
        configure.args-append  --enable-load-relative
}

# legacy systems suport
platform darwin {

    # Build requires 10.8 SDK when targeting 10.7
    # see https://trac.macports.org/ticket/57986
    if {${os.major} == 11} {
        set ten_eight_sdkpath ${developer_dir}/Platforms/MacOSX.platform/Developer/SDKs/MacOSX10.8.sdk
        if {[file exists $ten_eight_sdkpath]} {
            configure.sdkroot $ten_eight_sdkpath
        } else {
            pre-fetch {
                error "Building $name @${version} on Mac OS X 10.7 requires the MacOSX10.8.sdk to be present in ${developer_dir}/Platforms/MacOSX.platform/Developer/SDKs"
            }
        }
    }

    if {${os.major} < 11} {
        depends_build-append    port:gmake
        build.cmd               ${prefix}/bin/gmake
        configure.args-append   --disable-dtrace
        configure.cflags-append -std=c99
    }

    # Xcode gcc 4.2 building x86_64 assembler code passes "-arch i386" to the
    # assembler, breaking x86_64 builds, unless we override it.
    if {[string match *gcc-4.2 ${configure.compiler}]
        && "x86_64" in [get_canonical_archs]} {
        configure.env-append    ASFLAGS=[get_canonical_archflags]
    }

    # Building for 10.5 x86_64 fails if libunwind-headers is active.
    # It's unknown why only this one case fails.
    if {${os.major} == 9 && "x86_64" in [get_canonical_archs]} {
        PortGroup    conflicts_build 1.0

        conflicts_build-append libunwind-headers
    }

    if {${os.major} == 8} {
        configure.cppflags-append -DCPU_SUBTYPE_MASK=0xff000000
    }
}


livecheck.type  regex
livecheck.url   https://cache.ruby-lang.org/pub/ruby/2.6/
livecheck.regex ruby-(2\\.6\\.\\d+)[quotemeta ${extract.suffix}]
