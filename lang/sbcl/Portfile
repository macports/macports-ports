# -*- mode: tcl; coding: utf-8; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- # vim:fenc=utf-8:filetype=tcl:et:sw=4:ts=4:sts=4

PortSystem      1.0

PortGroup       legacysupport 1.1

name            sbcl

# Note to maintainers:
#
# Please bump the revision of math/maxima (and when it exists
# math/maxima-devel) and fricas when this port changes.
version         2.3.5
revision        1

categories      lang
license         BSD
maintainers     {easieste @easye} {@catap korins.ky:kirill} openmaintainer

description     The Steel Bank Common Lisp system

long_description \
    Steel Bank Common Lisp (SBCL) is a Open Source implementation of ANSI \
    Common Lisp. It provides an interactive environment including an \
    integrated native compiler, interpreter, and debugger. SBCL is quite \
    suitable for the creation of long running system services, as the compiler \
    trades the time for a long initial compilation for blazingly fast loading \
    of its binary runtime fasl representation.


homepage        http://www.sbcl.org
master_sites    sourceforge

use_bzip2       yes

# See: https://github.com/catap/sbcl/tree/macports
patchfiles      0001-fix-building-when-root-directory-contain-non-ASCII-c.patch \
                0002-add-MacPorts-XDG_DATA_DIRS.patch

checksums       rmd160  95a02d7a489137602ebba333b83bfc1d9eb794be \
                sha256  89c90720cf9d05dbcd90d690e381a2514c0f1807159e0d7222220c5a8c2d5186 \
                size    7474392

# NOTE: keep so-called legacy version of SBCL on everything before 10.7
# Historically 2.2.5 was the last version with support x86/Darwin, and 2.2.2 was
# the last version with ppc/Darwin. We had backported support of ppc/Darwin to
# 2.2.5 and use it as baseline for legacy version.
if {${os.platform} eq "darwin" && ${os.major} < 11} {
    version     2.2.5
    revision    1
    epoch       1

    checksums   rmd160  55225af6c20d39db1b158b7c07685ff6e3a8d5bf \
                sha256  8584b541370fd6ad6e58d3f97982077dfcab240f30d4e9b18f15da91c2f13ed1 \
                size    7029912

    # See: https://github.com/catap/sbcl/tree/macports-legacy
    patchfiles-append \
                1001-Darwin-disable-custom-nanosleep-before-10.6.patch \
                1002-Darwin-recovery-support-non-dispatch-semaphores.patch \
                1003-x86-Darwin-mask-out-O_LARGEFILE-in-fcntl.1-test.patch \
                1004-x86-Darwin-revert-OS_THREAD_STACK.patch \
                1005-x86-Darwin-fix-build-by-old-preprocessor.patch \
                1006-x86-Darwin-disable-failing-stat-mode-tests.patch \
                1007-x86-Darwin-add-missed-OS_CONTEXT_PC.patch \
                1008-x86-Darwin-fix-syntax-typos.patch \
                1009-ppc-Darwin-Revert-Delete-support-for-and-darwin-ppc.patch \
                1010-ppc-Darwin-Config.ppc-use-right-compiler-and-deploym.patch \
                1011-ppc-Darwin-ppc-assem.S-fix-wrong-assembler-code-chun.patch \
                1012-ppc-Darwin-Misc-fixes-for-PPC.patch \
                1013-ppc-Darwin-make-config.sh-use-gcc-tls-for-ppc.patch \
                1014-ppc-Darwin-ppc-assem-a-minor-fix-to-assembler-syntax.patch

    # NOTE: Mach exception handler was a pain and was removed since 2.2.9,
    # let me disabled it anyway that makes things more consistent and allows to
    # avoid issues by different SDK / XCode version for legacy systems.
    # See: https://github.com/sbcl/sbcl/commit/7026bb841aa63c4d43339afb49d837a4497c468a
    build.post_args-append \
                --without-mach-exception-handler
}

distfiles       ${name}-${version}-source${extract.suffix}
worksrcdir      ${name}-${version}

depends_build-append \
                port:sbcl-bootstrap

# clock_gettime
legacysupport.newest_darwin_requires_legacy 15
# NOTE: check that it can be removed
patchfiles-append \
                0003-Use-CLOCK_MONOTONIC-when-CLOCK_PROCESS_CPUTIME_ID-un.patch

post-patch {
    reinplace "s|@@PREFIX@@|${prefix}|g" \
                ${worksrcpath}/contrib/asdf/uiop.lisp

    reinplace "s|/usr/local/lib/${name}|${prefix}/lib/${name}|g" \
                ${worksrcpath}/doc/sbcl.1

    # Enable legacy support when it required
    set legacy_support_cpp_flags [legacysupport::get_cpp_flags]
    set legacy_support_link_flags [legacysupport::get_library_link_flags]
    if {${legacy_support_cpp_flags} ne "" || ${legacy_support_link_flags} ne ""} {
        set config_path ${worksrcpath}/src/runtime
        foreach f [glob -tails -directory ${config_path} Config.*-darwin] {
            ui_debug "Adding LegacySupport into ${f}"
            set config [open ${config_path}/$f a]
            puts ${config} "CFLAGS += ${legacy_support_cpp_flags}"
            puts ${config} "DEPEND_FLAGS += ${legacy_support_cpp_flags}"
            puts ${config} "OS_LIBS += ${legacy_support_link_flags}"
            close ${config}
        }
    }
}

use_configure   no

pre-build {
    # disable all warning
    set sbclrc [open $env(HOME)/.sbclrc w+]
    puts ${sbclrc} "(declaim (sb-ext:muffle-conditions cl:warning))"
    close ${sbclrc}
}

build.env-append \
                CC=${configure.cc} \
                CXX=${configure.cxx} \
                CPP=${configure.cpp} \
                SBCL_MACOSX_VERSION_MIN=${macosx_deployment_target}

use_parallel_build  no

build.cmd       sh
build.target    make.sh
build.args-append \
                --prefix=${prefix} \
                --xc-host=${prefix}/libexec/sbcl-bootstrap/bin/sbcl

if {${configure.build_arch} eq "i386"} {
    build.args-append \
                --arch=x86
} elseif {${configure.build_arch} eq "x86_64"} {
    build.args-append \
                --arch=x86-64
} else {
    build.args-append \
                --arch=${configure.build_arch}
}

post-build {
    if {[variant_isset html] || [variant_isset pdf]} {
        system -W ${worksrcpath}/doc "INSTALL_ROOT=${destroot}${prefix} sh ${worksrcpath}/doc/make-doc.sh"
    }
}

# NOTE seems that threads never works on PowerPC
# See: https://github.com/sbcl/sbcl/commit/33a6b2817f3e2a8cf98d162a2b40c50d259295c4
if {![variant_isset threads] && ${configure.build_arch} ne "ppc"} {
    default_variants +fancy
}

variant html conflicts pdf description {Installs documentation in HTML and Info formats.} {
    patchfiles-append    0004-Disable-pdf-manual.patch
    depends_build-append bin:texi2dvi:texinfo
    depends_build-append bin:makeinfo:texinfo
}

variant pdf conflicts html description {Installs documentation in PDF, HTML and Info formats.  Involves the installation of a TexLive dependency chain, which can dramatically slow down the installation of the SBCL port.} {
    depends_build-append bin:dvips:texlive
    depends_build-append bin:texi2dvi:texinfo
    depends_build-append bin:makeinfo:texinfo
}

variant threads description {Enable multi-threaded runtime using the Mach pthreads interface.} {
    build.args-append   --with-sb-thread
}

variant fancy conflicts threads description {Configure SBCL compilation with all available compatible options (including threading).} {
    build.args-append   --fancy
    # Before 2.2.6 it ueses lz to compress core
    depends_lib-append  port:zlib
    # As of version 2.2.6, zstd is used for core compression.
    if {[vercmp ${version} 2.2.6] >= 0} {
        depends_lib-delete  port:zlib
        depends_lib-append  port:zstd
        patchfiles-append   0005-Use-MacPorts-zstd.patch

        post-patch {
            reinplace -W ${worksrcpath} "s|@@PREFIX@@|${prefix}|g" \
                {*}[glob ${worksrcpath}/src/runtime/Config.*-darwin]
        }
    }
}

test.run        yes
test.dir        ${worksrcpath}/tests
test.env        CC=${configure.cc} \
                CXX=${configure.cxx} \
                CPP=${configure.cpp}
test.target     run-tests.sh

destroot.env    INSTALL_ROOT=${destroot}${prefix}
destroot.target install.sh
destroot.destdir

livecheck.url   http://sourceforge.net/api/file/index/project-id/1373/rss?path=%2F${name}
livecheck.regex guid.*${name}-(\\d+(\\.\\d+)+)-source
