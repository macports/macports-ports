# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0
PortGroup           gpg_verify 1.0
PortGroup           haskell_stack 1.0
PortGroup           legacysupport 1.1

github.setup        commercialhaskell stack 2.7.1 v
revision            0

name                stack
categories          lang haskell
maintainers         {ieee.org:s.t.smith @essandess} openmaintainer
license             BSD
platforms           darwin

description         The Haskell Tool Stack
long_description    Stack is a cross-platform command line interface \
                    for developing Haskell projects. It features: \
                    installing GHC automatically, installing packages \
                    needed for your project, building your project, \
                    testing your project, benchmarking your project. \
                    It is intended for Haskellers both new and experienced.

homepage            https://haskellstack.org

variant prebuilt \
	    description {Do not bootstrap stack; install the pre-built binary.} {}

supported_archs     x86_64

distname            ${name}-${github.version}-osx-x86_64

worksrcdir          ${name}-${github.version}

master_sites        ${github.homepage}/releases/download/${github.tag_prefix}${github.version}/:release \
                    ${github.homepage}/archive/refs/tags/:archive

distfiles           ${distname}-bin:release \
                    ${github.tag_prefix}${github.version}${extract.suffix}:archive

extract.only        ${github.tag_prefix}${github.version}${extract.suffix}

checksums           ${distname}-bin \
                    rmd160  01fb6c671882d667611b247b1d0ec147f09f1405 \
                    sha256  dd31f7827172f07e3f6bd0145bd6f1e67fb9ce88da0bf6103ce0f13b25a8b6e3 \
                    size    36107528 \
                    ${github.tag_prefix}${github.version}${extract.suffix} \
                    rmd160  bc30354c95c82d29ff124bfd23b2270086a95f3c \
                    sha256  eb849d5625084a6de57e8520ddf8172aca64ddadd9fee37cdafeefad80895b62 \
                    size    686145 \
                    ${distname}-bin.asc \
                    size    488

gpg_verify.use_gpg_verification \
                    yes

if {[option gpg_verify.use_gpg_verification]} {
    distfiles-append \
                    ${distname}-bin.asc:release

    post-checksum {
        # check GPG signature: https://docs.haskellstack.org/en/stable/SIGNING_KEY/
        set gpg_keyid 575159689befb442

        gpg_verify.verify_gpg_signature \
            ${filespath}/keyid-${gpg_keyid}.txt \
            ${distpath}/${distname}-bin.asc \
            ${distpath}/${distname}-bin
    }
}

post-extract {
    xinstall -W ${workpath} -d ./bin
    xinstall -m 0755 -W ${distpath} ./${distname}-bin ${workpath}/bin
}

# bootstrap binaries seg fault on 10.7 and older
set max_darwin 12
if { ${os.platform} eq "darwin" && ${os.major} < ${max_darwin} } {
    known_fail yes
    pre-fetch {
        ui_error "${name} @${version} requires OS X 10.[expr ${max_darwin} - 4] or later"
        return -code error "unsupported Mac OS X version"
    }
}

# Builds fail if ccache is enabled in MacPorts
configure.ccache    no

# Darwin 16 and older fails to build from source, e.g.
# Undefined symbols for architecture x86_64:
#  "_utimensat", referenced from:
#      _Lc6xB_info in libHSdirectory-1.3.6.0.a(Posix.o)
if { ${os.platform} eq "darwin" && ${os.major} <= 16 } {
    default_variants-append +prebuilt
}

if { [variant_isset "prebuilt"] } {
    use_configure   no

    build {}

    if { ${os.major} <= [option legacysupport.newest_darwin_requires_legacy] } {
        depends_build-append port:cctools
    }

    destroot {
        copy        ${workpath}/bin/${distname}-bin \
                    ${destroot}${prefix}/bin/${name}
        if { ${os.major} <= [option legacysupport.newest_darwin_requires_legacy] } {
            # Relink against LegacySupport libSystem
            set         sLib /usr/lib/libSystem.B.dylib
            set         lLib ${prefix}/lib/libMacportsLegacySystem.B.dylib
            system "install_name_tool -change ${sLib} ${lLib} ${destroot}${prefix}/bin/${name}"
        }
    }
} else {
    post-extract {
        xinstall -m 0755 -d "[option haskell_stack.stack_root]"

        # standard stack install with 'curl | sh'; don't use
        # system -W ${worksrcpath} "/bin/mkdir ./bin && /usr/bin/curl -sSL https://get.haskellstack.org/ | /bin/sh -s - -d ./bin"
        # bootstrap using the pre-built ./stack binary
        ln -s       ${workpath}/bin/${distname}-bin \
                    ${workpath}/bin/${name}
    }

    set haskell_stack.bin ${workpath}/bin/stack
}
