# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0
PortGroup           gpg_verify 1.0
PortGroup           haskell_stack 1.0
PortGroup           legacysupport 1.1

github.setup        commercialhaskell stack 2.7.0.3 rc/v
version             ${github.version}.[lindex [split ${github.tag_prefix} /] 0]
revision            0

name                stack
categories          lang haskell
maintainers         {ieee.org:s.t.smith @essandess} openmaintainer
license             BSD
platforms           darwin

description         The Haskell Tool Stack
long_description    Stack is a cross-platform command line interface \
                    for developing Haskell projects. It features: \
                    installing GHC automatically, installing packages \
                    needed for your project, building your project, \
                    testing your project, benchmarking your project. \
                    It is intended for Haskellers both new and experienced.

homepage            https://haskellstack.org

variant prebuilt \
	    description {Do not bootstrap stack; install the pre-built binary.} {}

supported_archs     x86_64

distname            ${name}-${github.version}-osx-x86_64

worksrcdir          ${name}-${github.version}

set github_tag_prefix_quote \
                    {rc%2Fv}

master_sites        ${github.homepage}/releases/download/${github_tag_prefix_quote}${github.version}:release \
    ${github.homepage}/archive/refs/tags/[lindex [split ${github.tag_prefix} /] 0]/:archive

distfiles           ${distname}-bin:release \
                    [lindex [split ${github.tag_prefix} /] 1]${github.version}${extract.suffix}:archive

extract.only        [lindex [split ${github.tag_prefix} /] 1]${github.version}${extract.suffix}

checksums           ${distname}-bin \
                    rmd160  1ba6f3bb936aef0d6f980ad896ba5f110420285f \
                    sha256  d2e798a5a064ea8d7e9246d21c0c64c9c2ac1112e6971697edfcc24b8b745212 \
                    size    36107688 \
                    [lindex [split ${github.tag_prefix} /] 1]${github.version}${extract.suffix} \
                    rmd160  45c336711399c38b61ff6d8516de0b6ff9dff9ef \
                    sha256  7dd1d0c7f9895a9e43f6e245fc1fe10565f07f79e2bc68851733ee36db05280b \
                    size    686439 \
                    ${distname}-bin.asc \
                    size    488

gpg_verify.use_gpg_verification \
                    yes

if {[option gpg_verify.use_gpg_verification]} {
    distfiles-append \
                    ${distname}-bin.asc:release

    post-checksum {
        # check GPG signature: https://docs.haskellstack.org/en/stable/SIGNING_KEY/
        set gpg_keyid 575159689befb442

        gpg_verify.verify_gpg_signature \
            ${filespath}/keyid-${gpg_keyid}.txt \
            ${distpath}/${distname}-bin.asc \
            ${distpath}/${distname}-bin
    }
}

post-extract {
    xinstall -W ${workpath} -d ./bin
    xinstall -m 0755 -W ${distpath} ./${distname}-bin ${workpath}/bin
}

# bootstrap binaries seg fault on 10.7 and older
if {${os.platform} eq "darwin" && ${os.major} < 12} {
    known_fail yes
    pre-fetch {
        ui_error "${name} @${version} requires OS X 10.8 or later"
        return -code error "unsupported Mac OS X version"
    }
}

# Builds fail if ccache is enabled in MacPorts
configure.ccache    no

if { [variant_isset "prebuilt"] } {
    use_configure   no

    build {}

    if { ${os.major} <= [option legacysupport.newest_darwin_requires_legacy] } {
        depends_build-append port:cctools
    }

    destroot {
        copy        ${workpath}/bin/${distname}-bin \
                    ${destroot}${prefix}/bin/${name}
        if { ${os.major} <= [option legacysupport.newest_darwin_requires_legacy] } {
            # Relink against LegacySupport libSystem
            set         sLib /usr/lib/libSystem.B.dylib
            set         lLib ${prefix}/lib/libMacportsLegacySystem.B.dylib
            system "install_name_tool -change ${sLib} ${lLib} ${destroot}${prefix}/bin/${name}"
        }
    }
} else {
    post-extract {
        xinstall -m 0755 -d "[option haskell_stack.stack_root]"

        # standard stack install with 'curl | sh'; don't use
        # system -W ${worksrcpath} "/bin/mkdir ./bin && /usr/bin/curl -sSL https://get.haskellstack.org/ | /bin/sh -s - -d ./bin"
        # bootstrap using the pre-built ./stack binary
        ln -s       ${workpath}/bin/${distname}-bin \
                    ${workpath}/bin/${name}
    }

    set haskell_stack.bin ${workpath}/bin/stack
}
