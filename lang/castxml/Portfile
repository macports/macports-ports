# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           cmake 1.1
PortGroup           compiler_blacklist_versions 1.0
PortGroup           github 1.0

github.setup        CastXML CastXML 0.3.4 v

name                castxml
epoch               20190718
revision            0
categories          lang
platforms           darwin
license             Apache-2

maintainers         {stromnov @stromnov} openmaintainer

description         CastXML is a C-family abstract syntax tree XML output tool.
long_description    ${description}

checksums           rmd160  8f8a74e309e359d890a7acf92d4414cac7415fcc \
                    sha256  b9e04c7f5f2b08166808dc5842f3bb88a501ddb432c054cec4e0133d65e606bf \
                    size    95623

compiler.cxx_standard \
                    2014
# Fix MacPorts base selection of C++14-compatible compilers. Remove once
# https://github.com/macports/macports-base/pull/162 is released.
compiler.blacklist-append {clang < 602}

depends_lib-append  port:zlib \
                    port:libffi \
                    port:libedit \
                    port:ncurses

configure.args-append \
                    -DCastXML_INSTALL_DOC_DIR=share/doc/castxml

# List of clang versions to offer variants for
set clang_vers { 6.0 7.0 8.0 9.0 10 }

proc variant_name {ver} {
    return clang[string map {. {}} ${ver}]
}

# build list of all clang variant names
set clang_variants {}
foreach ver ${clang_vers} { lappend clang_variants [variant_name $ver] }

proc build_conflicts {v} {
    global clang_variants
    set i [lsearch -exact ${clang_variants} ${v}]
    return [lreplace ${clang_variants} ${i} ${i}]
}

set has_variant no
foreach ver ${clang_vers} {
    set v_name [variant_name ${ver}]
    set c [build_conflicts ${v_name}]
    variant ${v_name} conflicts {*}${c} description "Use clang ${ver} toolchain" { }
    if {[variant_isset ${v_name}]} {
        set has_variant yes
    }
}
if {!$has_variant} { default_variants +clang90 }

foreach ver ${clang_vers} {
    set v_name [variant_name ${ver}]
    if {[variant_isset ${v_name}]} {
        depends_lib-append          port:llvm-${ver}
        depends_build-append        port:clang-${ver}
        configure.args-append       -DLLVM_DIR=${prefix}/libexec/llvm-${ver}/lib/cmake/llvm
        cmake.install_rpath-append  ${prefix}/libexec/llvm-${ver}/lib
    }
}
