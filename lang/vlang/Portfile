# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0

github.setup        vlang v 0.2.4
name                vlang
github.tarball_from archive
revision            0

description         Simple, fast, safe, compiled language for developing maintainable software
long_description    {*}${description}. Compiles itself in <1s with zero library dependencies.

license             MIT
categories          lang
maintainers         {harens @harens} openmaintainer

set main_distfile   ${distfiles}

# Install the compiler for vlang as a separate distfile.
# Based on https://github.com/macports/macports-ports/blob/master/games/minetest/Portfile
set vc_commit       fd5f57740ff6d7a8566b774318df54c2fa460f92
set vc_distfile     ${vc_commit}${extract.suffix}
set vc_mastersite   https://github.com/vlang/vc/archive

distfiles           ${main_distfile}:main \
                    ${vc_distfile}:vc

master_sites        ${github.master_sites}:main \
                    ${vc_mastersite}:vc

checksums           ${main_distfile} \
                    rmd160  36c9549425ebec8cd11fc22617e1453fd618053c \
                    sha256  8cdbc32fb928051ce7959dd943af3efee26bddc4ed3700a1cb365be73a306bf9 \
                    size    3502493 \
                    ${vc_distfile} \
                    rmd160  abb6cba7c973bae25328b3faefa77075967aec00 \
                    sha256  f6896513cec7e065ae438fd098b8b13c08ee73eb3915578f703116ce98f72b3a \
                    size    1284143

compiler.c_standard 2011
use_configure       no

build {
    # Disable vlang self update feature.
    copy -force ${filespath}/vup.v ${worksrcpath}/cmd/tools

    system -W ${worksrcpath} "${configure.cc} ${configure.cflags} -v -o v ../vc-${vc_commit}/v.c -lm ${configure.ldflags}"
    system -W ${worksrcpath} "./v -cflags ${configure.cflags} -cc ${configure.cc} -prod self"
}

destroot {
    set library_path ${destroot}${prefix}/lib/${name}
    set examples_path ${destroot}${prefix}/share/examples

    xinstall -d ${examples_path}
    move ${worksrcpath}/examples ${examples_path}/${name}

    xinstall -d ${library_path}
    foreach f {cmd thirdparty v v.mod vlib} {
        move ${worksrcpath}/${f} ${library_path}/${f}
    }

    ln -s ${prefix}/lib/${name}/v ${destroot}${prefix}/bin

    # Vlang compiles the subcommands in the tools dir on first runtime usage (e.g. v doctor).
    # Allow running Vlang to those in the _developer group, rather than running sudo each time.
    # See https://github.com/vlang/v/issues/10324

    system "
        chgrp -R _developer ${library_path}/cmd/tools;
        chmod -R g+w ${library_path}/cmd/tools;
    "
}

# If /tmp/v exists, build fails if it isn't writable. Also fails at runtime if it isn't writable.
# Assume MacPorts creates /tmp/v, and so make it writable to the _developer group
# See https://github.com/vlang/v/issues/7713 and https://github.com/vlang/v/discussions/11796
post-activate {
    if {![file exists /tmp/v]} {
        file mkdir /tmp/v
    }
    system "
        chgrp -R _developer /tmp/v;
        chmod -R g+w /tmp/v;
    "
}

# Based on various Android ports - don't make files world-writable.
# e.g. https://github.com/macports/macports-ports/blob/master/java/android/Portfile
notes "
The Vlang tools and cache directory are group _developer writable. You need to be a member of the
_developer group to use Vlang. If you are not, run:

sudo dscl . append /Groups/_developer GroupMembership <username>
"

# GitHub releases and tags are filled up with weekly releases
# Stable release is many pages later, so MacPorts can't find it.
# Fetch directly from version file.
livecheck.url       https://raw.githubusercontent.com/vlang/v/master/v.mod
# Ignore dots just by themselves - and remove apostraphes from version number
livecheck.regex {([0-9]+.[0-9.]+)}
