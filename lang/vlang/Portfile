# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           compiler_blacklist_versions 1.0
PortGroup           github 1.0
PortGroup           legacysupport 1.1
PortGroup           makefile 1.0

# clock_gettime
legacysupport.newest_darwin_requires_legacy 15

github.setup        vlang v weekly.2023.01
github.tarball_from archive
revision            0

name                vlang
# Remove weekly from version number
version             [string range ${version} 7 end]
homepage            https://vlang.io/

description         Simple, fast, safe, compiled language for developing maintainable software
long_description    {*}${description}. Compiles itself in <1s with zero library dependencies. This port \
                    provides weekly builds of Vlang.

license             MIT
categories          lang
maintainers         {harens @harens} openmaintainer

# Install the V compiler as a separate distfile.
# Based on https://github.com/macports/macports-ports/blob/master/games/minetest/Portfile
set vc_commit       086ca946e05f0ef6ad1c8bc1cb15d397db4ef2e7
set main_distfile   ${distfiles}
set vc_distfile     ${vc_commit}${extract.suffix}
set vc_mastersite   https://github.com/vlang/vc/archive

distfiles           ${main_distfile}:main \
                    ${vc_distfile}:vc

master_sites        ${github.master_sites}:main \
                    ${vc_mastersite}:vc

checksums           ${main_distfile} \
                    rmd160  65aa881338b16408d66a03806b6655c220e522e9 \
                    sha256  f14167ef981974927693f32788e21cc9bed84d4c242197ed1e3b19371d7fa514 \
                    size    6152000 \
                    ${vc_distfile} \
                    rmd160  67124de64fd60a69e9b585f19336252b3c904044 \
                    sha256  1b2ba3867a7a90656e97c40b662dbb5488471dfe0a2b819c2295dfce7027d316 \
                    size    1568865

depends_lib         port:boehmgc

# https://trac.macports.org/ticket/64913
if {${build_arch} in "ppc ppc64"} {
    configure.ldflags-append -latomic
}

# Path to V compiler
set vc_path ${workpath}/vc-${vc_commit}

# ./thirdparty/stdatomic/nix/atomic.h:12:10: fatal error: 'stdatomic.h' file not found
# Based on cmus port: compatible compilers are Xcode > Xcode 7, clang > 3.7, and gcc > 4.8
compiler.c_standard 2011
compiler.blacklist-append {clang < 700} macports-clang-3.*

# Stop V from looking in Homebrew directories, and remove hardcoded MacPorts prefix.
# V has built-in font rendering, but users can optionally use freetype.
patchfiles          patch-boehmgc-prefix.diff \
                    patch-freetype2-prefix.diff \
                    patch-openssl-prefix.diff \
                    patch-pkgconfig-prefix.diff \
                    patch-postgresql-prefix.diff

# See https://github.com/vlang/v/issues/6605
# Header file <sys/errno.h>, needed for module `sync` was not found
if { ${os.major} <= [option legacysupport.newest_darwin_requires_legacy] } {
    # Use LegacySupport with the time module
    patchfiles-append patch-clock-gettime.diff \
                      patch-legacysupport-makefile.diff

    # Header file <sys/errno.h>, needed for module `sync` was not found
    if { ${os.major} <= 9 } {
        patchfiles-append patch-errno-header.diff
    }
}

post-patch {
    # https://github.com/vlang/v/issues/10796 - Set correct deployment target.
    foreach f [list ${worksrcpath}/vlib/v/builder/cc.v ${vc_path}/v.c] {
        reinplace "s|-mmacosx-version-min=10.7|-mmacosx-version-min=${macosx_deployment_target}|" ${f}
    }

    # Disable vlang self update feature.
    copy -force ${filespath}/vup.v ${worksrcpath}/cmd/tools

    foreach f [list net/openssl/c.v fontstash/a_d_use_freetype.v pg/pg.v v/pkgconfig/pkgconfig.v builtin/builtin_d_gcboehm.c.v] {
        reinplace -W ${worksrcpath} "s|@PREFIX@|${prefix}|g" vlib/${f}
    }

    if { ${os.major} <= [option legacysupport.newest_darwin_requires_legacy] } {
        reinplace "s|@PREFIX@|${prefix}|g" ${worksrcpath}/vlib/time/time_nix.c.v
    }
}

# -cg, -showcc and -show-c-output allows for better error debugging
# local prevents fetching the V compiler during the build
build.args-append   local=true \
                    VFLAGS="-v -cg -showcc -show-c-output -prod -cc ${configure.cc} -cflags [shellescape ${configure.cflags}]" \
                    VC=${vc_path}

# See https://trac.macports.org/ticket/64913
# In case _developer group doesn't exist
add_users vlang group=_developer

destroot {
    set library_path ${destroot}${prefix}/lib/${name}
    set examples_path ${destroot}${prefix}/share/examples

    xinstall -d ${examples_path}
    move ${worksrcpath}/examples ${examples_path}/${name}

    xinstall -d ${library_path}
    foreach f {cmd thirdparty v v.mod vlib} {
        move ${worksrcpath}/${f} ${library_path}/${f}
    }

    ln -s ${prefix}/lib/${name}/v ${destroot}${prefix}/bin

    # Vlang compiles the subcommands in the tools dir on first runtime usage (e.g. v doctor).
    # Allow running Vlang to those in the _developer group, rather than running sudo each time.
    # See https://github.com/vlang/v/issues/10324

    # Majority of subcommands only require ${library_path}/cmd/tools to be writable but v self requires
    # all of ${library_path}

    fs-traverse item ${library_path} {
        file attributes ${item} -group _developer -permissions g+w
    }
}

# Delete leftover subcommands compiled during runtime
post-deactivate {
    delete ${prefix}/lib/${name}
}

# Based on various Android ports - don't make files world-writable.
# e.g. https://github.com/macports/macports-ports/blob/master/java/android/Portfile
notes "
The Vlang library directory is group _developer writable. You need to be a member of the
_developer group to use Vlang. If you are not, run:

sudo dscl . append /Groups/_developer GroupMembership <username>

Optionally, to use the V PostgreSQL module, install PostgreSQL and set the POSTGRESQL_VERSION
environment variable. For PostgreSQL 14:

export POSTGRESQL_VERSION=14
"

# Check for weekly versions only
livecheck.version ${version}
livecheck.regex weekly.(\[0-9.\]+\[0-9\])
