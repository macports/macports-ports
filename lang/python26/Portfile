# $Id$

PortSystem 1.0
PortGroup select 1.0

name                    python26
version                 2.6.2
revision                2
set major               [lindex [split $version .] 0]
set branch              [join [lrange [split ${version} .] 0 1] .]
categories              lang
platforms               darwin
maintainers             blb mcalhoun

description             An interpreted, object-oriented programming language
long_description        Python is an interpreted, interactive, object-oriented \
                        programming language.

homepage                http://www.python.org/
master_sites            ${homepage}/ftp/python/${version}/ \
                        ftp://ftp.python.org/pub/python/${version}/

distname                Python-${version}
use_bzip2               yes

checksums               md5     245db9f1e0f09ab7e0faaa0cf7301011 \
                        sha1    125d4ed90f82de1d44f288150bbbda23a6cd27a4 \
                        rmd160  85fa3540cbacf4d0547d2381eb3aa76bef35c2e1

# patch-Lib-distutils-dist.py.diff comes from
# <http://bugs.python.org/issue1180>
# Setting PYTHONHOME with patch-Mac-IDLE-Makefile.in.diff,
# patch-Mac-Makefile.in.diff, and patch-Mac-PythonLauncher-Makefile.in.diff
# is to work around http://bugs.python.org/issue5766
patchfiles              patch-Makefile.pre.in.diff \
                        patch-setup.py.diff \
                        patch-Lib-cgi.py.diff \
                        patch-Lib-distutils-dist.py.diff \
                        patch-Mac-IDLE-Makefile.in.diff \
                        patch-Mac-Makefile.in.diff \
                        patch-Mac-PythonLauncher-Makefile.in.diff \
                        patch-Mac-Tools-Doc-setup.py.diff

depends_lib             port:gettext port:zlib port:openssl port:tk \
                        port:sqlite3 port:db46 port:ncurses port:gdbm \
                        port:bzip2 port:readline

configure.args          --enable-framework=${frameworks_dir} \
                        --enable-ipv6

use_parallel_build      no

post-patch {
   reinplace "s|@@PREFIX@@|${prefix}|g" ${worksrcpath}/Lib/cgi.py
   reinplace "s|@@APPLICATIONS_DIR@@|${applications_dir}|g" \
      ${worksrcpath}/Mac/Makefile.in ${worksrcpath}/Mac/IDLE/Makefile.in \
      ${worksrcpath}/Mac/Tools/Doc/setup.py \
      ${worksrcpath}/Mac/PythonLauncher/Makefile.in

   # See http://trac.macports.org/changeset/37861
   reinplace "s|xargs -0 rm -r|/usr/bin/xargs -0 /bin/rm -r|g" \
      ${worksrcpath}/Mac/PythonLauncher/Makefile.in
}

build.target            all

test.run                yes
test.target             test

destroot.target         frameworkinstall maninstall

# ensure that correct compiler is used
build.args-append       MAKE="${build.cmd} CC=${configure.cc}"
destroot.args-append    MAKE="${destroot.cmd} CC=${configure.cc}"

select.group            python
select.file             ${filespath}/python[string map {. {}} ${branch}]

post-destroot {
   set framewpath ${frameworks_dir}/Python.framework
   set framewdir  ${framewpath}/Versions/${branch}

   foreach dir { Headers Resources Python Versions/Current } {
      file delete ${destroot}${framewpath}/${dir}
   }

   ln -s ${framewdir}/share/man/man1/python.1 ${destroot}${prefix}/share/man/man1/python${branch}.1
   ln -s ${framewdir}/Python ${destroot}${prefix}/lib/libpython2.6.dylib

   # Without this, LINKFORSHARED is set to 
   # ... $(PYTHONFRAMEWORKDIR)/Versions/$(VERSION)/$(PYTHONFRAMEWORK)
   # (this becomes Python.framework/Versions/2.6/Python) which doesn't
   # quite work (see ticket #15099); instead specifically list the
   # full path to the proper Python framework file (which becomes
   # ${prefix}/Library/Frameworks/Python.framework/Versions/2.6/Python)
   reinplace {s|^\(LINKFORSHARED=.*\)$(PYTHONFRAMEWORKDIR).*$|\1 $(PYTHONFRAMEWORKINSTALLDIR)/Versions/$(VERSION)/$(PYTHONFRAMEWORK)|} ${destroot}${framewdir}/lib/python${branch}/config/Makefile

   # The framework version.plist isn't currently being installed so
   # we'll copy ours for now (see http://trac.macports.org/ticket/18773 and
   # http://bugs.python.org/issue4937)
   xinstall -m 644 ${filespath}/version.plist \
      ${destroot}${framewdir}/Resources/version.plist

   foreach unversionedFile {idle pydoc python python-config pythonw smtpd.py} {
      delete ${destroot}${prefix}/bin/${unversionedFile}
   }
}

post-activate {
   ui_msg "\nTo fully complete your installation and make python $branch the default, please run
\n\tsudo port install python_select \
\n\tsudo python_select $name\n"
}

platform darwin {
   post-configure {
      # See http://trac.macports.org/ticket/18376
      system "cd ${worksrcpath} && ed - pyconfig.h < ${filespath}/pyconfig.ed"
   }
}

platform darwin 7 {
   # there is no SystemStubs on 10.3
   post-patch {
      reinplace "s|-lSystemStubs||g" ${worksrcpath}/configure \
         ${worksrcpath}/configure.in
   }
   post-configure {
      reinplace "s|-lSystemStubs||g" ${worksrcpath}/Makefile.pre.in \
         ${worksrcpath}/Makefile.pre ${worksrcpath}/Makefile
   }
   # To avoid GCC incompatibility issue. See http://nxg.me.uk/note/2004/restFP/ (by ebgssth at gmail.com, ticket #13322)
   configure.ldflags-append   "-lcc_dynamic"
}

variant universal {
   post-patch {
      set universal_arch_flags {}
      set arch_run_32bit {}
      foreach arch ${universal_archs} {
         lappend universal_arch_flags -arch ${arch}
         if { ${arch}=="i386" || ${arch}=="ppc" } {
            lappend arch_run_32bit -${arch}
         }
      }
      reinplace \
         "s|UNIVERSAL_ARCH_FLAGS=\".*\"|UNIVERSAL_ARCH_FLAGS=\"${universal_arch_flags}\"|" \
         ${worksrcpath}/configure
      reinplace \
         "s|ARCH_RUN_32BIT=\".*\"|ARCH_RUN_32BIT=\"arch ${arch_run_32bit}\"|" \
         ${worksrcpath}/configure
   }
   if {[info exists universal_sysroot]} {
      configure.args-append   --enable-universalsdk=${universal_sysroot}
   } else {
      if {${macosx_version} == "10.4"} {
         configure.args-append   --enable-universalsdk=/Developer/SDKs/MacOSX${macosx_version}u.sdk
      } else {
         configure.args-append   --enable-universalsdk=/Developer/SDKs/MacOSX${macosx_version}.sdk
      }
   }
}

variant ucs4 description {Enable support for UCS4} {
   configure.args-append   --enable-unicode=ucs4
}

livecheck.check         regex
livecheck.url           ${homepage}download/releases/
livecheck.regex         Python (${branch}(?:\\.\\d+)*)

