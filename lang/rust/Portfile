# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=portfile:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           muniversal 1.0
PortGroup           active_variants 1.1

name                rust
version             1.34.2
categories          lang devel
platforms           darwin
supported_archs     i386 x86_64
license             {MIT Apache-2} BSD zlib NCSA Permissive
maintainers         {g5pw @g5pw} openmaintainer

description         A safe, concurrent, practical language

long_description    Rust is a curly-brace, block-structured expression \
                    language. It visually resembles the C language \
                    family, but differs significantly in syntactic and \
                    semantic details. Its design is oriented toward \
                    concerns of \"programming in the large\", that is, of \
                    creating and maintaining boundaries -- both abstract \
                    and operational -- that preserve large-system \
                    integrity, availability and concurrency.

homepage            https://www.rust-lang.org/

# Get from src/stage0.txt
set ruststd_version 1.33.0
set rustc_version   1.33.0
set cargo_version   0.34.0
set llvm_version    7.0

# can use cmake or cmake-devel; default to cmake.
depends_build       path:bin/cmake:cmake \
                    bin:python2.7:python27

depends_lib         port:llvm-${llvm_version}

master_sites        https://static.rust-lang.org/dist

distname            ${name}c-${version}-src

patchfiles          patch-src-librustc-llvm-lib.diff

if {![variant_isset universal]} {
    if {${build_arch} eq "i386"} {
        set architectures i686
    } else {
        set architectures ${build_arch}
    }
} else {
    set architectures {}
    foreach arch ${universal_archs} {
        if {${arch} eq "i386"} {
            lappend architectures i686
        } else {
            lappend architectures ${arch}
        }
    }
}

foreach arch ${architectures} {
    distfiles-append rust-std-${ruststd_version}-${arch}-apple-${os.platform}${extract.suffix} \
                     rustc-${rustc_version}-${arch}-apple-${os.platform}${extract.suffix} \
                     cargo-${cargo_version}-${arch}-apple-${os.platform}${extract.suffix}
}

checksums           ${distname}${extract.suffix} \
                    rmd160  5b8903967ae7689b12f57a6278b2a0abd8fde5a4 \
                    sha256  c69a4a85a1c464368597df8878cb9e1121aae93e215616d45ad7d23af3052f56 \
                    size    149925014

checksums-append \
                    rust-std-${ruststd_version}-i686-apple-${os.platform}${extract.suffix} \
                    rmd160  0c7e7f4987409ebc522ef3be70e634c9b0460c8d \
                    sha256  829fdae8a8100b43fd13e98122f8f2f266f818500caa29cf44c1b942f13e1d23 \
                    size    66061870 \
                    rustc-${rustc_version}-i686-apple-${os.platform}${extract.suffix} \
                    rmd160  26ad5ebb9f1c4b5d51636f5064a495810362379b \
                    sha256  9d1126687c6bad62ed1f93905351eaea8543eafea15e06c951c5d4df9e3c8b91 \
                    size    108346557 \
                    cargo-${cargo_version}-i686-apple-${os.platform}${extract.suffix} \
                    rmd160  b587efbaf4b2d06644ce373d083d9edf14d781dc \
                    sha256  252ab1451fcbcd4f570fa82fbcf9d41cd7ed88f52e3faddca80abb2dd88c2ef1 \
                    size    4831382

checksums-append \
                    rust-std-${ruststd_version}-x86_64-apple-${os.platform}${extract.suffix} \
                    rmd160  76632e142648b73682b7fbe248aab34041c51191 \
                    sha256  94247d4d11c631c9d4256f4b0aedd7fd0379fdb55174405c4c1c0dd0c40097ca \
                    size    67062787 \
                    rustc-${rustc_version}-x86_64-apple-${os.platform}${extract.suffix} \
                    rmd160  552e051b4085fcf41d54dee686567aa435bf28f2 \
                    sha256  ea1f0a95015bbefba9eac5890b12ee2887f464822ab579c8bbc2db3023c6dd08 \
                    size    111138823 \
                    cargo-${cargo_version}-x86_64-apple-${os.platform}${extract.suffix} \
                    rmd160  4ed69e80c7e87e3ea507b629c2dd042a1e679f65 \
                    sha256  5349b04cd783942d39be3223629efbf0325de04ee72751c4a1a1388e858caac6 \
                    size    4960446

pre-fetch {
    if {${os.platform} eq "darwin" && ${os.major} < 11} {
        ui_error "${name} is only supported on OS X 10.7 Lion or later."
        return -code error "unsupported platform version"
    }
}

post-extract {
    foreach arch ${architectures} {
        set rust_root     ${worksrcpath}/build/stage0-${arch}
        set rust_platform ${arch}-apple-${os.platform}
        file mkdir ${rust_root}
        system "cp -r ${workpath}/rust-std-${ruststd_version}-${rust_platform}/rust-std-${rust_platform}/* ${rust_root}"
        system "cp -r ${workpath}/rustc-${rustc_version}-${rust_platform}/rustc/* ${rust_root}"
        system "cp -r ${workpath}/cargo-${cargo_version}-${rust_platform}/cargo/* ${rust_root}"
    }
}

configure.args      --enable-vendor \
                    --default-linker=${configure.cc} \
                    --disable-codegen-tests \
                    --disable-docs \
                    --release-channel=stable

# see https://trac.macports.org/ticket/56351
# see https://github.com/rust-lang/rust/issues/50220
if {[variant_isset universal]} {
    # LLVM is or will need to be universal
    set copy_llvm 1
} else {
    if {![catch {set result [active_variants llvm-${llvm_version} "universal" ""]}] && $result} {
        # LLVM was installed with universal variant
        set copy_llvm 1
    } else {
        set copy_llvm 0
    }
}

if {!${copy_llvm}} {
    # LLVM is NOT universal, so use installed version
    configure.args-append \
        --llvm-root=${prefix}/libexec/llvm-${llvm_version}
} else {
    # copy LLVM and thin static libraries
    # see https://trac.macports.org/ticket/56351
    # see https://github.com/rust-lang/rust/issues/50220
    if {[variant_isset universal]} {
        set archs ${universal_archs}
        foreach arch ${universal_archs} {
            lappend merger_configure_args(${arch}) \
                --llvm-root=${workpath}/llvm-${llvm_version}-${arch}
        }
    } else {
        set archs ${build_arch}
        configure.args-append \
            --llvm-root=${workpath}/llvm-${llvm_version}-${build_arch}
    }

    depends_extract-append  port:llvm-${llvm_version}

    post-extract {
        foreach arch ${archs} {
            system -W ${workpath} "cp -R ${prefix}/libexec/llvm-${llvm_version} ${workpath}/llvm-${llvm_version}-${arch}"
            fs-traverse f ${workpath}/llvm-${llvm_version}-${arch} {
                if {[file isfile $f] && [file type $f]!="link" && [file extension $f]==".a"} {
                    catch {system "lipo -thin ${arch} ${f} -o ${f}"}
                }
            }
        }
    }
}

if {![variant_isset universal]} {
    if {${build_arch} eq "i386"} {
        set arch_name i686
    } else {
        set arch_name ${build_arch}
    }
    set rust_platform ${arch_name}-apple-${os.platform}
    set rust_root     ${worksrcpath}/build/stage0-${arch_name}
    configure.args-append \
                   --build=${rust_platform} \
                   --local-rust-root=${rust_root}
} else {
    foreach arch ${universal_archs} {
        if {${arch} eq "i386"} {
            set arch_name i686
        } else {
            set arch_name ${build_arch}
        }
        set rust_platform ${arch_name}-apple-${os.platform}
        set rust_root     ${worksrcpath}/build/stage0-${arch_name}
        lappend merger_configure_args(${arch}) \
                  --build=${rust_platform} \
                  --local-rust-root=${rust_root}
    }
}

foreach arch ${architectures} {
    set rust_platform ${arch}-apple-${os.platform}
    configure.args-append \
                    --set=target.${rust_platform}.cc=${configure.cc} \
                    --set=target.${rust_platform}.cxx=${configure.cxx} \
                    --set=target.${rust_platform}.linker=${configure.cc}
}

configure.universal_args-delete --disable-dependency-tracking

post-configure {
    # the bootstrap call to rustc uses cc for the linker
    # place config file in the HOME directory
    # see https://trac.macports.org/wiki/UsingTheRightCompiler
    xinstall -d -m 0755 ${workpath}/.home/.cargo
    set config [open ${workpath}/.home/.cargo/config w]
    foreach arch ${architectures} {
        set rust_platform ${arch}-apple-${os.platform}
        puts ${config} "\[target.${rust_platform}\]"
        puts ${config} "linker = \"${configure.cc}\""
    }
    close ${config}
}

build.env           RUSTC_SAVE_ANALYSIS=api
build.args          VERBOSE=1 BOOTSTRAP_ARGS="-v -j${build.jobs}"

test.run            yes
test.target         check
test.args           VERBOSE=1

destroot.args       VERBOSE=1
if {${subport} eq ${name}} {
    if {![variant_isset universal]} {
        post-destroot {
            if {${build_arch} eq "i386"} {
                set arch_name i686
            } else {
                set arch_name ${build_arch}
            }
            set rust_platform ${arch_name}-apple-${os.platform}
        file copy \
            ${worksrcpath}/build/${rust_platform}/stage1-std/${rust_platform}/release/deps/save-analysis \
            ${destroot}${prefix}/lib/rustlib/${rust_platform}/analysis

        xinstall -d ${destroot}${prefix}/share/${name}
        xinstall -m 644 ${worksrcpath}/src/etc/ctags.rust \
            ${destroot}${prefix}/share/${name}
    }
    } else {
        merger-post-destroot {
            foreach arch ${universal_archs} {
                if {${arch} eq "i386"} {
                    set arch_name i686
                } else {
                    set arch_name ${arch}
                }
                set rust_platform ${arch_name}-apple-${os.platform}
                file copy \
                    ${worksrcpath}-${arch}/build/${rust_platform}/stage1-std/${rust_platform}/release/deps/save-analysis \
                    ${destroot}-${arch}${prefix}/lib/rustlib/${rust_platform}/analysis

                xinstall -d ${destroot}-${arch}${prefix}/share/${name}
                xinstall -m 644 ${worksrcpath}-${arch}/src/etc/ctags.rust \
                    ${destroot}-${arch}${prefix}/share/${name}
            }
        }
    }
}

livecheck.type      regex
livecheck.url       https://github.com/rust-lang/rust/tags
livecheck.regex     archive/(\[\\d\\.\]+).zip

subport rust-src {
    # remove dependencies
    depends_build
    depends_lib

    description     Source code for the rust programming language
    long_description ${description}

    use_configure no

    build {}

    destroot {
        xinstall -d ${destroot}${prefix}/share/rust
        move ${worksrcpath}/src ${destroot}${prefix}/share/rust/src

        # correct the permissions
        system -W ${destroot}${prefix}/share/rust "find . -type d -exec chmod 755 {} \\;"
        system -W ${destroot}${prefix}/share/rust "find . -type f -exec chmod 644 {} \\;"

        # delete the test directories (which for some god awful reason contains binaries)
        system -W ${destroot}${prefix}/share/rust "find . -type d -name test -print0 | xargs -0 rm -rf"
    }
}
