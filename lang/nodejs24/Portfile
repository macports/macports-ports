# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               compiler_blacklist_versions 1.0
PortGroup               legacysupport 1.1
PortGroup               openssl 1.0

configure.cxx_stdlib    libc++
compiler.cxx_standard   2020

name                    nodejs24
version                 24.3.0
revision                0

categories              lang net
license                 {MIT BSD}
maintainers             {ciserlohn @ci42} openmaintainer

description             Evented I/O for V8 JavaScript

long_description        Node's goal is to provide an easy way to build scalable network programs in JavaScript. \
                        Node is similar in design to and influenced by systems like Ruby's Event \
                        Machine or Python's Twisted. Node takes the event model a bit further-it \
                        presents the event loop as a language construct instead of as a library.

conflicts               nodejs8 nodejs10 nodejs12 nodejs14 nodejs16 nodejs18 nodejs20 nodejs22

homepage                https://nodejs.org/
master_sites            ${homepage}dist/v${version}
use_xz                  yes

checksums               rmd160  a2a4055ddb866ed614c9d16c6b290f9df957852b \
                        sha256  eb688ef8a63fda9ebc0b5f907609a46e26db6d9aceefc0832009a98371e992ed \
                        size    49969656

distname                node-v${version}

set py_ver              3.13
set py_ver_nodot        [string map {. {}} ${py_ver}]

depends_build-append    port:pkgconfig \
                        port:python${py_ver_nodot}

depends_lib-append      path:lib/pkgconfig/icu-uc.pc:icu \
                        port:zlib \
                        path:lib/libuv.dylib:libuv-devel \
                        port:c-ares \
                        port:nghttp2 \
                        port:brotli

if {${os.platform} eq "darwin" && ${os.major} < 11} {
#for snow leopard node is pretty weird, we have to include
#libc++abi.a or it will fail to link libnode.a at the end
    configure.cxx-append      ${prefix}/lib/libcxx/libc++abi.a
#also need to include SecItem.h
    configure.cppflags-append -I${developer_dir}/SDKs/MacOSX10.6.sdk/System/Library/Frameworks/Security.framework/Versions/A/Headers/
} else {
   configure.cppflags-append -DON_LION_OR_LATER
}

# all sub-10.15 macs will need macports-libcxx, which (hopefully at this time)
# will be updated to llvm-19
if { ${os.platform} eq "darwin" && ${os.major} < 19} {
    if {${configure.cxx_stdlib} eq "libc++"} {
        depends_lib-append        port:macports-libcxx
        configure.cxx-append      -Wl,-L${prefix}/lib/libcxx
    }
}

#only mojave and higher have SecTrustEvaluateError
if { ${os.major} > 17 } {
    configure.cppflags-append  -DON_MOJAVE_OR_LATER
}

if { ${os.major} < 17 }  {
#all systems beneath high sierra need this flag since there is no aligned
#allocation. see https://trac.macports.org/ticket/69347
    configure.cppflags-append -fno-aligned-allocation
}

#we guard against using the pthread qos API for Yosemite (10.10) and lower,
#since it is not available.
if { ${os.major} < 14 } {
    configure.cppflags-append -DNOT_ON_BROSEMITE_OR_LATER=1
}

# suppress a warning-as-error that shows up with newer clang compilers
if {[string match *clang* ${configure.compiler}]} {
    configure.cxx-append      -Wno-error=enum-constexpr-conversion

    #the ventura buildbot will fail because it doesn't recognise this flag
    #so we disable unknown warnings as errors for older compilers
    configure.cxx-append      -Wno-error=unknown-warning-option
}

#there is a weird regression in clang-20 that is presumably used
#by macos14, and this will cause a failure to compile. this issue
#does not arise on macos13 or 15, which means 14's snapshot had 
#bad timing:
# https://github.com/llvm/llvm-project/issues/131511
##or so i think, anyways. there is no other simple
#explanation for why macos13/15 compile nodejs24 without problems.
if { ${os.platform} eq "darwin" && ${os.major} == 23 } {
    depends_build-append     port:clang-19
    configure.compiler       macports-clang-19
}

proc rec_glob {basedir pattern} {
    set files [glob -directory $basedir -nocomplain -type f $pattern]
    foreach dir [glob -directory $basedir -nocomplain -type d *] {
        lappend files {*}[rec_glob $dir $pattern]
    }
    return $files
}

configure.python        ${prefix}/bin/python${py_ver}

patchfiles              patch-common.gypi.diff \
                        patch-add-pthread-qos-guard-for-older-macs.patch \
                        patch-use-sectrustevaluate-on-1013-and-lower.patch \
                        patch-add-guard-for-map-jit.patch

if { ${os.major} < 11 } {
    #snow leopard does not use getsectiondata, so we need to patch it
    #accordingly.
    patchfiles-append   patch-modify-getsectiondata-for-snow-leopard.patch
}

post-patch {
    foreach f [concat ${worksrcpath}/configure \
                   ${worksrcpath}/tools/gyp/gyp \
                   ${worksrcpath}/deps/v8/tools/objdump-v8 \
                   [rec_glob ${worksrcpath} *.py]] {
        reinplace -q "s|/usr/bin/env python3|${configure.python}|" ${f}
        reinplace -q "s|/usr/bin/env python|${configure.python}|" ${f}
    }
    foreach gypfile [rec_glob ${worksrcpath} *.gyp*] {
        reinplace -q "s|'python3'|'${configure.python}'|" ${gypfile}
        reinplace -q "s|'python'|'${configure.python}'|" ${gypfile}
    }
    if { ${os.platform} eq "darwin" && ${os.major} <= 17 } {
        # Officially nodejs requires Xcode 11 to compile, however, the
        # only item that Xcode 11 has that Xcode 10.1 doesn't is
        # os/signpost.h, so disable system instrumentation which uses
        # it. This provides support for macOS 10.13 which can only use
        # Xcode 10.1.
        reinplace -q "s|'v8_enable_system_instrumentation': 1|'v8_enable_system_instrumentation': 0|" ${worksrcpath}/tools/v8_gypfiles/features.gypi
    }
}

pre-configure {
    # Copy zlib headers here because we do not want to prepend the entire
    # ${prefix}/include to the include path (the build will then attempt to use
    # ICU headers)
    file mkdir ${workpath}/zlib-inc ${workpath}/cares-inc
    file copy ${prefix}/include/zconf.h ${prefix}/include/zlib.h \
        ${workpath}/zlib-inc/
    copy {*}[glob ${prefix}/include/*ares*.h] ${workpath}/cares-inc
}

# use the system libuv instead of the bundled version, for two reasons:
#   1. originally the macports libuv had fixes for older macs
#           (like 10.7 panicking when spawning with FD_CLOEXEC)
#   2. now libuv only supports 10.15 and up
#         -plus the macports libuv should have all features for the appropriate target.
#
configure.args-append   --shared-libuv
configure.args-append   --without-npm
configure.args-append   --with-intl=system-icu
configure.args-append   --shared-zlib
configure.args-append   --shared-zlib-includes=${workpath}/zlib-inc
configure.args-append   --shared-zlib-libpath=${prefix}/lib
configure.args-append   --shared-openssl
configure.args-append   --shared-openssl-includes=[openssl::include_dir]
configure.args-append   --shared-openssl-libpath=[openssl::include_dir]
configure.args-append   --shared-nghttp2
configure.args-append   --shared-nghttp2-includes=${prefix}/include/nghttp2
configure.args-append   --shared-nghttp2-libpath=${prefix}/lib
configure.args-append   --shared-cares
configure.args-append   --shared-cares-includes=${workpath}/cares-inc
configure.args-append   --shared-cares-libpath=${prefix}/lib
configure.args-append   --shared-brotli
configure.args-append   --shared-brotli-libpath=${prefix}/lib

# V8 only supports ARM and IA-32 processors
supported_archs         i386 x86_64 arm64

universal_variant       no

test.run                yes
test.cmd                ${build.cmd} -j${build.jobs}

switch $build_arch {
    i386 {
        configure.args-append   --dest-cpu=ia32
    }
    x86_64 {
        configure.args-append   --dest-cpu=x64
    }
   arm64 {
        configure.args-append   --dest-cpu=arm64
    }
}

# The Node.js docs says it requires "Xcode >= 13 (Apple LLVM >= 12)" [1]
# and accordingly the compile fails on macOS 11.7 Big Sur with Xcode's
# clang. However, bumping the requirement for clang 1400 or greater
# gets it to successfully compile.
# [1] https://github.com/nodejs/node/blob/v24.x/BUILDING.md#supported-toolchains
compiler.blacklist-append {clang < 1400}

build.args-append   CC=${configure.cc} \
                    CXX=${configure.cxx} \
                    CXX.host=${configure.cxx} \
                    CPP=${configure.cpp} \
                    CPPFLAGS="${configure.cppflags}" \
                    CFLAGS="${configure.cflags} ${configure.cppflags}" \
                    CXXFLAGS="${configure.cxxflags} ${configure.cppflags}" \
                    LDFLAGS="${configure.ldflags} [legacysupport::get_library_link_flags]" \
                    PYTHON=${configure.python} \
                    V=1

destroot {
    set bindir ${destroot}${prefix}/bin
    set libdir ${destroot}${prefix}/lib
    set libndir ${libdir}/node
    set libddir ${libdir}/dtrace
    set incdir ${destroot}${prefix}/include/node
    set docdir ${destroot}${prefix}/share/doc/${name}

    xinstall -d ${bindir}
    xinstall -d ${libdir}
    xinstall -d ${libndir}
    xinstall -d ${libddir}
    xinstall -d ${incdir}
    xinstall -d ${incdir}/uv
    xinstall -d ${docdir}

    # install binaries
    xinstall -m 755 -W ${worksrcpath} \
        out/Release/node \
        ${bindir}

    # install headers
    xinstall -m 644 {*}[glob ${worksrcpath}/src/*.h]                ${incdir}
    xinstall -m 644 {*}[glob ${worksrcpath}/deps/v8/include/*.h]    ${incdir}
    xinstall -m 644 {*}[glob ${worksrcpath}/deps/uv/include/*.h]    ${incdir}
    xinstall -m 644 {*}[glob ${worksrcpath}/deps/uv/include/uv/*.h] ${incdir}/uv
    xinstall -m 644 {*}[glob ${worksrcpath}/deps/cares/include/*.h] ${incdir}

    # install manpage
    xinstall -m 644 -W ${worksrcpath} \
        doc/node.1 \
        ${destroot}${prefix}/share/man/man1

    # install docs
    xinstall -m 644 -W ${worksrcpath} \
        CHANGELOG.md \
        CONTRIBUTING.md \
        GOVERNANCE.md \
        LICENSE \
        README.md \
        SECURITY.md \
        ${docdir}
}

livecheck.url       ${homepage}dist/
livecheck.type      regex
livecheck.regex     {v(24\.\d+\.\d+)}
