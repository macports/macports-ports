# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
PortSystem              1.0
PortGroup               cxx11 1.1

name                    reduce
version                 20181123

set version_dashes      [regsub {^(\d{4})(\d{2})(\d{2})$} ${version} {\1-\2-\3}]
set svnrev              4829
checksums               rmd160  cf81887b9bab0d6fd7b4e4ff88e3eb67223d1330 \
                        sha256  9787f31ef1ec23988f4c551388c6ffffe5b97ca290de6eed1c12dff1737763c8 \
                        size    261842669

categories              math
license                 BINARY BSD LGPL-2.1
platforms               darwin
maintainers             {gmail.com:mark.brethen @mbrethen} openmaintainer

description             REDUCE Computer Algebra System

long_description        REDUCE is a graphical Computer Algebra System. It \
                        allows for solving differential equations, integration,\
                        matrix manipulation and 3D plotting. It also contains a\
                        large number of additional packages.

homepage                https://reduce-algebra.sourceforge.io/
master_sites            sourceforge:project/reduce-algebra/snapshot_${version_dashes}
distname                Reduce-svn${svnrev}-src

pre-fetch {
    # the following lines generate a (spurious) port lint error
    if {${prefix} ne "/opt/local"} {
        ui_error "${name} is only supported in /opt/local at present."
        return -code error "unsupported configuration"
    }
}

universal_variant       no

patchfiles-append       patch-packages-plot-gnuintfc.red.diff      \
                        patch-generic-breduce-breduce.1.diff       \
                        patch-reduce-deployment-target.diff

post-patch {
    reinplace           "s|@PREFIX@|${prefix}|g" ${worksrcpath}/packages/plot/gnuintfc.red
    reinplace           "s|@PREFIX@|${prefix}|g" ${worksrcpath}/generic/breduce/breduce.1
}

use_parallel_build      no

depends_build-append    port:automake        \
                        port:autoconf        \
                        port:libtool         \
                        port:netpbm

if {[variant_isset doc]} {
    depends_build-append  \
                        port:dvipng                   \
                        port:texlive-fonts-extra      \
                        port:texlive-formats-extra    \
                        port:texlive-latex-extra      \
                        port:texlive-plain-generic    \
                        port:texlive-bin-extra        \
                        port:texlive-fonts-recommended
}

depends_lib-append      port:libedit         \
                        port:xorg-libXcursor \
                        port:Xft2            \
                        port:xorg-libXrandr  \
                        port:fontconfig      \
                        port:freetype        \
                        port:ncurses

depends_run-append      port:gnuplot         \
                        path:bin/latex:texlive

configure.ldflags-append \
                       -lintl

configure {
    # we have to run configure twice, once --with-csl
    # and another time --with-psl, prior to building
    # this generates two different build directories
    # and both are built during the build phase
    # we do it like this to make it clear what is happening here...
    
    # Technically portconfigure::configure_main is not part of the official exposed
    # MacPorts API, so theoretically it could change in the future and the port
    # would break... but it should work for now.

    configure.args-append --with-csl
    portconfigure::configure_main

    configure.args-replace --with-csl --with-psl
    portconfigure::configure_main
}

# Documentation is also re-generated in the tree so that regardless of
# history it should end up clean and organized.
post-build {
    if {[variant_isset doc]} {
        system -W ${worksrcpath}/doc/misc ${build.cmd}
        system -W ${worksrcpath}/doc/manual ${build.cmd}
    }
}

destroot {
    set builddir [exec ${worksrcpath}/scripts/findhost.sh [exec ${worksrcpath}/config.guess]]

    set cslbuilddir ${worksrcpath}/cslbuild/${builddir}
    set pslbuilddir ${worksrcpath}/pslbuild/${builddir}
    set genericdir  ${worksrcpath}/generic

    set sharedir    ${prefix}/share/${subport}
    set libexecdir  ${prefix}/libexec/${subport}
    set bindir      ${prefix}/bin
    set mandir      ${prefix}/share/man/man1

    # Create target directories
    xinstall -d \
        ${destroot}${sharedir}                   \
        ${destroot}${libexecdir}/csl             \
        ${destroot}${libexecdir}/psl/psl         \
        ${destroot}${libexecdir}/psl/red         \
        ${destroot}${applications_dir}/${subport}

    # Manual files
    copy                                  \
        ${filespath}/redcsl.1             \
        ${filespath}/redpsl.1             \
        ${genericdir}/newfront/redfront.1 \
        ${destroot}${mandir}

    ln -s ${prefix}/share/man/man1/redfront.1 ${destroot}${mandir}/rfcsl.1
    ln -s ${prefix}/share/man/man1/redfront.1 ${destroot}${mandir}/rfpsl.1

    # CSL files
    foreach d {reduce bootstrapreduce csl} {
        copy ${cslbuilddir}/csl/${d}.app ${destroot}${libexecdir}/csl
        ln -s ${prefix}/libexec/${subport}/csl/${d}.app \
            ${destroot}${applications_dir}/${subport}/${d}.app
    }

    copy ${filespath}/runcsl.sh ${destroot}${bindir}/redcsl
    copy ${filespath}/runbootstrapreduce.sh ${destroot}${bindir}/bootstrapreduce
    copy ${filespath}/runcsllisp.sh ${destroot}${bindir}/csl

    foreach f {redcsl bootstrapreduce csl} {
        file attributes ${destroot}${bindir}/${f} -permissions +x
    }

    copy ${cslbuilddir}/redfront/rfcsl ${destroot}${bindir}

    # PSL files
    copy {*}[glob ${pslbuilddir}/psl/*] ${destroot}${libexecdir}/psl/psl
    copy {*}[glob ${pslbuilddir}/red/*] ${destroot}${libexecdir}/psl/red
    copy ${filespath}/runpsl.sh ${destroot}${bindir}/redpsl
    file attributes ${destroot}${bindir}/redpsl -permissions +x

    # Note (ha ha) that rfpsl is built in the cslbuild part of the tree.
    copy ${cslbuilddir}/redfront/rfpsl ${destroot}${bindir}

    set docdir ${prefix}/share/doc/${subport}
    xinstall -d ${destroot}${docdir}
    xinstall -m 0644 -W ${worksrcpath}/csl/reduce.doc \
        BINARY-LICENSE.txt                            \
        BSD-LICENSE.txt                               \
        LGPL-2.1.txt                                  \
        ${destroot}${docdir}

    # install breduce
    copy ${worksrcpath}/generic/breduce/breduce ${destroot}/${libexecdir}
    copy ${worksrcpath}/packages/breduce/breduce.red ${destroot}/${libexecdir}
    ln -s ${prefix}/libexec/reduce/breduce ${destroot}${prefix}/bin/breduce
    copy ${worksrcpath}/generic/breduce/breduce.1 ${destroot}/${mandir}

    xinstall -m 0644 -W ${worksrcpath}/generic/breduce \
        breduce.bbl\
        breduce.pdf\
        breduce.tex                                    \
        ${destroot}${docdir}

    # Install documentation if requested
    if {[variant_isset doc]} {
        set docsrcdir       ${worksrcpath}/doc/manual
        set docmiscdir      ${worksrcpath}/doc/misc
        set docbasename     manual
        set extradocimagebasenames {bild cmsy10 gnuplotex turtleeg}

        xinstall -d ${destroot}${docdir}/html ${destroot}${docdir}/pdf

        copy                                \
            {*}[glob ${docmiscdir}/*.pdf]   \
            ${docsrcdir}/${docbasename}.pdf \
            ${destroot}${docdir}/pdf
        copy                                            \
            ${docsrcdir}/redlogo.png                    \
            {*}[glob ${docsrcdir}/${docbasename}*.html] \
            {*}[glob ${docsrcdir}/${docbasename}*.png]  \
            ${docsrcdir}/${docbasename}.css             \
            ${docsrcdir}/index.html                     \
            ${destroot}${docdir}/html
        foreach n ${extradocimagebasenames} {
            copy {*}[glob ${docsrcdir}/${n}*.png] ${destroot}${docdir}/html
        }
    }
}

variant doc description {Install HTML and PDF documentation} {}

livecheck.version   ${version_dashes}
livecheck.url       https://sourceforge.net/projects/reduce-algebra/files/
livecheck.regex     {snapshot_(\d{4}-\d{2}-\d{2})}
