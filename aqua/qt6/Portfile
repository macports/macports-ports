# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

name                qt6

categories          aqua
platforms           macosx
maintainers         {mcalhoun @MarcusCalhoun-Lopez} openmaintainer

# for OpenSSLException, see source and header files in src/network/ssl of qtbase
license             {LGPL-3 GPL-3 OpenSSLException}

homepage            https://www.qt.io

version             6.2.4
set middle_name     everywhere

PortGroup qt6_variables 1.0

# get Qt's version numbers
set branch          [join [lrange [split ${version} .] 0 1] .]
set qt_major        [lindex [split ${version} .] 0]

# see https://www.qt.io/blog/qt-6.0-released
compiler.c_standard 2011
compiler.cxx_standard 2017

# Save off the actual macports prefix for use late
set mp_prefix       ${prefix}
set isSQL_module    false

master_sites        https://download.qt.io/official_releases/qt/${branch}/${version}/submodules
# file sizes are significantly smaller using xz
if { ${subport} ne ${name} && ${subport} ne "${name}-docs" } {
    use_xz yes
}

conflicts qt3 qt3-mac
# conflict with all other version of qt6
foreach {qt_test_name qt_test_info} [array get available_qt_versions] {
    if {${name} ne ${qt_test_name}} {
        conflicts-append [lindex ${qt_test_info} 0]
    }
}

# MacPorts LLVM/Clang version to use when required.
set llvm_version 15

############################################################################### Modules Not Considered
#
# No qtactiveqt      (Windows Only)
# No qtandroidextras (Android Only)
# No qtwayland       (Linux Only)
# No qtx11extras     (X11 Only)
# No qtwinextras     (Windows Only)
#
###############################################################################

############################################################################### Finding Dependencies
#
# grep -r "%dependencies" *
# find ./ -name sync.profile
#
# grep -r qtCompileTest *
#
# find ./ -name config.tests
#
# find ./ -name Find\*.cmake
#
# grep -r qtHaveModule *
#
# grep -r packagesExist *
#
# port provides `find ./ -name \*.dylib -exec otool -L {} \; | grep /opt/local/libexec/qt6 | cut -d ' ' -f1` | cut -d : -f2 | sort -u
# port provides `find ./ -name \*.dylib -exec otool -L {} \; | grep /opt/local/lib/ | cut -d ' ' -f1` | cut -d : -f2 | sort -u
#
# https://code.qt.io/cgit/qt/qt6.git/tree/.gitmodules?h=6.2
#
###############################################################################

############################################################################### Module Format
#
# "Qt Module Name" {
#     {
#         checksum, rmd160
#         checksum, sha256
#         checksum, size
#     }
#     dependencies, build
#     dependencies, lib
#     dependencies, Qt module name
#     Qt components provided
#     included in "standard" installation of Qt (empty string is no, explanation string is yes)
#     variant overrides
#     revision number
#     license replacement
# }
#
# module info found at https://doc.qt.io/qt-6.2/qtmodules.html
#
###############################################################################
array set modules {
    qt3d {
        {
            0fbdabb59466c03bc622ce996a97e43bc9ea5d8f \
            ff4478d32c0fc0999690b73418b26974b723d5520abf4035b330002d962c1523
            104021044
        }
        ""
        "port:assimp"
        "qtbase qtdeclarative qtmultimedia qtshadertools"
        {"Qt 3D"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qt5compat {
        {
        cb840056d1fabef6145ac68c9a2ed1efb67eefbb \
        5de2b9e25bf7de161fbb88ecdd468ed1788bc899392fc05ed80aa590ebb352fa
        8198524
        }
        ""
        ""
        "qtbase qtdeclarative"
        {"Qt5 Compatibility"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtbase {
        {
            041294e299236903695a4f35449b0e59c758396a \
            d9924d6fd4fa5f8e24458c87f73ef3dfc1e7c9b877a5407c040d89e6736e2634
            46541252
        }
        "port:ninja port:pkgconfig"
        "port:assimp port:brotli path:bin/cmake:cmake path:bin/dbus-daemon:dbus port:double-conversion port:freetype
            path:lib/pkgconfig/glib-2.0.pc:glib2 path:lib/pkgconfig/harfbuzz.pc:harfbuzz
            port:hunspell path:lib/pkgconfig/icu-uc.pc:icu port:jasper path:include/turbojpeg.h:libjpeg-turbo port:libb2
            port:libiconv port:libpng port:md4c path:bin/node:nodejs16 path:lib/pkgconfig/libpcre2-posix.pc:pcre2
            port:tiff port:webp port:zlib path:lib/pkgconfig/libzstd.pc:zstd"

        ""
        {"Qt Core" "Qt GUI" "Qt Network" "Qt SQL" "Qt Test" "Qt Widgets" "Qt Concurrent" "Qt D-Bus" \
         "Qt OpenGL" "Qt Platform Headers" "Qt Print Support" "Qt XML"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtcharts {
        {
            1b7bc3b8323489e967c928ff29204e0cb8a638d9 \
            3c2e6267ef0fb5345c7737e3a12e09ce9ec09117792e78af9a8617f828b2249a
            4391060
        }
        ""
        ""
        "qtbase qtdeclarative qtmultimedia"
        {"Qt Charts"}
        "GPLv3 license only"
        "variant overrides: "
        "revision 2"
        "License: GPL-3"
    }
    qtconnectivity {
        {
            766c472112aa7bc89a40177e6d5da2caf588b602 \
            5db8c8de26c8561e8e349ad9650307bf7e08af4f00668447c53f3a8f7bd15d97
            1087096
        }
        ""
        ""
        "qtbase qtdeclarative"
        {"Qt Bluetooth" "Qt NFC"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtdeclarative {
        {
            2a50253c159a837046d1a33c65c986f1988a74c3 \
            fa6a8b5bfe43203daf022b7949c7874240ec28b9c4b7ebc52b2e5ea440e6e94f
            29475416
        }
        "port:python310"
        ""
        "qtbase qtsvg qtimageformats qtshadertools"
        {"Qt QML" "Qt Quick" "Qt Quick Layouts" "Qt Quick Widgets"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtimageformats {
        {
            5df81b2f5becde04c8accb0e9101ba697f7a8d83 \
            ee22e84866ed3fb39bdaf88f533851658919ee92dad56eb6da3d31c311a97e5c
            1846040
        }
        ""
        "path:lib/pkgconfig/jasper.pc:jasper port:libmng port:tiff port:webp"
        "qtbase"
        {"Qt Image Formats"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtmultimedia {
        {
            cdca1e04909cf8c9efde2033f7159739586249a0 \
            ea703487c21613fcc55dd0c3fb2dc8e1ae77003ae6212a60c85d5210b64832b6
            3795508
        }
        ""
        ""
        "qtbase qtdeclarative qtshadertools"
        {"Qt Multimedia" "Qt Multimedia Widgets"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtnetworkauth {
        {
            7203e279bcb4b2eb65791c1c9407a1f31db266d6 \
            53e96704d34403e89b05ef8b46edb039cd2bb3dc85d62bffc5ac0856ecee0dcc
            146148
        }
        ""
        ""
        "qtbase"
        {"Qt Network Authorization"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtpositioning {
        {
            e831cc791a9ec8b75cd59fb76f51e78f28664835 \
            075eb22c86c05fd2367bcdc34a29aec8871dd8463d7b73f2a77bf27384c885ff
            1492336 
        }
        ""
        ""
        "qtbase qtdeclarative qtserialport"
        {"Provides the ability to determine a position by using a variety of possible sources"}
        ""
        "variant overrides: "
        "revision 2"
        "License: {GPL-3}"
    }
    qtremoteobjects {
       {
            892fad53e65bd6a439b4702e5bf00a4a52ab9da5 \
            d23ab298d2ebf5b8437b0794af47266bb3048b0f4b733e0db1335a09fc18d883
            367336
       }
       ""
       ""
       "qtbase qtdeclarative"
       {"Qt Remote Objects"}
       ""
       "variant overrides: "
       "revision 2"
       "License: "
    }
    qtsensors {
        {
            718054cf865c7de873ad29083584a286280a55a1 \
            79d43e38431c93996f803c6f946856c5ab35a60f9c9438ccceaa312bdb4a6a06
            2109744
        }
        ""
        ""
        "qtbase qtdeclarative"
        {"Qt Sensors"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtserialbus {
        {
            2b0764da4ae9d8c7c4a68305eaf3f2c9a61dfb3b \
            33c15752f8e7f99cc49b9d3bf9eabd857bfcd649e37ada8bffceedc5a66a8a64
            380088
        }
        ""
        ""
        "qtbase qtserialport"
        {"Qt Serial Bus"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtserialport {
        {
            17d011c970ef8d353ef9023f43d0b6cffa470eec \
            8fdbfbb2aeee0e6400c90406927a784d3790bb0dfa7e5d7b9da7b2ded52bb744
            320492
        }
        ""
        ""
        "qtbase"
        {"Qt Serial Port"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtshadertools {
        {
            0513ce360d99dc01609ddb327f6bf9c4b7f91602 \
            20881824cba0c1396c0fe6b27d0f995a261070b68fa3629b1a188147d58933cc
            987564
        }
        ""
        ""
        "qtbase"
        {"Qt ShaderTools"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtsvg {
        {
            812b8590980228f96a861e674e4b993590beb1e6 \
            23ec4c14259d799bb6aaf1a07559d6b1bd2cf6d0da3ac439221ebf9e46ff3fd2
            1727160
        }
        ""
        ""
        "qtbase"
        {"Qt SVG"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qttools {
        {
            47864565591ba588c9eb7f44a974661fa90c535d \
            17f40689c4a1706a1b7db22fa92f6ab79f7b698a89e100cab4d10e19335f8267
            8664772
        }
        ""
        "port:clang-${llvm_version}"
        "qtbase qtdeclarative"
        {"Qt Designer" "Qt Help" "Qt UI Tools"}
        ""
        "variant overrides: "
        "revision 3"
        "License: "
    }
    qttranslations {
        {
            467b490bbddc8114a2d71aaeb7a0e82649b1e089 \
            bd1aac74a892c60b2f147b6d53bb5b55ab7a6409e63097d38198933f8024fa51
            1446208
        }
        ""
        "port:qt6-qttools"
        ""
        {"translation files"}
        ""
        "variant overrides: ~examples ~tests noarch ~docs"
        "revision 3"
        "License: "
    }
    qtwebchannel {
        {
            a1ba285374474a4b1c242493b4783ca7289f6682 \
            20ce2bbc21b6f1fb665d37887d4af7e761baa3a1c1b9cc0f550ede212a8d296d
            213476
        }
        ""
        ""
        "qtbase qtdeclarative qtwebsockets"
        {"Qt WebChannel"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
    qtwebsockets {
        {
            11718a93bc62e904d69d0ee9759136a02a9ea7bf \
            702f1e3248b256045d76898875bca51e70d8c452cbbff243e16c0ae5b962f964
            265912
        }
        ""
        ""
        "qtbase qtdeclarative"
        {"Qt WebSockets"}
        ""
        "variant overrides: "
        "revision 2"
        "License: "
    }
}

############################################################################### SQL Plugin Format
#
# {
#     variant name
#     dependency, library
#     include directory
#     library directory
#     library name
#     obsolete? (empty string is no)
# }
#
###############################################################################
array set sql_plugins {
    {sqlite SQLite "revision 0"} {
        {
            "sqlite3"
            "port:sqlite3"
            "${prefix}/include"
            "${prefix}/lib"
            "libsqlite3.dylib"
            ""
        }
    }
    {psql PostgreSQL "revision 0"} {
        {
            "postgresql13"
            "port:postgresql13"
            "${prefix}/include/postgresql13"
            "${prefix}/lib/postgresql13"
            "libpq.dylib"
            ""
        }
        {
            "postgresql12"
            "port:postgresql12"
            "${prefix}/include/postgresql12"
            "${prefix}/lib/postgresql12"
            "libpq.dylib"
            ""
        }
        {
            "postgresql11"
            "port:postgresql11"
            "${prefix}/include/postgresql11"
            "${prefix}/lib/postgresql11"
            "libpq.dylib"
            ""
        }
        {
            "postgresql10"
            "port:postgresql10"
            "${prefix}/include/postgresql10"
            "${prefix}/lib/postgresql10"
            "libpq.dylib"
            ""
        }
    }
    {mysql MySQL "revision 0"} {
        {
            "mariadb10_5"
            "port:mariadb-10.5"
            "${prefix}/include/mariadb-10.5/mysql"
            "${prefix}/lib/mariadb-10.5/mysql"
            "libmysqlclient_r.dylib"
            ""
        }
        {
            "mariadb10_4"
            "port:mariadb-10.4"
            "${prefix}/include/mariadb-10.4/mysql"
            "${prefix}/lib/mariadb-10.4/mysql"
            "libmysqlclient_r.dylib"
            ""
        }
        {
            "mariadb10_3"
            "port:mariadb-10.3"
            "${prefix}/include/mariadb-10.3/mysql"
            "${prefix}/lib/mariadb-10.3/mysql"
            "libmysqlclient_r.dylib"
            ""
        }
        {
            "mariadb10_2"
            "port:mariadb-10.2"
            "${prefix}/include/mariadb-10.2/mysql"
            "${prefix}/lib/mariadb-10.2/mysql"
            "libmysqlclient_r.dylib"
            ""
        }
        {
            "mysql8"
            "port:mysql8"
            "${prefix}/include/mysql8/mysql"
            "${prefix}/lib/mysql8/mysql"
            "libmysqlclient.dylib"
            ""
        }
    }
}

# because CPATH is set, pkgconfig does not include ${prefix}/lib and ${prefix}/include even when requested
# this means that files in ${prefix}/lib and ${prefix}/include are the *last* to be found
# this causes problems when there are files in ${worksrpath} that are unintentionally found instead
configure.env-append \
    PKG_CONFIG_ALLOW_SYSTEM_CFLAGS=1 \
    PKG_CONFIG_ALLOW_SYSTEM_LIBS=1
build.env-append \
    PKG_CONFIG_ALLOW_SYSTEM_CFLAGS=1 \
    PKG_CONFIG_ALLOW_SYSTEM_LIBS=1

if { ${os.platform} eq "darwin" && ${os.major} < 18 } {
    known_fail  yes
    pre-fetch {
        ui_error "${subport} requires macOS 10.14 or later"
        return -code error "incompatible OS version"
    }
}

foreach {module module_info} [array get modules] {

    set revision_string [string trim [lindex ${module_info} 7]]
    set revision_string [string range ${revision_string} 1+[string last " " ${revision_string}] end]

    subport ${name}-${module} {

        distname        ${module}-${middle_name}-src-${version}

        revision ${revision_string}

        # see https://bugreports.qt.io/browse/QTBUG-35514
        build.target

        checksums                                        \
            rmd160  [lindex [lindex ${module_info} 0] 0] \
            sha256  [lindex [lindex ${module_info} 0] 1] \
            size    [lindex [lindex ${module_info} 0] 2]

        set license_string [lindex ${module_info} 8]
        set license_string [string range ${license_string} 1+[string first ":" ${license_string}] end]
        set license_string [string trim  ${license_string}]

        if { ${license_string} ne "" } {
            license {*}${license_string}
        }

        foreach deps [lindex ${module_info} 1] {
            depends_build-append [subst ${deps}]
        }

        foreach deps [lindex ${module_info} 2] {
            depends_lib-append [subst ${deps}]
        }

        foreach qtdeps [lindex ${module_info} 3] {
            depends_lib-append port:${name}-${qtdeps}
        }

        description     Tools and Module(s) for Qt Tool Kit ${qt_major}

        set modules_provided_list [lindex ${module_info} 4]
        if { [llength ${modules_provided_list}] == 1 } {
            set modules_provided_join [lindex ${modules_provided_list} 0]
        } elseif { [llength ${modules_provided_list}] == 2 } {
            set modules_provided_join [join ${modules_provided_list} " and "]
        } else {
            set modules_provided_join [join [list [join [lrange ${modules_provided_list} 0 end-1] ", "] [lindex ${modules_provided_list} end]] ", and "]
        }

        long_description  "Tools and Module(s) for Qt Tool Kit ${qt_major}: ${modules_provided_join}"


        if { ${module} eq "qtbase" } {
            # this subport uses configure script (NOT qmake)
            PortGroup               qt6 1.0
            PortGroup               developerversion 1.0
            PortGroup               openssl 1.0

            # use ninja for the build/installation
            build.cmd               "ninja"
            build.post_args-append  -v
            destroot.target         install
            # ninja needs the DESTDIR argument in the environment
            destroot.destdir
            destroot.env-append     DESTDIR=${destroot}

            if { ${os.platform} ne "darwin" } {
                pre-fetch {
                    ui_warn "${subport} is untested on \"${os.platform}\"."
                }
            }

            if { [variant_isset universal] } {
                pre-fetch {
                    ui_warn "Multiple architectures is not a Reference Configuration for Qt."
                    ui_warn "See https://doc.qt.io/qt-6/supported-platforms.html#reference-configurations"
                }
            }

            minimum_developerversions 18 9

            # see https://trac.macports.org/ticket/63805#comment:13
            patchfiles-append patch-sdk-no-stderr.diff

            # Allow building with macOS 10.14 SDK
            # see https://trac.macports.org/ticket/64345
            patchfiles-append patch-macos-10.14-sdk.diff

            #-----------------------------------------------------------------------------
            # qtbase is used for:
            #    1) building qtbase
            #    2) building MacPorts projects via qt-cmake
            #    3) building end-user projects
            #
            # 1 & 2 require consistency with the MacPorts environment
            # 3 requires consistency with the default Qt installation
            #
            # 2 can be achieved via environment variables
            #    (e.g. QMAKE_MACOSX_DEPLOYMENT_TARGET=${macosx_deployment_target})
            #
            # the only way 3 can be achieved is if no changes are made to the build system
            #
            # the following is an attempt to achieve 1 without destroying 3
            #-----------------------------------------------------------------------------

            # save default spec files
            post-extract {
                copy ${worksrcpath}/mkspecs ${worksrcpath}/mkspecs-save
            }

            # respect MacPorts build variables
            patchfiles-append patch-mkspecs.diff

            # respect configure.compiler
            if { ${configure.compiler} eq "clang" } {
                # let xargs find correct compiler (default behaviour)
                post-patch {
                    # let xargs find correct compiler
                    reinplace \
                        "s|__MACPORTS_CC__|clang|g" \
                        ${worksrcpath}/mkspecs/common/clang.conf

                    reinplace \
                        "s|__MACPORTS_CXX__|clang++|g" \
                        ${worksrcpath}/mkspecs/common/clang.conf
                }
            } else {
                post-patch {
                    reinplace \
                        "s|__MACPORTS_CC__|${configure.cc}|g" \
                        ${worksrcpath}/mkspecs/common/clang.conf

                    reinplace \
                        "s|__MACPORTS_CXX__|${configure.cxx}|g" \
                        ${worksrcpath}/mkspecs/common/clang.conf
                }
            }

            if { [string match macports-clang-* ${configure.compiler}] && [vercmp ${xcodeversion} "7.0"] >= 0 } {
                # non-Xcode clang does not seem to be able to understand tbd files
                # for an explanation of tbd files, see
                #    https://stackoverflow.com/questions/31450690/why-xcode-7-shows-tbd-instead-of-dylib
                # see https://trac.macports.org/ticket/53151
                pre-fetch {
                    ui_error "This configuration is known to fail"
                    ui_error "See https://trac.macports.org/ticket/53151"
                    ui_error "As a workaround, do not set configure.compiler manually"
                    return -code error "incompatible configuration"
                }
            }

            post-patch {
                # respect configure.cxx_stdlib
                reinplace \
                    "s|__MACPORTS_CXX_STDLIB__|${configure.cxx_stdlib}|g" \
                    ${worksrcpath}/mkspecs/common/clang-mac.conf

                # respect macosx_deployment_target
                reinplace \
                    "s|__MACPORTS_DEPLOYMENT_TARGET__|${macosx_deployment_target}|g" \
                    ${worksrcpath}/mkspecs/common/macx.conf

                # respect configure.optflags
                reinplace \
                    "s|__MACPORTS_OPTFLAGS__|${configure.optflags}|g" \
                    ${worksrcpath}/mkspecs/common/gcc-base.conf
            }

            platform darwin {
                # qt calls xcrun to find the SDK to use, so make sure this call will succeed

                ui_debug "qt6 Portfile: the initial SDK value is: macosx${configure.sdk_version}"
                # first try for a system-specific SDK
                if {[string first . ${configure.sdk_version}] == -1 && ${configure.sdkroot} ne ""} {
                    # xcrun doesn't like major version only (e.g. macosx11), try to find a full version
                    set sdks [lsort -command vercmp -decreasing [glob -nocomplain [file rootname ${configure.sdkroot}]*.sdk]]
                    configure.sdk_version [string map {MacOSX ""} [file rootname [file tail [lindex $sdks 0]]]]
                    ui_debug "using possibly more specific SDK version: ${configure.sdk_version}"
                }
                ui_debug "qt6 Portfile: testing for system-specific SDK:"
                if {[catch {exec -ignorestderr env DEVELOPER_DIR=${configure.developer_dir} /usr/bin/xcrun --sdk macosx${configure.sdk_version} --find ld  > /dev/null 2>@1}]} {

                    ui_debug "qt6 Portfile: system-specific SDK was not found, looking for generic SDK."
                    # if no specific sdk found, check for a generic macosx sdk
                    if {[catch {exec -ignorestderr env DEVELOPER_DIR=${configure.developer_dir} /usr/bin/xcrun --sdk macosx --find ld > /dev/null 2>@1}]} {
                        pre-fetch {
                            ui_error "${subport}: no usable SDK can be found"
                            return -code error "no usable SDK can be found"
                        }
                    } else {
                        ui_debug "${subport}: using generic macosx SDK as macosx${configure.sdk_version} was not found"
                        configure.sdk_version
                    }
                } else {
                    ui_debug "qt6 Portfile: system-specific SDK was found."
                }
                ui_debug "qt6 Portfile: the final SDK value is: macosx${configure.sdk_version}"
            }

            # respect configure.sdk_version
            post-patch {
                reinplace \
                    "s|__MACPORTS_MAC_SDK__|macosx${configure.sdk_version}|g" \
                    ${worksrcpath}/mkspecs/common/macx.conf
            }

            # use MacPorts X11
            post-patch {
                foreach spec {macx-clang} {
                    reinplace \
                        "s|__MACPORTS_PREFIX__|${prefix}|g" \
                        ${worksrcpath}/mkspecs/${spec}/qmake.conf
                }
            }

            # return modified spec files to the default values
            post-build {
                foreach conf {clang.conf macx.conf clang-mac.conf gcc-base.conf} {
                    move -force ${worksrcpath}/mkspecs-save/common/${conf} ${worksrcpath}/mkspecs/common/${conf}
                }
                foreach spec {macx-clang macx-g++ macx-icc} {
                        move -force ${worksrcpath}/mkspecs-save/${spec}/qmake.conf ${worksrcpath}/mkspecs/${spec}/qmake.conf
                    }
                }

            # --prefix is not recognized.
            configure.pre_args-delete       --prefix=${prefix}

            # worksrcpatch is not necessary since using the qt configure script
            configure.post_args-delete     ${worksrcpath}

            configure.post_args-append     -DFEATURE_pkg_config=ON

            # Installation options:
            #-extprefix     SYSROOT/PREFIX
            #-hostprefix    EXTPREFIX
            #-libexecdir    ARCHDATADIR/libexec
            #-hostbindir    HOSTPREFIX/bin
            #-hostlibdir    HOSTPREFIX/lib
            configure.args-append                      \
                -prefix         ${qt_dir}              \
                -bindir         ${qt_bins_dir}         \
                -headerdir      ${qt_includes_dir}     \
                -libdir         ${qt_libs_dir}         \
                -libexecdir     ${qt_bins_dir}         \
                -archdatadir    ${qt_archdata_dir}     \
                -plugindir      ${qt_plugins_dir}      \
                -qmldir         ${qt_qml_dir}          \
                -datadir        ${qt_data_dir}         \
                -docdir         ${qt_docs_dir}         \
                -translationdir ${qt_translations_dir} \
                -sysconfdir     ${qt_sysconf_dir}      \
                -examplesdir    ${qt_examples_dir}     \
                -testsdir       ${qt_tests_dir}        \
                -hostdatadir    ${qt_host_data_dir}

            # Configure options:
            configure.args-append \
                -release          \
                -opensource       \
                -confirm-license  \
                -shared           \
                -accessibility

            # SQL Options
            foreach driver { db2 ibase mysql oci odbc psql sqlite } {
                configure.args-append -no-sql-${driver}
            }

            configure.args-append "-platform ${qt_qmake_spec}"

            # use -Os instead of -O2
            configure.args-append -optimize-size

            # Set CMake variables (similar to what cmake portgroup does)
            # to allow using ccache (and possibly compiler selection
            # if .tbd file support was not an issue; see comment above)
            configure.post_args --
            configure.post_args-append \
                -DCMAKE_C_COMPILER=[option configure.cc] \
                -DCMAKE_CXX_COMPILER=[option configure.cxx] \
                -DCMAKE_OBJC_COMPILER=[option configure.objc] \
                -DCMAKE_OBJCXX_COMPILER=[option configure.objcxx]
            if {[option configure.ccache]} {
                # Do not use `configure.args-append -ccache`
                # or `configure.post_args-append -DQT_USE_CCACHE=1`
                # since that affects installed files.
                configure.post_args-append \
                    -DCMAKE_C_COMPILER_LAUNCHER=${prefix}/bin/ccache \
                    -DCMAKE_CXX_COMPILER_LAUNCHER=${prefix}/bin/ccache \
                    -DCMAKE_OBJC_COMPILER_LAUNCHER=${prefix}/bin/ccache \
                    -DCMAKE_OBJCXX_COMPILER_LAUNCHER=${prefix}/bin/ccache
            }

            configure.args-append \
                -no-testcocoon    \
                -force-pkg-config

            # Third Party Libraries:
            configure.args-append   \
                -system-zlib        \
                -no-mtdev           \
                -no-journald        \
                -no-syslog          \
                -system-libpng      \
                -system-libjpeg     \
                -system-freetype    \
                -system-harfbuzz    \
                -system-pcre        \
                -no-openssl         \
                -no-libproxy        \
                -glib               \
                -no-gtk

            # Additional options:
            configure.args-append       \
                {-make tools}           \
                {-nomake examples}      \
                {-nomake tests}         \
                -gui                    \
                -widgets                \
                -no-rpath               \
                -cups                   \
                -no-evdev               \
                -no-tslib               \
                -icu                    \
                -no-fontconfig          \
                -no-pch                 \
                -no-ltcg                \
                -dbus-linked            \
                -no-use-gold-linker     \
                -no-separate-debug-info \
                -no-xcb                 \
                -no-eglfs               \
                -no-gbm                 \
                -no-directfb            \
                -no-linuxfb             \
                -no-kms                 \
                -no-libinput            \
                -no-system-proxies      \
                -no-libudev             \
                -no-egl

            # do not opportunistically enable Vulkan support
            # (TODO: is Vulkan support desirable?)
            # see https://trac.macports.org/ticket/62104
            configure.args-append \
                -no-feature-vulkan

            # MacOS/iOS options:
            configure.args-append       \
                -framework              \
                -securetransport

            # Qt builds part of the system using environment provided by MacPorts.
            # It builds the rest using its own internal environment.
            # For consistency, clear MacPorts environment.
            #configure.cxx_stdlib
            proc portconfigure::should_add_stdlib {} {return false}
            configure.sdkroot
            configure.cc_archflags
            configure.cxx_archflags
            configure.objc_archflags
            configure.objcxx_archflags
            configure.ld_archflags
            configure.cppflags
            configure.cflags
            configure.cxxflags
            configure.objcflags
            configure.objcxxflags
            configure.ldflags
            configure.pipe  no
            configure.march
            configure.mtune
            configure.universal_ldflags
            configure.universal_cflags
            configure.universal_cxxflags
            configure.universal_cppflags

            # configure script uses gawk if it can find it,
            #    so require it for consistency
            depends_build-append port:gawk

            variant tests description {Enable tests} {
                configure.args-replace {-nomake tests} {-make tests}
            }

            variant examples description {Build examples} {
                configure.args-replace {-nomake examples} {-make examples}
            }

            variant debug description {Build both release and debug libraries} {
                configure.args-replace -release -debug-and-release
            }

            variant openssl description {Use OpenSSL instead of Secure Transport} {
                configure.args-delete -securetransport -no-openssl
                configure.args-append -openssl-linked

                # see https://trac.macports.org/ticket/51358
                #     for why not a path dependency
                depends_lib-append path:lib/libssl.dylib:openssl

                # configure has issues locating openssl files
                configure.pre_args-append                            \
                    -DOPENSSL_ROOT_DIR=[openssl::install_area]       \
                    -DOPENSSL_INCLUDE_DIR=[openssl::include_dir]     \
                    -DOPENSSL_LIBRARIES=[openssl::install_area]/lib  \
                    -DOPENSSL_LIBS='-L[openssl::install_area]/lib -lssl -lcrypto'
            }
            default_variants-append +openssl

        } else {
            # these subports use qt-cmake
            PortGroup   qt6 1.0
            PortGroup   active_variants 1.1

            depends_build-append \
                        path:bin/cmake:cmake \
                        port:ninja

            # use "qt-configure-module" to configure the build
            configure.cmd   ${qt_configure_module_cmd}

            # Attempting to match module configure instructions from here: https://www.qt.io/blog/qt-6-build-system
            # --prefix is not recognized.
            configure.dir   ${workpath}/build

            configure.pre_args-delete   --prefix=${prefix}
            configure.pre_args-append   ${worksrcpath}

            # build using using cmake
            build.dir       ${workpath}/build
            build.cmd       cmake --build ${build.dir}
            build.target

            destroot.target
            destroot.cmd    cmake --install ${build.dir}

            # ninja needs the DESTDIR argument in the environment
            destroot.destdir
            destroot.env-append DESTDIR=${destroot}

            # special case (needs to go before compiler options are read from)
            if { ${module} eq "qtdeclarative" } {
                PortGroup compiler_blacklist_versions 1.0
                # Xcode 10.3 compiler segfaults
                compiler.blacklist-append {clang < 1100}
            }

            # Set CMake variables (similar to what cmake portgroup does)
            # to allow using ccache and controlling compiler selection
            configure.post_args --
            configure.post_args-append \
                -DCMAKE_C_COMPILER=[option configure.cc] \
                -DCMAKE_CXX_COMPILER=[option configure.cxx] \
                -DCMAKE_OBJC_COMPILER=[option configure.objc] \
                -DCMAKE_OBJCXX_COMPILER=[option configure.objcxx]
            if {[option configure.ccache]} {
                configure.post_args-append -DQT_USE_CCACHE=1
            }

            # determine which variants are to be turned off
            set request_examples true
            set request_tests    true
            set def_var          ""

            if { [lsearch -exact [lindex ${module_info} 6] "~universal"] != -1 } {
                universal_variant no
            }

            if { [lsearch -exact [lindex ${module_info} 6] "noarch"] != -1 } {
                supported_archs   noarch
                universal_variant no
            }

            if { [lsearch -exact [lindex ${module_info} 6] "~debug"] != -1 } {
                qt6.debug_variant no
            }

            if { [lsearch -exact [lindex ${module_info} 6] "~examples"] != -1 } {
                set request_examples false
            }

            if { [lsearch -exact [lindex ${module_info} 6] "~tests"] != -1 } {
                set request_tests    false
            }

            if { [lsearch -exact [lindex ${module_info} 6] "++examples"] != -1 } {
                set request_examples true
                lappend def_var "+examples"
            }

            if { ${request_examples} } {
                variant examples description {Build examples} {}
            }

            if { ${request_tests} } {
                variant tests description {Enable tests} {}
            }

            if { ${def_var} ne "" } {
                default_variants-append ${def_var}
            }

            # accommodating variant request varies depending on how qtbase was built
            pre-configure {

                # determine if qmake builds examples by default (set via variants)
                if {[active_variants ${name}-qtbase examples ""]} {
                    set base_examples true
                } else {
                    set base_examples false
                }

                # determine if qmake runs tests by default (set via variants)
                if {[active_variants ${name}-qtbase tests ""]} {
                    set base_tests true
                } else {
                    set base_tests false
                }

                # determine if the user wants to build examples
                if { [variant_exists examples] && [variant_isset examples] } {
                    set this_examples true
                } else {
                    set this_examples false
                }

                # determine if the user wants to run tests
                if { [variant_exists tests] && [variant_isset tests] } {
                    set this_tests true
                } else {
                    set this_tests false
                }

                # determine if qmake's default and user requests are compatible; override qmake if necessary
                if { ${this_examples} && !${base_examples}  } {
                    configure.args-append "QT_BUILD_PARTS+=\"examples\""
                }

                if { !${this_examples} && ${base_examples}  } {
                    configure.args-append "QT_BUILD_PARTS-=\"examples\""
                }

                if { ${this_tests} && !${base_tests}  } {
                    configure.args-append "QT_BUILD_PARTS+=\"tests\""
                }

                if { !${this_tests} && ${base_tests}  } {
                    configure.args-append "QT_BUILD_PARTS-=\"tests\""
                }
            }

            ###############################################################################
            # Special Cases
            ###############################################################################
        }
    }
}

# see https://doc.qt.io/qt-6/sql-driver.html for info on building SQL Database Drivers
foreach {sql_names sql_info} [array get sql_plugins] {
    set driver          [lindex ${sql_names} 0]
    set dbms            [lindex ${sql_names} 1]
    set revision_string [lindex ${sql_names} 2]
    set revision_string [string range ${revision_string} 1+[string last " " ${revision_string}] end]

    subport ${name}-${driver}-plugin {
        PortGroup       cmake 1.1

        distname        qtbase-${middle_name}-src-${version}

        revision ${revision_string}

        checksums                                          \
            rmd160  [lindex [lindex $modules(qtbase) 0] 0] \
            sha256  [lindex [lindex $modules(qtbase) 0] 1] \
            size    [lindex [lindex $modules(qtbase) 0] 2]

        depends_build-append  port:${name}-qtbase

        description       ${dbms} Database Driver for Qt Tool Kit ${qt_major}
        long_description  ${dbms} Database Driver for Qt Tool Kit ${qt_major}

        # qtbase already creates the symlinks for the sql plugins
        set isSQL_module             true
        cmake.install_prefix         ${qt_dir}

        # use "qt-configure-module" to configure the build
        configure.cmd                ${qt_cmake_cmd}
        configure.dir                ${workpath}/build

        # need to specify ninja for the build environment
        cmake.generator         Ninja

        # if there is more than one version of the database system, create variants for each version
        if { [llength ${sql_info}] > 1 } {

            set any_variant_set false

            foreach variant_info ${sql_info} {

                set varName [lindex ${variant_info} 0]

                # find every other variant so it can be marked as conflicting
                set conflicts_list ""
                foreach variant_info2 ${sql_info} {
                    set varName2 [lindex ${variant_info2} 0]

                    if { ${varName} ne ${varName2} } {
                        lappend conflicts_list ${varName2}
                    }
                }

                # get only the numbers from the name
                regexp {[0-9].} ${varName} varVer

                variant ${varName} conflicts ${conflicts_list} description "use version ${varVer} of ${dbms}" {}

                # check if any variant has been set
                if { [variant_isset ${varName}] } {
                    set any_variant_set true
                }
            }

            # ensure at least one variant is set
            if { !${any_variant_set} } {
                default_variants-append +[lindex [lindex ${sql_info} 0] 0]
            }
        }

        set use_name [string toupper ${driver}]

        # In QT6, the include and library paths are now SQL database specific
        # with dbms name embedded in the argument name
        foreach variant_info ${sql_info} {
            set varName [lindex ${variant_info} 0]

            if { ( [variant_exists ${varName}] && [variant_isset ${varName}] ) || [llength ${sql_info}]==1 } {
                depends_lib-append [lindex ${variant_info} 1]

                configure.args-append                                              \
                    [subst -D${dbms}_INCLUDE_DIR=\"[lindex ${variant_info} 2]\"]   \
                    [subst -DCMAKE_INCLUDE_PATH=\"[lindex ${variant_info} 2]\"]    \
                    [subst -D${dbms}_LIBRARY=\"[lindex ${variant_info} 3]/[lindex ${variant_info} 4]\"] \
                    [subst -DCMAKE_LIBRARY_PATH=\"[lindex ${variant_info} 3]\"]
            }
        }

        post-destroot {
            # qt6-qtbase installs some Sql cmake files by default, we want to skip installing these
            # as of 6.2.1 this is necessary for:
            if { [ file exists ${qt_dir}/lib/cmake/Qt6Sql/Qt6QSQLiteDriverPluginConfig.cmake ] } {
                set cmake_SQL_dir ${destroot}${qt_dir}/lib/cmake/Qt6Sql
                file delete -force ${cmake_SQL_dir}/Qt6QSQLiteDriverPluginAdditionalTargetInfo.cmake
                file delete -force ${cmake_SQL_dir}/Qt6QSQLiteDriverPluginConfig.cmake
                file delete -force ${cmake_SQL_dir}/Qt6QSQLiteDriverPluginConfigVersion.cmake
                file delete -force ${cmake_SQL_dir}/Qt6QSQLiteDriverPluginTargets-release.cmake
                file delete -force ${cmake_SQL_dir}/Qt6QSQLiteDriverPluginTargets.cmake
            }
        }

        # Specify the actual location of the sqldrivers
        cmake.source_dir        ${cmake.source_dir}/src/plugins/sqldrivers/
    }
}

if { ${subport} eq ${name} } {
    # the main port is Meta-port to install various modules

    revision    0

    description         Qt Tool Kit ${qt_major}
    long_description    Qt Tool Kit: A cross-platform framework \
        (headers, data, and libraries) for writing \
        cross-platform GUI-based applications.

    master_sites
    distfiles
    use_configure       no
    supported_archs     noarch
    installs_libs       no
    universal_variant   no

    build {}

    # create a dummy file so the port can be successfully activated
    destroot {
        xinstall -d -m 0755 ${destroot}${prefix}/share/doc/${subport}
        set docfile [open ${destroot}${prefix}/share/doc/${subport}/README.txt "w"]
        puts ${docfile} "Meta-port for ${name}"
        puts ${docfile} "${long_description}"
        close ${docfile}
    }

    foreach {module module_info} [array get modules] {
        if { [lindex ${module_info} 6] eq "" } {
            depends_run-append port:${name}-${module}
        }
    }

    depends_run-append port:${name}-qtbase port:${name}-sqlite-plugin
}

set python_framework ""
set depends_check ""
if { [info exists depends_build] } {
    set depends_check "${depends_check} ${depends_build}"
}
if { [info exists depends_lib] } {
    set depends_check "${depends_check} ${depends_lib}"
}

foreach deps ${depends_check} {
    if { [string first ":python310" ${deps}] >= 0 } {
        # If Qt components use Python, ensure that MacPorts python310 is used
        set python_framework ${frameworks_dir}/Python.framework/Versions/3.10
    }
}
if { ${python_framework} ne "" } {
    configure.env-append PATH=${python_framework}/bin:$env(PATH)
    build.env-append     PATH=${python_framework}/bin:$env(PATH)
}
unset python_framework

post-destroot {
    # see #44204
    foreach f [glob -nocomplain -tails -directory ${destroot}${qt_libs_dir} *.framework] {
        set framework [file rootname ${f}]

        set include_list   [split ${qt_includes_dir}   '/']
        set framework_list [split ${qt_libs_dir} '/']

        while {[llength ${include_list}] && [llength ${framework_list}]} {
            set var_include   [lindex $include_list   0]
            set var_framework [lindex $framework_list 0]

            if { ${var_include} ne ${var_framework} } {
                break
            }

            # remove first element from list
            set include_list   [lreplace ${include_list} 0 0]
            set framework_list [lreplace ${framework_list} 0 0]
        }

        xinstall -d -m 0755 ${destroot}${qt_includes_dir}
        ln -s [string repeat ../ [llength ${include_list}]][join ${framework_list} /]/${f}/Headers ${destroot}${qt_includes_dir}/${framework}
    }

    # .app and non-.app programs are both put in qt_bins_dir
    # put a link of any .app programs in the ${qt_apps_dir}
    if { ${qt_bins_dir} ne ${qt_apps_dir} } {
        xinstall -d -m 0755 ${destroot}${qt_apps_dir}
        foreach app [glob -nocomplain -tails -directory ${destroot}${qt_bins_dir} *.app] {
            ln -s ${qt_bins_dir}/${app} ${destroot}${qt_apps_dir}
        }
    }

    # put configuration files in places they will be found automatically
    if { ${qt_libs_dir} ne "${mp_prefix}/lib" } {
        # put link to pkgconfig files in place where pkgconfig will find it
        # most Qt 6 pkgconfig files begin with Qt6, so link should not conflict with any other Qt installations
        xinstall -d -m 0755 ${destroot}${mp_prefix}/lib/pkgconfig
        foreach pcfile [glob -nocomplain -tails -directory ${destroot}${qt_libs_dir}/pkgconfig *.pc] {
            ln -s ${qt_libs_dir}/pkgconfig/${pcfile} ${destroot}${mp_prefix}/lib/pkgconfig
        }

        # put link to cmake files in place where cmake will find it
        # most Qt 6 cmake directories begin with Qt6, so link should not conflict with any other Qt installations
        xinstall -d -m 0755 ${destroot}${mp_prefix}/lib/cmake
        foreach cmakedir [glob -type d -nocomplain -tails -directory ${destroot}${qt_libs_dir}/cmake *] {
            # only symlink the top folder, checking to see if the symlink already exists
            if { ![ file exists ${mp_prefix}/lib/cmake/${cmakedir} ] } {
                ln -s ${qt_libs_dir}/cmake/${cmakedir} ${destroot}${mp_prefix}/lib/cmake/${cmakedir}
            }
        }
        # grab the cmake files in qt_bins_dir too
        foreach cmakefile [glob -type f -nocomplain -tails -directory ${destroot}${qt_bins_dir}/ *.cmake] {
            if { ![ file exists ${mp_prefix}/lib/cmake/${cmakefile} ] } {
                ln -s ${qt_bins_dir}/${cmakefile} ${destroot}${mp_prefix}/lib/cmake/${cmakefile}
            }
        }
    }
}

if {${subport} eq ${name}} {
    livecheck.type      regex
    livecheck.url       https://download.qt.io/archive/qt/${branch}/
    livecheck.regex     (\\d+(\\.\\d+)+)
} else {
    livecheck.type      none
}

foreach {component component_info} [array get qt6pg::qt6_component_lib] {

    set suffix [lindex ${component_info} 3]

    set qt_version            ${version}
    set qt_version_introduced [lindex ${component_info} 0]
    set qt_version_removed    [lindex ${component_info} 1]

    if { [vercmp ${qt_version_removed} ${qt_version}] <= 0 } {
        subport ${name}-${component}${suffix} {
            PortGroup obsolete 1.0

        }
    }
}

proc rglob { dirpath patterns } {
    set rlist {}
    foreach fpath [glob -nocomplain -types f -directory ${dirpath} {*}${patterns}] {
        lappend rlist ${fpath}
    }
    foreach dir [glob -nocomplain -types d -directory ${dirpath} *] {
        lappend rlist {*}[rglob ${dir} ${patterns}]
    }
    return ${rlist}
}
