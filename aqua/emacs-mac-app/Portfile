# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           bitbucket 1.0

set emacs_version   27.1
set emacs_mac_ver   8.1

bitbucket.setup     mituharu emacs-mac emacs-${emacs_version}-mac-${emacs_mac_ver}
name                emacs-mac-app
version             ${emacs_mac_ver}
revision            0
categories          aqua editors
maintainers         {amake @amake} openmaintainer

description         Emacs Mac port

long_description    ${name} is "Mac port" addition to GNU Emacs ${emacs_version}. \
                    This provides a native GUI support for Mac OS X 10.6 - macOS 10.14.

platforms           darwin
license             GPL-3+

checksums           rmd160  8c5f4110b4b541ab0b2117d79aa4d3249e70f33b \
                    sha256  cae62f036be66ebcdada4d4563859b70825d2a8c91edd24e9e9794ece9f30dee \
                    size    43579411

depends_lib         port:ncurses \
                    port:libxml2 \
                    port:gnutls \
                    port:dbus \
                    port:lcms2 \
                    port:gmp \
                    port:jansson

patchfiles          patch-src_emacs.c.diff

if {${subport} eq ${name}} {
    conflicts       emacs-mac-app-devel

    patchfiles-append \
                    patch-apple-silicon.diff
}

post-patch {
    reinplace "s|__PREFIX__|${prefix}|" ${worksrcpath}/src/emacs.c
}

universal_variant   no

use_autoreconf      yes
autoreconf.cmd      ./autogen.sh
autoreconf.args     all

depends_build       port:autoconf \
                    port:automake \
                    port:libtool \
                    port:pkgconfig \
                    port:texinfo

# The Mac port uses CoreText instead of HarfBuzz
configure.args      --with-mac \
                    --enable-mac-app=${applications_dir} \
                    --with-modules \
                    --without-rsvg \
                    --without-imagemagick \
                    --without-harfbuzz \
                    --with-json

if {${os.major} >= 11 && ${os.platform} eq "darwin"} {
    configure.cflags-append -fobjc-arc
}

subport ${name}-devel {
    # set emacs_version   27.1

    bitbucket.setup mituharu emacs-mac a8bd8997b4284dd9ab966e1e0eef87bf0f0086aa
    version         20201112
    revision        0

    long_description \
        ${name} is "Mac port" addition to GNU Emacs ${emacs_version}. \
        This provides a native GUI support for Mac OS X 10.6 - macOS 10.14.

    checksums       rmd160  7059c33f72d05210a5c7543f373110d17041e2ce \
                    sha256  dbd2e59c1825475e0d091d7ed13e0cf98ff14334fa23ab9f6145937e3c95c3f4 \
                    size    43580094

    conflicts       emacs-mac-app

    # Bitbucket doesn't seem to have a good way to get the latest commit on a
    # branch
    livecheck.type  none
}

post-destroot {
    # move files into the app bundle.
    # https://github.com/railwaycat/emacs-mac-port/blob/master/build-emacs.app.sh
    set app_dir ${destroot}${applications_dir}/Emacs.app/Contents/Resources
    foreach d [glob ${destroot}${prefix}/share/emacs/${emacs_version}/*] {
        move ${d} ${app_dir}
    }
    move ${destroot}${prefix}/share/info ${app_dir}
    move ${destroot}${prefix}/share/man  ${app_dir}
    move ${destroot}${prefix}/var        ${app_dir}
    move ${destroot}${prefix}/bin        ${app_dir}/../MacOS
    xinstall -d ${app_dir}/../MacOS/libexec
    foreach f [glob ${destroot}${prefix}/libexec/emacs/${emacs_version}/*/*] {
        move ${f} ${app_dir}/../MacOS/libexec
    }
    # Remove everything from ${destroot}${prefix} except ${app_dir}
    if {0 != [string first ${destroot}${prefix} ${app_dir}]} {
        # ${app_dir} is not a subdirectory of ${prefix}
        delete ${destroot}${prefix}
    } else {
        foreach d [glob ${destroot}${prefix}/*] {
            if {0 != [string first ${d} ${app_dir}]} {
                delete ${d}
            }
        }
    }

    # make the application binary a hard link.
    delete ${app_dir}/../MacOS/Emacs
    file link -hard ${app_dir}/../MacOS/Emacs ${app_dir}/../MacOS/bin/emacs

    # install site-start.el.
    set site_lisp ${destroot}${applications_dir}/Emacs.app/Contents/Resources/site-lisp
    xinstall -d ${site_lisp}
    file copy ${filespath}/site-start.el ${site_lisp}
    reinplace "s|__PREFIX__|${prefix}|g" ${site_lisp}/site-start.el

    # rename the app bundle to avoid a conflict with emacs-app.
    move ${destroot}${applications_dir}/Emacs.app \
         ${destroot}${applications_dir}/EmacsMac.app
}

variant multitty description {Enable Multi-TTY support} {
    patchfiles-append   patch-multi-tty
}

variant rsvg description {Enable librsvg bindings} {
    depends_lib-append     port:librsvg
    configure.args-delete  --without-rsvg
    configure.args-append  --with-rsvg
}

variant imagemagick description {Enable ImageMagick bindings} {
    depends_lib-append     port:ImageMagick
    configure.args-delete  --without-imagemagick
    configure.args-append  --with-imagemagick
}

livecheck.version   ${version}
