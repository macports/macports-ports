# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           bitbucket 1.0

set emacs_version   27.2
set emacs_mac_ver   8.3

bitbucket.setup     mituharu emacs-mac emacs-${emacs_version}-mac-${emacs_mac_ver}
name                emacs-mac-app
version             ${emacs_mac_ver}
revision            0
categories          aqua editors
maintainers         {amake @amake} openmaintainer

description         Emacs Mac port

long_description    ${name} is the \"Mac port\" of GNU Emacs ${emacs_version}. \
                    This provides a native GUI with tight OS integration.

platforms           darwin
license             GPL-3+

checksums           rmd160  3544f2607e6002e066bcdd967c88aa7162ef6451 \
                    sha256  4263e4e1897a950c16252b58c9af8ae4d369b25790873d0777c9547a89b90198 \
                    size    43674348

depends_lib         port:ncurses \
                    port:libxml2 \
                    port:gnutls \
                    port:dbus \
                    port:lcms2 \
                    port:gmp \
                    port:jansson

patchfiles          patch-src_emacs.c.diff

if {${subport} eq ${name}} {
    conflicts       emacs-mac-app-devel

    patchfiles-append patch-apple-silicon-arch.diff

    post-destroot {
        # move files into the app bundle.
        # https://github.com/railwaycat/emacs-mac-port/blob/master/build-emacs.app.sh
        set app_dir ${destroot}${applications_dir}/Emacs.app/Contents/Resources
        foreach d [glob ${destroot}${prefix}/share/emacs/${emacs_version}/*] {
            move ${d} ${app_dir}
        }
        move ${destroot}${prefix}/share/info ${app_dir}
        move ${destroot}${prefix}/share/man  ${app_dir}
        move ${destroot}${prefix}/var        ${app_dir}
        move ${destroot}${prefix}/bin        ${app_dir}/../MacOS
        xinstall -d ${app_dir}/../MacOS/libexec
        foreach f [glob ${destroot}${prefix}/libexec/emacs/${emacs_version}/*/*] {
            move ${f} ${app_dir}/../MacOS/libexec
        }
        # Remove everything from ${destroot}${prefix} except ${app_dir}
        if {0 != [string first ${destroot}${prefix} ${app_dir}]} {
            # ${app_dir} is not a subdirectory of ${prefix}
            delete ${destroot}${prefix}
        } else {
            foreach d [glob ${destroot}${prefix}/*] {
                if {0 != [string first ${d} ${app_dir}]} {
                    delete ${d}
                }
            }
        }

        # make the application binary a hard link.
        delete ${app_dir}/../MacOS/Emacs
        file link -hard ${app_dir}/../MacOS/Emacs ${app_dir}/../MacOS/bin/emacs

        # rename the app bundle to avoid a conflict with emacs-app.
        move ${destroot}${applications_dir}/Emacs.app \
            ${destroot}${applications_dir}/EmacsMac.app

        post_destroot_common
    }
}

post-patch {
    reinplace "s|__PREFIX__|${prefix}|" ${worksrcpath}/src/emacs.c
}

universal_variant   no

use_autoreconf      yes
autoreconf.cmd      ./autogen.sh
autoreconf.args     all

depends_build       port:autoconf \
                    port:automake \
                    port:libtool \
                    port:pkgconfig \
                    port:texinfo

# The Mac port uses CoreText instead of HarfBuzz
configure.args      --with-mac \
                    --enable-mac-app=${applications_dir} \
                    --with-modules \
                    --without-rsvg \
                    --without-imagemagick \
                    --without-harfbuzz \
                    --with-json

if {${os.major} >= 11 && ${os.platform} eq "darwin"} {
    configure.cflags-append -fobjc-arc
}

subport ${name}-devel {
    set emacs_version   28.0.91
    set date            2022-01-19

    version         [string map {- {}} ${date}]
    revision        1

    long_description \
        ${name} is the \"Mac port\" of GNU Emacs ${emacs_version}. \
        This provides a native GUI with tight OS integration.

    if {${os.major} <= 13} {
        known_fail  yes
    }

    fetch.type      git
    git.url         --shallow-since=${date}T00:00:00 --branch work https://bitbucket.org/mituharu/emacs-mac.git
    git.branch      379a15c67ad253bf0bd014ba25c135c293719562

    # --shallow-since needs a newer version of git than on some older systems,
    # so use MacPorts version
    depends_fetch-append port:git
    git.cmd         ${prefix}/bin/git

    conflicts       emacs-mac-app

    patchfiles-append patch-lib-src_Makefile.in.diff

    configure.args-append --enable-mac-self-contained
    configure.args-replace --enable-mac-app=${applications_dir} --enable-mac-app=${worksrcpath}

    destroot.destdir {}

    post-destroot {
        move ${worksrcpath}/Emacs.app ${destroot}${applications_dir}/EmacsMac.app

        post_destroot_common
    }

    variant nativecomp description {Builds emacs with native compilation support} {
        depends_lib-append       port:gcc11

        configure.args-append    --with-native-compilation

        compiler.cpath-prepend   ${prefix}/include/gcc11
        compiler.library_path-prepend \
                                 ${prefix}/lib/gcc11

        build.args-append        NATIVE_FULL_AOT=1
    }

    variant metal description {Enable experimental Metal support} {
        configure.args-append  --with-mac-metal
    }

    default_variants-append +nativecomp

    # Bitbucket doesn't seem to have a good way to get the latest commit on a
    # branch
    livecheck.type  none
}

proc post_destroot_common {} {
    global destroot applications_dir site_lisp filespath prefix worksrcpath

    # install site-start.el.
    set site_lisp ${destroot}${applications_dir}/EmacsMac.app/Contents/Resources/site-lisp
    xinstall -d ${site_lisp}
    file copy ${filespath}/site-start.el ${site_lisp}
    reinplace "s|__PREFIX__|${prefix}|g" ${site_lisp}/site-start.el

    # install emacs-module.h header file
    xinstall -m 0755 -d ${destroot}${prefix}/include/emacs-mac
    xinstall -m 0644 ${worksrcpath}/src/emacs-module.h \
        ${destroot}${prefix}/include/emacs-mac
}

variant multitty description {Enable Multi-TTY support} {
    patchfiles-append   patch-multi-tty
}

variant rsvg description {Enable librsvg bindings} {
    depends_lib-append     path:lib/pkgconfig/librsvg-2.0.pc:librsvg
    configure.args-delete  --without-rsvg
    configure.args-append  --with-rsvg
}

variant imagemagick description {Enable ImageMagick bindings} {
    depends_lib-append     port:ImageMagick
    configure.args-delete  --without-imagemagick
    configure.args-append  --with-imagemagick
}

livecheck.version   ${version}
