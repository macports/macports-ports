# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0
PortGroup           cxx11 1.1
PortGroup           muniversal 1.0
PortGroup           mpi 1.0
PortGroup           active_variants   1.1
PortGroup           compiler_blacklist_versions 1.0
mpi.setup

github.setup        OSGeo gdal 2.3.2 v
categories          gis
license             MIT BSD
platforms           darwin

maintainers         {vince @Veence} openmaintainer

description         GDAL - Geospatial Data Abstraction Library

long_description    GDAL is a translator library for raster geospatial \
                    data formats that is released under an X/MIT style \
                    Open Source license. As a library, it presents a \
                    single abstract data model to the calling application \
                    for all supported formats. The related OGR library \
                    (which lives within the GDAL source tree) provides a \
                    similar capability for simple features vector data.

homepage            http://www.gdal.org/

checksums           rmd160  50fe790adb52523a525cbae99d5b1790bcb5cdc8 \
                    sha256  9f0f61fedb7880d26947e2bb3c54885639c603edd53421aed0a7eeb150c6728f \
                    size    21577147

worksrcdir          ${worksrcpath}/gdal
patchfiles          patch-gdalwarpkernel_opencl_cpp.diff \
                    patch-gdalwarpkernel_opencl_h.diff \
                    patch-apps_GNUmakefile.diff \
                    patch-ogr_p_h.diff \
                    patch-ogr_api_cpp.diff \
                    patch-scripts-GNUmakefile.diff

platform darwin {
    # see https://trac.macports.org/ticket/55752
    if {${os.major} < 12} {
        patchfiles-append   MacTypes.diff
    }

    # see https://trac.macports.org/ticket/56517
    if {[string match *clang* ${configure.cxx}]} {
        configure.ldflags-append -stdlib=${configure.cxx_stdlib}
    }
}

# As suggested by Ryan in #56908
compiler.blacklist-append {clang < 800}

depends_build-append \
                    port:bash-completion \
                    port:pkgconfig

depends_lib-append  port:zlib \
                    port:zstd \
                    port:libpng \
                    port:giflib \
                    port:proj \
                    port:qhull \
                    port:sqlite3 \
                    port:spatialite \
                    port:curl \
                    port:charls

set PYINST          ""
set OPENCLBASE      "/System/Library/Frameworks/OpenCL.framework/"

configure.universal_args-delete --disable-dependency-tracking

configure.args-append \
                    --with-local=yes \
                    --with-libz=yes \
                    --with-zstd=yes \
                    --without-liblzma \
                    --with-png=yes \
                    --with-libtiff=internal \
                    --with-geotiff=internal \
                    --with-jpeg=internal\
                    --with-charls=yes \
                    --with-gif=yes \
                    --with-proj=${prefix}/lib/proj5 \
                    --with-qhull=yes \
                    --with-pam \
                    --with-sqlite3=yes \
                    --with-spatialite=${prefix} \
                    --with-curl=${prefix}/bin/curl-config \
                    --with-geos=${prefix}/bin/geos-config \
                    --with-expat=${prefix} \
                    --without-pg \
                    --without-grass \
                    --without-libgrass \
                    --without-cfitsio \
                    --without-netcdf \
                    --without-ogdi \
                    --without-fme \
                    --without-hdf4 \
                    --without-hdf5 \
                    --without-jasper \
                    --without-openjpeg \
                    --without-ecw \
                    --without-kakadu \
                    --without-mrsid \
                    --without-jp2mrsid \
                    --without-msg \
                    --without-bsb \
                    --without-oci \
                    --without-grib \
                    --without-mysql \
                    --without-ingres \
                    --without-xerces \
                    --without-odbc \
                    --without-idb \
                    --without-sde \
                    --without-opencl \
                    --without-perl \
                    --without-python \
                    --without-poppler \
                    --mandir=${prefix}/share/man

# Always choose internal libjson code (#44098)
configure.args-append   --with-libjson-c=internal

# for all platforms without C++11 support
# this is just a suboptimal approximation (doesn't hold for gcc)
if {${configure.cxx_stdlib} eq "libc++"} {
    configure.args-append   --with-cpp14
}

pre-configure {
    global worksrcpath_dirs
    if { ![variant_isset universal] } {
        set worksrcpath_dirs ${worksrcpath}
    } else {
        foreach arch ${universal_archs_to_use} {
            lappend worksrcpath_dirs ${worksrcpath}-${arch}
        }
    }
}

post-configure {
    foreach w ${worksrcpath_dirs} {
        reinplace -E "s|(INST_PYMOD).*|\\1\t=\t${PYINST}|" \
            ${w}/GDALmake.opt
    }
}

# Set target to none
build.target

variant lto description {Enable Link Time Optimization} {
    configure.args-append   --enable-lto
}

variant lzma \
    description {Enable LZMA (7Z) compression support} {
    depends_lib-append      port:lzma
    configure.args-delete   --without-liblzma
    configure.args-append   --with-liblzma=yes
}

variant grass description {Enable GRASS 7 GIS I/O} {
    if {[catch {lindex [registry_active grass7] 0}]} {
        ui_error "The grass variant requires grass 7 to be installed."
        ui_error "Since grass7 depends on gdal, please first install gdal WITHOUT the grass variant."
        ui_error "Then install grass7, then reinstall gdal with the grass variant."
        #return -code error "Missing GRASS 7." 
    } 

    configure.args-delete   --without-grass
    configure.args-append   --with-grass=${prefix}/share/grass-7.4.1
}

variant mrsid description {Enable MrSID file format} {
    depends_lib-append      port:geoexpress-sdk
    configure.args-delete   --without-mrsid
    configure.args-delete   --without-jp2mrsid
    configure.args-append   --with-mrsid=${prefix}/share/Geo_DSDK/Raster_DSDK
    configure.args-append   --with-jp2mrsid
    configure.args-append   --with-mrsid_lidar=${prefix}/share/Geo_DSDK/Lidar_DSDK
}

variant ecw description {Enable ECW file format} {
    configure.args-delete   --without-ecw
    configure.args-append   --with-ecw=${prefix}/lib/ECW

    if {${configure.cxx_stdlib} eq "libc++" } {
        patchfiles-append       patch-configure-ecw-libcxx.diff
    } else {
        patchfiles-append       patch-configure-ecw-libstdcxx.diff
    }

    pre-configure {
        if {(![file exists ${prefix}/lib/ECW]) && \
        (![file exists /Intergraph/ERDASEcwJpeg2000SDK5.1.1/Desktop_Read-Only])} {
            ui_error "
****
* The Intergraph ECW SDK version 5.1.1 is needed to
* compile gdal with the ecw variant. It must be installed separately.
* Please download the macOS version of the SDK
* 'ERDAS ECW/JP2 SDK v5.1.1 (MacOSX)' from
* http://download.intergraph.com/
* Choose to install it in ${prefix}/lib/ECW.
* Then try to install GDAL again.
****"
            return -code error "ECW SDK 5.1.1 not installed."
        }

        if {(![file exists ${prefix}/lib/ECW]) && \
            ([file exists /Intergraph/ERDASEcwJpeg2000SDK5.1.1/Desktop_Read-Only])} {

          ui_error "
****
* Please create symbolic links from the /Intergraph directory to
* ${prefix}/lib/ECW, by executing at the prompt:
* sudo ln -s /Intergraph/ERDASEcwJpeg2000SDK5.1.1/Desktop_Read-Only \
                  ${prefix}/lib/ECW
* Or reinstall the ECW SDK in ${prefix}/lib/ECW directly.
* Then try again.
****"
            return -code error "Symlinks missing."
        }
    }
}

variant hdf4 description {Enable HDF4 file format} {
    depends_lib-append      port:hdf4
    configure.args-delete   --without-hdf4
    configure.args-append   --with-hdf4=${prefix}
}

variant hdf5 description {Enable HDF5 file format} {

    if {![catch {set result [active_variants hdf5 openmpi]}]} {
        if {$result} {
            if {![variant_isset openmpi]} {
                return -code error "HDF5 has openmpi enabled, please use the same variant."
            }
        }
    }

    if {![catch {set result [active_variants hdf5 openmpi-devel]}]} {
        if {$result} {
            if {![variant_isset openmpi-devel]} {
                return -code error "HDF5 has openmpi_devel enabled, please use the same variant."
            }
        }
    }

    if {![catch {set result [active_variants hdf5 mpi]}]} {
        if {$result} {
            if {![variant_isset mpi]} {
                return -code error "HDF5 has mpi enabled, please use the same variant."
            }
        }
    }

    if {![catch {set result [active_variants hdf5 mpich-devel]}]} {
        if {$result} {
            if {![variant_isset mpich-devel]} {
                return -code error "HDF5 has mpich-devel enabled, please use the same variant."
            }
        }
    }

    if {[variant_isset mpich] || [variant_isset mpich-devel] || \
        [variant_isset openmpi] || [variant_isset openmpi_devel]} {
        
        mpi.enforce_variant     hdf5
    }   

    depends_lib-append      port:hdf5
    configure.args-delete   --without-hdf5
    configure.args-append   --with-hdf5=${prefix}
}

variant netcdf description {Enable NetCDF file format} {
    if {[variant_isset mpich] || [variant_isset mpich-devel] || \
        [variant_isset openmpi] || [variant_isset openmpi_devel]} {
    
        mpi.enforce_variant     netcdf
    }

    depends_lib-append      port:netcdf
    configure.args-delete   --without-netcdf
    configure.args-append   --with-netcdf=${prefix}
}

variant grib description {Enable GRIB (weather) file format} {
    configure.args-delete   --without-grib
}

#JPEG-2K
variant jasper \
    description {Enable JasPer JPEG-2000 format support} {
    depends_lib-append      port:jasper
    configure.args-delete   --without-jasper
    configure.args-append   --with-jasper=${prefix}/include
}

variant openjpeg \
    description {Enable OpenJPEG JPEG-2000 format support} {
    depends_lib-append      port:openjpeg
    configure.args-delete   --without-openjpeg
    configure.args-append   --with-openjpeg=${prefix}/include/openjpeg-2.3
}

variant xerces description {Enable xerces XML support for GML file format} {
    depends_lib-append      path:include/xercesc/util/XercesVersion.hpp:xercesc3
    configure.args-delete   --without-xerces
    configure.args-append   --with-xerces=${prefix}
}

# Database variants
set postgresql_suffixes {95 96 10}

set portsgresql_variants {}
foreach s ${postgresql_suffixes} {
    lappend portsgresql_variants postgresql${s}
}

foreach s ${postgresql_suffixes} {
    set p postgresql${s}
    set v [string index ${s} 0].[string index ${s} 1]
    set i [lsearch -exact ${portsgresql_variants} ${p}]
    set c [lreplace ${portsgresql_variants} ${i} ${i}]
    variant ${p} description "Enable PostgreSQL ${v} support" conflicts {*}${c} "
        depends_lib-append      port:${p}
        configure.args-delete   --without-pg
        configure.args-append   --with-pg=${prefix}/lib/postgresql${s}/bin/pg_config
    "
}

variant mysql5 conflicts mysql57 description {Enable MySQL 5 support} {
    depends_lib-append      path:bin/mysql_config5:mysql5
    configure.args-delete   --without-mysql
    configure.args-append   --with-mysql=${prefix}/lib/mysql5/bin/mysql_config
}

variant mysql57 conflicts mysql5 description {Enable MySQL 5.7 support} {
    depends_lib-append      port:mysql57
    configure.args-delete   --without-mysql
    configure.args-append   --with-mysql=${prefix}/lib/mysql57/bin/mysql_config
}

variant odbc description {Enable ODBC support} {
    depends_lib-append      port:unixODBC
    configure.args-delete   --without-odbc
    configure.args-append   --with-odbc=${prefix}
}

variant poppler description {Enable poppler support} {
    depends_lib-append      port:poppler
    configure.args-delete   --without-poppler
    configure.args-append   --with-poppler=${prefix}
}

variant cfitsio description {Enable fits support} {
    depends_lib-append      port:cfitsio
    configure.args-delete   --without-cfitsio
    configure.args-append   --with-cfitsio
}
# OpenCL
variant opencl description {Use OpenCL for parallel computations} {
    configure.args-delete   --without-opencl
    configure.args-append   --with-opencl
    configure.args-append   --with-opencl-include=${OPENCLBASE}/Headers
    configure.args-append   --with-opencl-lib='-framework OpenCL'
}

configure.optflags  -DGDAL_COMPILATION

# Proj version
variant proj4 description {Use Proj4 4.9 instead of Proj4 5+} {

    depends_lib-delete      port:proj
    depends_lib-append      port:proj4
    configure.args-delete   --with-proj=${prefix}/lib/proj5
    configure.args-append   --with-static-proj4=${prefix}/lib/proj49
}

# Perf variant to optimize code
variant perf description {Optimize for speed} {
    configure.optflags      -O3 -pipe -march=native -DGDAL_COMPILATION
}


set jpeg2k 0
foreach jpeg2kVariant {jasper openjpeg ecw} {
    if {[variant_isset ${jpeg2kVariant}]} {
        incr jpeg2k 1
    }
}
if {${jpeg2k} > 1} {
    notes-append "Multiple drivers are able to open JPEG-2000 datasets. See https://trac.osgeo.org/gdal/wiki/ConfigOptions#GDAL_SKIP."
}

# Postconfigure hack to avoid reading ${prefix}/*.h include files before
# GDAL own include directories

post-configure {
    file copy -force ${filespath}/template-ed-GDALmake_opt $env(TMPDIR)/ed-GDALmake_opt
    reinplace -E "s|@PREFIX@|${prefix}|" $env(TMPDIR)/ed-GDALmake_opt
    foreach w ${worksrcpath_dirs} {
        system "/bin/ed -s ${w}/GDALmake.opt < $env(TMPDIR)/ed-GDALmake_opt"
    }
    file delete $env(TMPDIR)/ed-GDALmake_opt
}

post-destroot {
    xinstall -m 755 -d ${destroot}${prefix}/share/doc/${name}
    xinstall -m 644 -W ${worksrcpath} HOWTO-RELEASE NEWS VERSION \
        ${destroot}${prefix}/share/doc/${name}

    if {[variant_isset ecw]} {
          foreach f [list \
            bin/gdal_contour \
            bin/gdal_grid \
            bin/gdal_rasterize \
            bin/gdal_translate \
            bin/gdaladdo \
            bin/gdalbuildvrt \
            bin/gdaldem \
            bin/gdalenhance \
            bin/gdalinfo \
            bin/gdallocationinfo \
            bin/gdalmanage \
            bin/gdalserver \
            bin/gdalsrsinfo \
            bin/gdaltindex \
            bin/gdaltransform \
            bin/gdalwarp \
            bin/nearblack \
            bin/ogr2ogr \
            bin/ogrinfo \
            bin/ogrlineref \
            bin/ogrtindex \
            bin/testepsg \
            lib/libgdal.20.dylib] \
            {
                if {${configure.cxx_stdlib} eq "libc++"} {
                    system "install_name_tool -change \
                        /Users/jenkins/hudson/workspace/RL_5.1.1_ECWJP2SDK_OSX/Master/libNCSEcw.dylib ${prefix}/lib/ECW/redistributable/libc++/libNCSEcw.dylib \
                        ${destroot}${prefix}/${f}"
                } else {
                    system "install_name_tool -change \
                        /Users/jenkins/hudson/workspace/RL_5.1.1_ECWJP2SDK_OSX/Master/libNCSEcw.dylib ${prefix}/lib/ECW/redistributable/libstdc++/libNCSEcw.dylib \
                        ${destroot}${prefix}/${f}"
                }
            }
    }

}

livecheck.type  regex
livecheck.url   http://download.osgeo.org/gdal/CURRENT
livecheck.regex ${name}-(\\d+(?:\\.\\d+)*)\\.tar
