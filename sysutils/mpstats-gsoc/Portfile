PortSystem          1.0

name                mpstats-gsoc
version             0.1.8
categories          sysutils macports
license             BSD
platforms           darwin
supported_archs     noarch
maintainers         {arjun @arjunsalyan} openmaintainer
description         Temporary port to enable stats submissions for macports-webapp
long_description \
   This is a copy of macports-ports/mpstats with different \
   configuration. The purpose of this port is to \
   temporarily enable stats submissions for macports-webapp \

homepage            https://frozen-falls-98471.herokuapp.com
distfiles

set launchd_dir     ${prefix}/etc/${startupitem.location}/${startupitem.uniquename}/

startupitem.create   no
startupitem.type     launchd
startupitem.autostart \
                    yes

extract.mkdir       yes
extract {
    xinstall -m 644 -W ${filespath} mpstats-gsoc.tcl mpstats-gsoc.plist.default ${worksrcpath}
}

configure {
    reinplace "s|@PREFIX@|${prefix}|g" \
        ${worksrcpath}/mpstats-gsoc.tcl \
        ${worksrcpath}/mpstats-gsoc.plist.default
    reinplace "s|@LABEL@|${startupitem.uniquename}|g" \
        ${worksrcpath}/mpstats-gsoc.plist.default
}

build {}

destroot {
    xinstall -m 755 \
        ${worksrcpath}/mpstats-gsoc.tcl \
        ${destroot}${prefix}/libexec/mpstats-gsoc

    xinstall -m 755 -d \
        ${destroot}${launchd_dir}
    xinstall -m 444 \
        ${worksrcpath}/mpstats-gsoc.plist.default \
        ${destroot}${launchd_dir}${startupitem.plist}.default

    xinstall -m 755 -d \
        ${destroot}${prefix}/etc/macports
    xinstall -m 444 \
        ${filespath}/stats-gsoc.conf \
        ${destroot}${prefix}/etc/macports/stats-gsoc.conf

    # install the plist, if startupitem.install is set
    if {[getuid] == 0 && ${startupitem.install}} {
        xinstall -m 755 -d ${destroot}/Library/${startupitem.location}
        # note this symlink *will* be broken at destroot time; we'll place the
        # correct file there on activation
        ln -sf "${launchd_dir}${startupitem.plist}" "${destroot}/Library/${startupitem.location}"
    } else {
        ln -sf ${launchd_dir}${startupitem.plist} ${destroot}${prefix}/etc/${startupitem.location}
    }
}

post-activate {
    set uuidfile ${prefix}/var/macports/stats-gsoc-uuid
    if {![file exists ${uuidfile}] || [file size ${uuidfile}] == 0} {
        set uuid [exec /usr/bin/uuidgen]
        set fd [open ${uuidfile} w]
        puts $fd $uuid
        close $fd
    }

    xinstall -m 644 \
        ${launchd_dir}${startupitem.plist}.default \
        ${launchd_dir}${startupitem.plist}
    reinplace "s|@WEEKDAY@|[expr {int(7 * rand())}]|g" \
        ${launchd_dir}${startupitem.plist}
    reinplace "s|@HOUR@|[clock format [clock seconds] -format %H]|g" \
        ${launchd_dir}${startupitem.plist}
    reinplace "s|@MINUTE@|[clock format [expr {[clock seconds] + 120}] -format %M]|g" \
        ${launchd_dir}${startupitem.plist}
}

post-deactivate {
    delete ${launchd_dir}${startupitem.plist}
}

notes \
    "Installing this port automatically enables weekly reporting of data to the stats server. \
     Uninstall or deactivate this port if you want to stop providing data to MacPorts."
