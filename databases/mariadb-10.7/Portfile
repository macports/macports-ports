# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           legacysupport 1.0
legacysupport.newest_darwin_requires_legacy 10

name                mariadb-10.7
set name_mysql      ${name}
version             10.7.1
set version_branch  [join [lrange [split ${version} .] 0 1] .]
# Please set revision_client and revision_server to 0 if you bump
# version; these can be changed independently for the 2 subports, but
# can be changed at the same time if that's what's required.
set revision_client 0
set revision_server 0
categories          databases
platforms           darwin
license             GPL-2
maintainers         {michaelld @michaelld} openmaintainer

if {$subport eq $name} {

    PortGroup           cmake 1.0
    PortGroup           select 1.0
    PortGroup           github 1.0
    PortGroup           compiler_blacklist_versions 1.0
    PortGroup           boost 1.0

    boost.version       1.71
    compiler.cxx_standard 2011

    # https://trac.macports.org/ticket/60805
    compiler.blacklist-append {clang < 900}

    github.setup        MariaDB server ${version} mariadb-
    revision            ${revision_client}

    license             GPL-2
    description         Multithreaded SQL database server
    long_description    Mariadb is a fork of the MySQL server, a multi-threaded SQL database.

    cmake.out_of_source yes
    use_parallel_build  yes

    # mariadb-connector-c, wsrep-lib and wsrep-API are submodules,
    # but because we're not
    # cloning into a GIT module in the first place, we can't use the
    # git submodule feature. Have to download separately and install
    # into the correct location to get this port to work! Which means
    # we need a mariadb-connector-c branch and so forth ... basically
    # replicate what the github PG does but without using it since it
    # is not designed to be used for multiple downloads.

    set libmariadb_github_author  MariaDB
    set libmariadb_github_project mariadb-connector-c
    set libmariadb_github_branch  0bf84c065e4cfa18764fe4823700d26a21a62483

    set wsrep_github_author  codership
    set wsrep_github_project wsrep-lib
    set wsrep_github_branch  22921e7082ddfb45222f21a585aa8b877e62aa8b

    set wsrep_api_github_author   codership
    set wsrep_api_github_project  wsrep-API
    set wsrep_api_github_branch   76cf223c690845bbf561cb820a46e06a18ad80d1

    set columnstore_github_author   mariadb-corporation
    set columnstore_github_project  mariadb-columnstore-engine
    set columnstore_github_branch   4dc5d3af0579a50ac1b4d158d2ff0bcdf960ac04

    set libmarias3_github_author   mariadb-corporation
    set libmarias3_github_project  libmarias3
    set libmarias3_github_branch   3846890513df0653b8919bc45a7600f9b55cab31

    set main_distname ${distname}
    set main_distfile ${distfiles}

    set libmariadb_distname ${libmariadb_github_project}-${libmariadb_github_branch}
    set libmariadb_distfile ${libmariadb_distname}${extract.suffix}

    set wsrep_distname ${wsrep_github_project}-${wsrep_github_branch}
    set wsrep_distfile ${wsrep_distname}${extract.suffix}

    set wsrep_api_distname ${wsrep_api_github_project}-${wsrep_api_github_branch}
    set wsrep_api_distfile ${wsrep_api_distname}${extract.suffix}

    set columnstore_distname ${columnstore_github_project}-${columnstore_github_branch}
    set columnstore_distfile ${columnstore_distname}${extract.suffix}

    set libmarias3_distname ${libmarias3_github_project}-${libmarias3_github_branch}
    set libmarias3_distfile ${libmarias3_distname}${extract.suffix}

    distfiles    ${main_distfile}:tagone \
                 ${libmariadb_distfile}:tagtwo \
                 ${wsrep_distfile}:tagthree \
                 ${wsrep_api_distfile}:tagfour \
                 ${columnstore_distfile}:tagfive \
                 ${libmarias3_distfile}:tagsix

    master_sites ${github.homepage}/archive/${git.branch}:tagone \
                 https://github.com/${libmariadb_github_author}/${libmariadb_github_project}/archive/${libmariadb_github_branch}:tagtwo \
                 https://github.com/${wsrep_github_author}/${wsrep_github_project}/archive/${wsrep_github_branch}:tagthree \
                 https://github.com/${wsrep_api_github_author}/${wsrep_api_github_project}/archive/${wsrep_api_github_branch}:tagfour \
                 https://github.com/${columnstore_github_author}/${columnstore_github_project}/archive/${columnstore_github_branch}:tagfive \
                 https://github.com/${libmarias3_github_author}/${libmarias3_github_project}/archive/${libmarias3_github_branch}:tagsix

    checksums       ${main_distfile} \
                    rmd160  8430ed102362c1c1434f9968886bc30af27b719e \
                    sha256  641e9cc8524a427e053473768475495564f7172d566d0f57d595233c23e7cc01 \
                    size    48923959 \
                    ${libmariadb_distfile} \
                    rmd160  2622ef32dc73b00530be27c832bf157a2af0fce5 \
                    sha256  e1b4f421e7d72f840d8ff554674df0f18876925e7f069eb17f6dc1bc7bb7aa91 \
                    size    1000875 \
                    ${wsrep_distfile} \
                    rmd160  92a588f98b396b1bad3f7099e5338b864a0d87de \
                    sha256  90d26692d384342f796c16249801fec6867b3ef56efc84096207cade530ce733 \
                    size    172530 \
                    ${wsrep_api_distfile} \
                    rmd160  847e8d58c8806437088d77d444ecfe2dcd8a76d1 \
                    sha256  214fb8701ae51bcdf8171475a93f2c28ddd56e642feb172ec5148b4d3c73d4a6 \
                    size    90155 \
                    ${columnstore_distfile} \
                    rmd160  bc89fc2c06029da42da464db47c183fa654e86f9 \
                    sha256  fe649c149241b772bba55f1c794fd8d20a8a082ca5e78c9756a6350a9178f319 \
                    size    14562818 \
                    ${libmarias3_distfile} \
                    rmd160  bf90ab4856c1fec9272309134837f1f50fdb8de8 \
                    sha256  0c9ce4cc300bfefcb9dd188a46d736c0e119210c01cbfb04725b07f5330be679 \
                    size    364270

    worksrcdir   ${github.project}-${git.branch}

    post-extract {
        # put the libmariadb_github_project directory into position
        # remove the original directory
        ui_debug "delete ${worksrcpath}/libmariadb"
        delete ${worksrcpath}/libmariadb
        ui_debug "move ${workpath}/${libmariadb_distname} ${worksrcpath}/libmariadb"
        move ${workpath}/${libmariadb_distname} ${worksrcpath}/libmariadb

        # put the wsrep_github_project directory into position
        # remove the original directory
        ui_debug "delete ${worksrcpath}/wsrep-lib"
        delete ${worksrcpath}/wsrep-lib
        ui_debug "move ${workpath}/${wsrep_distname} ${worksrcpath}/wsrep-lib"
        move ${workpath}/${wsrep_distname} ${worksrcpath}/wsrep-lib

        # put the wsrep_api_github_project directory into position
        # remove the original directory
        ui_debug "delete ${worksrcpath}/wsrep-lib/wsrep-API/v26"
        delete ${worksrcpath}/wsrep-lib/wsrep-API/v26
        ui_debug "move ${workpath}/${wsrep_api_distname} ${worksrcpath}/wsrep-lib/wsrep-API/v26"
        move ${workpath}/${wsrep_api_distname} ${worksrcpath}/wsrep-lib/wsrep-API/v26

        # put the columnstore_github_project directory into position
        # remove the original directory
        ui_debug "delete ${worksrcpath}/storage/columnstore/columnstore"
        delete ${worksrcpath}/storage/columnstore/columnstore
        ui_debug "move ${workpath}/${columnstore_distname} ${worksrcpath}/storage/columnstore/columnstore"
        move ${workpath}/${columnstore_distname} ${worksrcpath}/storage/columnstore/columnstore

        # put the libmarias3_github_project directory into position
        # remove the original directory
        ui_debug "delete ${worksrcpath}/storage/maria/libmarias3"
        delete ${worksrcpath}/storage/maria/libmarias3
        ui_debug "move ${workpath}/${libmarias3_distname} ${worksrcpath}/storage/maria/libmarias3"
        move ${workpath}/${libmarias3_distname} ${worksrcpath}/storage/maria/libmarias3
    }

    depends_build-append port:bison
    depends_lib-append  port:zlib port:tcp_wrappers port:ncurses port:judy \
                        path:lib/libssl.dylib:openssl port:libxml2 \
                        port:pcre2
    depends_run-append  port:mysql_select

    select.group        mysql
    select.file         ${filespath}/${name_mysql}

    post-extract {
        file mkdir ${cmake.build_dir}/macports
        copy ${filespath}/macports-default.cnf \
            ${filespath}/my.cnf \
            ${cmake.build_dir}/macports/
    }

    patch.pre_args -p1
    patchfiles     patch-cmake-install_layout.cmake.diff \
                   patch-CMakeLists.txt.diff \
                   patch-libmariadb_cmake_install.cmake.diff \
                   patch-libmariadb_libmariadb_CMakeLists.txt.diff \
                   patch-libmariadb_mariadb_config_CMakeLists.txt.diff \
                   patch-server_storage_perfschema_my_thread.h.diff \
                   patch-cmake_mysql_version.cmake.diff \
                   patch-cmake_mysql_columnstore_version.cmake.diff

    # patch to fix usage of rw_lock ; guessing this will be fixed in the next release.
    patchfiles-append patch-storage_innobase-fix-rw_lock_use.diff

    post-patch {
        reinplace "s|@NAME@|${name_mysql}|g" \
            ${worksrcpath}/cmake/install_layout.cmake
        reinplace "s|@NAME@|${name_mysql}|g" \
            ${worksrcpath}/libmariadb/cmake/install.cmake
        reinplace "s|@NAME@|${name_mysql}|g" \
            ${cmake.build_dir}/macports/macports-default.cnf \
            ${cmake.build_dir}/macports/my.cnf
        reinplace "s|@PREFIX@|${prefix}|g" \
            ${cmake.build_dir}/macports/macports-default.cnf \
            ${cmake.build_dir}/macports/my.cnf
        move ${worksrcpath}/VERSION ${worksrcpath}/VERSION.txt
        move ${worksrcpath}/storage/columnstore/columnstore/VERSION ${worksrcpath}/storage/columnstore/columnstore/VERSION.txt
    }

    configure.args-delete \
                        -DCMAKE_INSTALL_NAME_DIR=${prefix}/lib
    configure.args-append \
                        -DCMAKE_INSTALL_NAME_DIR:STRING=${prefix}/lib/${name_mysql}/mysql \
                        -DINSTALL_LAYOUT:STRING=MACPORTS \
                        -DSYSCONFDIR:PATH=${prefix}/etc/${name_mysql} \
                        -DMYSQL_UNIX_ADDR:PATH=${prefix}/var/run/${name_mysql}/mysqld.sock \
                        -DMYSQL_DATADIR:PATH=${prefix}/var/db/${name_mysql} \
                        -DDEFAULT_CHARSET:STRING=utf8 \
                        -DDEFAULT_COLLATION:STRING=utf8_general_ci \
                        -DWITH_EMBEDDED_SERVER:BOOL=ON \
                        -DWITH_ZLIB:STRING=system \
                        -DWITH_UNIT_TESTS:BOOL=ON \
                        -DWITHOUT_CASSANDRA_STORAGE_ENGINE:BOOL=ON \
                        -DWITHOUT_MROONGA_STORAGE_ENGINE:BOOL=ON \
                        -DWITHOUT_ROCKSDB_STORAGE_ENGINE:BOOL=ON \
                        -DWITHOUT_TOKUDB_STORAGE_ENGINE:BOOL=ON \
                        -DENABLE_GCOV:BOOL=OFF \
                        -DENABLE_DTRACE:BOOL=OFF \
                        -DWITH_READLINE:BOOL=ON \
                        -DWITH_LIBWRAP:BOOL=ON \
                        -DWITH_SSL:STRING=yes \
                        -DWITH_SSL_PATH=${prefix} \
                        -DMYSQL_MAINTAINER_MODE:STRING=AUTO \
                        -DICONV_LIBRARIES=${prefix}/lib/libiconv.dylib \
                        -DICONV_INCLUDE_DIR:PATH=${prefix}/include \
                        -DCONNECT_WITH_JDBC=OFF \
                        -DCONNECT_WITH_LIBXML2=ON \
                        -DCONNECT_WITH_MONGO=OFF \
                        -DCONNECT_WITH_ODBC=OFF \
                        -DCONNECT_WITH_REST=OFF \
                        -DWITH_PCRE=system \
                        -DBISON_EXECUTABLE=${prefix}/bin/bison

    post-destroot {
        # proc portdestroot::destroot_finish fails to find and compress our man pages
        # so borrow the compress command and run on our files now.
        set manpath "${destroot}${prefix}/share/man"
        set gzip [findBinary gzip ${portutil::autoconf::gzip_path}]
        foreach manpage [glob -type f ${destroot}${prefix}/share/man/${name_mysql}/man\[1-9\]/*] {
            # Fix paths in manpages
            reinplace -q "s|/etc/|${prefix}/etc/${name_mysql}/|g" ${manpage}
            # Compress all manpages with gzip
            system "$gzip -9vf ${manpage}"
        }
        #foreach samp_conffile [glob -type f ${destroot}${prefix}/share/${name_mysql}/support-files/my-*.cnf] {
        #    # Fix paths in sample configuration files
        #    reinplace -q "s|/etc/my.cnf|${prefix}/etc/${name_mysql}/my.cnf|g" ${samp_conffile}
        #}
        xinstall -m 755 -o root -d \
            ${destroot}${prefix}/etc/${name_mysql}
        copy ${cmake.build_dir}/macports/macports-default.cnf \
            ${destroot}${prefix}/etc/${name_mysql}/
        xinstall -m 755 -o root -d \
            ${destroot}${prefix}/share/${name_mysql}/support-files/macports
        copy ${cmake.build_dir}/macports/my.cnf \
            ${destroot}${prefix}/share/${name_mysql}/support-files/macports/
    }

    post-install {
        if {![file exists ${prefix}/etc/LaunchDaemons/org.macports.${name_mysql}/org.macports.${name_mysql}-server.plist]} {
            ui_msg "The ${name_mysql} client has been installed."
            ui_msg "To install the ${name_mysql} server, install the ${name_mysql}-server port."
        }
    }

    post-activate {
        if {![file exists ${prefix}/etc/${name_mysql}/my.cnf]} {
            copy ${prefix}/share/${name_mysql}/support-files/macports/my.cnf \
                ${prefix}/etc/${name_mysql}/
        }
    }

    variant system_readline description {Use system readline instead of bundled readline} {
        # Add readline support.
        # "-DWITH_READLINE:BOOL=OFF" has the peculiar meaning "do not use the bundled copy
        # of readline but use the system's (i.e. MacPorts') copy of readline"
        depends_lib-append      port:readline
        configure.args-delete   -DWITH_READLINE:BOOL=ON
        configure.args-append   -DWITH_READLINE:BOOL=OFF
    }

    notes "
On activation if no ${prefix}/etc/${name_mysql}/my.cnf file exists one
will be created which loads
${prefix}/etc/${name_mysql}/macports-default.cnf.

If a ${prefix}/etc/${name_mysql}/my.cnf file exists MacPorts does not
touch it and any changes you make to ${prefix}/etc/${name_mysql}/my.cnf
will be preserved (e.g., during port upgrades, deactivations or
activations). ${prefix}/etc/${name_mysql}/my.cnf is a good place to
customize your ${name_mysql} installation.

Any changes made to ${prefix}/etc/${name_mysql}/macports-default.cnf
will be lost during port upgrades, deactivations or activations so you
are advised to not make changes here. Currently
${prefix}/etc/${name_mysql}/macports-default.cnf contains only one
directive; to disable networking. With disabled networking it is
possible to install and have running all the MacPorts mysql ports
simultaneously.
"

    github.livecheck.regex (${version_branch}.\[0-9\]+)

}

subport ${name_mysql}-server {

    revision            ${revision_server}
    license             BSD
    description         (Beta version) Run ${name_mysql} as server
    long_description    {*}${description}

    supported_archs     noarch
    distfiles

    depends_run         port:${name_mysql}

    if {"darwin" eq ${os.platform} && ${os.major} > 8} {
        set mysqluser       _mysql
    } else {
        set mysqluser       mysql
    }
    add_users ${mysqluser} group=${mysqluser} realname=MySQL\ Server

    pre-extract {
        copy ${filespath}/org.macports.mysql-server.plist ${workpath}/org.macports.${subport}.plist
    }

    post-patch {
        reinplace "s|@NAMEMYSQL@|${name_mysql}|g" \
            ${workpath}/org.macports.${subport}.plist
        reinplace "s|@PREFIX@|${prefix}|g" \
            ${workpath}/org.macports.${subport}.plist
        reinplace "s|@SUBPORT@|${subport}|g" \
            ${workpath}/org.macports.${subport}.plist
        reinplace "s|@USER@|${mysqluser}|g" \
            ${workpath}/org.macports.${subport}.plist
        reinplace "s|@GROUP@|${mysqluser}|g" \
            ${workpath}/org.macports.${subport}.plist
    }

    use_configure       no

    build {}

    destroot {
        xinstall -d -m 755 ${destroot}${prefix}/etc/LaunchDaemons/org.macports.${subport}
        xinstall -m 0644 -o root -W ${workpath} \
            org.macports.${subport}.plist \
            ${destroot}${prefix}/etc/LaunchDaemons/org.macports.${subport}
        xinstall -d -m 755 ${destroot}/Library/LaunchDaemons
        ln -s ${prefix}/etc/LaunchDaemons/org.macports.${subport}/org.macports.${subport}.plist \
            ${destroot}/Library/LaunchDaemons/org.macports.${subport}.plist
        xinstall -m 755 -o root -d \
            ${destroot}${prefix}/var/run
        xinstall -m 755 -o ${mysqluser} -g ${mysqluser} -d \
            ${destroot}${prefix}/etc/${name_mysql} \
            ${destroot}${prefix}/var/db/${name_mysql} \
            ${destroot}${prefix}/var/log/${name_mysql} \
            ${destroot}${prefix}/var/run/${name_mysql}
        destroot.keepdirs-append  \
            ${destroot}${prefix}/var/db/${name_mysql} \
            ${destroot}${prefix}/var/log/${name_mysql} \
            ${destroot}${prefix}/var/run/${name_mysql}
    }

    notes "
If this is a new install you might want to run:

\$ sudo -u ${mysqluser} ${prefix}/lib/${name_mysql}/bin/mysql_install_db
"

    livecheck.type          none
}

homepage https://mariadb.org/
