# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           compiler_blacklist_versions 1.0
PortGroup           github 1.0
PortGroup           muniversal 1.0

# Please keep the mongodb and mongodb-devel ports as similar as possible.

epoch               1
github.setup        mongodb mongo 5.0.8 r
revision            0
checksums           rmd160  d4a5ab37b563fd9ea8eab0329a4926d212cbc1e9 \
                    sha256  92ef194385597f049bca604d4ff686f63027e4948431d589cec5cd6032864591 \
                    size    55941830 \
                    certifi-2021.10.8.tar.gz \
                    rmd160  8972d4fb7b192cf6e3a13a7ea9dfc497b0866eea \
                    sha256  78884e7c1d4b00ce3cea67b44566851c4343c120abd683433ce934a68ea58872 \
                    size    151214 \
                    charset-normalizer-2.0.12.tar.gz \
                    rmd160  f9bbde2f4ca286e47416d01eebd5b8c22cd8bff7 \
                    sha256  2857e29ff0d34db842cd7ca3230549d1a697f96ee6d3fb071cfa6c7393832597 \
                    size    79105 \
                    Cheetah3-3.2.6.post1.tar.gz \
                    rmd160  280c221d47dd24d46f9b7e135effd4048fd1a8ef \
                    sha256  58b5d84e5fbff6cf8e117414b3ea49ef51654c02ee887d155113c5b91d761967 \
                    size    295992 \
                    Cython-0.29.28.tar.gz \
                    rmd160  0597dde8c2b6fe6bf651fcabde5b43bef5ecac41 \
                    sha256  d6fac2342802c30e51426828fe084ff4deb1b3387367cf98976bb2e64b6f8e45 \
                    size    2081368 \
                    idna-3.3.tar.gz \
                    rmd160  39ab62813b06e88c80f13848c34c03a1c8600bb8 \
                    sha256  9d643ff0a55b762d5cdb124b8eaa99c66322e2157b69160bc32796e824360e6d \
                    size    286689 \
                    oauthlib-3.1.1.tar.gz \
                    rmd160  1ee81d217311e971298be031730cc72a5dc4bafd \
                    sha256  8f0215fcc533dd8dd1bee6f4c412d4f0cd7297307d43ac61666389e3bc3198a3 \
                    size    161395 \
                    pkce-1.0.3.tar.gz \
                    rmd160  5001b60d76c136c98e47f6bb9ee2d5bac9e4e924 \
                    sha256  9775fd76d8a743d39b87df38af1cd04a58c9b5a5242d5a6350ef343d06814ab6 \
                    size    2757 \
                    poetry-core-1.0.8.tar.gz \
                    rmd160  a6bd36aa0963c0abaf3194b91bac8484bc51c13c \
                    sha256  951fc7c1f8d710a94cb49019ee3742125039fc659675912ea614ac2aa405b118 \
                    size    346260 \
                    psutil-5.8.0.tar.gz \
                    rmd160  ac0a0c786848276d50cfac4c5dfc67254c1d4763 \
                    sha256  0c9ccb99ab76025f2f0bbecf341d4656e9c1351db8cc8a03ccd62e318ab4b5c6 \
                    size    470886 \
                    pymongo-3.12.3.tar.gz \
                    rmd160  e196fb76bef669b7826e313ef71b56b152d7ff7f \
                    sha256  0a89cadc0062a5e53664dde043f6c097172b8c1c5f0094490095282ff9995a5f \
                    size    821234 \
                    PyYAML-6.0.tar.gz \
                    rmd160  da0f60184d72f5a360b297d0131f5a5b08086abf \
                    sha256  68fb519c14306fec9720a2a5b45bc9f0c8d1b9c72adf45c37baedfcd949c35a2 \
                    size    124996 \
                    regex-2021.11.10.tar.gz \
                    rmd160  b4e567eb32eb5e19d39f8859dcaa57fe15adeb54 \
                    sha256  f341ee2df0999bfdf7a95e448075effe0db212a59387de1a70690e4acb03d4c6 \
                    size    702813 \
                    requests-2.26.0.tar.gz \
                    rmd160  0b532167e01570e015b9abd52bb7d442d93a50bd \
                    sha256  b8aa58f8cf793ffd8782d3d8cb19e66ef36f7aba4353eec859e74678b01b07a7 \
                    size    104433 \
                    requests-oauthlib-1.3.0.tar.gz \
                    rmd160  13aa3a01f10e5d80bb572634c34433e356aea670 \
                    sha256  b4261601a71fd721a8bd6d7aa1cc1d6a8a93b4a9f5e96626f8e4d91e8beeaa6a \
                    size    88157 \
                    requirements-parser-0.3.1.tar.gz \
                    rmd160  29cbe92d031f6ff3d8029611a3ae333fa62af257 \
                    sha256  3852277618e653dd1d8fa4129e59b4338358dffafeb3d5106e9f88504db9c460 \
                    size    10080 \
                    setuptools-60.10.0.tar.gz \
                    rmd160  3a401372ca441ed73e6fc7853cb94a09f8ff49d8 \
                    sha256  6599055eeb23bfef457d5605d33a4d68804266e6cb430b0fb12417c5efeae36c \
                    size    2420706 \
                    types-setuptools-57.4.11.tar.gz \
                    rmd160  95aed2e15888481fe1cb9119d4fc4523c5fc325a \
                    sha256  262f7406e0c7d705ad6bb4526b5b761fa500bf99eab74de85ac3592187d62935 \
                    size    16119 \
                    urllib3-1.26.9.tar.gz \
                    rmd160  cff7b02fbe2920307cd79b5162da931f40abac8a \
                    sha256  aabaf16477806a5e1dd19aa41f8c2b7950dd3c746362d7e3223dbe6de6ac448e \
                    size    295258 \
                    wheel-0.37.1.tar.gz \
                    rmd160  6c4df8aa7c6d59f8b97cf85cce9e362c81b0c523 \
                    sha256  e9a504e793efbca1b8e0e9cb979a249cf4a0a7b5b8c9e8b65a5e39d49529c1c4 \
                    size    66376

name                mongodb
conflicts           mongodb-devel
license             SSPL
categories          databases
maintainers         {ryandesign @ryandesign}

description         high-performance, schema-free, document-oriented (\"NoSQL\") database

long_description    MongoDB is a {*}${description}. The ${name} port \
                    tracks the annual major release cycle which \
                    focuses on stability.

homepage            https://www.mongodb.com/community
master_sites        https://fastdl.mongodb.org/src:mongodb
distname            ${name}-src-r${version}
extract.only        ${distfiles}
checksums           ${distfiles} {*}${checksums}
distfiles           ${distfiles}:mongodb

for {set index 1} {${index} < [llength ${checksums}]} {incr index} {
    set item [lindex ${checksums} ${index}]
    switch ${item} {
        md5 -
        sha1 -
        sha256 -
        size -
        rmd160 {
            incr index
        }
        default {
            regsub -- {-\d.*$} ${item} {} tag
            distfiles-append ${item}:${tag}
            master_sites-append https://files.pythonhosted.org/packages/source/[string index ${tag} 0]/${tag}/:${tag}
        }
    }
}

set python_branch   3.10
set python_version  [string map {. {}} ${python_branch}]

depends_build       port:python${python_version}

depends_lib         port:curl \
                    port:cyrus-sasl2 \
                    port:libpcap \
                    port:libstemmer \
                    port:pcre \
                    port:snappy \
                    port:yaml-cpp \
                    port:zlib \
                    port:zstd

pre-configure {
    # Upstream recommends installing specific versions of the required
    # Python modules modules in a virtualenv. They do not support using
    # newer versions of those modules such as the versions that we have
    # in MacPorts. https://jira.mongodb.org/browse/SERVER-62686
    system -W ${workpath} "${prefix}/bin/python${python_branch} -m venv venv"
}

configure.env       "MAKEFLAGS=-j${build.jobs}"
configure.cmd       ${workpath}/venv/bin/pip
configure.pre_args  install
configure.args      --find-links [shellescape ${distpath}] \
                    --no-index \
                    --requirement etc/pip/compile-requirements.txt \
                    --verbose \
                    --verbose

compiler.c_standard 2011
compiler.cxx_standard 2017

# This matches a version check of __apple_build_version__ in SConstruct.
compiler.blacklist-append {clang < 1001.0.46}

# It sets its own optimization flags via the --opt flag.
configure.optflags

# -L flags are handled via LIBPATH.
configure.ldflags-delete -L${prefix}/lib

build.cmd           ${workpath}/venv/bin/python buildscripts/scons.py
build.target        install-core
build.args          CC="${configure.cc}" \
                    CFLAGS="${configure.cflags}" \
                    CPPPATH="${prefix}/include" \
                    CXX="${configure.cxx}" \
                    CXXFLAGS="${configure.cxxflags}" \
                    DESTDIR="${destroot}" \
                    LIBPATH="${prefix}/lib" \
                    PREFIX="${prefix}" \
                    --disable-warnings-as-errors \
                    --enable-http-client=on \
                    --install-action=hardlink \
                    --libc++ \
                    --opt=size \
                    --release \
                    --ssl=on \
                    --use-sasl-client \
                    --use-system-pcre \
                    --use-system-snappy \
                    --use-system-stemmer \
                    --use-system-yaml \
                    --use-system-zlib \
                    --use-system-zstd \
                    --wiredtiger=on

# It should build with --use-system-asio, but doesn't:
# TODO: Report bug and put link here

# It should build with --use-system-icu, but doesn't:
# TODO: Report bug and put link here

# Do not build mongoc_embedded.
# See https://jira.mongodb.org/browse/SERVER-44592
build.args-append   --use-system-mongo-c=off

# Specify a short variant dir because the default variant dir is deeply nested
# and can be very long, so long that on some systems the build will fail with
# "Argument list too long"; see https://jira.mongodb.org/browse/SERVER-13829
build.args-append   VARIANT_DIR=MP

# i386 was never supported on macOS.
# See https://jira.mongodb.org/browse/SERVER-22810
supported_archs     arm64 x86_64

array set merger_build_args {arm64 "TARGET_ARCH=aarch64" x86_64 "TARGET_ARCH=x86_64"}
foreach a ${supported_archs} {
    lappend merger_build_args($a) \
                    CCFLAGS="-arch ${a}" \
                    LINKFLAGS="${configure.ldflags} -arch ${a}" \
}

# JavaScript engine support for arm64 is blocked pending the integration
# of MozJS/Spidermonkey ESR 91 into MongoDB.
# https://jira.mongodb.org/browse/SERVER-50115?focusedCommentId=3948549&page=com.atlassian.jira.plugin.system.issuetabpanels%3Acomment-tabpanel#comment-3948549
# https://jira.mongodb.org/browse/SERVER-42427
if {[vercmp ${version} 6.0.0] < 0} {
    lappend merger_build_args(arm64) --js-engine=none
}

if {![info exists universal_possible]} {
    set universal_possible [expr {${os.universal_supported} && [llength ${configure.universal_archs}] >= 2}]
}
if {(!${universal_possible} || ![variant_isset universal]) && [info exists merger_build_args(${build_arch})]} {
    build.args-append {*}$merger_build_args(${build_arch})
}

set dbdir           ${prefix}/var/db/mongodb
set logdir          ${prefix}/var/log/mongodb
set docdir          ${prefix}/share/doc/mongodb
set mongouser       _mongo
add_users           ${mongouser} group=${mongouser} realname=MongoDB\ Server

destroot {
    # The programs have already been installed into the destroot during
    # the build phase because I don't know how to prevent it from doing
    # that. Documentation files have also already been installed to an
    # unreasonable location so move them.
    xinstall -d ${destroot}${docdir}
    foreach f [glob ${destroot}${prefix}/*] {
        if {[file isfile ${f}]} {
            move ${f} ${destroot}${docdir}
        }
    }

    xinstall -m 0755 -o ${mongouser} -g ${mongouser} -d \
        ${destroot}${dbdir} \
        ${destroot}${logdir}
    destroot.keepdirs-append  \
        ${destroot}${dbdir} \
        ${destroot}${logdir}
}

startupitem.create      yes
startupitem.executable  sudo -u ${mongouser} ${prefix}/bin/mongod --dbpath ${dbdir} --logpath ${logdir}/mongodb.log --logappend

# mongodb 5.0.x requires macOS 10.13 or later to build.
# mongodb 5.1 and later will require macOS 10.14 or later.
# https://jira.mongodb.org/browse/SERVER-58746
if {${os.platform} eq "darwin" && ${os.major} < 17} {
    known_fail          yes
    depends_build
    depends_lib
    pre-fetch {
        ui_error "${name} @${version} requires macOS 10.13 or greater"
        return -code error "incompatible macOS version"
    }
}

if {${os.platform} eq "darwin" && ${os.major} >= 16 && [vercmp ${os.version} 18.2.0] < 0} {
    notes-append {
        Warning: MONGODB MAY LOSE DATA if it crashes on macOS 10.14.0 or\
        earlier and the database is on an APFS volume. This problem is fixed\
        in macOS 10.14.1 and later so please update your operating system or\
        make sure your database is on a Mac OS Extended (Journaled) volume.\
        See https://jira.mongodb.org/browse/WT-4112 for more information.
    }
}

github.livecheck.regex  {(\d+\.0\.[0-9.]+)}
