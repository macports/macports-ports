# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0

github.setup        godotengine godot 3.2.1 "" -stable
categories          games graphics multimedia
platforms           darwin
license             MIT
maintainers         @jasonliu-- openmaintainer
homepage            https://godotengine.org
description         cross-platform 2-D and 3-D game development \
                    environment
long_description    Godot Engine is a cross-platform game engine for \
                    creating 2-D and 3-D games. Godot aims to offer a \
                    fully integrated game development environment. It \
                    provides a comprehensive set of common tools, and \
                    games can be exported to a number of target \
                    platforms, including major desktop platforms \
                    (Linux, macOS, Windows), as well as mobile \
                    (Android, iOS) and web-based (HTML5) platforms.


checksums           rmd160  42bed19653bb0964f4f1b8a66c760d0724faec16 \
                    sha256  f794b1ecd443220e25cc969a0262d0f6e0d81fba1d85ed6dc6101473b60711d2 \
                    size    22089237

depends_build       port:scons \
                    port:yasm

post-patch {
    reinplace "/yasm_paths =/a\\
\\      \"${prefix}/bin/yasm\",\\
" \
        ${worksrcpath}/modules/webm/libvpx/SCsub
}

use_configure       no
use_xcode           yes

build.cmd           scons
build.env           BUILD_NAME=macports_build
build.target        platform=osx target=release_debug

destroot {
    copy ${worksrcpath}/misc/dist/osx_tools.app \
         ${destroot}${applications_dir}/Godot.app
    xinstall -d ${destroot}${applications_dir}/Godot.app/Contents/MacOS
    move ${worksrcpath}/bin/godot.osx.opt.tools.64 \
         ${destroot}${applications_dir}/Godot.app/Contents/MacOS/Godot
    ln -s ${applications_dir}/Godot.app/Contents/MacOS/Godot \
       ${destroot}${prefix}/bin/godot
}

livecheck.type      regex
livecheck.url       https://godotengine.org/download/osx
livecheck.regex     Godot <em>(\\d+\\.\\d+(?:\\.\\d+)*)</em>

### The following code was adapted from the portfiles
### of ports such as openvdb and openimageio

set clang_suffixes {5 6 7 8 9}

# this default version should stay synchronized with
#    the default macports_clang option in the SConstruct
#    file that is part of the Godot source code
set sconstruct_default_mp_clang 5

set clang_ports {}
foreach s ${clang_suffixes} {
    lappend clang_ports clang${s}
}

foreach s ${clang_suffixes} {
    global use_xcode sconstruct_default_mp_clang
    set p clang${s}
    if {${s} < 10} {
        set v ${s}.0
    } else {
        set v ${s}
    }
    set i [lsearch -exact ${clang_ports} ${p}]
    set c [lreplace ${clang_ports} ${i} ${i}]
    variant ${p} conflicts {*}${c} \
        description "Build using the MacPorts Clang ${v} compiler" \
    {
        depends_build-append    port:clang-${v}

        compiler.whitelist      macports-clang-${v}
        use_xcode               no

        post-configure {
            if {${s} != ${sconstruct_default_mp_clang}} {
                reinplace s|${sconstruct_default_mp_clang}\.0|${v}|g \
                    ${worksrcpath}/SConstruct
            }

            # References:
            # * https://trac.macports.org/ticket/58770
            # * https://trac.macports.org/ticket/58779#comment:28
            ui_warn "
****
**** Installing port ${name} with variant +${p} uses
**** MacPorts Clang instead of Xcode Clang. It also
**** circumvents the Xcode toolchain, which means that
**** if you attempt to perform a build using trace mode
**** (port -t install ${name} +${p}), the build will
**** fail!
****
**** If you are not using trace mode, you can safely
**** ignore this message.
****
"
        }
        build.args-append   macports_clang=${v}
    }
}
