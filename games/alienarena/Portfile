# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

name                alienarena
version             7.70.0-20180721
checksums           rmd160  f2e081eb64324c0e3576bb2b796c46ecb9c2a079 \
                    sha256  100fee4494aa11cfc3fc846e9b3a0cb64e5eb134d1afaf802024d0fc7309c27b \
                    size    913540088

set version_number  [lindex [split ${version} -] 0]
set version_date    [lindex [split ${version} -] 1]
categories          games
platforms           darwin
maintainers         {ryandesign @ryandesign}
license             GPL-2+

description         retro sci-fi old school deathmatch game similar to Quake \
                    III and Unreal Tournament

long_description    Alien Arena combines some of the very best aspects of \
                    such games as Quake III and Unreal Tournament and wraps \
                    them up with a retro alien theme, while adding tons of \
                    original ideas to make the game quite unique. Alien Arena \
                    is a furious frag fest with arenas ranging from the \
                    small, to the massive. With a large built-in player base, \
                    it's never hard to find a good match going on, at any \
                    hour of the day. The community is friendly, as well as \
                    prolific. Dozens of maps, models, and various accessories \
                    have been created by community members to add on to the \
                    game experience.

homepage            http://red.planetarena.org/
master_sites        ${homepage}files/
dist_subdir         ${name}/${version}
use_zip             yes
distname            AlienArena
worksrcdir          Alien Arena

platform darwin {
    if {${os.major} < 9} {
        # 10.4.11:
        # ld: common symbols not allowed with MH_DYLIB output format with the -multi_module option
        # The usual advice is to use -fno-common (e.g. https://gcc.gnu.org/ml/gcc/2005-06/msg00378.html)
        # but this leads to several "multiple definitions of symbol" errors.
        known_fail  yes
        pre-fetch {
            ui_error "Alien Arena requires Mac OS X 10.5 or greater at the moment."
            return -code error "incompatible Mac OS X version"
        }
    }
}

if {${name} eq ${subport}} {
    revision                    0

    PortGroup                   muniversal 1.0
    
    depends_build               port:autoconf-archive \
                                port:pkgconfig
    
    depends_lib                 port:curl \
                                port:freetype \
                                port:jpeg \
                                port:libogg \
                                port:libvorbis \
                                port:mesa \
                                port:ode \
                                port:openal-soft \
                                port:xorg-libX11 \
                                port:xorg-libXxf86dga \
                                port:xorg-libXxf86vm \
                                port:zlib
    
    depends_run                 port:alienarena-data
    
    extract {
        # Extract only the files we need: exclude Windows DLLs, EXEs, icons and
        # source; Linux executables; the configure script that we'll regenerate
        # anyway; and the game data that the alienarena-data subport installs.
        # MacPorts base doesn't let us change unzip args so we have to override.
        # TODO: Use shellescape once MacPorts 2.7.0 is out.
        system -W ${workpath} "unzip -q '${distpath}/${distfiles}' -x '${worksrcdir}/*.dll' '${worksrcdir}/*.exe' '${worksrcdir}/*.ico' '${worksrcdir}/alienarena-linux64*' '${worksrcdir}/configure' '${worksrcdir}/data1/*' '${worksrcdir}/source/win32/*'"

        # autoreconf complains if this directory doesn't exist.
        file mkdir ${worksrcpath}/data1

        # Make sure the port's version number is correct, since it's an
        # unversioned distfile.
        set fp [open ${worksrcpath}/configure.ac r]
        set file_data [read ${fp}]
        close ${fp}
        if {![regexp {AC_INIT\(\[alienarena\],\[([^]]+)\],} ${file_data} -> configure_version]} {
            ui_error "version not found in configure.ac
            return -code error "version not found"
        }
        if {${configure_version} ne ${version_number}} {
            ui_error "port version ${version_number} doesn't match version in configure.ac ${configure_version}"
            return -code error "version mismatch"
        }
    }
    
    patchfiles                  patch-configure.ac.diff
    
    post-patch {
        # The @PREFIX@ placeholder is present in configure.ac as shipped by the
        # developers. They mistakenly applied a MacPorts patch (that added that
        # placeholder to use MacPorts openal-soft instead of Apple's OpenAL
        # framework) that was never meant for upstream consumption.
        reinplace "s|@PREFIX@|${prefix}|g" ${worksrcpath}/configure.ac
    }
    
    # The zip file contains a configure script but it's missing the execute bit
    # and none of the supporting files are present. Also, we patch configure.ac.
    use_autoreconf              yes
    autoreconf.args             -fvi
    
    configure.args              --disable-silent-rules \
                                --with-xf86dga \
                                --with-xf86vm \
                                --with-zlib \
                                --without-data1pkg \
                                --x-includes=${prefix}/include \
                                --x-libraries=${prefix}/lib
    
    variant debug description {Enable debugging symbols} {
        configure.args-append   --enable-debugging-symbols
        configure.optflags      -O0
    }
    
    notes "
To play Alien Arena, type:

${name}

To run your own dedicated Alien Arena server, use:

${name}-ded
"
    
    livecheck.version           [regsub {(....)(..)(..)} ${version_date} {\1-\2-\3}]
    livecheck.type              regex
    livecheck.url               [lindex ${master_sites} 0]
    livecheck.regex             {>AlienArena\.zip</a>[[:space:]]*</td><td[^>]*>([0-9-]+)}
} else {
    livecheck.type              none
}

subport alienarena-data {
    revision                    0
    license                     Restrictive
    supported_archs             noarch
    
    description                 Assets for the Alien Arena game
    
    long_description            {*}${description}
    
    worksrcdir                  {*}${worksrcdir}/data1
    
    extract {
        # Extract only the data1 directory.
        # MacPorts base doesn't let us change unzip args so we have to override.
        # TODO: Use shellescape once MacPorts 2.7.0 is out.
        system -W ${workpath} "unzip -q '${distpath}/${distfiles}' '${worksrcdir}/*'"

        # The configure script doesn't have the execute bit.
        file attributes ${worksrcpath}/configure -permissions a+x
    }
}
