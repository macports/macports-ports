# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0

name                    MacOSX.sdk
version                 10.12
revision                0
categories              devel
platforms               macosx
maintainers             {ryandesign @ryandesign}
license                 Restrictive Nomirror
installs_libs           no

homepage                https://developer.apple.com/macos/
default master_sites    {https://download.developer.apple.com/Developer_Tools/${master_sites.mirror_subdir}}
default master_sites.mirror_subdir \
                        {${distname}}
extract.suffix          .dmg

options extract_paths
default extract_paths   {"Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/MacOSX${sdk_major}.sdk"}

options xar_file
default xar_file        {}

options xar_pbzx
default xar_pbzx        yes

options xar_payload
default xar_payload     {}

subport MacOSX10.12.sdk {
#   SDK                   Size  Date        File                                                Notes
#   10.12-16A300    4415370488  2019-11-08  Xcode_8.xip
#   10.12.1-16B2649 4446762222  2019-11-08  Xcode_8.1.xip
#   10.12.2-16C58    154679192  2019-11-08  Command_Line_Tools_macOS_10.11_for_Xcode_8.2.dmg
#   10.12.2-16C58    154863510  2019-11-08  Command_Line_Tools_macOS_10.12_for_Xcode_8.2.dmg
#   10.12.2-16C58   4526912760  2019-11-08  Xcode_8.2.xip
#   10.12.2-16C58   4527043832  2019-11-08  Xcode_8.2.1.xip
#   10.12.4-16E185   166119977  2019-11-08  Command_Line_Tools_macOS_10.12_for_Xcode_8.3.dmg    newest smallest
#   10.12.4-16E185  4505613560  2019-11-08  Xcode_8.3.xip
#   10.12.4-16E185  4495662305  2017-04-06  Xcode_8.3.1.xip                                     removed in 2019
#   10.12.4-16E185   166128165  2019-11-08  Command_Line_Tools_macOS_10.12_for_Xcode_8.3.2.dmg
#   10.12.4-16E185  4494079225  2019-11-08  Xcode_8.3.2.xip
#   10.12.4-16E185  4522587381  2019-11-08  Xcode_8.3.3.xip
    set macos_name      {macOS Sierra}
    version             10.12.4-16E185
    revision            0
    checksums           rmd160  f11bd2fc98b7b7ab81779da94673f7649df1eeed \
                        sha256  80f34f206562a10dc8dc14269ab744c16a58134f2bcd26933234fe06ca594683 \
                        size    166119977
    distname            Command_Line_Tools_macOS_10.12_for_Xcode_8.3
    xar_file            Command Line Tools (macOS Sierra version 10.12).pkg
    xar_payload         CLTools_SDK_OSX1012.pkg/Payload
    extract_paths       Library/Developer/CommandLineTools/SDKs/MacOSX.sdk
}

subport MacOSX10.11.sdk {
#   SDK                   Size  Date        File                                                Notes
#   10.11-15A278    3731744540  2019-11-13  Xcode_7.dmg
#   10.11-15A278    3733068903  2019-11-13  Xcode_7.0.1.dmg
#   10.11-15A278    4474328556  2019-11-13  Xcode_7.1.dmg
#   10.11-15A278    4474204264  2019-11-13  Xcode_7.1.1.dmg
#   10.11.2-15C43   4591512140  2019-11-13  Xcode_7.2.dmg
#   10.11.2-15C43   4591829253  2019-11-13  Xcode_7.2.1.dmg
#   10.11.4-15E60   5047652942  2019-11-13  Xcode_7.3.dmg                                       newest smallest
#   10.11.4-15E60   5050193754  2019-11-13  Xcode_7.3.1.dmg
    set macos_name      {OS X El Capitan}
    version             10.11.4-15E60
    revision            0
    checksums           rmd160  c15f1f966be258a60cbabdc95cc0085b007fa7df \
                        sha256  5db90607c1f4f7222b40664c160b033be37433d29ed639f335b5d01545e3bb9c \
                        size    5047652942
    distname            Xcode_7.3
}

subport MacOSX10.10.sdk {
#   SDK                   Size  Date        File                                                Notes
#   10.10-14A382    2611748750  2019-12-11  Xcode_6.1.dmg
#   10.10-14A382    2612045447  2019-12-11  Xcode_6.1.1.dmg
#   10.10-14A383    2699970727  2019-12-11  Xcode_6.2.dmg
#   10.10.3-14D125  2685818165  2019-12-11  Xcode_6.3.dmg
#   10.10.3-14D125  2685577832  2019-12-11  Xcode_6.3.1.dmg                                     newest smallest
#   10.10.3-14D125  2687136616  2019-12-11  Xcode_6.3.2.dmg
#   10.10.3-14D125  2804514980  2019-12-11  Xcode_6.4.dmg                                       not reissued in 2019
    set macos_name      {OS X Yosemite}
    version             10.10.3-14D125
    revision            0
    checksums           rmd160  fab391862be2d7fbd78ba69dcf01b283d2bde7c7 \
                        sha256  205830f485200252b2d7c34b15c8037ce06de6cee95f6f4ab23f856016057df5 \
                        size    2685577832
    distname            Xcode_6.3.1
}

subport MacOSX10.9.sdk {
#   SDK                   Size  Date        File                                                Notes
#   10.9-13A595     2171659981  2019-12-11  Xcode_5.0.1.dmg
#   10.9-13A595     2173178480  2019-12-11  Xcode_5.0.2.dmg
#   10.9.2-13C64    2261628363  2019-12-11  Xcode_5.1.dmg
#   10.9.2-13C64    2260656898  2019-12-11  Xcode_5.1.1.dmg
#   10.9.5-13F26    2542512797  2019-12-05  Xcode_6.0.1.dmg
#   10.9.5-13F26    2611748750  2019-12-11  Xcode_6.1.dmg
#   10.9.5-13F26    2612045447  2019-12-11  Xcode_6.1.1.dmg
#   10.9.5-13F26    2699970727  2019-12-11  Xcode_6.2.dmg
#   10.9.5-13F34    2685818165  2019-12-11  Xcode_6.3.dmg
#   10.9.5-13F34    2685577832  2019-12-11  Xcode_6.3.1.dmg                                     newest smallest
#   10.9.5-13F34    2687136616  2019-12-11  Xcode_6.3.2.dmg
#   10.9.5-13F34    2804514980  2019-12-11  Xcode_6.4.dmg                                       not reissued in 2019
    set macos_name      {OS X Mavericks}
    version             10.9.5-13F34
    revision            0
    checksums           rmd160  fab391862be2d7fbd78ba69dcf01b283d2bde7c7 \
                        sha256  205830f485200252b2d7c34b15c8037ce06de6cee95f6f4ab23f856016057df5 \
                        size    2685577832
    distname            Xcode_6.3.1
}

subport MacOSX10.8.sdk {
#   SDK                   Size  Date        File                                                Notes
#   10.8-12A264     1941984563  2019-12-20  Xcode_4.4.dmg
#   10.8-12A264     1941992755  2019-12-20  Xcode_4.4.1.dmg
#   10.8.2-12C37    1662589104  2019-12-20  Xcode_4.5.dmg
#   10.8.2-12C37    1662484314  2019-12-20  Xcode_4.5.1.dmg
#   10.8.2-12C37    1663452382  2019-12-20  Xcode_4.5.2.dmg
#   10.8.2-12C37    1723665548  2013-01-25  xcode460417218a.dmg                                 not reissued in 2019
#   10.8.3-12D75    1712373581  2019-12-20  Xcode_4.6.1.dmg
#   10.8.3-12D75    1712053690  2019-12-20  Xcode_4.6.2.dmg
#   10.8.3-12D75    1712074184  2019-12-20  Xcode_4.6.3.dmg
#   10.8.3-12D75    2024415296  2019-12-11  Xcode_5.dmg
#   10.8.5-12F37    2171659981  2019-12-11  Xcode_5.0.1.dmg                                     newest smallest
#   10.8.5-12F37    2173178480  2019-12-11  Xcode_5.0.2.dmg
#   10.8.5-12F37    2261628363  2019-12-11  Xcode_5.1.dmg
#   10.8.5-12F37    2260656898  2019-12-11  Xcode_5.1.1.dmg
    set macos_name      {OS X Mountain Lion}
    version             10.8.5-12F37
    revision            0
    checksums           rmd160  275e6cf1ca701792933ec9b7cb59eceb0663deb3 \
                        sha256  92a38fc37e6928a5b4b4066796dc402bcb8dbbb0070a5c64c3b48c8487901b1a \
                        size    2171659981
    master_sites.mirror_subdir \
                        xcode_5.0.1
    distname            Xcode_5.0.1
}

subport MacOSX10.7.sdk {
#   SDK                   Size  Date        File                                                Notes
#   10.7-11A511a    3175033467  2012-03-21  installxcode_41_lion.dmg
#   10.7.2-11C63    1807251365  2012-03-21  installxcode_42_lion.dmg
#   10.7.2-11C63    1807220808  2012-03-21  installxcode_421_lion.dmg
#   10.7.3-11D50a   1493268524  2019-12-20  Xcode_4.3_for_Lion.dmg
#   10.7.3-11D50a   1575888770  2019-12-20  Xcode_4.3.1_for_Lion.dmg
#   10.7.3-11D50a   1981952725  2019-12-20  Xcode_4.3.2_for_Lion.dmg
#   10.7.4-11E52    1975435989  2019-12-20  Xcode_4.3.3_for_Lion.dmg
#   10.7.4-11E52    1941984563  2019-12-20  Xcode_4.4.dmg
#   10.7.4-11E52    1941992755  2019-12-20  Xcode_4.4.1.dmg
#   10.7.4-11E52    1662589104  2019-12-20  Xcode_4.5.dmg
#   10.7.4-11E52    1662484314  2019-12-20  Xcode_4.5.1.dmg                                     newest smallest
#   10.7.4-11E52    1663452382  2019-12-20  Xcode_4.5.2.dmg
#   10.7.4-11E52    1723665548  2013-01-25  xcode460417218a.dmg                                 not reissued in 2019
#   10.7.4-11E52    1712373581  2019-12-20  Xcode_4.6.1.dmg
#   10.7.4-11E52    1712053690  2019-12-20  Xcode_4.6.2.dmg
#   10.7.4-11E52    1712074184  2019-12-20  Xcode_4.6.3.dmg
    set macos_name      {Mac OS X Lion}
    version             10.7.4-11E52
    revision            0
    checksums           rmd160  687b76d45c132485489ff59930cbe96dc64fbf2d \
                        sha256  aaffbc3949548259e2c05aa40dddff6ed3215ec37733a15cfef888f95b938575 \
                        size    1662484314
    master_sites.mirror_subdir \
                        xcode_4.5.1
    distname            Xcode_4.5.1
}

subport MacOSX10.6.sdk {
#   SDK                   Size  Date        File                                                Notes
#   10.6-10A432      785073678  2009-08-27  xcode3210a432.dmg
#   10.6-10M2003     787658017  2009-10-08  xcode321_10m2003_developerdvd.dmg
#   10.6-10M2148     780887329  2010-03-30  xcode322_2148_developerdvd.dmg
#   10.6-10M2262    2446068106  2010-07-07  xcode_3.2.3_and_ios_sdk_4.0.1.dmg
#   10.6-10M2262    2675189630  2010-08-11  xcode_3.2.3_and_ios_sdk_4.0.2.dmg
#   10.6-10M2309    3154281265  2010-09-07  xcode_3.2.4_and_ios_sdk_4.1.dmg
#   10.6-10M2423    3782241266  2010-11-19  xcode_3.2.5_and_ios_sdk_4.2_final.dmg
#   10.6-10M2518    4443150993  2011-03-07  xcode_3.2.6_and_ios_sdk_4.3.dmg
#   10.6-4A304a     4597154754  2011-03-09  xcode_4_and_ios_sdk_4.3__final.dmg
#   10.6-4A1006     4607939522  2011-03-23  xcode_4.0.1_and_ios_sdk_4.3.dmg
#   10.6-4A2002a    4608009154  2011-04-12  xcode_4.0.2_and_ios_sdk_4.3.dmg
#   10.6-4B110f     4645556106  2011-08-29  xcode_4.1_for_snow_leopard.dmg
#   10.6.6-10J567   3175033467  2012-03-21  installxcode_41_lion.dmg
#   10.6.8-10K549   1761397888  2011-10-12  xcode_4.2_for_snow_leopard.dmg
#   10.6.8-10K549   1807251365  2012-03-21  installxcode_42_lion.dmg
#   10.6.8-10K549   1807220808  2012-03-21  installxcode_421_lion.dmg
#   10.6.8-10K549   1493268524  2019-12-20  Xcode_4.3_for_Lion.dmg                              newest smallest
#   10.6.8-10K549   1575888770  2019-12-20  Xcode_4.3.1_for_Lion.dmg
#   10.6.8-10K549   1981952725  2019-12-20  Xcode_4.3.2_for_Lion.dmg
#   10.6.8-10K549   1975435989  2019-12-20  Xcode_4.3.3_for_Lion.dmg
    set macos_name      {Mac OS X Snow Leopard}
    version             10.6.8-10K549
    revision            0
    checksums           rmd160  1c4cf2f7b384840655b24aef57a21fbcca4349c4 \
                        sha256  51ad4ad86be70532bec5ba29da1cec9aedfdd040eb14b6b7e15a4d886c2be330 \
                        size    1493268524
    master_sites.mirror_subdir \
                        xcode_4.3_for_lion_21266
    distname            Xcode_4.3_for_Lion
}

pre-fetch {
    foreach distfile ${distfiles} {
        if {![file isfile ${distpath}/${distfile}]} {
            ui_error "This port cannot download the needed files automatically."
            ui_error "Please log in to your Apple Developer account at:"
            ui_error ""
            ui_error "https://developer.apple.com/download/"
            ui_error ""
            ui_error "Then paste this URL into your browser:"
            ui_error ""
            ui_error "[lindex ${master_sites} 0]/${distfile}"
            ui_error ""
            ui_error "Place the downloaded file in this directory:"
            ui_error ""
            ui_error "${distpath}"
            ui_error ""
            ui_error "Then retry installing this port."
            return -code error "distfiles missing"
        }
    }
}

# You'd think this would be in Tcl or MacPorts base already.
# http://wiki.tcl.tk/4884
proc map {prefix list} {
    set result {}
    foreach item ${list} {
        lappend result [{*}${prefix} ${item}]
    }
    return ${result}
}

set sdk_dir             ${prefix}/Developer/SDKs

use_configure           no

if {${os.platform} eq "darwin" && ${os.major} < 11} {
    # Having the stdlib set to libc++ on 10.6 causes a dependency on a
    # macports-clang compiler to be added, which is unnecessary.
    configure.cxx_stdlib
}

pre-destroot {
    xinstall -d ${destroot}${sdk_dir}
}

if {${subport} eq ${name}} {
    description         latest macOS SDK

    long_description    This port installs a link to the latest macOS SDK.

    distfiles
    supported_archs     noarch

    depends_run-append  port:MacOSX${version}.sdk

    build {}

    destroot {
        ln -s MacOSX${version}.sdk ${destroot}${sdk_dir}/MacOSX.sdk
    }
} else {
    set sdk_version     [lindex [split ${version} -] 0]
    set sdk_build       [lindex [split ${version} -] 1]
    set sdk_major       [join [lrange [split ${sdk_version} .] 0 1] .]

    description         ${macos_name} v${sdk_version} (${sdk_build}) SDK

    long_description    This port installs the ${macos_name} v${sdk_version} \
                        (${sdk_build}) SDK.

    # /usr/bin/cpio does not like to deal with some dmg contents:
    # cpio: Cannot restore xattr:com.apple.decmpfs: Permission denied
    depends_extract     port:libarchive

    set distfile        [lindex ${extract.only} 0]
    set ext             [file extension ${distfile}]
    if {${ext} eq ".xip"} {
        xar_file        ${distfile}
        xar_payload     Content
    }
    if {${xar_file} ne ""} {
        # /usr/bin/xar does not support -O / --to-stdout
        depends_extract-append \
                        port:xar
        if {${xar_pbzx}} {
            depends_extract-append \
                        port:xz
        }
    }

    set plist           /System/Library/CoreServices/SystemVersion.plist

    extract {
        set dmg [expr {${ext} eq ".dmg"}]
        set source_dir ${distpath}
        if {${xar_file} ne ""} {
            if {${xar_pbzx}} {
                set payload_decompress ${workpath}/pbzx
                system "${configure.cc} ${configure.cflags} ${configure.cppflags} ${configure.ldflags} [shellescape ${filespath}/pbzx.c] -llzma -o [shellescape ${payload_decompress}]"
            } else {
                set payload_decompress bzcat
            }
        }
        if {${dmg}} {
            set source_dir [mkdtemp "/tmp/mports.XXXXXX"]
            set hdiutil [findBinary hdiutil ${portutil::autoconf::hdiutil_path}]
            system "${hdiutil} attach [shellescape ${distpath}/${distfile}] -private -readonly -nobrowse -mountpoint [shellescape ${source_dir}]"
        }
        set escaped_extract_paths [join [map {shellescape} ${extract_paths}] { }]
        xinstall -d ${worksrcpath}
        if {${xar_file} ne ""} {
            system -W ${worksrcpath} "${prefix}/bin/xar -xOf [shellescape ${source_dir}/${xar_file}] [shellescape ${xar_payload}] | ${payload_decompress} | ${prefix}/bin/bsdcpio -idmu --quiet ${escaped_extract_paths}"
        } elseif {[llength ${extract_paths}] > 0} {
            system -W ${source_dir} "[findBinary find ${portutil::autoconf::find_path}] ${escaped_extract_paths} -depth -perm -+r | ${prefix}/bin/bsdcpio -pdmu --quiet [shellescape ${worksrcpath}]"
        }
        if {${dmg}} {
            system "${hdiutil} detach [shellescape ${source_dir}]"
            delete ${source_dir}
        }
        if {![file exists ${worksrcpath}/[lindex ${extract_paths} 0]${plist}]} {
            ui_error "we don't appear to have extracted everything properly"
            return -code error "extract problem"
        }
    }

    build {
        if {${os.major} < 9} {
            set plistbuddy {}
            fs-traverse item [list /Library/Receipts] {
                if {[file tail ${item}] eq "PlistBuddy" && [file isfile ${item}] && [file executable ${item}]} {
                    set plistbuddy ${item}
                    break
                }
            }
            if {${plistbuddy} eq {}} {
                ui_error "can't find PlistBuddy in /Library/Receipts"
                return -code error "PlistBuddy not found"
            }
        } else {
            set plistbuddy /usr/libexec/PlistBuddy
        }
        set extracted_version [join [split [exec ${plistbuddy} -c Print:ProductVersion -c Print:ProductBuildVersion ${worksrcpath}/[lindex ${extract_paths} 0]${plist}] "\n"] "-"]
        if {${version} ne ${extracted_version}} {
            ui_error "port version is ${version} but SDK version is ${extracted_version}"
            return -code error "version mismatch"
        }
    }

    destroot {
        move ${worksrcpath}/[lindex ${extract_paths} 0] ${destroot}${sdk_dir}/MacOSX${sdk_major}.sdk
        if {[vercmp ${sdk_version} 11.0] >= 0} {
            ln -s MacOSX${sdk_major}.sdk ${destroot}${sdk_dir}/MacOSX[lindex [split ${sdk_major} .] 0].sdk
        }
    }

    if {[vercmp ${sdk_version} 11.0] >= 0} {
        supported_archs arm64 x86_64
    } elseif {[vercmp ${sdk_version} 10.14] >= 0} {
        supported_archs x86_64
    } elseif {[vercmp ${sdk_version} 10.7] >= 0} {
        supported_archs i386 x86_64
    } elseif {[vercmp ${sdk_version} 10.6] >= 0} {
        supported_archs i386 ppc x86_64
    }

    if {[llength ${supported_archs}] > 1} {
        configure.universal_archs {*}${supported_archs}
        variant universal {}
        default_variants +universal
        variant_set universal
    }
}

livecheck.type          none
