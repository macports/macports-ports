# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

name                MacOSX.sdk
categories          devel
platforms           macosx
maintainers         {ryandesign @ryandesign}
license             Restrictive Nomirror

homepage            https://developer.apple.com/macos/
master_sites        https://download.developer.apple.com/Developer_Tools
default dist_subdir {Xcode/${xcode_vers}}
default distname    {Xcode_${xcode_vers}}

# Only extract these paths from the distfile.
options my_extract_only_paths
default my_extract_only_paths {"Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/MacOSX${macos_major}.sdk"}

# MacPorts base doesn't support "use_xip" yet.
proc my_use_xip {use_xip} {
    if [tbool use_xip] {
        # /usr/bin/cpio generates suprious error messages
        # /usr/bin/xar does not support -O / --to-stdout
        depends_extract-append  port:cpio port:pbzx port:xar
        extract.suffix  .xip
    }
}

# "use_dmg" would copy the entire contents of the dmg to workpath.
proc my_use_dmg {use_dmg} {
    if [tbool use_dmg] {
        extract.suffix  .dmg
    }
}

subport MacOSX10.14.sdk {
    # Xcode   SDK             File                                          Size
    # 10.0    10.14-18A384    Xcode_10.xip                            5876903092
    # 10.1    10.14.1-18B71   Xcode_10.1.xip                          6047730870
    # 10.1 fails to extract due to presumed pbzx bug:
    # http://newosxbook.com/forum/viewtopic.php?f=3&t=19362
    set macos_name  {macOS Mojave}
    set xcode_vers  10.1
    version         10.14.1-18B71
    revision        0
    checksums       rmd160  a60f79d8651d3879c17f06727189afd4afcc3421 \
                    sha256  b6eaa0d2b3e863a4a5ff612989c560abb682d08733663e1ab71d513ed3cfb328 \
                    size    6047730870

    homepage        https://developer.apple.com/documentation/macos_release_notes/macos_mojave_10_14_release_notes
    master_sites    ${master_sites}/Xcode_${xcode_vers}
    my_use_xip      yes
    my_extract_only_paths
}

subport MacOSX10.13.sdk {
    # Xcode   SDK             File                                          Size
    # 9.0     10.13-17A360    Xcode_9.xip                             5370313953
    # 9.0.1   10.13-17A360    Xcode_9.0.1.xip                         5442825442
    # 9.1     10.13.1-17B48   Xcode_9.1.xip                           5442772192
    # 9.2     10.13.2-17C76   Xcode_9.2.xip                           5478644961
    # 9.3     10.13.4-17E189  Xcode_9.3.xip                           5235010787
    # 9.3.1   10.13.4-17E189  Xcode_9.3.1.xip                         5235006688
    # 9.4     10.13.4-17E189  Xcode_9.4.xip                           5271108831
    # 9.4.1   10.13.4-17E189  Xcode_9.4.1.xip                         5271280865
    # 9.3.1 fails to extract due to presumed pbzx bug:
    # http://newosxbook.com/forum/viewtopic.php?f=3&t=19362
    set macos_name  {macOS High Sierra}
    set xcode_vers  9.3.1
    version         10.13.4-17E189
    revision        0
    checksums       rmd160  5bae84a8c8205b0370a0a26115e5c83e2ce8e5a4 \
                    sha256  7e54f742a5c14a06b065e64a68930639519d0abca1703bef216b1ad86534cc18 \
                    size    5235006688

    homepage        https://developer.apple.com/library/archive/releasenotes/General/RN-macOSSDK-10.13/
    master_sites    ${master_sites}/Xcode_${xcode_vers}
    my_use_xip      yes
    my_extract_only_paths
}

subport MacOSX10.12.sdk {
    # Xcode   SDK             File                                          Size
    # 8.0     10.12-16A300    Xcode_8.xip                             4409982185
    # 8.1     10.12.1-16B2649 Xcode_8.1.xip                           4441292005
    # 8.2     10.12.2-16C58   Xcode_8.2.xip                           4521217251
    # 8.2.1   10.12.2-16C58   Xcode_8.2.1.xip                         4521385190
    # 8.3     10.12.4-16E185  Xcode_8.3.xip                           4499938533
    # 8.3.1   10.12.4-16E185  Xcode_8.3.1.xip                         4495662305
    # 8.3.2   10.12.4-16E185  Xcode8.3.2.xip                          4488391909
    # 8.3.3   10.12.4-16E185  Xcode8.3.3.xip                          4517010666
    # 8.3 and 8.3.3 fail to extract due to presumed pbzx bug:
    # http://newosxbook.com/forum/viewtopic.php?f=3&t=19362
    set macos_name  {macOS Sierra}
    set xcode_vers  8.3.2
    version         10.12.4-16E185
    revision        0
    checksums       rmd160  1158a41a7db5002c4161382017d86dc0d8cf8861 \
                    sha256  5f67e3d54a9725729d1b0186bc9fe8f589bf76a76bb92fe36e8cda17d130c58b \
                    size    4488391909

    master_sites    ${master_sites}/Xcode_${xcode_vers}
    distname        Xcode${xcode_vers}
    my_use_xip      yes
    my_extract_only_paths
}

subport MacOSX10.11.sdk {
    # Xcode   SDK             File                                          Size
    # 7.0     10.11-15A278    Xcode_7.dmg                             3846389102
    # 7.0.1   10.11-15A278    Xcode_7.0.1.dmg                         3847783905
    # 7.1     10.11-15A278    Xcode_7.1.dmg                           4617042305
    # 7.1.1   10.11-15A278    Xcode_7.1.1.dmg                         4616944303
    # 7.2     10.11.2-15C43   Xcode_7.2.dmg                           4733718212
    # 7.2.1   10.11.2-15C43   Xcode_7.2.1.dmg                         4733690222
    # 7.3     10.11.4-15E60   Xcode_7.3.dmg                           5199845256
    # 7.3.1   10.11-4-15E60   Xcode_7.3.1.dmg                         5202388728
    set macos_name  {OS X El Capitan}
    set xcode_vers  7.3.1
    version         10.11.4-15E60
    revision        0
    checksums       rmd160  ab7f32b4c6052004442910543c3e8de89d5f19a3 \
                    sha256  bb0dedf613e86ecb46ced945913fa5069ab716a0ade1035e239d70dee0b2de1b \
                    size    5202388728

    master_sites    ${master_sites}/Xcode_${xcode_vers}
    my_use_dmg      yes
}

subport MacOSX10.10.sdk {
    # Xcode   SDK             File                                          Size
    # 6.1     10.10-14A382    xcode_6.1.dmg                           2689103840
    # 6.1.1   10.10-14A382    xcode_6.1.1.dmg                         2689510545
    # 6.2     10.10-14A383    Xcode_6.2.dmg                           2778059898
    # 6.3     10.10.3-14D125  Xcode_6.3.dmg                           2766981471
    # 6.3.1   10.10.3-14D125  Xcode_6.3.1.dmg                         2766551469
    # 6.3.2   10.10.3-14D125  Xcode_6.3.2.dmg                         2768602015
    # 6.4     10.10.3-14D125  Xcode_6.4.dmg                           2804514980
    set macos_name  {OS X Yosemite}
    set xcode_vers  6.3.1
    version         10.10.3-14D125
    revision        0
    checksums       rmd160  63e01ae11e9fd7639c19253c741c4cbc16994dfa \
                    sha256  05b7d246cb7925dab51c84f3737213c127fef2363ae5010f42546fd833a4cf8f \
                    size    2766551469

    master_sites    ${master_sites}/Xcode_${xcode_vers}
    my_use_dmg      yes
}

subport MacOSX10.9.sdk {
    # Xcode   SDK             File                                          Size
    # 5.0.1   10.9-13A595     xcode_5.0.1.dmg                         2194212232
    # 5.0.2   10.9-13A595     xcode_5.0.2.dmg                         2195476273
    # 5.1     10.9.2-13C64    xcode_5.1.dmg                           2286987512
    # 5.1.1   10.9.2-13C64    xcode_5.1.1.dmg                         2285917493
    # 6.0.1   10.9.5-13F26    xcode_6.0.1.dmg                         2620630196
    # 6.1     10.9.5-13F26    xcode_6.1.dmg                           2689103840
    # 6.1.1   10.9.5-13F26    xcode_6.1.1.dmg                         2689510545
    # 6.2     10.9.5-13F26    Xcode_6.2.dmg                           2778059898
    # 6.3     10.9.5-13F34    Xcode_6.3.dmg                           2766981471
    # 6.3.1   10.9.5-13F34    Xcode_6.3.1.dmg                         2766551469
    # 6.3.2   10.9.5-13F34    Xcode_6.3.2.dmg                         2768602015
    # 6.4     10.9.5-13F34    Xcode_6.4.dmg                           2804514980
    set macos_name  {OS X Mavericks}
    set xcode_vers  6.3.1
    version         10.9.5-13F34
    revision        0
    checksums       rmd160  63e01ae11e9fd7639c19253c741c4cbc16994dfa \
                    sha256  05b7d246cb7925dab51c84f3737213c127fef2363ae5010f42546fd833a4cf8f \
                    size    2766551469

    master_sites    ${master_sites}/Xcode_${xcode_vers}
    my_use_dmg      yes
}

subport MacOSX10.8.sdk {
    # Xcode   SDK             File                                          Size
    # 4.4     10.8-12A264     xcode446938108a.dmg                     1941976253
    # 4.4.1   10.8-12A264     xcode_4.4.1_6938145.dmg                 1941984445
    # 4.5     10.8.2-12C37    xcode_4.5.dmg                           1671716725
    # 4.5.1   10.8.2-12C37    xcode4510417539a.dmg                    1672022344
    # 4.5.2   10.8.2-12C37    xcode4520418508a.dmg                    1674382124
    # 4.6     10.8.2-12C37    xcode460417218a.dmg                     1723665548
    # 4.6.1   10.8.3-12D75    xcode4610419628a.dmg                    1723812415
    # 4.6.2   10.8.3-12D75    xcode4620419895a.dmg                    1723664346
    # 4.6.3   10.8.3-12D75    xcode4630916281a.dmg                    1723816316
    # 5.0     10.8.3-12D75    xcode_5.dmg                             2046959503
    # 5.0.1   10.8.5-12F37    xcode_5.0.1.dmg                         2194212232
    # 5.0.2   10.8.5-12F37    xcode_5.0.2.dmg                         2195476273
    # 5.1     10.8.5-12F37    xcode_5.1.dmg                           2286987512
    # 5.1.1   10.8.5-12F37    xcode_5.1.1.dmg                         2285917493
    set macos_name  {OS X Mountain Lion}
    set xcode_vers  5.0.1
    version         10.8.5-12F37
    revision        0
    checksums       rmd160  e4affb60d6a34e95b7b4ac5c55f34be2e9ac218a \
                    sha256  416b2b5d61045c46a5c589cbbca627647b1d6ea2d649eee22cfe38a355f793a3 \
                    size    2194212232

    master_sites    ${master_sites}/xcode_${xcode_vers}
    distname        xcode_${xcode_vers}
    my_use_dmg      yes
}

subport MacOSX10.7.sdk {
    # Xcode   SDK             File                                          Size
    # 4.1     10.7-11A511a    installxcode_41_lion.dmg                3175033467
    # 4.2     10.7.2-11C63    installxcode_42_lion.dmg                1807251365
    # 4.2.1   10.7.2-11C63    installxcode_421_lion.dmg               1807220808
    # 4.3     10.7.3-11D50a   xcode_43_lion.dmg                       1507343939
    # 4.3.1   10.7.3-11D50a   xcode_431_lion.dmg                      1591796004
    # 4.3.2   10.7.3-11D50a   xcode_432_lion.dmg                      1981944415
    # 4.3.3   10.7.4-11E52    xcode_4.3.3_for_lion.dmg                1975427679
    # 4.4     10.7.4-11E52    xcode446938108a.dmg                     1941976253
    # 4.4.1   10.7.4-11E52    xcode_4.4.1_6938145.dmg                 1941984445
    # 4.5     10.7.4-11E52    xcode_4.5.dmg                           1671716725
    # 4.5.1   10.7.4-11E52    xcode4510417539a.dmg                    1672022344
    # 4.5.2   10.7.4-11E52    xcode4520418508a.dmg                    1674382124
    # 4.6     10.7.4-11E52    xcode460417218a.dmg                     1723665548
    # 4.6.1   10.7.4-11E52    xcode4610419628a.dmg                    1723812415
    # 4.6.2   10.7.4-11E52    xcode4620419895a.dmg                    1723664346
    # 4.6.3   10.7.4-11E52    xcode4630916281a.dmg                    1723816316
    set macos_name  {Mac OS X Lion}
    set xcode_vers  4.5
    version         10.7.4-11E52
    revision        0
    checksums       rmd160  1631fe986b61673baa8cb22ad4fcad0fb7548297 \
                    sha256  2dd44f484f3756f9aeba8c22e064dd96fd19548bfdf67a4367f9e56b4ed8cda0 \
                    size    1671716725

    master_sites    ${master_sites}/xcode_${xcode_vers}
    distname        xcode_${xcode_vers}
    my_use_dmg      yes
}

subport MacOSX10.6.sdk {
    # Xcode   SDK             File                                          Size
    # 3.2     10.6-10A432     xcode3210a432.dmg                        785073678
    # 3.2.1   10.6-10M2003    xcode321_10m2003_developerdvd.dmg        787658017
    # 3.2.2   10.6-10M2148    xcode322_2148_developerdvd.dmg           780887329
    # 3.2.3   10.6-10M2262    xcode_3.2.3_and_ios_sdk_4.0.1.dmg       2446068106
    # 3.2.3   10.6-10M2262    xcode_3.2.3_and_ios_sdk_4.0.2.dmg       2675189630
    # 3.2.4   10.6-10M2309    xcode_3.2.4_and_ios_sdk_4.1.dmg         3154281265
    # 3.2.5   10.6-10M2423    xcode_3.2.5_and_ios_sdk_4.2_final.dmg   3782241266
    # 3.2.6   10.6-10M2518    xcode_3.2.6_and_ios_sdk_4.3.dmg         4443150993
    # 4.0.1   10.6-4A1006     xcode_4.0.1_and_ios_sdk_4.3.dmg         4607939522
    # 4.0.2   10.6-4A2002a    xcode_4.0.2_and_ios_sdk_4.3.dmg         4608009154
    # 4.1     10.6-4B110f     xcode_4.1_for_snow_leopard.dmg          4645556106
    # 4.1     10.6.6-10J567   installxcode_41_lion.dmg                3175033467
    # 4.2     10.6.8-10K549   xcode_4.2_for_snow_leopard.dmg          1761397888
    # 4.2     10.6.8-10K549   installxcode_42_lion.dmg                1807251365
    # 4.2.1   10.6.8-10K549   installxcode_421_lion.dmg               1807220808
    # 4.3     10.6.8-10K549   xcode_43_lion.dmg                       1507343939
    # 4.3.1   10.6.8-10K549   xcode_431_lion.dmg                      1591796004
    # 4.3.2   10.6.8-10K549   xcode_432_lion.dmg                      1981944415
    # 4.3.3   10.6.8-10K549   xcode_4.3.3_for_lion.dmg                1975427679
    set macos_name  {Mac OS X Snow Leopard}
    set xcode_vers  4.3
    version         10.6.8-10K549
    revision        0
    checksums       rmd160  df28439edd8347c3afa9bbde02df670b4b1ef55c \
                    sha256  94baaca2dcdc61ede95333b76167972147de807c530c7622ba2770010963a330 \
                    size    1507343939

    master_sites    ${master_sites}/xcode_${xcode_vers}_for_lion_21266/
    distname        xcode_43_lion
    my_use_dmg      yes
}

pre-fetch {
    foreach distfile ${distfiles} {
        if {![file isfile ${distpath}/${distfile}]} {
            ui_error "This port cannot download the needed files automatically."
            ui_error "Please log in to your Apple Developer account at:"
            ui_error ""
            ui_error "https://developer.apple.com/download/more/"
            ui_error ""
            ui_error "Then download Xcode ${xcode_vers} by pasting this URL into your browser:"
            ui_error ""
            ui_error "[lindex ${master_sites} 0]/${distfile}"
            ui_error ""
            ui_error "Place the downloaded file in this directory:"
            ui_error ""
            ui_error "${distpath}"
            ui_error ""
            ui_error "Then retry installing this port."
            return -code error "distfiles missing"
        }
    }
}

# You'd think this would be in Tcl or MacPorts base already.
# http://wiki.tcl.tk/4884
proc map {prefix list} {
    set result {}
    foreach item ${list} {
        lappend result [{*}${prefix} ${item}]
    }
    return ${result}
}

# You'd think this would be in MacPorts base already.
proc shell_quote {path} {
    # This is terrible, but good enough for this port.
    return "\"${path}\""
}

proc check_disk_space {path requested_bytes} {
    set requested_mbytes [expr {${requested_bytes} / 1024 / 1024}]
    set df_k [lindex [split [exec df -k ${path}] \n] end]
    set volume_mbytes [expr {[lindex ${df_k} 1] / 1024}]
    set free_mbytes [expr {[lindex ${df_k} 3] / 1024}]
    set needed_mbytes [expr {ceil((1.05 * ${requested_mbytes} + 0.01 * ${volume_mbytes}) - ${free_mbytes})}]
    ui_debug "${requested_mbytes} MiB requested; ${free_mbytes} MiB free"
    if {${needed_mbytes} > 0} {
        ui_error "The disk is nearly full. Please free up an additional ${needed_mbytes} MiB."
        return -code error "insufficient disk space"
    }
}

extract {
    set my_quoted_extract_only_paths [join [map {shell_quote} ${my_extract_only_paths}] " "]
    foreach distfile ${extract.only} {
        set suffix [file extension ${distfile}]
        switch ${suffix} {
            .dmg {
                set dmg_mount   [mkdtemp "/tmp/mports.XXXXXXXX"]
                extract.cmd     [findBinary hdiutil ${portutil::autoconf::hdiutil_path}]
                system "${extract.cmd} attach [shell_quote ${distpath}/${distfile}] -private -readonly -nobrowse -mountpoint [shell_quote ${dmg_mount}]"
                system -W ${dmg_mount} "[findBinary find ${portutil::autoconf::find_path}] [expr {[llength ${my_extract_only_paths}] > 0 ? ${my_quoted_extract_only_paths} : {.}}] -depth -perm -+r -print0 | [findBinary cpio ${portutil::autoconf::cpio_path}] -p0dmu [shell_quote ${extract.dir}/${worksrcdir}]"
                system "${extract.cmd} detach [shell_quote ${dmg_mount}]"
                delete ${dmg_mount}
            }
            .xip {
                if {![file isfile ${worksrcpath}/Metadata]} {
                    xinstall -d ${worksrcpath}
                    # Does this work on all OS versions?
                    if {1} {
                        system "pkgutil --check-signature [shell_quote ${distpath}/${distfile}]"
                    }
                    system -W ${worksrcpath} "${prefix}/bin/xar -xf [shell_quote ${distpath}/${distfile}] Metadata"
                }
                check_disk_space ${worksrcpath} [exec /usr/libexec/PlistBuddy -c Print:UncompressedSize ${worksrcpath}/Metadata]
                system -W ${worksrcpath} "${prefix}/bin/xar -xOf [shell_quote ${distpath}/${distfile}] Content | ${prefix}/bin/pbzx | ${prefix}/bin/gnucpio -idmu [expr {[llength ${my_extract_only_paths}] > 0 ? ${my_quoted_extract_only_paths} : {}}]"
            }
            default {
                return -code error "unhandled extract suffix ${suffix}"
            }
        }
    }
}

set sdk_dir         ${prefix}/Developer/SDKs

use_configure       no

build {}

pre-destroot {
    xinstall -d ${destroot}${sdk_dir}
}

if {${subport} eq ${name}} {
    version             10.14
    revision            0
    description         latest macOS SDK
    long_description    This port installs a link to the latest macOS SDK.
    distfiles
    supported_archs     noarch
    depends_run-append  port:MacOSX${version}.sdk
    destroot {
        ln -s MacOSX${version}.sdk ${destroot}${sdk_dir}/MacOSX.sdk
    }
} else {
    set macos_version   [lindex [split ${version} -] 0]
    set macos_build     [lindex [split ${version} -] 1]
    set macos_major     [join [lrange [split ${macos_version} .] 0 1] .]
    if {[vercmp ${macos_version} 10.14] >= 0} {
        supported_archs x86_64
    } else {
        supported_archs i386 x86_64
    }
    description         ${macos_name} v${macos_version} (${macos_build}) SDK
    long_description    This port installs the ${macos_name} v${macos_version} (${macos_build}) SDK.
    destroot {
        move ${worksrcpath}/Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/MacOSX${macos_major}.sdk ${destroot}${sdk_dir}
    }
}

if {[llength ${supported_archs}] > 1} {
    variant universal {}
    default_variants +universal
    variant_set universal
}

livecheck.type      none
