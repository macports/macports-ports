# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem                                        1.0
PortGroup             github                      1.0
PortGroup             cxx11                       1.1
PortGroup             java                        1.0
PortGroup             compilers                   1.0
PortGroup             xcodeversion                1.0
PortGroup             compiler_blacklist_versions 1.0

# epoch unfortunately needed to go back to 0.18.0 as 0.19.0
# built tensorflow badily, giving runtime seg. faults...
# Now resolved, but epoch must NOT be removed though now.
epoch                 1

# ports
name                  bazel
subport               bazel21 { }

if { ${name} eq ${subport} } {
    
    # Main port
    github.setup      bazelbuild ${name} 0.25.3
    revision          0
    
    conflicts         bazel21
    
    checksums         rmd160  ad2a1e95f08ef94bfe0afdf3c02fa7700e3816d6 \
                      sha256  23eafd3e439bc71baba9c592b52cb742dabc8640a13b9da1751fec090a2dda99 \
                      size    275804363

    pre-fetch {
        # https://trac.macports.org/ticket/58518
        if { ${os.major} < 14 } {
            ui_error "${name} ${version} requires macOS10.10 or newer."
            return -code error "incompatible macOS version"
        }
    }
    
    # Required java version.
    java.version      12
    # JDK port to install if required java not found
    java.fallback     openjdk12
    
} elseif { "bazel21" eq ${subport} } {
    
    # legacy 0.21.x port
    github.setup      bazelbuild ${name} 0.21.0
    revision          0
    
    livecheck.type    none
    conflicts         bazel
    
    checksums         rmd160  db49db0e036eee72451c7c83a92359c8b5b3a7ec \
                      sha256  6ccb831e683179e0cfb351cb11ea297b4db48f9eab987601c038aa0f83037db4 \
                      size    88279748
    
    # Required java version.
    java.version      11
    # JDK port to install if required java not found
    java.fallback     openjdk11
    
}

github.tarball_from   releases

categories            devel

maintainers           {tfmnet.com:mohamed.issa @RootFunction} \
                      {jonesc @cjones051073} \
                      openmaintainer

description           A tool for automating builds and tests.
long_description      ${description}

platforms             darwin
license               Apache-2

distname              ${distname}-dist

use_zip               yes
extract.mkdir         yes

use_configure         no

patch.pre_args        -p1

# The oldest Xcode version to use default Xcode compiler
# Note setting here should be in sync with that in py-tensorflow
set bazel_min_xcode   9.0

depends_build-append  bin:zip:zip 

depends_lib-append    port:cctools

# Older OS hacks
if {${os.major} < 16} {
    # https://github.com/bazelbuild/bazel/issues/6188
    depends_build-append  port:unzip
}
post-patch {
    if       {[vercmp ${xcodeversion} 7.0] < 0} {
        # Use hacked version that does not find anything.
        # We use MacPorts compiler toolchain anyway so not a problem.
        file copy -force ${filespath}/xcode_locator_fixes/Xcode6-xcode_locator.m ${worksrcpath}/tools/osx/xcode_locator.m
    } elseif {[vercmp ${xcodeversion} 8.0] < 0} {
        # Revert xcode-locator to an older version
        # https://raw.githubusercontent.com/bazelbuild/bazel/7372c04d40d7ab45fa2649c8be002ee6909668d1/tools/osx/xcode_locator.m
        file copy -force ${filespath}/xcode_locator_fixes/Xcode7-xcode_locator.m ${worksrcpath}/tools/osx/xcode_locator.m
    } elseif {[vercmp ${xcodeversion} 9.0] < 0} {
        # Revert xcode-locator to an older version
        # From bazel 0.21.0
        file copy -force ${filespath}/xcode_locator_fixes/Xcode8-xcode_locator.m ${worksrcpath}/tools/osx/xcode_locator.m
    }
}

compiler.blacklist-append {clang < 710}
#compilers.setup           require_fortran
build.env-append  CC=${configure.cc} \
                 CXX=${configure.cxx} \
              TMPDIR=${workpath}/tmp \
           JAVA_HOME=${java.home}
# Disable Xcode detection on older OSes, as we want the
# MP provided commandline utilities to be used instead.
if {[vercmp ${xcodeversion} ${bazel_min_xcode}] < 0} {
    build.env-append BAZEL_USE_CPP_ONLY_TOOLCHAIN=1
}

variant bash description {Enable bash completion support} {
    depends_build-append  bin:bash:bash 
}

build {

    if {[vercmp ${xcodeversion} ${bazel_min_xcode}] < 0} {
        # Patch build files to enforce correct compiler
        foreach f { scripts/bootstrap/compile.sh tools/osx/crosstool/CROSSTOOL.tpl src/test/shell/bazel/testdata/bazel_toolchain_test_data/tools/arm_compiler/CROSSTOOL tools/cpp/unix_cc_configure.bzl tools/cpp/CROSSTOOL third_party/zlib/configure src/test/java/com/google/devtools/build/lib/analysis/mock/MOCK_CROSSTOOL } {
            if {[file exists ${f}]} {
                foreach cmd {ar nm strip libtool ld objdump} {
                    reinplace -q "s|/usr/bin/${cmd}|${prefix}/bin/${cmd}|g" ${f}
                }
                reinplace -q "s|/usr/bin/clang|${configure.cc}|g"       ${f}
                reinplace -q "s|\"clang\"|\"${configure.cc}\"|g"        ${f}
                reinplace -q "s| clang | ${configure.cc} |g"            ${f}
                reinplace -q "s|/usr/local/include|${prefix}/include|g" ${f}
                reinplace -q "s|std=c++0x|std=c++11|g"                  ${f}
            }
        }
    }

    # Build bazel
    system -W ${worksrcpath} "JAVA_HOME=${java.home} VERBOSE=yes BAZEL_WRKDIR=${workpath} ./compile.sh"

    # Put executable in main working folder
    xinstall -m 755 -W ${worksrcpath}/output ${name} ${worksrcpath}
    
    # Include bash completion support ?
    if { [variant_isset bash]} {

        # Get ready to build the bash completion script
        file delete -force /var/tmp/_${name}_root

        # Build the bash completion script
        system -W ${worksrcpath} "./${name} --max_idle_secs=60 --output_user_root=${workpath} build -s --javabase //:jdk10 --host_javabase //:jdk10 //scripts:${name}-complete.bash"

    }
}

destroot {
    # Copy compiled binary
    set bindir ${prefix}/bin
    xinstall -d ${destroot}${bindir}
    xinstall -m 755 -W ${worksrcpath}/output ${name} ${destroot}${bindir}
    # Copy bash and zsh completion scripts to designated area for on-demand usage
    if { [variant_isset bash]} {
        set bcompldir ${prefix}/share/bash-completion/completions
        xinstall -d ${destroot}${bcompldir}
        xinstall -m 755 ${worksrcpath}/${name}-bin/scripts/${name}-complete.bash ${destroot}${bcompldir}/${name}
        set zcompldir ${prefix}/share/zsh/site-functions
        xinstall -d ${destroot}${zcompldir}
        xinstall -m 755 ${worksrcpath}/scripts/zsh_completion/_${name} ${destroot}${zcompldir}
    }
}

post-destroot {
    # Mark documentation, source, and example directories
    set docdir ${prefix}/share/doc/${name}
    set srcdir ${prefix}/src/${name}
    set expdir ${prefix}/share/examples/${name}

    # Copy documentation files
    xinstall -d ${destroot}${docdir}
    xinstall -m 644 -W ${worksrcpath} AUTHORS \
                                      CHANGELOG.md \
                                      CONTRIBUTING.md \
                                      CONTRIBUTORS \
                                      ISSUE_TEMPLATE.md \
                                      LICENSE \
                                      README.md \
                       ${destroot}${docdir}

    # Copy source files
    xinstall -d ${destroot}${srcdir}
    file copy -force {*}[glob ${worksrcpath}/src/*] ${destroot}${srcdir}

    # Copy example files
    xinstall -d ${destroot}${expdir}
    file copy -force {*}[glob ${worksrcpath}/examples/*] ${destroot}${expdir}
}

notes "
See http://bazel.build/docs/getting-started.html to start a new project!
"
