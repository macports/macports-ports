# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem                                            1.0
PortGroup             github                          1.0
PortGroup             java                            1.0
PortGroup             compiler_blacklist_versions     1.0

epoch                 1

# List of all bazel sub-ports ( e.g. as required by tensorflow )
set bazel_subports {bazel-0.25 bazel-2.0 bazel-3.5 bazel-3.7}
set all_bazels ${bazel_subports}
lappend all_bazels bazel

# define ports and sub-ports
name bazel
foreach b ${bazel_subports} { subport $b {} }

# Set the conflicts list
foreach b [lsearch -inline -all -not -exact $all_bazels ${subport}] {
    conflicts-append $b
}

# Min supported Darwin version
set min_darwin 16

# The oldest Xcode version to use default Xcode compiler
# Note setting here should be in sync with that in py-tensorflow
set bazel_min_xcode 9.0

if { ${name} eq ${subport} } {

    # Main port
    github.setup      bazelbuild ${name} 4.0.0
    revision          0

    checksums         rmd160  38aeaae4a1f6a5970fe72e892928f5842b9ba29a \
                      sha256  d350f80e70654932db252db380d2ec0144a00e86f8d9f2b4c799ffdb48e9cdd1 \
                      size    274505338

    set bazel_min_xcode 12.0
    compiler.blacklist-append {clang < 1200}
    compiler.blacklist-append {macports-clang-[5-9].0}

} elseif { "bazel-3.7" eq ${subport} } {

    # Specific version for py-tensorflow - DO NOT CHANGE
    github.setup      bazelbuild ${name} 3.7.2
    revision          0

    # livecheck none is correct here, see above.
    livecheck.type    none

    checksums         rmd160  abf23b277194854bbd4e4390ab6b3f971d26826c \
                      sha256  de255bb42163a915312df9f4b86e5b874b46d9e8d4b72604b5123c3a845ed9b1 \
                      size    313677756

    compiler.blacklist-append {clang < 710}
    compiler.blacklist-append {macports-clang-[7-9].0}

} elseif { "bazel-3.5" eq ${subport} } {

    # Specific version for py-tensorflow - DO NOT CHANGE
    github.setup      bazelbuild ${name} 3.5.1
    revision          0

    # livecheck none is correct here, see above.
    livecheck.type    none

    checksums         rmd160  8c52fab5214d862b83459f807c39ae392db49a75 \
                      sha256  67eae714578b22d24192b0eb3a2d35b07578bbd57a33c50f1e74f8acd6378b3c \
                      size    307982168

    compiler.blacklist-append {clang < 710}
    compiler.blacklist-append {macports-clang-[7-9].0}

} elseif { "bazel-2.0" eq ${subport} } {
    
    # Specific version for py-tensorflow - DO NOT CHANGE
    github.setup      bazelbuild ${name} 2.0.0
    revision          0

    # livecheck none is correct here, see above.
    livecheck.type    none
    
    checksums         rmd160  acf9b886d2ec69f2c7019037e3aeba6de48cb011 \
                      sha256  724da3c656f68e787a86ebb9844773aa1c2e3a873cc39462a8f1b336153d6cbb \
                      size    259625808

    compiler.blacklist-append {clang < 710}
    compiler.blacklist-append {macports-clang-[7-9].0}

} elseif { "bazel-0.25" eq ${subport} } {

    # Specific version for py-tensorflow1 - DO NOT CHANGE
    github.setup      bazelbuild ${name} 0.25.2
    revision          1

    # livecheck none is correct here, see above.
    livecheck.type    none

    checksums         rmd160  cfff49a4be85f7693ae615c293b13d0a447b80d7 \
                      sha256  7456032199852c043e6c5b3e4c71dd8089c1158f72ec554e6ec1c77007f0ab51 \
                      size    275804130

    # min OS
    set min_darwin 14
    compiler.blacklist-append {clang < 710}
    compiler.blacklist-append {macports-clang-[7-9].0}

}

# Check OS support
if { ${os.platform} eq "darwin" && ${os.major} < ${min_darwin} } {
    known_fail yes
    # https://trac.macports.org/ticket/58518
    pre-fetch {
        ui_error "${subport} ${version} requires Darwin ${min_darwin} or newer"
        return -code error "incompatible macOS version"
    }
}

# Required java version.
java.version          11+
# LTS JDK port to install if required java not found
java.fallback         openjdk11
# JDK only needed at build time, but java PG sets lib dependency so
# declare no conflict to allow redistribution of binaries.
license_noconflict    ${java.fallback}

github.tarball_from   releases

categories            devel

maintainers           {tfmnet.com:mohamed.issa @missa-prime} \
                      {jonesc @cjones051073} \
                      openmaintainer

description           A tool for automating builds and tests.
long_description      {*}${description}

platforms             darwin
license               Apache-2

distname              ${distname}-dist

use_zip               yes
extract.mkdir         yes

use_configure         no

patch.pre_args        -p1

# Even though bazel can build without Xcode, mark use Xcode for now since it fails to
# build with tracemode on latest master if both CLT and Xcode are available.
# Better solution is to respect MacPorts environment configure.developer_dir
use_xcode             yes

compiler.cxx_standard 2011

# python versions. Build needs both 'python2' and 'python3'
set py3ver 3.8
set py2ver 2.7

depends_lib-append    port:cctools

depends_build-append  bin:zip:zip \
                      port:python[string map {. {}} ${py2ver}] \
                      port:python[string map {. {}} ${py3ver}]

post-extract {
    # Make dir with selected MP 'python2' and 'python3' and add to PATH during build below
    file mkdir ${workpath}/bin
    ln -s ${prefix}/bin/python${py2ver} ${workpath}/bin/python2
    ln -s ${prefix}/bin/python${py3ver} ${workpath}/bin/python3
}

if {${os.platform} eq "darwin" && ${os.major} < 16} {
    # https://github.com/bazelbuild/bazel/issues/6188
    depends_build-append  port:unzip
}
post-patch {
    if       {[vercmp ${xcodeversion} 7.0] < 0} {
        # Use hacked version that does not find anything.
        # We use MacPorts compiler toolchain anyway so not a problem.
        file copy -force ${filespath}/xcode_locator_fixes/Xcode6-xcode_locator.m ${worksrcpath}/tools/osx/xcode_locator.m
    } elseif {[vercmp ${xcodeversion} 8.0] < 0} {
        # Revert xcode-locator to an older version
        # https://raw.githubusercontent.com/bazelbuild/bazel/7372c04d40d7ab45fa2649c8be002ee6909668d1/tools/osx/xcode_locator.m
        file copy -force ${filespath}/xcode_locator_fixes/Xcode7-xcode_locator.m ${worksrcpath}/tools/osx/xcode_locator.m
    } elseif {[vercmp ${xcodeversion} 9.0] < 0} {
        # Revert xcode-locator to an older version
        # From bazel 0.21.0
        file copy -force ${filespath}/xcode_locator_fixes/Xcode8-xcode_locator.m ${worksrcpath}/tools/osx/xcode_locator.m
    }

    # Patch build files to enforce correct compiler
    foreach f [ exec find ${worksrcpath}/ -name "configure" -or -name "configure.py" -or -name "compile.sh" -or -name "*.tpl" -or -name "*.bzl" -or -name "CROSSTOOL" -or -name "configure.py" -or -name "MOCK_CROSSTOOL" ] {
        foreach cmd {ar nm strip libtool ld objdump} {
            reinplace -q "s|/usr/bin/${cmd}|${prefix}/bin/${cmd}|g" ${f}
        }
        reinplace -q "s|/usr/bin/clang|${configure.cc}|g"       ${f}
        reinplace -q "s|\"clang\"|\"${configure.cc}\"|g"        ${f}
        reinplace -q "s| clang | ${configure.cc} |g"            ${f}
        reinplace -q "s|/usr/local/include|${prefix}/include|g" ${f}
        reinplace -q "s|std=c++0x|std=c++11|g"                  ${f}
        reinplace -q "s|std=c++1y|std=c++14|g"                  ${f}
        reinplace -q "s|std=c++1z|std=c++17|g"                  ${f}
    }
}

build.env-append  CC=${configure.cc} \
                 CXX=${configure.cxx} \
              TMPDIR=${workpath}/tmp \
           JAVA_HOME=${java.home}

# Work out if we should be using macports clang
set use_mp_clang [ expr ( [ string match macports-clang-* ${configure.compiler} ] || [ vercmp ${xcodeversion} ${bazel_min_xcode} ] < 0 ) ]

# Disable Xcode detection on older OSes, as we want the
# MP provided commandline utilities to be used instead.
if { ${use_mp_clang} } {
    build.env-append     BAZEL_USE_CPP_ONLY_TOOLCHAIN=1
    configure.env-append BAZEL_USE_CPP_ONLY_TOOLCHAIN=1
}

variant bash_completion {
    depends_build-append  bin:bash:bash
}

build {

    # bazel cannot be built if gcc is 'port selected'
    # https://trac.macports.org/ticket/58569
    # Until it can be figured out how to stop bazel finding and using macports gcc
    # just detect when this situation arises and error out...
    if { [file exists ${prefix}/bin/gcc] } {
        ui_error "${subport} cannot be built with 'port select gcc' active."
        ui_error "Please run 'sudo port select gcc none' and try again."
        ui_error "Once the build is complete, you can safely re-select your preferred gcc."
        return -code error "build error"
    }

    # Build bazel
    set bazel_cmd "PATH=${workpath}/bin:\$PATH JAVA_HOME=${java.home} VERBOSE=yes BAZEL_WRKDIR=${workpath} ./compile.sh"
    # If using MP toolchain pass options to enforce this
    if { ${use_mp_clang} } {
        set bazel_cmd "EXTRA_BAZEL_ARGS='--action_env=CC=${configure.cc} --action_env=CXX=${configure.cxx}' ${bazel_cmd}"
        set bazel_cmd "BAZEL_USE_CPP_ONLY_TOOLCHAIN=1 ${bazel_cmd}"
    }
    system -W ${worksrcpath} "${bazel_cmd}"

    # Put executable in main working folder
    xinstall -m 755 -W ${worksrcpath}/output ${name} ${worksrcpath}
    
    # Include bash completion support ?
    if { [variant_isset bash_completion]} {

        # Get ready to build the bash completion script
        file delete -force /var/tmp/_${name}_root

        # Build the bash completion script
        system -W ${worksrcpath} "./${name} --max_idle_secs=60 --output_user_root=${workpath} build -s //scripts:${name}-complete.bash"

    }
}

destroot {
    # Copy compiled binary
    set bindir ${prefix}/bin
    xinstall -d ${destroot}${bindir}
    xinstall -m 755 -W ${worksrcpath}/output ${name} ${destroot}${bindir}
    # Copy bash and zsh completion scripts to designated area for on-demand usage
    if { [variant_isset bash_completion]} {
        set bcompldir ${prefix}/share/bash-completion/completions
        xinstall -d ${destroot}${bcompldir}
        xinstall -m 755 ${worksrcpath}/${name}-bin/scripts/${name}-complete.bash ${destroot}${bcompldir}/${name}
        set zcompldir ${prefix}/share/zsh/site-functions
        xinstall -d ${destroot}${zcompldir}
        xinstall -m 755 ${worksrcpath}/scripts/zsh_completion/_${name} ${destroot}${zcompldir}
    }
}

post-destroot {
    # Mark documentation, source, and example directories
    set docdir ${prefix}/share/doc/${name}
    set srcdir ${prefix}/src/${name}
    set expdir ${prefix}/share/examples/${name}

    # Copy documentation files
    xinstall -d ${destroot}${docdir}
    xinstall -m 644 -W ${worksrcpath} AUTHORS \
                                      CHANGELOG.md \
                                      CONTRIBUTING.md \
                                      CONTRIBUTORS \
                                      ISSUE_TEMPLATE.md \
                                      LICENSE \
                                      README.md \
                       ${destroot}${docdir}

    # Copy source files
    xinstall -d ${destroot}${srcdir}
    file copy -force {*}[glob ${worksrcpath}/src/*] ${destroot}${srcdir}

    # Copy example files
    xinstall -d ${destroot}${expdir}
    file copy -force {*}[glob ${worksrcpath}/examples/*] ${destroot}${expdir}
}

notes "
See http://bazel.build/docs/getting-started.html to start a new project!
"
