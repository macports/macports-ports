# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

name                poco
version             1.8.0.1
categories          devel
maintainers         {gmail.com:vandervelde.ag @arjanvandervelde} openmaintainer
platforms           darwin

license             Boost-1
supported_archs     i386 x86_64

default_variants    +sqlite

description         POCO C++ Libraries

long_description \
    POCO aims to be for network-centric, cross-platform C++ software development \
    what Apple's Cocoa is for Mac development, or Ruby on Rails is for Web \
    development -- a powerful, yet easy to use platform to build your applications upon.

homepage            http://pocoproject.org/
master_sites        ${homepage}releases/poco-[strsed ${version} {s/[^0-9.].*$//}]/
distname            ${name}-${version}-all

checksums           rmd160  51b6d56429e118014e568443240b0e90a84c517d \
                    sha256  61f1e26e25af2201295b6a58a8e2bf74063ad3bf49c8e969ba08af42310716c2

set docdir          ${prefix}/share/doc/${name}

if {${subport} eq ${name}} {

    use_bzip2           yes
    
    set omit            {NetSSL_OpenSSL Data/SQLite Data/ODBC Data/MySQL Crypto}
    
    if {"x86_64" eq ${configure.build_arch}} {
        set darwin      Darwin64
    } else {
        set darwin      Darwin
    }
    
    if {[string match *clang* ${configure.compiler}]} {
        set config      ${darwin}-clang-libc++
    } else {
        set config      ${darwin}
    }
    
    universal_variant   no
    configure.args      --static --shared --no-samples --unbundled --config=${config}
    
    # Make sure poco build uses the right compiler.
    build.args-append   CC=${configure.cc} \
                        CXX=${configure.cxx} \
                        CPP=${configure.cpp} \
                        POCO_HOST_OSARCH=${configure.build_arch}
    
    depends_lib-append  port:zlib port:pcre port:expat
    
    pre-configure {
        configure.args-append   --omit=[join ${omit} ,]
    }
    
    post-patch {
        reinplace -E "/^C(XX)?FLAGS +=/s|\$| ${configure.cppflags} ${configure.cflags}|" \
            ${worksrcpath}/build/config/${config}
        reinplace -E "/^((SH|DY)LIB|LINKFLAGS) +=/s|\$| -L\$(LIBPATH) ${configure.ldflags}|" \
            ${worksrcpath}/build/config/${config}
        reinplace -E "/^SHLIB +=/s|\$| -Wl,-install_name,${prefix}/lib/\$(notdir \$@)|" \
            ${worksrcpath}/build/config/${config}
    }
    
    use_parallel_build  yes
    build.target        poco
    test.run            yes
    
    test {
        proc luniq {list} {
            set x() {}; unset x
            foreach {item} ${list} {
                set x(${item}) ""
            }
            return [array names x]
        }
    
        set file [open ${worksrcpath}/components]
    
        set components {}
        while {[gets ${file} component] != -1} {
            lappend components ${component}
        }
    
        close ${file}
    
        foreach {component} [luniq ${components}] {
            foreach {testrunner} [glob -nocomplain ${worksrcpath}/${component}/testsuite/bin/*/*/testrunner{,d}] {
                catch {system "${testrunner} -all -print"}
            }
        }
    }
    
    post-destroot {
        xinstall -d ${destroot}${docdir}/html
        xinstall -m 0644 -W ${worksrcpath} CHANGELOG CONTRIBUTORS LICENSE NEWS README VERSION \
            ${destroot}${docdir}
        xinstall -m 0644 -W ${worksrcpath}/doc Acknowledgements.html ${destroot}${docdir}/html
        copy ${worksrcpath}/CppUnit/doc ${destroot}${docdir}/html/CppUnit
    }
    
    variant ssl description {Build NetSSL and Crypto library} {
        depends_lib-append      path:lib/libssl.dylib:openssl
        set omit [ldelete [ldelete ${omit} NetSSL_OpenSSL] Crypto]
    }
    
    variant odbc description {Build ODBC backend for Data library} {
        depends_lib-append      lib:libiodbc:unixODBC
        set omit [ldelete ${omit} Data/ODBC]
    }
    
    variant mysql description {Build MySQL backend for Data library} {
        depends_lib-append      path:bin/mysql_config5:mysql5
        configure.cppflags-append   -I${prefix}/include/mysql5/mysql
        configure.ldflags-append    -L${prefix}/lib/mysql5/mysql
        set omit [ldelete ${omit} Data/MySQL]
    }
    
    variant sqlite description {Build SQLite backend for Data library} {
        depends_lib-append      port:sqlite3
        set omit [ldelete ${omit} Data/SQLite]
    }
}

subport ${name}-docs {
    license             unknown
    supported_archs     noarch
    description         documentation for POCO
    long_description    This port contains an offline copy of the POCO documentation.
    homepage            ${homepage}documentation/
    distname            ${distname}-doc
    checksums           rmd160  237e1dca693c4e4985998d7d3f278d97663e8e4d \
                        sha256  867ca0983740514ba2ef266530340a6d56608c48e139a3e529bf622b5a1bfb4e
    use_configure       no

    build {}
    destroot {
        xinstall -d ${destroot}${docdir}
        copy ${worksrcpath} ${destroot}${docdir}/html
    }
}

if {${subport} eq ${name}} {
    livecheck.type  regex
    livecheck.url   ${homepage}download/
    livecheck.regex /${name}-(\[0-9.p\]+)\\.tar
} else {
    livecheck.type  none
}
