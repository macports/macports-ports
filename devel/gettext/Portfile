# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               compiler_blacklist_versions 1.0
PortGroup               muniversal 1.0
PortGroup               clang_dependency 1.0

name                    gettext
version                 0.20.2
categories              devel
maintainers             {ryandesign @ryandesign}
homepage                https://www.gnu.org/software/gettext/
master_sites            gnu
platforms               darwin freebsd linux
use_parallel_build      yes

checksums               rmd160  32e99c55361970315dd9d43f9e36dd43f05fc09e \
                        sha256  ecb9d0908ca41d5ca5fef974323b3bba6bec19eebba0b44f396de98cfcc089f1 \
                        size    23717789

# https://trac.macports.org/ticket/31167
compiler.blacklist-append   {clang < 211.10.1}

configure.cppflags      -no-cpp-precomp

# Also needed by later clangs.
if {${os.platform} eq "darwin" && ${os.major} < 11 && ${cxx_stdlib} eq "libc++"} {
    clang_dependency.extra_versions 3.7
}

configure.args-append   ac_cv_prog_AWK=/usr/bin/awk \
                        ac_cv_path_GREP=/usr/bin/grep \
                        ac_cv_path_SED=/usr/bin/sed

if {${subport} in "${name} ${name}-runtime"} {
    set localbindir     ${workpath}/bin

    post-patch {
        file mkdir ${localbindir}

        # Don't find any Java programs.
        foreach prog {gcj java javac jikes} {
            set fp [open ${localbindir}/${prog} w]
            puts ${fp} {#!/bin/sh}
            puts ${fp} {exit 1}
            close ${fp}
            file attributes ${localbindir}/${prog} -permissions a+x
        }
    }

    configure.env-append \
                        PATH=${localbindir}:$env(PATH)

    configure.args-append \
                        --disable-csharp \
                        --disable-java
}

test.run                yes
test.target             check

subport libasprintf {
    revision                0

    license                 LGPL-2.1+

    description             autosprintf library, part of gettext

    long_description        ${subport} makes the C formatted output \
                            routines (fprintf et al.) usable in C++ \
                            programs, for use with <string> strings \
                            and <iostream> streams. This library is \
                            part of gettext.

    worksrcdir              ${distname}/${name}-runtime/${subport}
}

subport libtextstyle {
    revision                0

    license                 GPL-3+

    description             a text styling library, part of gettext

    long_description        ${subport} provides an easy way to add styling \
                            to programs that produce output to a console or \
                            terminal emulator window. This library is part \
                            of gettext and a prerequisite for tools like \
                            msgfmt and friends.

    worksrcdir              ${distname}/${subport}

    depends_lib-append      port:libiconv \
                            port:ncurses
}

subport ${name}-runtime {
    revision                0

    # libintl is LGPL; programs are GPL
    license                 {LGPL-2.1+ GPL-3+}

    description             GNU internationalization (i18n) and \
                            localization (l10n) runtime library

    long_description        ${subport} provides libintl, the ${description}. \
                            It is part of gettext, which is used for writing \
                            multilingual programs. To build applications that \
                            use gettext, the tools provided by the separate \
                            gettext port are required.

    worksrcdir              ${distname}/${subport}

    depends_lib-append      port:libiconv

    configure.args-append   --disable-libasprintf \
                            --with-included-gettext

    post-destroot {
        delete ${destroot}${prefix}/lib/charset.alias
        delete ${destroot}${prefix}/share/locale/locale.alias
    }
}

if {${subport} in "${name}-runtime libasprintf"} {
    pre-activate {
        # gettext < 0.20 installed files now installed by gettext-runtime and
        # libasprintf. This deactivate hack can be removed after June 2021.
        if {![catch {set installed [lindex [registry_active gettext] 0]}]} {
            set installed_version [lindex ${installed} 1]
            if {[vercmp ${installed_version} 0.20] < 0} {
                registry_deactivate_composite gettext "" [list ports_nodepcheck 1]
            }
        }
    }
}

if {${subport} eq ${name}} {
    epoch                   2
    revision                0

    license                 GPL-3+

    description             GNU internationalization (i18n) and \
                            localization (l10n) tools

    long_description        ${name} provides the tools like msgfmt for \
                            writing multilingual applications. The tools \
                            are required to build applications which use \
                            gettext. The runtime library libintl is in \
                            the separate gettext-runtime port.

    depends_lib-append      port:libiconv \
                            port:libtextstyle \
                            port:gettext-runtime

    # Set configure.dir and build.dir rather than worksrcdir so that the
    # muniversal portgroup will copy the entire worksrcdir and not just
    # this subdirectory, because the build compiles files outside of this
    # directory and we need separate builds of everything for each arch.
    configure.dir           ${worksrcpath}/${name}-tools
    build.dir               ${configure.dir}

    configure.args-append   --disable-openmp \
                            --with-included-libunistring \
                            --with-included-libxml \
                            --with-installed-libtextstyle \
                            --without-emacs

    # Don't use possibly installed cvs or git to create the autopoint
    # archive because the archives they create for each architecture
    # under muniversal will differ and cannot be merged.
    configure.args-append   --without-cvs \
                            --without-git

    # Don't use xz because then xz would be required by various ports
    # that just want to use gettext.
    configure.args-append   --without-xz

    # Prevent the lang-c++ test from failing.
    # TODO: report this to the developers; there's no reason why
    # libasprintf should be getting built again. Might be the same as
    # https://savannah.gnu.org/bugs/?58669
    configure.args-append   --disable-libasprintf

    post-destroot {
        # Remove files already installed by gettext-runtime.
        # https://savannah.gnu.org/bugs/?58669
        delete {*}[glob ${destroot}${prefix}/*/libintl.*] \
            ${destroot}${prefix}/share/locale/locale.alias
    }

    livecheck.type      regex
    livecheck.url       https://ftp.gnu.org/gnu/gettext/?C=M&O=D
    livecheck.regex     ${name}-(\[0-9.\]+)\\.tar
} else {
    livecheck.typoe     none
}
