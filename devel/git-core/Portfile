# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0

name                git-core
version             1.8.3.4
description         A fast version control system
long_description    Git is a fast, scalable, distributed open source version \
                    control system focusing on speed and efficiency.
maintainers         ciserlohn
categories          devel
license             GPL-2 LGPL-2.1+
platforms           darwin
homepage            http://git-scm.com/
master_sites        googlecode
distname            git-${version}
distfiles           git-${version}${extract.suffix} \
                    git-manpages-${version}${extract.suffix}

checksums           git-${version}${extract.suffix} \
                    rmd160  3781bcb5b3ff4923d48a88a5d38a563f72d58815 \
                    sha256  dfa2cdf2df92b4abe956b1e7586030381c4e39e89161ab789a660d8d1f24d9d9 \
                    git-manpages-${version}${extract.suffix} \
                    rmd160  ebdfaebeaf594f22fbe8edac964ebf4e83711bd8 \
                    sha256  6943c3b2362c107ab32d8ed81b8f4811c6e5a2eddd91b8e4188ccf6b4be2b740 \

depends_run         port:rsync \
                    port:p5.12-error \
                    port:p5.12-term-readkey

depends_lib         port:perl5.12 \
                    port:curl \
                    port:zlib \
                    port:openssl \
                    port:expat \
                    port:libiconv

patchfiles          patch-Makefile.diff git-subtree.1.diff
patch.pre_args      -p1

extract.only        git-${version}${extract.suffix} \
                    git-manpages-${version}${extract.suffix}

use_configure       no

variant universal   {}

set CFLAGS          "-I. -Wall -O2 -I${prefix}/include [get_canonical_archflags cc]"
set LDFLAGS         "-L${prefix}/lib [get_canonical_archflags ld]"

build.args          CFLAGS="${CFLAGS}" \
                    LDFLAGS="${LDFLAGS}" \
                    CC=${configure.cc} \
                    prefix=${prefix} \
                    CURLDIR=${prefix} \
                    OPENSSLDIR=${prefix} \
                    ICONVDIR=${prefix} \
                    PERL_PATH="${prefix}/bin/perl5.12" \
                    NO_FINK=1 \
                    NO_DARWIN_PORTS=1 \
                    NO_R_TO_GCC_LINKER=1 \
                    V=1

test.run            yes
test.cmd            make
test.target         test
test.dir            ${worksrcpath}
pre-test {
    eval test.args  ${build.args}
}

destroot.target     install
pre-destroot {
    eval destroot.args  ${build.args}
    xinstall -m 644 ${worksrcpath}/contrib/subtree/git-subtree.1 ${workpath}/man1
}

set docdestroot ${destroot}${prefix}/share/doc/${name}

post-destroot {
    foreach f {1 5 7} {
        xinstall -d ${destroot}${prefix}/share/man/man${f}
        foreach m [glob -directory ${workpath} man${f}/*.${f}] {
            xinstall ${m} ${destroot}${prefix}/share/man/man${f}
        }
    }
    if {![variant_isset svn]} {
        system "rm ${destroot}${prefix}/libexec/git-core/git-svn*"
    }
    xinstall -d ${docdestroot}

    foreach badfile [exec find ${destroot} -name perllocal.pod] {
        ui_info "Removing ${badfile}"
        file delete ${badfile}
    }

    set completions_path ${destroot}${prefix}/share/bash-completion/completions
    xinstall -d ${completions_path}
    xinstall -m 644 ${worksrcpath}/contrib/completion/git-completion.bash \
        ${completions_path}/git
   
    set share_path ${destroot}${prefix}/share/${name}
    xinstall -d ${share_path}
    xinstall -m 644 ${worksrcpath}/contrib/completion/git-prompt.sh \
        ${share_path}/git-prompt.sh

    xinstall -m 755 ${worksrcpath}/contrib/subtree/git-subtree.sh \
        ${destroot}${prefix}/libexec/git-core/git-subtree

    file delete -force ${share_path}/contrib
    copy ${worksrcpath}/contrib ${share_path}

}

if {![variant_isset python26] && ![variant_isset python27]} {
    default_variants    +python27
}

variant python26 conflicts python27 description {Use Python 2.6} {
    build.args-append       PYTHON_PATH="${prefix}/bin/python2.6"
    depends_lib-append      port:python26
}

variant python27 conflicts python26 description {Use Python 2.7} {
    build.args-append       PYTHON_PATH="${prefix}/bin/python2.7"
    depends_lib-append      port:python27
}

variant pcre {
    build.args-append       LIBPCREDIR=${prefix} USE_LIBPCRE=1
    depends_lib-append      port:pcre
}

variant doc description {Install HTML and plaintext documentation} {
    distfiles-append        git-htmldocs-${version}${extract.suffix}
    checksums-append        git-htmldocs-${version}${extract.suffix} \
                            rmd160  4a8e4a2a0bf53b0de7e7bfaa9e6deba0df5747d7 \
                            sha256  da13f9453489145d5aaefeb6535ce43621fdb7ff426c50304acf2c2e4430005c

    patchfiles-append       git-subtree.html.diff

    post-extract {
        file mkdir ${workpath}/htmldocs
        system -W ${workpath}/htmldocs "${extract.cmd} ${extract.pre_args} \
            ${distpath}/git-htmldocs-${version}${extract.suffix} \
            ${extract.post_args}"
    }

    pre-destroot {
        xinstall -m 644 ${worksrcpath}/contrib/subtree/git-subtree.html ${workpath}/htmldocs
    }

    post-destroot {
        foreach f [glob ${workpath}/htmldocs/*] {
            file delete -force "${docdestroot}/[file tail ${f}]"
            copy ${f} ${docdestroot}
        }
    }
}

variant gitweb description {Install gitweb.cgi} {
    build.target-append     gitweb/gitweb.cgi

    post-destroot {
        xinstall -d ${destroot}${prefix}/share/${name}/gitweb
        xinstall -W ${worksrcpath}/gitweb \
            gitweb.cgi \
            ${destroot}${prefix}/share/${name}/gitweb
        xinstall -m 444 -W ${worksrcpath}/gitweb/static \
            gitweb.css \
            gitweb.js \
            git-favicon.png \
            git-logo.png \
            ${destroot}${prefix}/share/${name}/gitweb
        xinstall -d ${docdestroot}/gitweb
        xinstall -m 444 -W ${worksrcpath}/gitweb README INSTALL \
            ${docdestroot}/gitweb
    }
}

variant svn description {Bi-directional subversion repository support} {
    depends_run-append  port:subversion \
                        port:p5.12-libwww-perl \
                        port:p5.12-svn-simple
}

variant bash_completion {
    depends_run-append  path:etc/bash_completion:bash-completion
}


variant credential_osxkeychain description {Install git credential-osxkeychain utility from contrib} {

    post-build {
        system -W "${worksrcpath}/contrib/credential/osxkeychain" "make [join ${build.args}]"
    }

    pre-destroot {
        xinstall -m 755 "${worksrcpath}/contrib/credential/osxkeychain/git-credential-osxkeychain" \
            "${destroot}${prefix}/bin/"
    }
}

platform darwin 8 {
    build.args-append   NO_APPLE_COMMON_CRYPTO=1
}

default_variants    +doc +pcre +credential_osxkeychain

livecheck.type          regex
livecheck.regex         {<span class='version'>(.*?)</span>}
