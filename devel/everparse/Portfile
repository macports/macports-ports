# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup github    1.0

name                everparse
github.setup        project-everest everparse f266a357262d0ff0ae3c291d77e280a98c3c0feb
version             20200908-[string range ${github.version} 0 6]
revision            0

categories          devel
platforms           darwin
maintainers         {landonf @landonf} openmaintainer
license             Apache-2
description         A verified secure parser framework for F*.
long_description    EverParse provides LowParse, verified-secure F*/Low* parser combinator library, and \
                    QuackyDucky, an untrusted compiler for generating verified secure parsers from a \
                    message format specification.

checksums           rmd160  fa393a8fdedcbc6d32308f1e7f82c7772ce89527 \
                    sha256  bd60f0796c2902256473ab1d7e7becf851bbcc2b8e0f998dc76f96d877c00440 \
                    size    802706

depends_lib         port:fstar-devel \
                    port:kremlin-devel

depends_build       port:ocaml \
                    port:ocaml-ocamlbuild \
                    port:ocaml-batteries \
                    port:ocaml-hex

patchfiles          patch-add_FStar_Getopt.ml \
                    patch-fix-tests-mkdir \
                    patch-batch-driver-toolchain-paths

options             lowparse.home \
                    fstar.home \
                    kremlin.home

default fstar.home      {${prefix}/libexec/fstar-devel}
default kremlin.home    {${prefix}/libexec/kremlin-devel}
default lowparse.home   {${prefix}/libexec/${subport}}

pre-patch {
    # Fix CRLF newlines before trying to patch
    reinplace "s|\r||g" src/3d/ocaml/Makefile
}

post-patch {
    foreach {key path_var} {
        @FSTAR_HOME@        fstar.home
        @KREMLIN_HOME@      kremlin.home
        @QD_HOME@           lowparse.home
    } {
        reinplace "s|${key}|[set ${path_var}]/home|g" \
            src/3d/ocaml/Batch.ml
    }
}

# z3 hints significantly decrease build time; can be updated after a successful build+test run via:
#   tar -Jcf files/everparse-hints.tar.xz -C work lowparse.hints
distfiles-append    everparse-hints.tar.xz

use_configure       no

build.type          gnu
build.env-append    "OTHERFLAGS=--hint_dir .hints --record_hints"
build.args          FSTAR_HOME=${fstar.home}/home \
                    KREMLIN_HOME=${kremlin.home}/home

pre-build {
    # Link lowparse's .hint directories to the top-level ${workpath} directory
    # we actually package up in everparse-hints.tar.xz
    set eph_hints ${workpath}/lowparse.hints
    file mkdir ${eph_hints}
    foreach {eph_src} {
        src/lowparse
        src/3d
        src/3d/prelude
        tests/lowparse
        tests/sample
        tests/bitfields
        tests/unit
    } {
        file mkdir ${eph_hints}/${eph_src}
        ln -sf ${eph_hints}/${eph_src} ${worksrcpath}/${eph_src}/.hints
    }
}

destroot {
    # Create QD_HOME layout compatible with src/package/package.sh
    set everparse_root      ${prefix}/libexec/everparse
    set everparse_bin       ${everparse_root}/bin
    set everparse_home      ${everparse_root}/src
    set everparse_home_3d   ${everparse_home}/3d

    xinstall -d \
        ${destroot}${everparse_home} \
        ${destroot}${everparse_home_3d} \
        ${destroot}${everparse_bin}

    xinstall \
        ${worksrcpath}/_build/src/quackyducky.native \
        ${destroot}${everparse_bin}/qd

    xinstall \
        ${worksrcpath}/src/3d/3d \
        ${destroot}${everparse_bin}/3d

    foreach {ep_src ep_dst} [list \
        lowparse                    ${everparse_home}       \
        3d/prelude                  ${everparse_home_3d}    \
        3d/.clang-format            ${everparse_home_3d}    \
        3d/copyright.txt            ${everparse_home_3d}    \
        3d/EverParseEndianness.h    ${everparse_home_3d}    \
        3d/noheader.txt             ${everparse_home_3d}    \
    ] {
        copy ${worksrcpath}/src/${ep_src} ${destroot}${ep_dst}
    }

    fs-traverse {f} ${destroot}${everparse_home} {
        switch -glob -- "[file tail $f] [file type $f]" {
            {.hints link}       -
            {.gitignore file}   -
            {.depend file}      -
            {Makefile file}     { delete $f }
        }
    }

    # Symlink the binaries into ${prefix}/bin
    ln -sf ${everparse_bin}/qd  ${destroot}${prefix}/bin/quackyducky
    ln -sf quackyducky          ${destroot}${prefix}/bin/qd
    ln -sf ${everparse_bin}/3d  ${destroot}${prefix}/bin/quackyducky-3d
}

test.run            yes
test.env            {*}${build.env}
test.args           -j${build.jobs} \
                    {*}${build.args}
