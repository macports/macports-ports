# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup github    1.0

name                everparse
github.setup        project-everest everparse d61b5afa052aff422c146e3db297d257802c043d
version             20210727-[string range ${github.version} 0 6]
revision            0

categories          devel
platforms           darwin
maintainers         {landonf @landonf} openmaintainer
license             Apache-2
description         A verified secure parser framework for F*.
long_description    EverParse provides LowParse, verified-secure F*/Low* parser combinator library, and \
                    QuackyDucky, an untrusted compiler for generating verified secure parsers from a \
                    message format specification.

checksums           rmd160  369d843747ec78391e9ba4762c5e0044792b67fe \
                    sha256  44df578f65e07df5c3c125707dd6c2417c8c5f9f2b26b2527f3e51adca63b0e3 \
                    size    863604

depends_lib         port:fstar-devel \
                    port:kremlin-devel \
                    port:ocaml-hacl-star

depends_build       port:ocaml \
                    port:ocaml-ocamlbuild \
                    port:ocaml-batteries \
                    port:ocaml-hex \
                    port:ocaml-re

patchfiles          patch-add_FStar_Getopt.ml \
                    patch-fix-tests-mkdir \
                    patch-batch-driver-toolchain-paths

options             lowparse.home \
                    fstar.home \
                    kremlin.home

default fstar.home      {${prefix}/libexec/fstar-devel}
default kremlin.home    {${prefix}/libexec/kremlin-devel}
default lowparse.home   {${prefix}/libexec/${subport}}

pre-patch {
    # Fix CRLF newlines before trying to patch
    reinplace "s|\r||g" src/3d/ocaml/Makefile
}

post-patch {
    foreach {key path_var subdir} {
        @FSTAR_HOME@        fstar.home      "/home"
        @KREMLIN_HOME@      kremlin.home    "/home"
        @QD_HOME@           lowparse.home   ""
    } {
        reinplace "s|${key}|[set ${path_var}]${subdir}|g" \
            src/3d/ocaml/Batch.ml
    }
}

# z3 hints significantly decrease build time; can be updated after a successful build+test run via:
#   tar -Jcf files/everparse-hints.tar.xz -C work lowparse.hints
distfiles-append    everparse-hints.tar.xz

use_configure       no

build.type          gnu
build.env-append    "OTHERFLAGS=--hint_dir .hints --record_hints --use_hints"
build.args          FSTAR_HOME=${fstar.home}/home \
                    KREMLIN_HOME=${kremlin.home}/home

pre-build {
    # Link lowparse's .hint directories to the top-level ${workpath} directory
    # we actually package up in everparse-hints.tar.xz
    set eph_hints ${workpath}/lowparse.hints
    file mkdir ${eph_hints}
    foreach {eph_src} {
        src/lowparse
        src/3d
        src/3d/prelude
        tests/lowparse
        tests/sample0
        tests/sample
        tests/bitfields
        tests/unit
    } {
        file mkdir ${eph_hints}/${eph_src}
        ln -sf ${eph_hints}/${eph_src} ${worksrcpath}/${eph_src}/.hints
    }
}

destroot {
    # Create QD_HOME layout compatible with src/package/package.sh
    set everparse_root      ${prefix}/libexec/everparse
    set everparse_bin       ${everparse_root}/bin
    set everparse_lib       ${everparse_root}/lib
    set everparse_lib_3d    ${everparse_root}/lib/3d
    set everparse_src       ${everparse_root}/src

    xinstall -d \
        ${destroot}${everparse_lib}    \
        ${destroot}${everparse_lib_3d} \
        ${destroot}${everparse_bin}

    xinstall \
        ${worksrcpath}/_build/src/quackyducky.native \
        ${destroot}${everparse_bin}/qd

    xinstall \
        ${worksrcpath}/src/3d/3d \
        ${destroot}${everparse_bin}/3d

    foreach {ep_src ep_dst} [list \
        lowparse                    ${everparse_lib}    \
        3d/prelude                  ${everparse_lib_3d} \
        3d/.clang-format            ${everparse_lib_3d} \
        3d/copyright.txt            ${everparse_lib_3d} \
        3d/EverParseEndianness.h    ${everparse_lib_3d} \
        3d/noheader.txt             ${everparse_lib_3d} \
    ] {
        copy ${worksrcpath}/src/${ep_src} ${destroot}${ep_dst}
    }

    fs-traverse {f} ${destroot}${everparse_lib} {
        switch -glob -- "[file tail $f] [file type $f]" {
            {.hints link}       -
            {.gitignore file}   -
            {.depend file}      -
            {Makefile file}     { delete $f }
        }
    }

    # src/package/package.sh expects a 'src' directory
    ln -sf lib                  ${destroot}/${everparse_src}

    # Symlink the binaries into ${prefix}/bin
    ln -sf ${everparse_bin}/qd  ${destroot}${prefix}/bin/quackyducky
    ln -sf quackyducky          ${destroot}${prefix}/bin/qd
    ln -sf ${everparse_bin}/3d  ${destroot}${prefix}/bin/quackyducky-3d
}

test.run            yes
test.env            {*}${build.env}
test.args           -j${build.jobs} \
                    {*}${build.args}
