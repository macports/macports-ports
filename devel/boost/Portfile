# $Id$

PortSystem 1.0

name			boost
version			1.34.0
categories		devel
maintainers		sanchom@gmail.com
description		Collection of portable C++ source libraries
long_description 	Boost provides free portable peer-reviewed C++ \
			libraries. The emphasis is on portable libraries \
			which work well with the C++ Standard Library.
homepage		http://www.boost.org
master_sites		sourceforge
distname		${name}_[strsed ${version} {g/[.]/_/}]
use_bzip2		yes
checksums		md5 ed5b9291ffad776f8757a916e1726ad0
platforms		darwin

depends_build		bin:bjam:boost-jam

platform darwin {
	post-destroot {
		cd ${destroot}${prefix}/lib

		# get the library version as it shows up in the library names:
		# eg. 1_34
		set libver [join [lrange [split ${version} {.}] 0 1] {_}]
		
		# ensure the identification name of the dynamic libraries agree
		# with their final destination path (not the destroot path that
		# they've just been installed to)
		foreach lib [glob *-${libver}.dylib] {
			system "install_name_tool -id ${prefix}/lib/${lib} ${lib}"
		}

		# create relative symbolic links to the versioned libraries (.dylib only;
		# .a were built with unversioned extensions that are duplicate files of the
		# versioned extensions... handled next)
		foreach lib [glob *-${libver}.dylib] {
			set libname [join [lrange [split [file rootname ${lib}] {-}] 0 end-1] {-}]
			set libext [file extension ${lib}]
			set liblink "${libname}${libext}"
			if {![catch {set libtype [file type ${liblink}]}]} {
				if {${libtype} == "link"} {
					file delete -force ${liblink}
				}
			}
			system "ln -s ${lib} ${liblink}"
		}

		# change the duplicated non-version-named .a libraries to
		# symbolic links to the version-named .a libraries
		foreach lib [glob *-${libver}.a] {
			set libname [join [lrange [split [file rootname ${lib}] {-}] 0 end-1] {-}]
			set libext [file extension ${lib}]
			set liblink "${libname}${libext}"
			# if this file is around, it is a duplicate, delete it
			if {[file exists ${liblink}]} {
				file delete -force ${liblink}
			}
			# add a symbolic link instead
			system "ln -s ${lib} ${liblink}"
		}
	}
}

destroot.args		PREFIX=${destroot}${prefix} EPREFIX=${destroot}${prefix} \
			LIBDIR=${destroot}${prefix}/lib INCLUDEDIR=${destroot}${prefix}/include \
			install

configure.args		--without-libraries=python --without-icu

variant python {
	set pyversion		2.4
	depends_lib		lib:libpython${pyversion}:python[strsed ${pyversion} {g/[.]//}]

	configure.args-delete	--without-libraries=python
}

variant icu {
	depends_lib		port:icu

	configure.args-delete	--without-icu
	configure.args-append	--with-icu
}
