From 131b311fe32d328b68c21443a28b08390815e9ac Mon Sep 17 00:00:00 2001
From: Julien Bernard <julien.bernard@univ-fcomte.fr>
Date: Fri, 7 Aug 2020 15:24:53 +0200
Subject: [PATCH 01/10] add install for cmake

---
 CMakeLists.txt        | 14 ++++++++++++++
 src/CMakeLists.txt    | 41 +++++++++++++++++++++++++++++++++++++++--
 src/box2dConfig.cmake |  3 +++
 3 files changed, 56 insertions(+), 2 deletions(-)
 create mode 100644 src/box2dConfig.cmake

diff --git a/CMakeLists.txt b/CMakeLists.txt
index 92b2c00f9..987d0fb1a 100644
--- a/CMakeLists.txt
+++ b/CMakeLists.txt
@@ -5,6 +5,15 @@ project(box2d VERSION 2.4.0)
 
 # set(CMAKE_CONFIGURATION_TYPES "Debug;RelWithDebInfo" CACHE STRING "" FORCE)
 
+include(GNUInstallDirs)
+
+if(NOT CMAKE_CONFIGURATION_TYPES)
+  if(NOT CMAKE_BUILD_TYPE)
+    message(STATUS "Setting build type to 'RelWithDebInfo' as none was specified.")
+    set(CMAKE_BUILD_TYPE "RelWithDebInfo")
+  endif()
+endif()
+
 set_property(GLOBAL PROPERTY USE_FOLDERS ON)
 
 add_subdirectory(src)
@@ -39,3 +48,8 @@ if (BOX2D_BUILD_TESTBED)
 		set_property(TARGET testbed PROPERTY VS_DEBUGGER_WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}/testbed")
 	endif()
 endif()
+
+install(
+  DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/include/box2d"
+  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
+)
diff --git a/src/CMakeLists.txt b/src/CMakeLists.txt
index 6ef515f8c..0a036bad2 100644
--- a/src/CMakeLists.txt
+++ b/src/CMakeLists.txt
@@ -96,8 +96,13 @@ set(BOX2D_HEADER_FILES
 	../include/box2d/box2d.h)
 
 add_library(box2d STATIC ${BOX2D_SOURCE_FILES} ${BOX2D_HEADER_FILES})
-target_include_directories(box2d PUBLIC ${CMAKE_CURRENT_SOURCE_DIR}/../include)
-target_include_directories(box2d PRIVATE ${CMAKE_CURRENT_SOURCE_DIR})
+target_include_directories(box2d
+  PUBLIC
+    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/../include>
+    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
+  PRIVATE
+    ${CMAKE_CURRENT_SOURCE_DIR}
+)
 set_target_properties(box2d PROPERTIES
 	CXX_STANDARD 11
     CXX_STANDARD_REQUIRED YES
@@ -106,3 +111,35 @@ set_target_properties(box2d PROPERTIES
 
 source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}" PREFIX "src" FILES ${BOX2D_SOURCE_FILES})
 source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}/../include" PREFIX "include" FILES ${BOX2D_HEADER_FILES})
+
+install(
+  TARGETS box2d
+  EXPORT box2dTargets
+  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
+  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
+  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
+)
+
+install(
+  EXPORT box2dTargets
+  NAMESPACE box2d::
+  DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/box2d"
+)
+
+install(
+  FILES box2dConfig.cmake
+  DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/box2d"
+)
+
+include(CMakePackageConfigHelpers)
+
+write_basic_package_version_file(
+  "${CMAKE_CURRENT_BINARY_DIR}/box2dConfigVersion.cmake"
+  VERSION ${PROJECT_VERSION}
+  COMPATIBILITY SameMajorVersion
+)
+
+install(
+  FILES "${CMAKE_CURRENT_BINARY_DIR}/box2dConfigVersion.cmake"
+  DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/box2d"
+)
diff --git a/src/box2dConfig.cmake b/src/box2dConfig.cmake
new file mode 100644
index 000000000..e5f8bc00b
--- /dev/null
+++ b/src/box2dConfig.cmake
@@ -0,0 +1,3 @@
+get_filename_component(BOX2D_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" DIRECTORY)
+include("${BOX2D_CMAKE_DIR}/box2dTargets.cmake")
+include("${BOX2D_CMAKE_DIR}/box2dConfigVersion.cmake")

From 82317c288e9052eb3272afd4f4db2a0d5654c69d Mon Sep 17 00:00:00 2001
From: Julien Bernard <julien.bernard@univ-fcomte.fr>
Date: Sat, 8 Aug 2020 22:32:01 +0200
Subject: [PATCH 02/10] install documentation too

---
 docs/CMakeLists.txt | 4 ++++
 1 file changed, 4 insertions(+)

diff --git a/docs/CMakeLists.txt b/docs/CMakeLists.txt
index 623bc5e35..edffb4edd 100644
--- a/docs/CMakeLists.txt
+++ b/docs/CMakeLists.txt
@@ -26,3 +26,7 @@ add_custom_target(docs ALL "${DOXYGEN_EXECUTABLE}"
                   WORKING_DIRECTORY "${CMAKE_BINARY_DIR}/docs"
                   COMMENT "Generating HTML documentation" VERBATIM)
 
+install(
+  DIRECTORY "${CMAKE_BINARY_DIR}/docs/html"
+  DESTINATION "${CMAKE_INSTALL_DOCDIR}"
+)

From 55bd00ea63739f82e6aa12e67d577c7e207bf465 Mon Sep 17 00:00:00 2001
From: Julien Bernard <julien.bernard@univ-fcomte.fr>
Date: Tue, 18 Aug 2020 21:48:28 +0200
Subject: [PATCH 03/10] ${PROJECT_VERSION} is the default for VERSION

---
 src/CMakeLists.txt | 1 -
 1 file changed, 1 deletion(-)

diff --git a/src/CMakeLists.txt b/src/CMakeLists.txt
index 0a036bad2..3667fb785 100644
--- a/src/CMakeLists.txt
+++ b/src/CMakeLists.txt
@@ -135,7 +135,6 @@ include(CMakePackageConfigHelpers)
 
 write_basic_package_version_file(
   "${CMAKE_CURRENT_BINARY_DIR}/box2dConfigVersion.cmake"
-  VERSION ${PROJECT_VERSION}
   COMPATIBILITY SameMajorVersion
 )
 

From 605a9acf403dfdfc52ffd5b0b15f4be9777cb259 Mon Sep 17 00:00:00 2001
From: Julien Bernard <julien.bernard@univ-fcomte.fr>
Date: Wed, 19 Aug 2020 18:26:00 +0200
Subject: [PATCH 04/10] export to box2dConfig directly

---
 src/CMakeLists.txt    | 9 ++-------
 src/box2dConfig.cmake | 3 ---
 2 files changed, 2 insertions(+), 10 deletions(-)
 delete mode 100644 src/box2dConfig.cmake

diff --git a/src/CMakeLists.txt b/src/CMakeLists.txt
index 3667fb785..025efb991 100644
--- a/src/CMakeLists.txt
+++ b/src/CMakeLists.txt
@@ -114,23 +114,18 @@ source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}/../include" PREFIX "include" FILE
 
 install(
   TARGETS box2d
-  EXPORT box2dTargets
+  EXPORT box2dConfig
   LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
   ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
   RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
 )
 
 install(
-  EXPORT box2dTargets
+  EXPORT box2dConfig
   NAMESPACE box2d::
   DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/box2d"
 )
 
-install(
-  FILES box2dConfig.cmake
-  DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/box2d"
-)
-
 include(CMakePackageConfigHelpers)
 
 write_basic_package_version_file(
diff --git a/src/box2dConfig.cmake b/src/box2dConfig.cmake
deleted file mode 100644
index e5f8bc00b..000000000
--- a/src/box2dConfig.cmake
+++ /dev/null
@@ -1,3 +0,0 @@
-get_filename_component(BOX2D_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" DIRECTORY)
-include("${BOX2D_CMAKE_DIR}/box2dTargets.cmake")
-include("${BOX2D_CMAKE_DIR}/box2dConfigVersion.cmake")

From cc07acd9326a850256a4885ae14c319a00c0d487 Mon Sep 17 00:00:00 2001
From: Julien Bernard <julien.bernard@univ-fcomte.fr>
Date: Wed, 19 Aug 2020 18:26:24 +0200
Subject: [PATCH 05/10] use GENERATOR_IS_MULTI_CONFIG instead of
 CMAKE_CONFIGURATION_TYPES

---
 CMakeLists.txt | 2 +-
 1 file changed, 1 insertion(+), 1 deletion(-)

diff --git a/CMakeLists.txt b/CMakeLists.txt
index 987d0fb1a..cda3f4d08 100644
--- a/CMakeLists.txt
+++ b/CMakeLists.txt
@@ -7,7 +7,7 @@ project(box2d VERSION 2.4.0)
 
 include(GNUInstallDirs)
 
-if(NOT CMAKE_CONFIGURATION_TYPES)
+if(NOT GENERATOR_IS_MULTI_CONFIG)
   if(NOT CMAKE_BUILD_TYPE)
     message(STATUS "Setting build type to 'RelWithDebInfo' as none was specified.")
     set(CMAKE_BUILD_TYPE "RelWithDebInfo")

From 177cbf9e4780db8232b007a7a9b5939f2f477043 Mon Sep 17 00:00:00 2001
From: Julien Bernard <julien.bernard@univ-fcomte.fr>
Date: Thu, 20 Aug 2020 08:47:47 +0200
Subject: [PATCH 06/10] GENERATOR_IS_MULTI_CONFIG is a property, not a variable

---
 CMakeLists.txt | 4 +++-
 1 file changed, 3 insertions(+), 1 deletion(-)

diff --git a/CMakeLists.txt b/CMakeLists.txt
index cda3f4d08..e8e4262e6 100644
--- a/CMakeLists.txt
+++ b/CMakeLists.txt
@@ -7,7 +7,9 @@ project(box2d VERSION 2.4.0)
 
 include(GNUInstallDirs)
 
-if(NOT GENERATOR_IS_MULTI_CONFIG)
+get_property(BOX2D_MULTI_CONFIG GLOBAL PROPERTY GENERATOR_IS_MULTI_CONFIG)
+
+if(NOT BOX2D_MULTI_CONFIG)
   if(NOT CMAKE_BUILD_TYPE)
     message(STATUS "Setting build type to 'RelWithDebInfo' as none was specified.")
     set(CMAKE_BUILD_TYPE "RelWithDebInfo")

From 8aca2f19931d33f1e6be681bb281de833d72ec56 Mon Sep 17 00:00:00 2001
From: Julien Bernard <julien.bernard@univ-fcomte.fr>
Date: Sun, 23 Aug 2020 14:41:04 +0200
Subject: [PATCH 07/10] remove default CMAKE_BUILD_TYPE

---
 CMakeLists.txt | 9 ---------
 1 file changed, 9 deletions(-)

diff --git a/CMakeLists.txt b/CMakeLists.txt
index e8e4262e6..a0ab10988 100644
--- a/CMakeLists.txt
+++ b/CMakeLists.txt
@@ -7,15 +7,6 @@ project(box2d VERSION 2.4.0)
 
 include(GNUInstallDirs)
 
-get_property(BOX2D_MULTI_CONFIG GLOBAL PROPERTY GENERATOR_IS_MULTI_CONFIG)
-
-if(NOT BOX2D_MULTI_CONFIG)
-  if(NOT CMAKE_BUILD_TYPE)
-    message(STATUS "Setting build type to 'RelWithDebInfo' as none was specified.")
-    set(CMAKE_BUILD_TYPE "RelWithDebInfo")
-  endif()
-endif()
-
 set_property(GLOBAL PROPERTY USE_FOLDERS ON)
 
 add_subdirectory(src)

From e0b2d79ab0358c1fc05d8f967bec162b628df18a Mon Sep 17 00:00:00 2001
From: Julien Bernard <julien.bernard@univ-fcomte.fr>
Date: Sun, 23 Aug 2020 15:15:08 +0200
Subject: [PATCH 08/10] create and install pdb file for static library

---
 src/CMakeLists.txt | 10 ++++++++++
 1 file changed, 10 insertions(+)

diff --git a/src/CMakeLists.txt b/src/CMakeLists.txt
index 025efb991..5a014d464 100644
--- a/src/CMakeLists.txt
+++ b/src/CMakeLists.txt
@@ -109,6 +109,16 @@ set_target_properties(box2d PROPERTIES
     CXX_EXTENSIONS NO
 )
 
+set_target_properties(box2d PROPERTIES
+  COMPILE_PDB_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}"
+  COMPILE_PDB_NAME box2d
+)
+
+install(
+  FILES "${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/box2d.pdb"
+  DESTINATION ${CMAKE_INSTALL_LIBDIR}
+)
+
 source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}" PREFIX "src" FILES ${BOX2D_SOURCE_FILES})
 source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}/../include" PREFIX "include" FILES ${BOX2D_HEADER_FILES})
 

From facbe7b47e0299b0c2ddcdd8aecdecdf43c2f217 Mon Sep 17 00:00:00 2001
From: Julien Bernard <julien.bernard@univ-fcomte.fr>
Date: Sun, 23 Aug 2020 15:22:36 +0200
Subject: [PATCH 09/10] handle pdb files only with MSVC

---
 src/CMakeLists.txt | 18 ++++++++++--------
 1 file changed, 10 insertions(+), 8 deletions(-)

diff --git a/src/CMakeLists.txt b/src/CMakeLists.txt
index 5a014d464..7a66cf6a8 100644
--- a/src/CMakeLists.txt
+++ b/src/CMakeLists.txt
@@ -109,15 +109,17 @@ set_target_properties(box2d PROPERTIES
     CXX_EXTENSIONS NO
 )
 
-set_target_properties(box2d PROPERTIES
-  COMPILE_PDB_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}"
-  COMPILE_PDB_NAME box2d
-)
+if(MSVC)
+  set_target_properties(box2d PROPERTIES
+    COMPILE_PDB_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}"
+    COMPILE_PDB_NAME box2d
+  )
 
-install(
-  FILES "${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/box2d.pdb"
-  DESTINATION ${CMAKE_INSTALL_LIBDIR}
-)
+  install(
+    FILES "${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/box2d.pdb"
+    DESTINATION ${CMAKE_INSTALL_LIBDIR}
+  )
+endif()
 
 source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}" PREFIX "src" FILES ${BOX2D_SOURCE_FILES})
 source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}/../include" PREFIX "include" FILES ${BOX2D_HEADER_FILES})

From b2e31d2d02980bd76d7d2013bd2e8d2332af246d Mon Sep 17 00:00:00 2001
From: Julien Bernard <julien.bernard@univ-fcomte.fr>
Date: Sun, 23 Aug 2020 16:58:40 +0200
Subject: [PATCH 10/10] make the install optional if the file has not been
 generated

---
 src/CMakeLists.txt | 1 +
 1 file changed, 1 insertion(+)

diff --git a/src/CMakeLists.txt b/src/CMakeLists.txt
index 7a66cf6a8..a68e91435 100644
--- a/src/CMakeLists.txt
+++ b/src/CMakeLists.txt
@@ -118,6 +118,7 @@ if(MSVC)
   install(
     FILES "${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/box2d.pdb"
     DESTINATION ${CMAKE_INSTALL_LIBDIR}
+    OPTIONAL
   )
 endif()
 
