# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0
PortGroup           haskell_stack 1.0

github.setup        gibiansky IHaskell ac0882d
name                ihaskell
version             2019.08.30
revision            1

categories          devel haskell
platforms           darwin
license             MIT
maintainers         nomaintainer

description         A Haskell kernel for IPython.
long_description    \
    IHaskell is a kernel for the Jupyter project, which allows you to \
    use Haskell inside Jupyter frontends (including the console and \
    notebook). For a tour of some IHaskell features, check out the \
    demo Notebook at \
    http://nbviewer.ipython.org/github/gibiansky/IHaskell/blob/master/notebooks/IHaskell.ipynb. More \
    example notebooks are available on the wiki at \
    https://github.com/gibiansky/IHaskell/wiki. The wiki also has more \
    extensive documentation of IHaskell features.

checksums           rmd160  47cf8fed7674b0e2421019ff4c813ee4eea93882 \
                    sha256  c6e6c9a7d71ba6349027f1fb818608f8355acafe6fa7cbe57265ec9951a5ddb4 \
                    size    753553

# use these to specify python versions, python3 required 
set python3_version 3.7
set python3_version_nickname \
    [join [lrange [split ${python3_version} .] 0 1] {}]

# See https://github.com/gibiansky/IHaskell#mac,
# https://github.com/gibiansky/IHaskell/blob/master/requirements.txt
depends_lib-append  \
                    path:lib/pkgconfig/pango.pc:pango \
                    port:ghc \
                    port:python${python3_version_nickname} \
                    port:py${python3_version_nickname}-cairo \
                    port:py${python3_version_nickname}-ipykernel \
                    port:py${python3_version_nickname}-ipywidgets \
                    port:py${python3_version_nickname}-jupyter \
                    port:py${python3_version_nickname}-jupyter_client \
                    port:py${python3_version_nickname}-jupyter_core \
                    port:py${python3_version_nickname}-magic \
                    port:py${python3_version_nickname}-nbformat \
                    port:py${python3_version_nickname}-pkgconfig \
                    port:py${python3_version_nickname}-widgetsnbextension \
                    port:zmq

haskell_stack.system_ghc \
                    yes

# relative paths to ${prefix}
set ihaskell_datadir \
                    share/${name}
set jupyter_dir     share/jupyter
set hlint_datadir \
                    share/hlint

post-extract {
    xinstall -m 0755 -d \
        "[option haskell_stack.stack_root]" \
        ${destroot}${prefix}/${ihaskell_datadir}/html \
        ${destroot}${prefix}/${jupyter_dir}

    # append configure-options to stack.yaml
    reinplace "\$ a\\
\\
# Avoid MacPorts iconv link error\\
# See https://github.com/commercialhaskell/stack/issues/825\\
extra-lib-dirs:\\
\\ \\ - /usr/lib\\
" \
        stack.yaml

    # Fix for cabal data-files hardcoded path in binary
    # See:
    # https://github.com/commercialhaskell/stack/issues/848
    # https://github.com/commercialhaskell/stack/issues/4857
    # https://github.com/haskell/cabal/issues/462
    # https://github.com/haskell/cabal/issues/3586
    xinstall -m 0644 -W ${worksrcpath} \
        ${filespath}/Paths_NAME.hs ./src/Paths_${name}.hs

    reinplace "s|@PREFIX@|${prefix}|g" \
        ${worksrcpath}/src/Paths_${name}.hs
    reinplace "s|@NAME@|${name}|g" \
        ${worksrcpath}/src/Paths_${name}.hs
}

# no jupyter_select yet, so hack PATH to find `which jupyter`: 
# https://trac.macports.org/ticket/50608
# note: this command does not change the destroot PATH environment, so export
# PATH explicitly in the necessary system command below
destroot.env-append \
    PATH=$env(PATH):${frameworks_dir}/Python.framework/Versions/${python3_version}/bin \
    ${name}_datadir=${destroot}${prefix}/${ihaskell_datadir}

post-destroot {
    # install the data-files into destroot (see ${name}.cabal)
    fs-traverse f ${worksrcpath}/.stack-work {
        if { [file isfile ${f}] } {
            foreach datafile {
                html/kernel.js
                html/logo-64x64.svg
                } {
                if { [string match "*/${datafile}" ${f}] } {
                    xinstall -m 0644 ${f} \
                        ${destroot}${prefix}/${ihaskell_datadir}/${datafile}
                }
            }
        }
    }
    
    # run ihaskell to install the IPython files into destroot
    system -W ${worksrcpath} "\
        export \
            PATH=$env(PATH):${frameworks_dir}/Python.framework/Versions/${python3_version}/bin \
            ${name}_datadir=${destroot}${prefix}/${ihaskell_datadir} \
            ; \
        ${destroot}${prefix}/bin/ihaskell install \
            --prefix=${destroot}${prefix} \
        "

    # delete any destroot path appearing in text files
    fs-traverse f ${destroot}${prefix} {
        if {[file isfile ${f}]} {
            if {[string match "text/*" [lindex [exec /usr/bin/file --mime-type ${f}] end]]} {
                reinplace -q "s|${destroot}||g" ${f}
            }
        }
    }
}

# Remove hlint_datadir instructions after this cabal issue is fixed:
# https://github.com/haskell/cabal/issues/6234
notes "

The environment variable `hlint_datadir` must be set before running the
Jupyter notebook (due to a Cabal path issue):

        export hlint_datadir=${prefix}/${hlint_datadir}
        jupyter-${python3_version} notebook-${python3_version}
"
