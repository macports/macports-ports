# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           perl5 1.0

name                git
version             2.35.0
revision            0

description         A fast version control system
long_description    Git is a fast, scalable, distributed open source version \
                    control system focusing on speed and efficiency.
maintainers         {ciserlohn @ci42} \
                    {gmail.com:herby.gillot @herbygillot} \
                    openmaintainer
categories          devel
license             GPL-2 LGPL-2.1+
installs_libs       no
homepage            https://git-scm.com/
master_sites        https://www.kernel.org/pub/software/scm/git/ \
                    https://cdn.kernel.org/pub/software/scm/git/
distname            git-${version}
use_xz              yes
distfiles           git-${version}${extract.suffix} \
                    git-manpages-${version}${extract.suffix}

checksums           git-${version}${extract.suffix} \
                    rmd160  bd85ccf0d942c8a920ececcd1173111bf3a7e11a \
                    sha256  47e677b475226857ceece569fb6ded8b85838ede97ae1e01bd365ac32a468fc8 \
                    size    6873768 \
                    git-manpages-${version}${extract.suffix} \
                    rmd160  258f5461c509cb19a4a04adae4c374fd154fcc88 \
                    sha256  438625a0639d2e627a7a2edab94d999c200ec7464cc19a8052322641b5272ca9 \
                    size    497388

perl5.require_variant   false
perl5.conflict_variants yes
perl5.branches          5.28 5.30 5.32 5.34
perl5.default_branch    5.28
perl5.create_variants   ${perl5.branches}

depends_build-append \
                    port:gettext

depends_lib-append  port:curl \
                    port:zlib \
                    port:expat \
                    port:gettext-runtime \
                    port:libiconv

depends_run-append  path:bin/rsync:rsync

patchfiles          patch-Makefile.diff \
                    patch-git-gui-Makefile.diff \
                    patch-git-subtree.1.diff \
                    patch-sha1dc-older-apple-gcc-versions.diff \
                    patch-gitk-git-gitk.diff
patch.pre_args      -p1

extract.only        git-${version}${extract.suffix} \
                    git-manpages-${version}${extract.suffix}

use_configure       no

variant universal   {}

set CFLAGS          "-I. -Wall -O2 -I${prefix}/include [get_canonical_archflags cc]"
set LDFLAGS         "-L${prefix}/lib [get_canonical_archflags ld]"

build.args          CFLAGS="${CFLAGS}" \
                    LDFLAGS="${LDFLAGS}" \
                    CC=${configure.cc} \
                    prefix=${prefix} \
                    CURLDIR=${prefix} \
                    ICONVDIR=${prefix} \
                    NO_FINK=1 \
                    NO_DARWIN_PORTS=1 \
                    NO_R_TO_GCC_LINKER=1 \
                    NO_PERL=1 \
                    V=1

if {[variant_isset ${perl5.variant}]} {
    depends_run-append \
                    port:p${perl5.major}-authen-sasl \
                    port:p${perl5.major}-error \
                    port:p${perl5.major}-net-smtp-ssl \
                    port:p${perl5.major}-term-readkey \
                    port:p${perl5.major}-cgi

    build.args-delete \
                    NO_PERL=1

    build.args-append \
                    PERL_PATH="${perl5.bin}" \
                    NO_PERL_CPAN_FALLBACK=1
} elseif {[variant_isset svn]} {
    pre-configure {
        return -code error "+svn does not work without +perl5_xx"
    }
}

test.run            yes
test.cmd            make
test.target         test
test.dir            ${worksrcpath}
pre-test {
    test.args  {*}${build.args}
}

destroot.target     install
pre-destroot {
    destroot.args  {*}${build.args}
    xinstall -m 644 ${worksrcpath}/contrib/subtree/git-subtree.1 ${workpath}/man1
}

set docdestroot         ${destroot}${prefix}/share/doc/git-doc
set system_gitconfig    ${prefix}/etc/gitconfig

post-destroot {
    foreach f {1 5 7} {
        xinstall -d ${destroot}${prefix}/share/man/man${f}
        foreach m [glob -directory ${workpath} man${f}/*.${f}] {
            xinstall ${m} ${destroot}${prefix}/share/man/man${f}
        }
    }
    if {![variant_isset svn]} {
        system "rm ${destroot}${prefix}/libexec/git-core/git-svn*"
    }

    xinstall -d ${docdestroot}

    fs-traverse badfile ${destroot} {
        if {[string last perllocal.pod ${badfile}] != -1} {
            ui_info "Removing ${badfile}"
            file delete ${badfile}
        }
    }

    set completions_path ${destroot}${prefix}/share/bash-completion/completions
    xinstall -d ${completions_path}
    xinstall -m 644 ${worksrcpath}/contrib/completion/git-completion.bash \
        ${completions_path}/git

    set share_path ${destroot}${prefix}/share/${name}
    xinstall -d ${share_path}
    xinstall -m 644 ${worksrcpath}/contrib/completion/git-prompt.sh \
        ${share_path}/git-prompt.sh

    xinstall -d ${destroot}${prefix}/libexec/git-core
    xinstall -m 755 ${worksrcpath}/contrib/subtree/git-subtree.sh \
        ${destroot}${prefix}/libexec/git-core/git-subtree

    file delete -force ${share_path}/contrib
    copy ${worksrcpath}/contrib ${share_path}
}

variant pcre description {Use pcre} {
    build.args-append       LIBPCREDIR=${prefix} USE_LIBPCRE2=1
    depends_lib-append      port:pcre2
}

variant doc description {Install HTML and plaintext documentation} {
    distfiles-append        git-htmldocs-${version}${extract.suffix}
    checksums-append        git-htmldocs-${version}${extract.suffix} \
                            rmd160  e3fc12a69c5cf5136886dad79e9fed6abaf28633 \
                            sha256  a92cafb3668d066c68b86037ac4752a72a08a101d6540d972b993f6939274d0d \
                            size    1410536

    patchfiles-append       patch-git-subtree.html.diff

    post-extract {
        file mkdir ${workpath}/htmldocs
        system -W ${workpath}/htmldocs "${extract.cmd} ${extract.pre_args} \
            '${distpath}/git-htmldocs-${version}${extract.suffix}' \
            ${extract.post_args}"
    }

    pre-destroot {
        xinstall -m 644 ${worksrcpath}/contrib/subtree/git-subtree.html ${workpath}/htmldocs
    }

    post-destroot {
        foreach f [glob ${workpath}/htmldocs/*] {
            file delete -force "${docdestroot}/[file tail ${f}]"
            file attribute ${f} -permissions ugo+r
            copy ${f} ${docdestroot}
        }

        fs-traverse f [list ${docdestroot}/howto ${docdestroot}/technical ${docdestroot}/RelNotes] {
            if {[file isdirectory ${f}]} {
                file attribute ${f} -permissions ugo+rx
            } else {
                file attribute ${f} -permissions ugo+r
            }
        }
    }
}

variant gitweb description {Install gitweb.cgi} {
    depends_run-append  port:lighttpd

    build.target-append gitweb

    post-destroot {
        xinstall -d ${destroot}${prefix}/share/${name}/gitweb
        xinstall -W ${worksrcpath}/gitweb \
            gitweb.cgi \
            ${destroot}${prefix}/share/${name}/gitweb
        xinstall -m 444 -W ${worksrcpath}/gitweb/static \
            gitweb.css \
            gitweb.js \
            git-favicon.png \
            git-logo.png \
            ${destroot}${prefix}/share/${name}/gitweb
        xinstall -d ${docdestroot}/gitweb
        xinstall -m 444 -W ${worksrcpath}/gitweb README INSTALL \
            ${docdestroot}/gitweb
    }
}

variant svn description {Bi-directional subversion repository support} {
    depends_run-append  port:subversion \
                        port:p${perl5.major}-libwww-perl \
                        port:p${perl5.major}-svn-simple
}

variant credential_osxkeychain description {Install git credential-osxkeychain utility from contrib} {

    post-build {
        system -W "${worksrcpath}/contrib/credential/osxkeychain" "make [join ${build.args}]"
    }

    post-destroot {
        xinstall -m 0755 \
            "${worksrcpath}/contrib/credential/osxkeychain/git-credential-osxkeychain" \
            "${destroot}${prefix}/libexec/git-core/"
    }

    pre-activate {
        set osxkc_config        "\[credential]\n\thelper = osxkeychain"

        # Ensure the osxkeychain credential helper is configured in the
        # system-wide gitconfig.  If the gitconfig file already exists,
        # then append the configuration to the pre-existing file only
        # if the file is currently missing the desired config.

        if { ![file exists ${system_gitconfig}] } {
            set f [open ${system_gitconfig} w 0644]
            puts ${f} ${osxkc_config}
            close ${f}
        } else {
            if { [catch {exec grep "helper = osxkeychain" ${system_gitconfig}} result] } {
                set f [open ${system_gitconfig} a]
                puts ${f} ${osxkc_config}
                close ${f}
            }
        }
    }

    notes "
Configuration to enable the osxkeychain credential helper has been added to
the system-wide gitconfig at ${system_gitconfig}. If you do not wish to use
this credential helper, you can override this setting in your own personal
git config file \(\$HOME/.gitconfig\) with e.g.

    \[credential]
            helper = some_other_credential_helper

For more information, run

    git help credentials
"
}

variant diff_highlight description {Install git diff-highlight utility from contrib} {

    post-build {
        system -W "${worksrcpath}/contrib/diff-highlight" "make [join ${build.args}]"
    }

    post-destroot {
        xinstall -m 755 "${worksrcpath}/contrib/diff-highlight/diff-highlight" \
            "${destroot}${prefix}/bin/"
    }
}

# Python is only needed to enable certain tools for interacting with other
# version control systems, such as git-p4 (for interfacing with Perforce).
# This variant enables git to use MacPorts' Python as it builds these
# additional tools.
variant python description {Build with Python support} {
    depends_lib-append  port:python39
    build.args-append   PYTHON_PATH="${prefix}/bin/python3.9"
}

platform darwin 8 {
    build.args-append   NO_APPLE_COMMON_CRYPTO=1
    build.args-append   CC_LD_DYNPATH=-R
}

default_variants        +doc +pcre +credential_osxkeychain +diff_highlight

livecheck.type          regexm
livecheck.regex         {<span class="version">.*?(\d+\.\d+\.\d+).*?</span>}
