# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

name                qt5-qtcreator

if { ${subport} eq ${name}  } {
    PortGroup       muniversal 1.0
}
PortGroup           qmake5 1.0
PortGroup           cxx11  1.1

version             4.3.1
revision            1
categories          devel aqua
platforms           darwin
# from https://blog.qt.io/blog/2016/01/13/new-agreement-with-the-kde-free-qt-foundation/
# The GPL license comes with two exceptions that ensure that there are no license restrictions on generated code, and that bridging to 3rd party code is still possible.
license             GPL-3
maintainers         mcalhoun openmaintainer
description         Cross-platform integrated development environment (IDE) tailored to the needs of Qt developers.
long_description    Qt Creator is a cross-platform integrated development environment (IDE) tailored to the needs of Qt developers.

homepage            http://qt-project.org/wiki/Category:Tools::QtCreator
distname            qt-creator-opensource-src-${version}
master_sites        https://download.qt.io/official_releases/qtcreator/[join [lrange [split ${version} .] 0 1] .]/${version}/

checksums           rmd160  200b302418f4e0387df1713326ff78be099668f2 \
                    sha256  db43c796adf11f02fd22d01820678cd118fc5bd9a7bb5d2d76ed0f86e0dd8a69

pre-fetch {
    if {${os.platform} eq "darwin" && ${os.major} < 12} {
        # last version that runs on Lion is 3.6.1
        ui_error "${subport} requires OS X 10.8 or later"
        return -code error "incompatible OS version"
    }
}

# qtcreator does not depend directly on openssl
# qtcreator links against QtNetwork.framework, and any OpenSSL functionality
#    used is through QtNetwork
license_noconflict  openssl

# avoid make[4]: write error
use_parallel_build  no

if { ${subport} eq ${name}  } {

    qt5.depends_component  qtscript qtdeclarative qttools qtmacextras qtquickcontrols qtsvg

    depends_lib-append     port:botan
    configure.args-append  "USE_SYSTEM_BOTAN=1"

    depends_lib-append     port:qbs
    configure.args-append  "QBS_INSTALL_DIR=${prefix}"

    # do not opportunistically use QtWebengine
    patchfiles-append      patch-no_qtwebengine.diff

    # qtwebengine does not have a universal build
    variant qtwebengine conflicts universal description {build with QtWebengine support} {
        patchfiles-delete  patch-no_qtwebengine.diff
        qt5.depends_component qtwebengine qtwebchannel
    }

    if { ![variant_isset universal] } {
        destroot {
            # make install attempts to install command line tools into /bin, etc.
            xinstall -m 755 -d ${destroot}${qt_apps_dir}
            copy "${worksrcpath}/bin/Qt Creator.app" "${destroot}${qt_apps_dir}"
        }
    } else {
        # We should really just override destroot, but the muniversal Portgroup would override our override.
        destroot.cmd "true"
        merger-post-destroot {
            foreach arch ${universal_archs_to_use} {
                xinstall -m 755 -d ${destroot}-${arch}${qt_apps_dir}
                copy "${worksrcpath}-${arch}/bin/Qt Creator.app" "${destroot}-${arch}${qt_apps_dir}"
            }
        }
    }

    # put following in post-destroot so it works with universal
    post-destroot {
        # sdktool is a tool located in "Qt Creator.app/Contents/Resources"
        #
        # two necessary files that sdktool creates are:
        #    "${qt_apps_dir}/Qt Creator.app/Contents/Resources/QtProject/qtcreator/qtversion.xml"
        #    "${qt_apps_dir}/Qt Creator.app/Contents/Resources/QtProject/qtcreator/profiles.xml"
        #
        # from the installer.dat file, it seems the following are run by the binary installer of Qt Creator version 5.9.0
        # see http://code.qt.io/cgit/qtsdk/qtsdk.git/tree/packaging-tools/configurations/pkg_templates/pkg_59/qt.59.clang_64/meta/installscript.qs
        #    sdktool addQt --id qt.59.clang_64 --name "Qt %{Qt:Version} clang 64bit" --type Qt4ProjectManager.QtVersion.Desktop --qmake qmakeBinary
        #    sdktool addKit --id qt.59.clang_64_kit --name "Desktop Qt %{Qt:Version} clang 64bit" --Ctoolchain x86-macos-generic-mach_o-64bit --Cxxtoolchain x86-macos-generic-mach_o-64bit --qt qt.59.clang_64 --debuggerengine 256 --devicetype Desktop
        #
        # modify for our purposes
        #
        # for unknown reason, using system instead of exec results in "QWidget: Cannot create a QWidget without QApplication"
        #
        # surround sdktool with catch since, as of 4.2.0, sdktool causes Tcl to register an error when asked to create a file
        #
        set sdktool ${destroot}${qt_apps_dir}/Qt\ Creator.app/Contents/Resources/sdktool

        if { [catch {
            exec                                             \
                "${sdktool}"                                 \
                addQt                                        \
                --id    qt.5.macports                        \
                --name  "Qt %{Qt:Version} from MacPorts"     \
                --qmake "${qt_qmake_cmd}"                    \
                --type  Qt4ProjectManager.QtVersion.Desktop
        } msg] } {
            if { ![file exists ${destroot}${qt_apps_dir}/Qt\ Creator.app/Contents/Resources/QtProject/qtcreator/qtversion.xml] } {
                ui_error ${msg}
                return -code error "sdktool failure"
            }
        }

        if { [variant_isset universal] } {
            foreach arch ${universal_archs_to_use} {
                if { ${arch} eq "i386" } {
                    set toolchain_bit      32
                    set qt_qmake_spec_arch ${qt_qmake_spec_32}
                } else {
                    set toolchain_bit      64
                    set qt_qmake_spec_arch ${qt_qmake_spec_64}
                }

                if { [catch {
                    exec                                                                   \
                        "${sdktool}"                                                       \
                        addKit                                                             \
                        --id             qt.5.macports_${arch}_kit                         \
                        --name           "Desktop Qt %{Qt:Version} from MacPorts ${arch}"  \
                        --Ctoolchain     x86-macos-generic-mach_o-${toolchain_bit}bit      \
                        --Cxxtoolchain   x86-macos-generic-mach_o-${toolchain_bit}bit      \
                        --qt             qt.5.macports                                     \
                        --debuggerengine 256                                               \
                        --devicetype Desktop                                               \
                        --mkspec     ${qt_qmake_spec_arch}
                } msg] } {
                    if { ![file exists ${destroot}${qt_apps_dir}/Qt\ Creator.app/Contents/Resources/QtProject/qtcreator/profiles.xml] } {
                        ui_error ${msg}
                        return -code error "sdktool failure"
                    }
                }
            }
        } else {
            if { ${build_arch} eq "i386" } {
                set toolchain_bit 32
            } else {
                set toolchain_bit 64
            }

            if { [catch {
                exec                                                                         \
                    "${sdktool}"                                                             \
                    addKit                                                                   \
                    --id             qt.5.macports_${build_arch}_kit                         \
                    --name           "Desktop Qt %{Qt:Version} from MacPorts ${build_arch}"  \
                    --Ctoolchain     x86-macos-generic-mach_o-${toolchain_bit}bit            \
                    --Cxxtoolchain   x86-macos-generic-mach_o-${toolchain_bit}bit            \
                    --qt             qt.5.macports                                           \
                    --debuggerengine 256                                                     \
                    --devicetype     Desktop                                                 \
                    --mkspec         ${qt_qmake_spec}
            } msg] } {
                if { ![file exists ${destroot}${qt_apps_dir}/Qt\ Creator.app/Contents/Resources/QtProject/qtcreator/profiles.xml] } {
                    ui_error ${msg}
                    return -code error "sdktool failure"
                }
            }
        }

        # N.B. that at some point in the future, QtCreator.ini may need to be created
        #    to include more search locations for documentation and examples
    }
}

subport ${name}-docs {

    universal_variant no
    supported_archs   noarch
    qt5.debug_variant no

    qt5.depends_build_component sqlite-plugin
    depends_run-append   port:${name}
    build.target       docs

    destroot {
        xinstall -m 755 -d ${destroot}${qt_docs_dir}

        foreach dir "qtcreator qtcreator-dev" {
            xinstall -m 0644 "${worksrcpath}/bin/Qt Creator.app/Contents/Resources/doc/${dir}.qch" ${destroot}${qt_docs_dir}
            copy ${worksrcpath}/doc/${dir} ${destroot}${qt_docs_dir}
        }
    }
}

livecheck.type      regex
livecheck.url       http://www.qt.io/download-open-source/
livecheck.regex     qt-creator-opensource-src-(\[a-z0-9.\]+)${extract.suffix}
