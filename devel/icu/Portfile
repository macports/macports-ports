# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:filetype=tcl:et:sw=4:ts=4:sts=4

PortSystem      1.0

name            icu
set my_name     icu4c
# Don't forget to increase the revision number of the dependents (e.g. boost)
# whenever the library version number changes. Thanks.
# To find dependencies, use:
# port file all | sort -u | xargs grep -El ':icu( |$)' | rev | cut -d / -f 2 | rev | sort -u
version                     58.2
# Don't update to 59 or later because they require C++11
categories      devel textproc
platforms       darwin freebsd
maintainers                 {ryandesign @ryandesign} openmaintainer
license                     Permissive

description     International Components for Unicode

long_description \
    The International Components for Unicode (ICU) libraries provide robust \
    and full-featured Unicode services on a wide variety of platforms. ICU \
    supports the most current version of the Unicode standard, and they provide \
    support for supplementary Unicode characters (needed for GB 18030 repertoire support).

homepage                    http://site.icu-project.org/
master_sites    http://download.icu-project.org/files/${my_name}/${version}/

distname        ${my_name}-[join [split ${version} .] _]
extract.suffix              .tgz
distfiles                   ${distname}-src${extract.suffix}
checksums                   rmd160  df06e7b18a87e383d3762564f2e9a59fd75865f9 \
                            sha256  2b0a4410153a9b20de0e20c7d8b66049a72aef244b53683d0d7521371683da0c

worksrcdir      ${name}/source
set docdir      ${prefix}/share/doc/${name}

configure.args              --disable-layoutex \
                            --disable-samples \
                            --enable-static

subport ${name}-docs {
    revision                0
    conflicts           ${name}-doxygen-docs
    supported_archs     noarch

    description-append  (documentation)
    long_description-append Documentation.
    
    use_zip             yes
    distfiles               ${distname}-docs${extract.suffix}
    checksums               rmd160  64c9385c43f994d283a38980c77638dc061bfe84 \
                            sha256  dc17983debc1f23f1bd1ac1ba053f2df5b07858f5af9bd48eeb3b8044c8d3146

    extract.dir         ${worksrcpath}/doc/html

    use_configure       no

    build {}

    destroot {
        xinstall -m 755 -d ${destroot}${docdir}
        copy ${worksrcpath}/doc/html ${destroot}${docdir}
    }
}

subport ${name}-doxygen-docs {
    revision                0
    conflicts           ${name}-docs
    supported_archs     noarch
    
    depends_build-append    port:doxygen

    description-append  (build documentation)
    long_description-append Build documentation.
    
    build.target     doc
    destroot.target  install-doc
}

subport ${name}-lx {
    revision                0
    # http://userguide.icu-project.org/layoutengine/paragraph
    description             ICU paragraph layout library
    long_description        ${description}

    depends_build-append    port:pkgconfig
    depends_lib-append      port:icu \
                            port:icu-le-hb

    configure.args-replace  --disable-layoutex --enable-layoutex

    destroot.dir            ${worksrcpath}/layoutex
}

if {${subport} eq ${name}} {
    revision                1
}

if { ${subport} ne "${name}-docs" } {
    if { ${supported_archs} ne "noarch" } {
        PortGroup       muniversal 1.0
    }
    
    patchfiles-append   patch-config-mh-darwin.diff
    patchfiles-append   CVE-2017-7867-CVE-2017-7868.patch
    
    # ICU has three mechanisms to aid other projects in building properly
    #     1) .pc files for pkg-config
    #     2) shell script icu-config that can output compiler flags
    #     3) Makefile include files Makefile.inc and pkgdata.inc
    # For a time, all three assumed other projects would want to be
    #     built as ICU had been built, regardless of whether it was appropriate.
    # For the most part, the extraneous flags have been removed.
    #     1) They have been removed completely from the .pc files.
    #        pkg-config is the recommended way of building against ICU.
    #     2) icu-config does not print them out, but they are still contained in the script itself.
    #     3) They still exist in the Makefile include files.
    #
    # for descriptions of the problem, see
    #     r101578
    #     #40069
    #     https://bugs.gentoo.org/show_bug.cgi?id=202059
    #
    # for upstream reports and discussion, see
    #    http://bugs.icu-project.org/trac/ticket/10308
    #    http://bugs.icu-project.org/trac/ticket/6102
    post-build {
        if {[variant_exists universal] && [variant_isset universal]} {
            set dirs {}
            foreach arch ${universal_archs_to_use} {
                lappend dirs ${worksrcpath}-${arch}
            }
        } else {
            set dirs ${worksrcpath}
        }
        foreach dir ${dirs} {
            # Removing architecture-specific differences is necessary to facilitate merging.
            # This should not be a problem for projects using icu-config because we do not
            # believe icu-config uses SIZEOF_VOID_P anyway. Projects using the Makefile
            # include files may experience problems. The developers of ICU suggest using
            # pkg-config over both icu-config and the Makefile includes.
            reinplace {s| -DSIZEOF_VOID_P=[48]||g} \
                ${dir}/config/Makefile.inc \
                ${dir}/config/icu-config
            reinplace -E {s| -arch +[^ ]+||g} \
                ${dir}/config/pkgdata.inc
        }
    }
    
    set platform [switch ${os.platform} {darwin {format MacOSX} freebsd {format FreeBSD}}]
    configure.cmd   ./runConfigureICU ${platform}
    
    configure.universal_args-delete --disable-dependency-tracking
    if {[variant_isset universal]} {
        # ICU detects cross compiling by trying to run binaries.
        # r73849 and #29904 indicate that cross compiling is a problem.
        # Of the solutions tried (r79660, r73865, and r73849), simply disallowing cross compiling is by far the simplest.
        # Note that #29904 should not show up again because universal now better recognizes 32/64-bit cross compiling (r79658)
        set merger_must_run_binaries "yes"
    }
    
    # Fix bug #11981 that prevents ICU from building when upgrading.
    # The default configure flags causes utilisation of outdated ICU
    # headers/libs instead of the right ones.
    configure.cppflags
    configure.ldflags
    
    build.type      gnu
    build.args      VERBOSE=1
    
    test.run        yes
    test.target     check
    test.args       VERBOSE=1
    
    if { ${name} eq ${subport} } {
        post-destroot {
            xinstall -d ${destroot}${docdir}
            xinstall -m 0644 {*}[glob ${worksrcpath}/../*.{css,html,txt}] ${destroot}${docdir}
        }
    }
    
    platform freebsd {
        build.env       MAKE=/usr/local/bin/gmake
        destroot.env    MAKE=/usr/local/bin/gmake
    }
    
    platform darwin 8 {
        patchfiles-append patch-common-putil.cpp.diff
    }
}

if {${subport} eq ${name}} {
    livecheck.url       http://site.icu-project.org/download
    livecheck.regex     "ICU4C-Download\">(\[0-9.\]+)"
} else {
    livecheck.type      none
}
