# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           muniversal 1.0

name                mlt
epoch               3
version             6.4.1
revision            2
categories          multimedia
maintainers         dennedy.org:dan gmail.com:rjvbertin openmaintainer
license             GPL-2+

description         Open source multimedia framework - core libraries
long_description    MLT is an open source multimedia framework, designed and developed for \
                    television broadcasting. It provides a toolkit for broadcasters, video \
                    editors, media players, transcoders, web streamers and many more types of \
                    applications. The functionality of the system is provided via an assortment \
                    of ready to use tools, xml authoring components, and an extendible plug-in \
                    based API. \
                    \
                    This package contains the files necessary for running applications that use \
                    the MLT multimedia framework.
homepage            http://www.mltframework.org/
master_sites        sourceforge:project/mlt/mlt
platforms           darwin

checksums           rmd160  647cdad41974b93c5745a358e1129f2ed5f6e577 \
                    sha256  d3a992f3e67463e68630cb0b455d408a2a12f4da7a19e46807fa08a79f09b2b6

subport ${name}-qt5 {
    description     ${description} - Qt5 version
    long_description \
                    ${long_description} Built against Qt5.
}

if {${subport} eq "${name}"} {
    conflicts       ${name}-qt5
    PortGroup       qt4 1.0
} else {
    conflicts       ${name}
    PortGroup       qt5 1.0
    if {[info procs qt5.active_version] eq ""} {
        proc qt5.active_version {} {
            global prefix
            if {[file exists ${prefix}/bin/pkg-config]} {
                set av [exec ${prefix}/bin/pkg-config --modversion Qt5Core]
                return ${av}
            } else {
                return 0.0.0
            }
        }
    }

    qt5.depends_component \
                    qtsvg
    if {[vercmp [qt5.active_version] 5.7.0] >= 0} {
        configure.cxxflags-append \
                    -stdlib=libc++ \
                    -std=c++11
    }
}

depends_build-append port:git \
                    port:doxygen

depends_lib-append  port:atk \
                    port:frei0r-plugins \
                    path:lib/libavcodec.dylib:ffmpeg \
                    port:libsamplerate \
                    port:libsdl \
                    port:libsdl_image \
                    port:libvorbis \
                    port:sox \
                    port:libxml2 \
                    port:libexif

if {[variant_isset opencv]} {
    PortGroup       active_variants 1.1
    if {${subport} eq "${name}"} {
        require_active_variants opencv qt4
    } else {
        require_active_variants opencv qt5
    }
}

# cherrypicked commit from the next release that fixes a crash-on-exit issue
# in recent Kkdenlive versions.
patchfiles-append   patch-commit-a3188e3.diff

# NB: qt-includedir can and should be set to ${qt_includes_dir},
# not ${qt_includes_dir}/Qt !!
configure.args-append \
                    --enable-gpl \
                    --qt-libdir=${qt_frameworks_dir} \
                    --qt-includedir=${qt_includes_dir} \
                    --disable-jackrack \
                    --disable-mmx \
                    --disable-gtk2 \
                    --disable-swfdec \
                    --disable-opencv

platform darwin 10 {
    if {${build_arch} eq "x86_64"} {
        configure.args-delete --disable-mmx
    }
}

configure.cflags-append     "-I${prefix}/include"
configure.cflags-append     "-I${qt_includes_dir}"
configure.ldflags-delete    "-L${prefix}/lib"

# make sure the build system actually sees our settings by amending the toplevel config.mak.
post-configure {
    set config.mak [open ${worksrcpath}/config.mak "a"]
    puts ${config.mak} "CC=${configure.cc}"
    puts ${config.mak} "CXX=${configure.cxx}"
    puts ${config.mak} "CFLAGS+=${configure.cflags} ${configure.cppflags}"
    puts ${config.mak} "CXXFLAGS+=${configure.cxxflags} ${configure.cppflags}"
    puts ${config.mak} "LDLAGS+=${configure.ldflags}"
    close ${config.mak}
}

build.args-append           CC="${configure.cc}" CXX="${configure.cxx}" CPP="${configure.cpp}"

if {[variant_isset universal]} {
    foreach arch ${configure.universal_archs} {
        lappend merger_build_env(${arch})   {*}"CFLAGS='${configure.cflags} -arch ${arch}'"
        lappend merger_build_env(${arch})   {*}"CXXFLAGS='${configure.cxxflags} -arch ${arch}'"
        lappend merger_build_env(${arch})   {*}"LDFLAGS='${configure.ldflags} -L${worksrcpath}-${arch}/src/framework -L${prefix}/lib -arch ${arch}'"
    }
} else {
    build.env-append                        CFLAGS="${configure.cflags} [get_canonical_archflags cc]"
    build.env-append                        CXXFLAGS="${configure.cxxflags} [get_canonical_archflags cxx]"
    build.env-append                        LDFLAGS="${configure.ldflags} -L${worksrcpath}/src/framework -L${prefix}/lib [get_canonical_archflags ld]"
}

variant gtk2 description {include the gtk2 module (for additional image and text producers)} {
    configure.args-delete   --disable-gtk2
    depends_lib-append      port:gtk2
}

variant gpl3 description {enable GPLv3 components} {
    configure.args-append   --enable-gpl3
    license                 GPL-3+
}

# use an opencv variant to avoid opportunistic dependencies on this library
# the opencv dependency must match our choice of Qt version, which is
# another reason why opencv support is provided through a variant.
variant opencv description {enable OpenCV support} {
    depends_lib-append      port:opencv
    configure.args-delete   --disable-opencv
}

# Allow building without SDL support, useful when used in dependents that themselves
# depend on SDL2, to avoid (warnings about) runtime conflicts. It can be hoped that
# one day MLT will support SDL2 too.
variant disable_sdl description {disable SDL support to avoid SDL vs. SDL2 conflicts} {
    patchfiles-append       patch-no-sdl.diff
    configure.args-append   --disable-sdl
    configure.cppflags-append \
                            -DMELT_NOSDL
    depends_lib-delete \
                            port:libsdl \
                            port:libsdl_image
}

livecheck.type      regex
livecheck.url       http://sourceforge.net/projects/${name}/files/
livecheck.regex     /${name}-(\[0-9.\]+)${extract.suffix}
