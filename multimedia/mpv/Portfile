# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:filetype=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               github 1.0
PortGroup               meson 1.0
PortGroup               compiler_blacklist_versions 1.0
PortGroup               legacysupport 1.1
PortGroup               active_variants 1.1

# Please revbump mpv whenever ffmpeg{,-devel} is updated!
github.setup            mpv-player mpv 0.37.0 v
revision                0
categories              multimedia
license                 GPL-2+
maintainers             {ionic @Ionic} {i0ntempest @i0ntempest} openmaintainer
platforms               darwin

description             mpv is a movie player based on MPlayer and mplayer2.
long_description        {*}${description} It plays most MPEG/VOB, AVI, Ogg/OGM, \
                        VIVO, ASF/WMA/WMV, QT/MOV/MP4, RealMedia, Matroska, \
                        NUT, NuppelVideo, FLI, YUV4MPEG, FILM, RoQ, PVA files, \
                        supported by many native, XAnim, and Win32 DLL codecs. \
                        You can watch VideoCD, SVCD, DVD, 3ivx, DivX 3/4/5, WMV \
                        and even H.264 movies.
homepage                http://www.mpv.io/

checksums               rmd160  9cd4ada6decf8ca7a82c3e462fbc8a7b6859406a \
                        sha256  26c9007c655483438d83f17070190fc4b6bccb8ff5efc30fcedbb7d162681a7f \
                        size    3384587

installs_libs           no

depends_build-append    port:pkgconfig
depends_lib             path:lib/libavcodec.dylib:ffmpeg \
                        path:bin/perl:perl5 \
                        port:libiconv \
                        port:zlib \
                        port:libass \
                        port:lcms2 \
                        port:libplacebo

universal_variant       no

default_variants        +network +osd +opengl +bluray +dvd +audiocd +libarchive

if {${os.platform} eq "darwin" && ${os.major} > 10} {
    # current macOS bundle errors on < 10.7 due to unhandled -psn_* argument
    default_variants-append +bundle
}

# at this moment in time (20190127) rubberband does not build on PPC
platform darwin {
    if {${configure.build_arch} ne "ppc"} {
        default_variants-append +rubberband
    }
}

configure.args-append   --buildtype=release \
                        -Dmanpage-build=enabled \
                        -Dcoreaudio=enabled \
                        -Dcocoa=enabled \
                        -Dcplugins=enabled \
                        -Dzlib=enabled \
                        -Dswift-build=enabled \
                        -Dhtml-build=disabled \
                        -Dvideotoolbox-gl=disabled \
                        -Dcuda-hwaccel=disabled \
                        -Dopensles=disabled \
                        -Degl-drm=disabled \
                        -Degl-angle-lib=disabled \
                        -Degl-angle-win32=disabled \
                        -Daudiounit=disabled \
                        -Dios-gl=disabled \
                        -Ddirect3d=disabled \
                        -Dandroid-media-ndk=disabled \
                        -Drpi=disabled \
                        -Ddvbin=disabled \
                        -Dlibmpv=false

# mpv autodetects many support libs. To prevent undeclared
# dependencies, explicitly disable everything optional first.
# This list should be expanded each time the port is updated to a
# new version, disabling any newly added options.
configure.args-append   -Dcaca=disabled \
                        -Dcdda=disabled \
                        -Dpthread-debug=disabled \
                        -Ddvdnav=disabled \
                        -Degl-x11=disabled \
                        -Dgl=disabled \
                        -Dgl-cocoa=disabled \
                        -Dvulkan=disabled \
                        -Dgl-x11=disabled \
                        -Djack=disabled \
                        -Djavascript=disabled \
                        -Djpeg=disabled \
                        -Dlibarchive=disabled \
                        -Dlibbluray=disabled \
                        -Dlua=disabled \
                        -Dopenal=disabled \
                        -Dpdf-build=disabled \
                        -Dpulse=disabled \
                        -Drubberband=disabled \
                        -Duchardet=disabled \
                        -Dvapoursynth=disabled \
                        -Dx11=disabled \
                        -Dxv=disabled

# Fix picking up the correct talloc version.
# -isystem has the added benefit of moving the include
# directory specified to the end of the include path list.
# This will help the build system respect custom include
# paths correctly (i.e., searching them before the MP include
# directory.)
configure.cppflags-replace  -I${prefix}/include -isystem${prefix}/include

# Clear CPATH and LIBRARY_PATH because a ncurses include file
# conflicts with the one in MacPorts. It doesn't matter much anyway
# because the swift module in mpv doesn't have any dependencies to
# link against.
compiler.cpath
compiler.library_path

platform macosx {
    if {${os.major} > 10} {
        # Force recent enough Xcode.
        # We have to compile with the compiler provided by Xcode/CLT
        # due to only it shipping libarclite on 10.7 and knowing where
        # it is.
        # Could "emulate" the same behavior by using MacPorts' clang
        # and appending -L/usr/lib/arc/ to CFLAGS/CXXFLAGS on 10.7,
        # but I don't particularly like that idea (yet.)
        # Keep using the "system compiler" for now. Also means one less
        # dependency on recent systems, so yay.
        PortGroup                   xcodeversion 1.0

        minimum_xcodeversions       {11 4.4}

        # Force recent enough SDK.
        if {[vercmp ${configure.sdk_version} 10.8] < 0} {
            configure.sdk_version   10.8
        }

        # Force clang compiler as installed by Xcode's CLT's... hopefully.
        compiler.blacklist-append   *gcc* cc macports-*
    }

    if {${os.major} <= 14 || ${configure.build_arch} ni [list x86_64 arm64]} {
        # Disable Cocoa output.
        # Users on old systems will need to embrace X11.
        configure.args-replace      -Dcocoa=enabled \
                                    -Dcocoa=disabled

        default_variants-append     +x11
    }
}

platform darwin {
    notes-append [subst {
                    The maintainer ships his personal config file in
                    * ${prefix}/share/doc/${name}/examples/config-maintainer.

                    You are strongly encouraged to copy this file to either
                    * ${prefix}/etc/${name}/${name}.conf (globally for all users of the system) or
                    * ~/.config/${name}/config (only for your current user)
                    and adjust it to your needs.

                    If you previously had your configuration file in
                    * ~/.${name}/config
                    please migrate it to
                    * ~/.config/${name}/config
                    which is the location preferred by upstream.
    }]

    # cocoa-cb backend requires Swift 5 or 4 support, which is only available
    # in Xcode 9.1 onwards.
    # From version 0.34.0 Swift 4.1 (Xcode 9.3+) or later is required
    if {[vercmp ${xcodeversion} 9.3] < 0} {
        configure.args-replace  -Dswift-build=enabled \
                                -Dswift-build=disabled
    }

    # VideotoolBox, a new hardware acceleration framework, is supported on 10.8+ and "here to stay".
    # It provides support for H265, H264, H263, MPEG1, MPEG2 and MPEG4.
    if {${os.major} > 14} {
        configure.args-replace  -Dvideotoolbox-gl=disabled \
                                -Dvideotoolbox-gl=enabled
    }

    if {${os.major} > 11} {
        notes-append {
                        This config file already defines the necessary video output settings to make\
                        use of the hardware acceleration features provided by your operating system.
        }
    } else {
        notes-append {
                        Sadly, your system is incapable of utilizing mpv's hardware decoding features.

                        The example config file will not include a hardware decoding option.\
                        Consult ``man mpv'' for further information.
        }
    }

    # C11 atomics is mandatory from version 0.33.0
    compiler.c_standard 2011
    compiler.blacklist-append \
                        {clang < 700}

    # Should eliminate most swift (module) related problems
    if {[vercmp ${xcodeversion} 9.1] >= 0 || ${os.major} >= 16} {
        use_xcode yes
    }

    # Shared build fixes for 10.6 and 10.7.
    if {${os.major} < 12} {
        patchfiles-append   patch-misc-import-CoreVideo.diff
    }

    # Make mpv work as good as possible on 10.6 and below. No Cocoa support, instead concentrate
    # on getting the OpenGL output to work via X11.
    # Also fix some other minor compile issues only manifesting on 10.6 and below along the way.
    if {${os.major} < 11} {
        patchfiles-append   patch-audio_out_ao_coreaudio_utils.c-add-missing-header-for-getpid.diff

        notes-append {
                        On systems older than Lion (10.7) or on architectures other than x86_64, Cocoa output support is not available.

                        The X11 backend should have been enabled automatically.
        }
    }

    # Fix youtube-dl hook for streaming youtube videos
    patchfiles   patch-youtubedl-edl-track-error.diff

    # coreaudio does not build on < 10.6 so use pulseaudio instead (tested and works).
    if {${os.major} < 10} {
        configure.args-replace  -Dcoreaudio=enabled \
                                -Dcoreaudio=disabled

        default_variants-append +pulseaudio
        notes-append {
                        On systems older than SnowLeopard (10.6), coreaudio output support is not available.

                        The pulseaudio backend should have been enabled automatically.
        }
    }

    post-extract {
        xinstall -m 0644 -W "${filespath}" config-maintainer "${worksrcpath}/TOOLS/"
    }

    pre-configure {
        if {${use_xcode}} {
            if {[vercmp ${xcodeversion} 4.3] < 0} {
                set sdks_dir ${developer_dir}/SDKs
            } else {
                set sdks_dir ${developer_dir}/Platforms/MacOSX.platform/Developer/SDKs
            }
            set sdks [glob -directory ${sdks_dir} MacOSX*.sdk]
            configure.sdkroot [lindex [lsort -command vercmp ${sdks}] end]
        }
        if {${configure.sdkroot} ne ""} {
            configure.sdk_version [exec /usr/libexec/PlistBuddy -c Print:ProductVersion ${configure.sdkroot}/System/Library/CoreServices/SystemVersion.plist]
        }
        if {[llength [split ${configure.sdk_version} .]] < 2} {
            configure.sdk_version ${configure.sdk_version}.0
        }
        configure.env-append \
                        MACOS_SDK=${configure.sdkroot} \
                        MACOS_SDK_VERSION=${configure.sdk_version}

        if {[variant_isset network]} {
            reinplace -W "${worksrcpath}/TOOLS" "s/@@NETWORK@@//" config-maintainer
        } else {
            reinplace -W "${worksrcpath}/TOOLS" "/@@NETWORK@@/d" config-maintainer
        }

        if {${os.major} > 11} {
            reinplace -W "${worksrcpath}/TOOLS" "s/@@HWDEC@@//" config-maintainer
        } else {
            reinplace -W "${worksrcpath}/TOOLS" "/@@HWDEC@@/d" config-maintainer
        }

        if {${os.major} > 12} {
            reinplace -W "${worksrcpath}/TOOLS" "s/@@HWDEC_CUDA@@//" config-maintainer
        } else {
            reinplace -W "${worksrcpath}/TOOLS" "/@@HWDEC_CUDA@@/d" config-maintainer
        }

        if {${os.major} < 11 || ${configure.build_arch} ni [list x86_64 arm64]} {
            reinplace -W "${worksrcpath}/TOOLS" "/@@VO@@/d" config-maintainer
            reinplace -W "${worksrcpath}/TOOLS" "s/@@BACKEND@@/x11/" config-maintainer
        } else {
            reinplace -W "${worksrcpath}/TOOLS" "s/@@VO@@//" config-maintainer
            reinplace -W "${worksrcpath}/TOOLS" "/@@BACKEND@@/d" config-maintainer
        }
    }
}


# Shameless copy from ipe-tools.
set python.versions         {39 310 311 312}
dict set python.legacy_versions 38 39
# After Python 3.9 is released, and mpv can be built with it,
# replace with the above lines with the following:
#set python.versions         {27 39}
#dict set python.legacy_versions 38 39

set python.default_version  311
set python.version          ""

# Create python variants.
foreach ver ${python.versions} {
    set variant_line {variant python${ver} description }
    set python_branch [string index $ver 0].[string range $ver 1 end]
    append variant_line " {Use Python ${python_branch} to build ${name} and generate man pages}"

    foreach over ${python.versions} {
        if {${ver} == ${over}} {
            continue
        }

        append variant_line " conflicts python${over}"
    }

    append variant_line " {
        depends_build-append    port:py${ver}-docutils
        license_noconflict      py${ver}-docutils
    }"
    eval $variant_line
}

# Create legacy python variants, mapping to the replacement version.
dict for {legacy_ver replace_ver} ${python.legacy_versions} {
    set variant_line {variant python${legacy_ver} description }
    set legacy_branch [join [split ${legacy_ver} {}] "."]
    set replace_branch [join [split ${replace_ver} {}] "."]
    append variant_line " {Legacy variant for Python ${legacy_branch} mapping to Python ${replace_branch}}"
    append variant_line " requires python${replace_ver}"

    foreach over ${python.versions} {
        if {${legacy_ver} == ${over} || ${replace_ver} == ${over}} {
            continue
        }

        append variant_line " conflicts python${over}"
    }

    append variant_line " { }"
    eval $variant_line
}

# Set default python variant.
set variant_none true
foreach ver ${python.versions} {
    if {[variant_isset python${ver}]} {
        set variant_none [expr $variant_none && ![variant_isset python${ver}]]
    }
}

if {${variant_none}} {
    default_variants-append "+python${python.default_version}"
}

# Set ${python.version} to the currently active variant.
foreach ver ${python.versions} {
    if {[variant_isset python${ver}]} {
        set python.version ${ver}
        set python_branch [string index ${python.version} 0].[string range ${python.version} 1 end]
    }
}

if {[string length ${python.version}] == 0} {
    ui_error "No python variant selected. You must select at least one. The highest value will be used."
    error "Select a python variant."
}


post-patch {
    set python_ver_dot [string index ${python.version} 0].[string range ${python.version} 1 end]
    reinplace "s|'rst2man.py'|'rst2man-${python_ver_dot}.py'|" ${worksrcpath}/meson.build
}

post-destroot {
    # Since we don't have conffiles handling, install mpv.conf as mpv.conf.example
    # to not overwrite this file on each (re-)build.
    file rename ${worksrcpath}/etc/mpv.conf ${worksrcpath}/etc/mpv.conf.example

    xinstall -d -m 0755 ${destroot}${prefix}/etc/${name}
    foreach etcfile {encoding-profiles.conf mpv.conf.example input.conf} {
        xinstall -m 0644 ${worksrcpath}/etc/${etcfile} \
            ${destroot}${prefix}/etc/${name}/${etcfile}
    }
    xinstall -d -m 0755 ${destroot}${prefix}/share/doc/${name}/examples
    copy {*}[glob ${worksrcpath}/TOOLS/lua ${worksrcpath}/TOOLS/*.pl \
                  ${worksrcpath}/TOOLS/*.sh ${worksrcpath}/TOOLS/*.py \
                  ${worksrcpath}/TOOLS/umpv ${worksrcpath}/TOOLS/lib] \
         ${destroot}${prefix}/share/doc/${name}/examples

    if {${os.platform} eq "darwin"} {
        xinstall -m 0644 ${worksrcpath}/TOOLS/config-maintainer \
                         ${destroot}${prefix}/share/doc/${name}/examples
    }
}


# When enabling features, make sure to replace the "disabled" flags with
# "enabled" flags in order to make the meson build system error out in case
# a feature cannot be used/found. Problems like that indicate a missing
# runtime dependency.

variant screenshot description {Enable optional screenshot support} {
    depends_lib-append      path:include/turbojpeg.h:libjpeg-turbo
    configure.args-replace  -Djpeg=disabled \
                            -Djpeg=enabled
}

variant network conflicts ytdlp description {Enable networking support via youtube-dl (supports wide variety of pages)} {
    depends_run-append      port:youtube-dl
}

variant ytdlp conflicts network description {Enable networking support via yt-dlp instead of youtube-dl (supports wide variety of pages)} {
    depends_run-append      port:yt-dlp
}

variant bluray description {Enable Bluray and AACS/BD+ encryption support} {
    depends_lib-append     port:libbluray
    depends_run-append     port:libaacs \
                           port:libbdplus
    configure.args-replace -Dlibbluray=disabled \
                           -Dlibbluray=enabled
    notes-append {
                    
                    To play Bluray discs with AACS encryption, decryption key database file "keydb.cfg" needs to be placed at:
                    * ~/Library/Preferences/aacs/ (on macOS, or)
                    * ~/.config/aacs/ (on other darwin).
                    You can easily find this file on the internet.
    }
}

variant dvd description {Enable DVD and DeCSS support} {
    depends_lib-append      port:libdvdread \
                            port:libdvdnav
    configure.args-replace  -Ddvdnav=disabled \
                            -Ddvdnav=enabled
}

variant audiocd description {Enable Audio CD support via libcdio-paranoia} {
    depends_lib-append      port:libcdio-paranoia
    configure.args-replace   -Dcdda=disabled \
                             -Dcdda=enabled
}

variant libarchive description {Enable transparent handling of Zip files and other compressed formats} {
    depends_lib-append      port:libarchive
    configure.args-replace  -Dlibarchive=disabled \
                            -Dlibarchive=enabled
}

variant rubberband description {Enable support for the Rubber Band library, adding audio pitch and speed control} {
    depends_lib-append      port:rubberband
    configure.args-replace  -Drubberband-disabled \
                            -Drubberband-enabled
}

variant pulseaudio description {Enable PulseAudio support} {
    depends_lib-append      port:pulseaudio
    configure.args-replace  -Dpulse=disabled \
                            -Dpulse=enabled
}

variant jack description {Enable Jack Audio Connection Kit support} {
    depends_lib-append      port:jack
    configure.args-replace  -Djack=disabled \
                            -Djack=enabled
}

# TODO: switch that to openal-soft? Leave it as-is?
variant openal description {Enable OpenAL support} {
    depends_lib-append      port:openal
    configure.args-replace  -Dopenal=disabled \
                            -Dopenal=enabled
}

variant x11 {
    depends_lib-append      port:xorg-libXext \
                            port:xorg-libXScrnSaver \
                            port:xorg-libXinerama \
                            port:xorg-libXv \
                            port:xorg-libXrandr \
                            port:xorg-libXpresent

    configure.args-replace  --Dx11=disabled \
                            --Dx11=enabled
    configure.args-replace  --Dxv=disabled \
                            --Dxv=enabled
}

variant opengl description {Enable OpenGL output support. Both the CoreVideo and X11 (GLX) outputs are supported} {
    if {${os.platform} eq "darwin" && ${os.major} > 14} {
        configure.args-replace  -Dgl-cocoa=disabled \
                                -Dgl-cocoa=enabled
    }

    configure.args-replace  -Dgl=disabled \
                            -Dgl=enabled

    if {[variant_isset x11]} {
        depends_lib-append      port:mesa
        configure.args-replace  -Dgl-x11=disabled \
                                -Dgl-x11=enabled
        require_active_variants libplacebo opengl
        # FIXME: add EGL? Leave it disabled? Test!
        # As far as testing went, EGL is not available on macOS yet.
        #configure.args-replace  -Degl-x11=disabled \
        #                        -Degl-x11=enabled
    }
}

variant vulkan description {Enable Vulkan output support} {
    depends_build-append \
                        port:vulkan-headers
    depends_lib-append  port:vulkan-loader
    configure.args-replace \
                        -Dvulkan=disabled \
                        -Dvulkan=enabled
    require_active_variants libplacebo vulkan
    
    # The following are needed as no .pc files are available from Vulkan ports above
    post-patch {
        reinplace "s|'vulkan', version: '>= 1.1.70'|'vulkan'|" ${worksrcpath}/meson.build
    }
    post-build {
        system -W ${build.dir} "/usr/bin/install_name_tool -change @rpath/libvulkan.1.dylib ${prefix}/lib/libvulkan.1.dylib mpv"
    }
}

variant caca description {Enable animated ASCII art video output} {
    depends_lib-append      port:libcaca
    configure.args-replace  -Dcaca=disabled \
                            -Dcaca=enabled
}

variant osd description {Enable onscreen display and TrueType font support} {
    depends_lib-append      port:lua52
    configure.args-replace  -Dlua=disabled \
                            -Dlua=lua5.2
}

variant debug description {Compile with debugging symbols} {
    configure.args-replace  --buildtype=release \
                            --buildtype=debug
    configure.args-replace  -Dpthread-debug=disabled \
                            -Dpthread-debug=enabled
}

variant printable_doc description {Generate printable documents (PDF help)} {
    # py27-pdfrw and py27-Pillow are needed by rst2pdf.
    depends_build-append    port:rst2pdf \
                            port:py27-pdfrw \
                            port:py27-Pillow
    configure.args-replace  -Dpdf-build=disabled \
                            -Dpdf-build=enabled
}

#variant javascript description {Enable JavaScript support via mujs} {
#   depends_lib-append      port:mujs
#   configure.args-replace  -Djavascript=disabled \
#                           -Djavascript=enabled
#}

variant uchardet description {Enable the uchardet encoding detector} {
    depends_lib-append      port:uchardet
    configure.args-replace  -Duchardet=disabled \
                            -Duchardet=enabled
}

variant libmpv description {Enable the libmpv library} {
    configure.args-replace  -Dlibmpv=false \
                            -Dlibmpv=true
}

variant bundle description {Enable the optional macOS bundle of mpv} {
    post-build {
        system -W "${worksrcpath}" "${prefix}/bin/python${python_branch} TOOLS/osxbundle.py -s ${build.dir}/mpv"
    }

    post-destroot {
        xinstall -d -m 0755 ${destroot}${applications_dir}
        copy ${build.dir}/mpv.app ${destroot}${applications_dir}
    }
}
