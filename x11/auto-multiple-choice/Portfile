# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               perl5 1.0
PortGroup               texlive 1.0

perl5.branches          5.28
name                    auto-multiple-choice
categories              x11 tex education
platforms               darwin
license                 GPL-2+
maintainers             {laposte.fr:joel.brogniart @nortcele} openmaintainer

description             Auto Multiple Choice - Multiple Choice Papers Management
long_description        Utility to manage multiple choice questionnaires, \
                        with optionally mixed questions and answers. \
                        AMC provides automatic marking from papers' scans. \
                        Annotated papers are produced. Marks can be exported \
                        as an OpenOffice.org sheet file.

homepage                http://www.auto-multiple-choice.net/

subport auto-multiple-choice-devel {}

if {${subport} eq ${name}} {
    # release
    version                 1.4.0
    checksums               rmd160  1c4774798321281f2fa76d0fcaeb490bd6ac58d3 \
                            sha256  6d447bc5053fd2a57f854ead33ac50bb5c3a09e99e6f662c85febc5eb85a9fad \
                            size    8513750

    depends_lib-append      port:opencv

    conflicts               auto-multiple-choice-devel
} else {
    # devel
    set amc.version.main        1.5.0
    set amc.version.secondary   rc2-2-gd3d7ea24
    version                 ${amc.version.main}_${amc.version.secondary}
    checksums               rmd160  5e6df40422f2848525f61eb125093e0ea5a9acff \
                            sha256  7ea5f7171c98f02177581c0759544a0e1bd5b4c42380af97741e492e5ff17fa1 \
                            size    11126038

    depends_lib-append      port:opencv4

    conflicts               auto-multiple-choice
}

master_sites            https://download.auto-multiple-choice.net/precomp/
distfiles               ${name}_${version}_dist${extract.suffix}

use_configure           no

depends_build           port:gmake
build.cmd               ${prefix}/bin/gmake

depends_run             \
                        port:gdk-pixbuf2 \
                        port:libnotify \
                        port:librsvg \
                        port:netpbm \
                        port:p${perl5.major}-archive-zip \
                        port:p${perl5.major}-clone \
                        port:p${perl5.major}-dbd-sqlite \
                        port:p${perl5.major}-digest-md5 \
                        port:p${perl5.major}-email-mime \
                        port:p${perl5.major}-email-sender \
                        port:p${perl5.major}-file-basedir \
                        port:p${perl5.major}-file-mimeinfo \
                        port:p${perl5.major}-filesys-dfportable \
                        port:p${perl5.major}-glib-object-introspection \
                        port:p${perl5.major}-gtk3 \
                        port:p${perl5.major}-io-compress \
                        port:p${perl5.major}-locale-codes \
                        port:p${perl5.major}-locale-gettext \
                        port:p${perl5.major}-module-load-conditional \
                        port:p${perl5.major}-openoffice-oodoc \
                        port:p${perl5.major}-perlmagick \
                        port:p${perl5.major}-text-csv \
                        port:p${perl5.major}-xml-simple \
                        port:p${perl5.major}-xml-writer \
                        port:poppler \
                        port:qpdf \
                        port:sqlite3 \
                        port:texlive \
                        port:texlive-fonts-extra \
                        port:texlive-lang-japanese \
                        port:texlive-latex-extra \
                        port:unzip

build.args              AMCCONF=macports \
                        BASEPATH=${prefix}

pre-build {
    build.post_args-append PERLDIR=${perl5.lib}
    if {![variant_isset mactex]} {
        set amc.texmflocal ${texlive_texmflocal}
    }
    build.post_args-append TEXDIR="${amc.texmflocal}/tex/latex/AMC"
    build.post_args-append TEXDOCDIR="${amc.texmflocal}/doc/latex/AMC"
}

destroot.args           AMCCONF=macports \
                        BASEPATH=${prefix}

pre-destroot {
    destroot.args-append PERLDIR=${perl5.lib}
    if {![variant_isset mactex]} {
        set amc.texmflocal ${texlive_texmflocal}
    }
    destroot.args-append TEXDIR="${amc.texmflocal}/tex/latex/AMC"
    destroot.args-append TEXDOCDIR="${amc.texmflocal}/doc/latex/AMC"
}

# The mactex variant expects MacTeX to be installed
# and installs dblatex's stylefiles to MacTeX's texmf (local)
set amc.texmflocal ""
set amc.mactex_bin ""

variant mactex description {The mactex variant allow Auto Multiple Choice to use a MacTeX installation \
instead of Macports texlive tools. To install the mactex variant \
/Library/TeX/texbin must be added to binpath in macports.conf.} {
    # First, check if MacTeX actually seems to be thereâ€¦
    set amc.mactex_candidates { \
       "/Library/TeX/texbin" \
       "/usr/texbin" \
    }
    foreach dir ${amc.mactex_candidates} {
        if {[file executable "${dir}/kpsewhich"]} {
            set amc.mactex_bin ${dir}
            break
        }
    }
    if { ${amc.mactex_bin} != "" } {
        set amc.texmflocal \
            [exec ${amc.mactex_bin}/kpsewhich --expand-var='\$TEXMFLOCAL']
        regsub -all {'} ${amc.texmflocal} "" amc.texmflocal
    } else {
        return -code error "Cannot find MacTeX installation; aborting"
    }

    depends_run-delete      \
                            port:texlive \
                            port:texlive-fonts-extra \
                            port:texlive-lang-japanese \
                            port:texlive-latex-extra

    notes "The mactex variant will install styles to MacTeX's texmf-local
    ${amc.texmflocal}
(which is outside macport's common directory structure).
For the mactex variant to work,
${amc.mactex_bin} must also be added to binpath in macports.conf"

    # AND, since we're installing files outside macports' normal directories
    destroot.violate_mtree  yes
}

proc amc.mktexlsr {} {
    if {![variant_isset mactex]} {
        texlive.mktexlsr
    } else {
        global amc.mactex_bin
        system "${amc.mactex_bin}/mktexlsr"
        if {[file exists "${amc.mactex_bin}/mtxrun"]} {
            system "${amc.mactex_bin}/mtxrun --generate"
        }
    }
}

post-activate {
    amc.mktexlsr
}

post-deactivate {
    amc.mktexlsr
}

livecheck.type          regex
livecheck.url           [lindex ${master_sites} 0]#
livecheck.regex         ${name}_(\[0-9.a-z-\]+_\[0-9.a-z-\]+)_
