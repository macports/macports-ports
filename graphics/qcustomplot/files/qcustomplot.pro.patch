diff --git qcustomplot.pro qcustomplot.pro
new file mode 100644
index 0000000..b285cd3
--- /dev/null
+++ qcustomplot.pro
@@ -0,0 +1,35 @@
+#
+#  Project to compile QCustomPlot as shared library (.so/.dll) from the amalgamated sources
+#
+
+QT += core gui
+greaterThan(QT_MAJOR_VERSION, 4): QT += widgets printsupport
+
+greaterThan(QT_MAJOR_VERSION, 4): CONFIG += c++11
+lessThan(QT_MAJOR_VERSION, 5): QMAKE_CXXFLAGS += -std=c++11
+
+DEFINES += QCUSTOMPLOT_COMPILE_LIBRARY
+TEMPLATE = lib
+CONFIG += debug_and_release build_all
+static {
+  CONFIG += static
+} else {
+  CONFIG += shared
+}
+
+VERSION = @@VERSION@@
+
+TARGET = qcustomplot
+CONFIG(debug, debug|release) {
+  TARGET = $$join(TARGET,,,d) # if compiling in debug mode, append a "d" to the library name
+  QMAKE_TARGET_PRODUCT = "QCustomPlot (debug mode)"
+  QMAKE_TARGET_DESCRIPTION = "Plotting library for Qt (debug mode)"
+} else {
+  QMAKE_TARGET_PRODUCT = "QCustomPlot"
+  QMAKE_TARGET_DESCRIPTION = "Plotting library for Qt"
+}
+QMAKE_TARGET_COMPANY = "www.qcustomplot.com"
+QMAKE_TARGET_COPYRIGHT = "Copyright (C) by Emanuel Eichhammer"
+
+SOURCES += qcustomplot.cpp
+HEADERS += qcustomplot.h
