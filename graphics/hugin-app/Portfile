# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               cmake 1.1
PortGroup               cxx11 1.1
PortGroup               perl5 1.0
PortGroup               wxWidgets 1.0
PortGroup               active_variants 1.1

name                    hugin-app
version                 2017.0.0
revision                3
set branch              [join [lrange [split ${version} .] 0 1] .]
categories              graphics
maintainers             nomaintainer
description             Panorama photo stitcher
long_description        An easy to use cross-platform panoramic imaging \
                        toolchain based on Panorama Tools. With hugin you \
                        can assemble a mosaic of photographs into a complete \
                        immersive panorama, stitch any series of overlapping \
                        pictures and much more.

homepage                http://hugin.sourceforge.net
license                 GPL-2
platforms               darwin

master_sites            sourceforge:project/hugin/hugin/hugin-${branch}
distname                hugin-${version}
use_bzip2               yes

perl5.branches          5.26
wxWidgets.use           wxWidgets-3.0-cxx11

checksums               rmd160  2a0cf1af75d384abca0a62d80480fc815f63e0bf \
                        sha256  86646ea27f32b959d38e7d36270703214b1ae961e379dd64b14d027139cd4609

# do not assume default values that MacPorts sets
patchfiles-append       patch-no_default.diff

# in /usr/include/sys/termios.h, VT1 is defined as a number
patchfiles-append       patch-terios.diff

# change how we look for OpenMP
patchfiles-append       patch-find_openmp.diff
post-patch {
    reinplace \
        "s|___MACPORTS_FIND_OPENMP__|set(OPENMP_FOUND FALSE)|g" \
        ${worksrcpath}/CMakeLists.txt
}

# icon files are not installed
patchfiles-append       patch-icns.diff
post-extract {
    # icon files are not found
    ln -s ../hugin/Hugin.icns        ${worksrcpath}/src/hugin1/stitch_project
    ln -s ../hugin/PTBatcherGui.icns ${worksrcpath}/src/hugin1/ptbatcher

    # use the cmake version of the following packages
    foreach cmakePackage "LAPACK GLUT GLEW JPEG PNG PackageHandleStandardArgs PkgConfig TIFF ZLIB OpenMP" {
        delete ${worksrcpath}/CMakeModules/Find${cmakePackage}.cmake
    }
}

# do not find freeglut if installed
configure.pre_args-replace        \
    -DCMAKE_FIND_FRAMEWORK=LAST   \
    -DCMAKE_FIND_FRAMEWORK=FIRST

depends_build-append    port:pkgconfig

# see http://wiki.panotools.org/Development_of_Open_Source_tools#Dependencies
depends_lib-append          \
    port:${wxWidgets.port}  \
    port:boost              \
    port:zlib               \
    port:tiff               \
    port:libpano13          \
    port:jpeg               \
    port:libpng             \
    port:ilmbase            \
    port:openexr            \
    port:vigra              \
    port:exiv2              \
    port:glew               \
    port:gettext            \
    port:fftw-3

require_active_variants glew "" x11

depends_lib-append      \
    port:flann          \
    port:lcms2          \
    port:sqlite3

depends_run                              \
    port:enblend                         \
    port:p${perl5.major}-image-exiftool

configure.args-append                                    \
    -DINSTALL_OSX_BUNDLE_DIR=${applications_dir}         \
    -DwxWidgets_CONFIG_EXECUTABLE=${wxWidgets.wxconfig}  \
    -DBUILD_HSI=OFF                                      \
    -DENABLE_LAPACK=OFF

set pythons_suffixes {27 36}

set pythons_ports {}
foreach s ${pythons_suffixes} {
    lappend pythons_ports python${s}
}

set any_python 0
foreach s ${pythons_suffixes} {
    set p python${s}
    set v [string index ${s} 0].[string index ${s} 1]
    set i [lsearch -exact ${pythons_ports} ${p}]
    set c [lreplace ${pythons_ports} ${i} ${i}]
    set python_prefix       ${frameworks_dir}/Python.framework/Versions/${v}
    variant ${p} description "Build Python Scripting Interface with Python ${v}" conflicts {*}${c} "
        depends_lib-append    \
            port:${p}         \
            port:swig-python

        configure.python      ${python_prefix}/bin/python${v}

        configure.args-append                                    \
            -DPYTHON_EXECUTABLE=${python_prefix}/bin/python${v}  \
            -DPYTHON_INCLUDE_DIR=${python_prefix}/Headers        \
            -DPYTHON_LIBRARY=${python_prefix}/Python

        configure.args-replace  \
            -DBUILD_HSI=OFF     \
            -DBUILD_HSI=ON
    "
    if {[variant_isset python${s}]} {
        set any_python 1
    }
}
if {!${any_python}} {
    default_variants +python27
}

pre-fetch {
    set any_python 0
    foreach s ${pythons_suffixes} {
        if {[variant_isset python${s}]} {
            set any_python 1
        }
    }
    if {!${any_python}} {
        return -code error "You must choose at least one Python variant."
    }
}

variant accelerate conflicts atlas openblas description {Use Apple Accelerate Libraries} {
    configure.args-replace   \
        -DENABLE_LAPACK=OFF  \
        -DENABLE_LAPACK=ON

    configure.args-append -DBLA_VENDOR=Apple
}

variant atlas conflicts accelerate openblas description {Use MacPorts ATLAS Libraries} {
    depends_lib-append port:atlas

    configure.args-replace   \
        -DENABLE_LAPACK=OFF  \
        -DENABLE_LAPACK=ON

    configure.args-append   \
        -DBLA_VENDOR=ATLAS

    # FindBLAS.cmake and FindLAPACK.cmake do not seem to find MacPorts Atlas properly
    configure.args-append          \
        -DBLAS_LIBRARIES=satlas    \
        -DLAPACK_LIBRARIES=satlas
}

variant openblas conflicts atlas accelerate description {Use MacPorts OpenBLAS Libraries} {
    depends_lib-append path:lib/libopenblas.dylib:OpenBLAS
    require_active_variants path:lib/libopenblas.dylib:OpenBLAS lapack

    configure.args-replace   \
        -DENABLE_LAPACK=OFF  \
        -DENABLE_LAPACK=ON

    configure.args-append  -DBLA_VENDOR=OpenBLAS
}

if {![variant_isset accelerate] && ![variant_isset atlas] && ![variant_isset openblas]} {
    default_variants-append +accelerate
}

pre-fetch {
    if {![variant_isset accelerate] && ![variant_isset openblas] && ![variant_isset atlas] } {
        ui_error "You must select either the +accelerate, +atlas, or +openblas variant for linear algebra."
        return -code error "No linear-algebra variant selected."
    }
}

livecheck.type          regex
livecheck.url           ${homepage}
livecheck.regex         Hugin-(\[.0-9\]+) released
