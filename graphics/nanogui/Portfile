# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0
PortGroup           cmake 1.1

github.setup        mitsuba-renderer nanogui 21e5cbc880b2e26b28b2a35085a9e6706da1e2a8
version             20200404-[string range ${github.version} 0 7]
categories          graphics
platforms           darwin
license             BSD
maintainers         @jasonliu-- openmaintainer
description         minimalistic C++/Python GUI library for OpenGL, \
                    GLES2/3, Metal, and WebAssembly/WebGL
long_description    NanoGUI is a minimalistic cross-platform widget \
                    library for OpenGL 3+, GLES 2/3, and Metal. It \
                    supports automatic layout generation, stateful C++ \
                    lambdas callbacks, a variety of useful widget \
                    types, and Retina-capable rendering on Apple \
                    devices.

checksums           rmd160  192baaa0a7f0ac862b2d09021daa98733e342932 \
                    sha256  f105aa23476f99c2db1b28bccf4751498584e366a30e5e5078d6963a8bab6c93 \
                    size    2911997

depends_lib-append  port:glfw \
                    port:nanovg

compiler.cxx_standard 2017
configure.cxxflags-append -std=c++17

post-patch {
    # By default, NanoGUI's code assumes that the source codes of its
    # dependencies have been copied into NanoGUI's source tree. The
    # following replacements allow NanoGUI to compile against MacPorts
    # packages.
    set regexes [list \
        "/NOT IS_DIRECTORY.*glfw/,/endif/s/^/#/g" \
        "s|\(NANOGUI_PYBIND11_DIR.*\)tools|\\1share/cmake/pybind11|" \
        "s|\(NANOGUI_EXTRA_DEFS -DNANOGUI_USE_METAL\)|\\1 -DTARGET_OS_OSX|" \
        "s|\".*CMAKE_CURRENT_SOURCE_DIR.*glfw/include|\"${prefix}/include/GLFW|" \
        "s|\".*CMAKE_CURRENT_SOURCE_DIR.*nanovg/src|\"${prefix}/include/nanovg|" \
        "s|ext/\(nanovg\)/\(src\)/\(.*\.c\)|${prefix}/\\2/\\1/\\3|g" \
        "s|ext/\(nanovg\)/src/\(.*\.h\)|${prefix}/include/\\1/\\2|g" \
        "s|ext/\(nanovg\)/\(src\)|${prefix}/include/\\1|g" \
        "s|ext/\(nanovg_metal\)/\(src\)/\(.*\.m\)|${prefix}/\\2/\\1/\\3|g" \
        "s|ext/\(nanovg_metal\)/src/\(.*\.h\)|${prefix}/include/\\1/\\2|g" \
        "s|ext/\(nanovg_metal\)/\(src\)|${prefix}/include/\\1|g" \
        "s|\(example\[0-9\].*nanogui\)|\\1 \${NANOGUI_EXTRA_LIBS}|g" \
        "s|\(COPY resources.*DESTINATION \).*|\\1 share/nanogui/examples)|" \
    ]
    foreach re $regexes {
        reinplace -E $re ${worksrcpath}/CMakeLists.txt
    }

    # Add MacPorts paths
    reinplace "/^if.*CMAKE_SYSTEM_NAME.*MATCHES.*BSD/i\\
if (APPLE)\\
\\  include_directories(${prefix}/include)\\
\\  link_directories(${prefix}/lib)\\
endif()\\
" \
        ${worksrcpath}/CMakeLists.txt

    # Make it so that CMake will be able to link to GL libraries,
    # even though we are running on "APPLE" and not "Linux".
    reinplace "/elseif .CMAKE_SYSTEM MATCHES.*Linux.*OR CMAKE_SYSTEM_NAME.*BSD/a\\
endif()\\
if (APPLE)\\
" \
        ${worksrcpath}/CMakeLists.txt
    reinplace "/NANOGUI_EXTRA_LIBS GL)/s/^/#/" \
        ${worksrcpath}/CMakeLists.txt
    reinplace "/Xrandr Xinerama/s/^/#/" ${worksrcpath}/CMakeLists.txt
    reinplace -E "s|NOT \(CMAKE_SYSTEM_NAME MATCHES.*OpenBSD\)|\\1|" \
        ${worksrcpath}/CMakeLists.txt

    # Make it so that CMake can find MacPorts' glfw
    reinplace "/include_directories.*glfw/a\\
else()\\
\\  include_directories(${prefix}/include/GLFW)\\
" \
        ${worksrcpath}/CMakeLists.txt
    reinplace "/TARGET_OBJECTS:glfw_objects/a\\
else()\\
\\  list(APPEND NANOGUI_EXTRA_LIBS glfw)\\
" \
        ${worksrcpath}/CMakeLists.txt

    # For some reason, NanoGUI's source code compiles the examples,
    # but then never actually installs them anywhere and simply
    # leaves them in the build directory.
    reinplace "/COPY resources/a\\
\\  install(DIRECTORY src/ DESTINATION share/nanogui/examples\\
\\          FILES_MATCHING PATTERN \"example*\")\\
\\  install(TARGETS example1 example2 example3 example4 example_icons\\
\\          RUNTIME DESTINATION share/nanogui/examples)\\
" \
        ${worksrcpath}/CMakeLists.txt
    reinplace "s|file.COPY resources|install(DIRECTORY resources|" \
        ${worksrcpath}/CMakeLists.txt

    # Fix typo in reference to GLES3 header file
    reinplace "s|GLES3/gl2ext|GLES3/gl3ext|" \
        ${worksrcpath}/include/nanogui/opengl.h

    if {${os.major} < 16} {
        # Reference: https://github.com/catchorg/Catch2/issues/1218#issuecomment-372105189
        reinplace "s|std::uncaught_exceptions() > 0|std::uncaught_exception()|" \
            ${worksrcpath}/src/widget.cpp
    }
}

configure.args-append   -DNANOGUI_BUILD_GLFW=NO \
                        -DNANOGUI_BUILD_PYTHON=NO

variant opengl conflicts gles2 gles3 metal \
    description {Build NanoGUI using OpenGL backend} \
{
    configure.args-append   -DNANOGUI_BACKEND="OpenGL"
}

variant gles2 conflicts opengl gles3 metal \
    description {Build NanoGUI using GLES 2 backend} \
{
    depends_lib-append      lib:libGLESv2:mesa
    configure.args-append   -DNANOGUI_BACKEND="GLES 2"
}

variant gles3 conflicts opengl gles2 metal \
    description {Build NanoGUI using GLES 3 backend} \
{
    depends_lib-append      lib:libGLESv3:mesa
    configure.args-append   -DNANOGUI_BACKEND="GLES 3"
}

# NanoGUI's Metal-related code requires at least Metal 2, which is
# only available starting in macOS 10.13 High Sierra and above.
if {${os.major} >= 17} {

    variant metal conflicts opengl gles2 gles3 \
        description {Build NanoGUI using Metal backend} \
        {
            depends_lib-append      port:metalnanovg
            configure.args-append   -DNANOGUI_BACKEND="Metal"
            use_xcode               yes
        }

    default_variants +metal

} else {

    default_variants +opengl

}

### The following code was adapted from the portfiles
### of ports such as openvdb and openimageio

set pythons_suffixes    {34 35 36 37 38}

set pythons_ports       {}
foreach s ${pythons_suffixes} {
    lappend pythons_ports python${s}
}

foreach s ${pythons_suffixes} {
    set p python${s}
    set v [string index ${s} 0].[string index ${s} 1]
    set i [lsearch -exact ${pythons_ports} ${p}]
    set c [lreplace ${pythons_ports} ${i} ${i}]
    variant ${p} conflicts {*}${c} \
        description "Build the Python ${v} plugin" {}
    if {[variant_isset ${p}]} {
        # Note: We basically need to import the value of ${v}, otherwise
        # all foreach iterations will end up with the same value of
        # ${v}! This appears to be related to how Tcl does scoping.
        set selected_v          ${v}
        depends_lib-append      port:${p} \
                                port:pybind11

        configure.args-replace  -DNANOGUI_BUILD_PYTHON=NO \
                                -DNANOGUI_BUILD_PYTHON=YES
        configure.args-append   -DNANOGUI_PYBIND11_DIR=${prefix}

        post-patch {
            reinplace "/find_package.PythonLibsNew/i\\
\\  set(PythonLibsNew_FIND_VERSION ${selected_v})\\
" \
                ${worksrcpath}/CMakeLists.txt
        }
    }
}
