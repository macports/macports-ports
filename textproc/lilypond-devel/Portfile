# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           compiler_blacklist_versions 1.0
PortGroup           cxx11 1.1

name                lilypond-devel
set my_name         lilypond
version             2.19.83
revision            2
set branch          [join [lrange [split ${version} .] 0 1] .]
categories          textproc
maintainers         {snc @nerdling} openmaintainer
license             GPL-3+
description         An automated engraving system for typesetting sheet music.
long_description    Lilypond is a unix-based automated engraving system that \
                    generates beautiful sheet music from input files. Lilypond \
                    uses its own input format, .ly, which in many ways is \
                    similar to LaTeX. Lilypond can export sheet music to PDF, \
                    EPS, SVG, and PNG formats, and can also create MIDI files.

homepage            http://lilypond.org/
conflicts           lilypond
platforms           darwin
master_sites        http://lilypond.org/download/sources/v${branch}/
# master_sites      http://download.linuxaudio.org/lilypond/sources/v${branch}/
distname            ${my_name}-${version}
checksums           rmd160  6250667c2a2799ec404e10e3e141101d043ab7f6 \
                    sha256  96ba4f4b342d21057ad74d85d647aea7e47a5c24f895127c2b3553a252738fb3 \
                    size    17996428

depends_build-append \
                    port:autoconf \
                    port:bison \
                    port:dblatex \
                    port:flex \
                    port:fontforge \
                    port:libtool \
                    port:netpbm \
                    port:pkgconfig \
                    port:texinfo \
                    port:texi2html \
                    port:texlive \
                    port:texlive-fonts-recommended \
                    port:texlive-lang-cyrillic \
                    port:texlive-metapost \
                    port:t1utils \
                    port:urw-core35-fonts

depends_lib-append  path:lib/pkgconfig/glib-2.0.pc:glib2 \
                    path:lib/pkgconfig/pango.pc:pango \
                    port:fontconfig \
                    port:freetype \
                    port:gettext \
                    port:ghostscript \
                    port:guile18 \
                    port:python27

configure.cmd       autoconf -f && ./configure
configure.python    ${frameworks_dir}/Python.framework/Versions/2.7/bin/python2.7

set lilyponddevel.texgyredir \
    "${prefix}/share/texmf-texlive/fonts/opentype/public/tex-gyre"
set lilyponddevel.mactex_bin   ""
set lilyponddevel.temp         ""

variant mactex description {Allow lilypond-devel to use MacTeX or another\
    external TeXLive installation instead of MacPorts TeXLive tools.  To\
    install the 'mactex' variant the path to the TeX distribution's\
    binary directory (for example '/Library/TeX/texbin') must be added\
    to 'binpath' in 'macports.conf' *before* installing this port.} {

    # Find the binary directory of the external TeX distribution by
    # searching the kpsewhich program in the path.  We assume that all
    # other binaries of the distribution are in this directory, too.
    if {[auto_execok kpsewhich] ne ""} {
        set lilyponddevel.temp {*}[auto_execok kpsewhich]
    }
    set lilyponddevel.mactex_bin \
        [file dirname [file normalize ${lilyponddevel.temp}]]
    if {${lilyponddevel.mactex_bin} eq "."} {
        pre-fetch {
            return -code error "Cannot find MacTeX or external TeXLive\
                installation; aborting.  Have you added the path to\
                your TeX distribution's binary directory to 'binpath'\
                in 'macports.conf'?"
        }
    } elseif {${lilyponddevel.mactex_bin} eq \
                  [file dirname [file normalize "${prefix}/bin/kpsewhich"]]} {
        pre-fetch {
            return -code error "Variant 'mactex' doesn't work with\
                installed MacPorts TeXLive packages.  Either\
                uninstall them or don't use the 'mactex' variant of\
                lilyponddevel."
        }
    } else {
        set lilyponddevel.have_texgyre \
            [regexp -line {installed: *Yes} \
                 [exec ${lilyponddevel.mactex_bin}/tlmgr info --only-installed tex-gyre]]
    }

    if {${lilyponddevel.have_texgyre}} {
        set lilyponddevel.texgyredir \
            [file dirname \
                [exec ${lilyponddevel.mactex_bin}/kpsewhich texgyreschola-regular.otf]]
    } else {
        pre-fetch {
            return -code error "TeXLive package 'tex-gyre' not\
                installed; aborting.  Please install it, then\
                try again"
        }
    }

    depends_build-delete port:texlive \
                         port:texlive-fonts-recommended \
                         port:texlive-lang-cyrillic \
                         port:texlive-metapost

    configure.args-append --with-texgyre-dir=${lilyponddevel.texgyredir}
}

patchfiles          patch-scripts-build-mf2pt1.pl.diff \
                    patch-no-Wstrict-prototypes-warning.diff
post-patch {
    # Use guile18 header files
    reinplace s|libguile\.h|libguile18.h|g \
        ${worksrcpath}/aclocal.m4 \
        ${worksrcpath}/configure.ac \
        ${worksrcpath}/lily/include/lily-guile.hh

    # help fontforge date calculation
    reinplace s|20110222|2011-02-22|g ${worksrcpath}/configure
}

# lilypond currently relies on a g++ extension (which appears to be
# non-standard C++) that prevents compilation with clang, cf.
#
#   https://lists.gnu.org/archive/html/bug-lilypond/2016-07/msg00071.html
#
# and the thread starting with
#
#   http://lists.llvm.org/pipermail/cfe-users/2018-November/001417.html
#
# According to one of the main developers of lilypond, it is not
# possible to circumvent this issue without rewriting a considerable
# amount of code.

# g++ uses libstdc++; setting this prevents the application from being
# flagged as broken.
configure.cxx_stdlib macports-libstdc++

# blacklist any clang compiler and any old gcc (a better expression is needed)
compiler.blacklist  *clang* \
                    *llvm-gcc* \
                    *apple-gcc* \
                    gcc \
                    gcc-3.3 gcc-4.0 gcc-4.2

compiler.fallback-append \
                    macports-gcc-9 \
                    macports-gcc-8 \
                    macports-gcc-7 \
                    macports-gcc-6 \
                    macports-gcc-5 \
                    macports-gcc-4.9 macports-gcc-4.8 macports-gcc-4.7

configure.args-append \
                    --with-urwotf-dir=${prefix}/share/fonts/urw-core35-fonts \
                    --enable-documentation
if {![variant_isset mactex]} {
    configure.args-append --with-texgyre-dir=${lilyponddevel.texgyredir}
}

configure.env       LTDL_LIBRARY_PATH=${prefix}/lib \
                    PYTHON_CONFIG=${configure.python}-config \
                    GUILE=${prefix}/bin/guile18 \
                    GUILE_CONFIG=${prefix}/bin/guile18-config \
                    GUILE_TOOLS=${prefix}/bin/guile18-tools

build.env           LTDL_LIBRARY_PATH=${prefix}/lib

destroot.env        LTDL_LIBRARY_PATH=${prefix}/lib
post-destroot {
    move ${destroot}${prefix}/bin/lilypond \
        ${destroot}${prefix}/libexec/lilypond-bin
    xinstall -m 755 ${filespath}/lilypond.in ${destroot}${prefix}/bin/lilypond
    reinplace "s|@@PREFIX@@|${prefix}|g" ${destroot}${prefix}/bin/lilypond
}

livecheck.type      regex
livecheck.url       ${homepage}/development.html
livecheck.regex     ${my_name}-(\\d+(\\.\\d+)+)
