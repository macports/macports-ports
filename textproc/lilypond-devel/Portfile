# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           active_variants 1.1

name                lilypond-devel
set my_name         lilypond
version             2.21.1
revision            1
set branch          [join [lrange [split ${version} .] 0 1] .]
categories          textproc
maintainers         {snc @nerdling} openmaintainer
license             GPL-3+
conflicts           lilypond
description         An automated engraving system for typesetting sheet music.
long_description    Lilypond is a unix-based automated engraving system that \
                    generates beautiful sheet music from input files. Lilypond \
                    uses its own input format, .ly, which in many ways is \
                    similar to LaTeX. Lilypond can export sheet music to PDF, \
                    EPS, SVG, and PNG formats, and can also create MIDI files.

homepage            https://lilypond.org
master_sites        https://lilypond.org/download/sources/v${branch}/
distname            ${my_name}-${version}
checksums           rmd160  afafa73c517c086e3f42f942f8175229f33debdf \
                    sha256  b7ccd72488b0838bc1ae5f490d6acefb292a902d977f6ed05f1eb26d30137e5e \
                    size    18135688

platforms           darwin
universal_variant   no

depends_build-append \
                    port:autoconf \
                    port:bison \
                    port:flex \
                    port:fontforge \
                    port:gmake \
                    port:p5.28-encode \
                    port:p5.28-pod-escapes \
                    port:p5.28-pod-simple \
                    port:p5.28-podlators \
                    port:p5.28-scalar-list-utils \
                    port:pkgconfig \
                    port:t1utils \
                    port:texinfo \
                    port:texlive-fonts-recommended \
                    port:texlive-metapost \
                    port:urw-core35-fonts

# Either 'pango' or 'pango-devel' will do, thus we depend on its
# pkgconfig file.
depends_lib-append  path:lib/pkgconfig/pango.pc:pango \
                    port:extractpdfmark \
                    port:ghostscript \
                    port:guile18 \
                    port:python38

# Needed for PNG generation.
# Will no longer be needed for next development release.
depends_run-append  port:netpbm


require_active_variants fontforge python38

build.type          gnu
configure.cmd       autoconf -f && ./configure
configure.python    ${prefix}/bin/python3.8

compiler.cxx_standard 2011

set lilyponddevel.texgyredir \
    "${prefix}/share/texmf-texlive/fonts/opentype/public/tex-gyre"
set lilyponddevel.mactex_bin   ""
set lilyponddevel.temp         ""
set lilyponddevel.have_texgyre false

notes-append "\
    Pre-installation note for 'mactex' variant:

    MacTeX or another external TeXLive distribution gets used for\
    installation instead of MacPorts's texlive packages; the path to\
    the TeX distribution's binary directory (for example\
    '/Library/TeX/texbin') must be added to 'binpath' in\
    'macports.conf' *before* installing this port.

    Note that TeX is not needed after installation."

variant mactex description {Use MacTeX or non-MacPorts TeX; see\
    "port notes" for more information} {

    # Find the binary directory of the external TeX distribution by
    # searching the kpsewhich program in the path.  We assume that all
    # other binaries of the distribution are in this directory, too.
    if {[auto_execok kpsewhich] ne ""} {
        set lilyponddevel.temp {*}[auto_execok kpsewhich]
    }
    set lilyponddevel.mactex_bin \
        [file dirname [file normalize ${lilyponddevel.temp}]]
    if {${lilyponddevel.mactex_bin} eq "."} {
        pre-fetch {
            return -code error "Cannot find MacTeX or external TeXLive\
                installation; aborting.  Have you added the path to\
                your TeX distribution's binary directory to 'binpath'\
                in 'macports.conf'?"
        }
    } elseif {${lilyponddevel.mactex_bin} eq \
                  [file dirname [file normalize "${prefix}/bin/kpsewhich"]]} {
        pre-fetch {
            return -code error "Variant 'mactex' doesn't work with\
                installed MacPorts TeXLive packages.  Either\
                uninstall them or don't use the 'mactex' variant of\
                lilyponddevel."
        }
    } else {
        set lilyponddevel.have_texgyre \
            [regexp -line {installed: *Yes} \
                 [exec ${lilyponddevel.mactex_bin}/tlmgr info --only-installed tex-gyre]]
    }

    if {${lilyponddevel.have_texgyre}} {
        set lilyponddevel.texgyredir \
            [file dirname \
                [exec ${lilyponddevel.mactex_bin}/kpsewhich texgyreschola-regular.otf]]
    } else {
        pre-fetch {
            return -code error "TeXLive package 'tex-gyre' not\
                installed; aborting.  Please install it, then\
                try again"
        }
    }

    depends_build-delete port:texlive-fonts-recommended \
                         port:texlive-metapost

    configure.args-append --with-texgyre-dir=${lilyponddevel.texgyredir}
}

configure.args-append   --disable-documentation
configure.args-append   --with-urwotf-dir=${prefix}/share/fonts/urw-core35-fonts
if {![variant_isset mactex]} {
    configure.args-append  --with-texgyre-dir=${lilyponddevel.texgyredir}
}

configure.env-append    GUILE=${prefix}/bin/guile18
configure.env-append    GUILE_CONFIG=${prefix}/bin/guile18-config
configure.env-append    GUILE_TOOLS=${prefix}/bin/guile18-tools

configure.env-append    PYTHON=${configure.python}
build.env-append        PYTHON=${configure.python}
destroot.env-append     PYTHON=${configure.python}

configure.env-append    PYTHON_CONFIG=${configure.python}-config
build.env-append        PYTHON_CONFIG=${configure.python}-config
destroot.env-append     PYTHON_CONFIG=${configure.python}-config

configure.env-append    LTDL_LIBRARY_PATH=${prefix}/lib
build.env-append        LTDL_LIBRARY_PATH=${prefix}/lib
destroot.env-append     LTDL_LIBRARY_PATH=${prefix}/lib

patchfiles              patch-wcxx11-narrowing.diff

post-patch {
    # Backport upstream commit 250d841a51eb to make dependency on
    # dblatex optional.
    reinplace -W ${worksrcpath} \
        "s|\\(DBLATEX.*\\), \\\$DOCUMENTATION_REQUIRED,|\\1, OPTIONAL,|g" \
        configure.ac

    # Currently, +docs only builds the info files.  For a real
    # documentation build a texi2html 1.82 bundle is necessary, which
    # doesn't exist yet in MacPorts.
    if {[variant_isset docs]} {
        reinplace -W ${worksrcpath} \
            "s|\\(TEXI2HTML.*\\), \\\$DOCUMENTATION_REQUIRED,|\\1, OPTIONAL,|g" \
            configure.ac
    }

    # Use guile18 header files.
    reinplace -W ${worksrcpath} \
        s|libguile\.h|libguile18.h|g \
        configure.ac \
        lily/include/lily-guile.hh

    # echo -n is not portable.
    reinplace -W ${worksrcpath} \
        "s|echo -n|printf %s|g" \
        mf/invoke-mf2pt1.sh

    # Correct mf2pt1 binary location.
    reinplace -W ${worksrcpath} \
        s|"-mem=mf2pt1"|"-mem=../../mf2pt1"|g \
        scripts/build/mf2pt1.pl

    # Correct python shebang.
    reinplace -W ${worksrcpath} -E \
        s|/usr/bin/(env\\\ )?python|${configure.python}|g \
        python/auxiliar/manuals_definitions.py \
        scripts/auxiliar/check_texi_refs.py \
        scripts/auxiliar/check_translation.py \
        scripts/auxiliar/find-superfluous-includes.py \
        scripts/auxiliar/fixcc.py \
        scripts/auxiliar/makelsr.py \
        scripts/auxiliar/musicxml_generate_intervals.py \
        scripts/auxiliar/musicxml_generate_keys.py \
        scripts/auxiliar/musicxml_generate_timesignatures.py \
        scripts/auxiliar/node-menuify.py \
        scripts/auxiliar/prepare-web-media.py \
        scripts/auxiliar/readlink.py \
        scripts/auxiliar/skyline_viewer.py \
        scripts/auxiliar/strip-whitespace.py \
        scripts/auxiliar/tely-gettext.py \
        scripts/auxiliar/texi-langutils.py \
        scripts/auxiliar/texi-skeleton-update.py \
        scripts/auxiliar/translations-status.py \
        scripts/auxiliar/update-snippets.py
}

post-destroot {
    move \
        ${destroot}${prefix}/bin/lilypond \
        ${destroot}${prefix}/libexec/lilypond-bin
    xinstall -m 755 \
        ${filespath}/lilypond.in \
        ${destroot}${prefix}/bin/lilypond
    reinplace \
        "s|@@PREFIX@@|${prefix}|g" \
        ${destroot}${prefix}/bin/lilypond
}

variant docs description {Build documentation files} {
    configure.args-delete   --disable-documentation
    configure.args-append   --enable-documentation
}

livecheck.type  regex
livecheck.url   ${homepage}/development.html
livecheck.regex ${my_name}-(\\d+(\\.\\d+)+)
