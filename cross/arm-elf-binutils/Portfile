# -*- coding: utf-8; mode: tcl; tab-width: 4; truncate-lines: t; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:et:sw=4:ts=4:sts=4
# $Id$

PortSystem			1.0
name				arm-elf-binutils
version				2.20.51.0.2

# Parameters for this port.
set crossgcc-target arm-elf

description			FSF Binutils for arm-elf cross development
long_description	Free Software Foundation development toolchain ("binutils") for \
					arm-elf cross development.
platforms			darwin
categories			cross devel
maintainers			nomaintainer
homepage			http://www.gnu.org/software/binutils/binutils.html
master_sites		ftp://ftp.kernel.org/pub/linux/devel/binutils/ \
					http://ftp.kernel.org/pub/linux/devel/binutils/
distname			binutils-${version}
use_bzip2			yes
checksums           md5     b01b185a5eab43190fb83efaeb2ffef9 \
                    sha1    b9318d75250be2cfc0fbafbd0ed68aa45bd69005 \
                    rmd160  fd65a60fb0df360f20d0897ba62cb87f57ae6666

depends_lib			port:gettext

# All cross ports violate the mtree layout.
destroot.violate_mtree	yes

# Download everything to binutils/
dist_subdir			binutils

# Build in a different directory, as advised in the README file.
pre-configure   	{ system "cd ${workpath} && mkdir -p build"}
configure.dir   	${workpath}/build
configure.cmd   	${worksrcpath}/configure
configure.cc	   	"cc -no-cpp-precomp -I${prefix}/include"
configure.args  	--infodir='${prefix}/share/info' --target=${crossgcc-target} \
					--program-prefix=${crossgcc-target}- --mandir='${prefix}/share/man' \
					--disable-werror

build.dir       	${workpath}/build
					
# We don't want the etc module.
post-extract {
	system "rm -rf ${worksrcpath}/etc"
}

post-patch {
	namespace eval crossgcc {}

	# Fix the info pages and related stuff.
	#	
	# path: path to the doc directory (e.g. gas/doc/)
	# makefile: path to Makefile.in (e.g. gas/doc/Makefile.in)
	# name: name of the info page (e.g. as)
	# suffix: suffix of the souce page (texinfo or texi)
	proc crossgcc::fixinfo { path makefile name suffix } {
		global crossgcc-target worksrcpath

		# Fix the source
		reinplace "s|setfilename ${name}.info|setfilename ${crossgcc-target}-${name}.info|g" \
			${worksrcpath}/${path}/${name}.${suffix}
		reinplace "s|(${name})|(${crossgcc-target}-${name})|g" \
			${worksrcpath}/${path}/${name}.${suffix}
		reinplace "s|@file{${name}}|@file{${crossgcc-target}-${name}}|g" \
			${worksrcpath}/${path}/${name}.${suffix}
		
		# Fix the Makefile
		reinplace "s|${name}.info|${crossgcc-target}-${name}.info|g" \
			${worksrcpath}/${makefile}
		reinplace "s|${name}.${suffix}|${crossgcc-target}-${name}.${suffix}|g" \
			${worksrcpath}/${makefile}
		
		# Rename the source
		file rename ${worksrcpath}/${path}/${name}.${suffix} \
			${worksrcpath}/${path}/${crossgcc-target}-${name}.${suffix}
		
		# Fix install-info's dir.
		# (note: this may be effectless if there was no info dir to be fixed)
		reinplace "s|--info-dir=\$(DESTDIR)\$(infodir)|--dir-file=\$(DESTDIR)\$(infodir)/${crossgcc-target}-dir|g" \
			"${worksrcpath}/${makefile}"
	}

	# Fix the gettext files and related stuff.
	#	
	# module: name of the module (e.g. gas)
	proc crossgcc::fixgettext { module } {
		global crossgcc-target worksrcpath

		if { [ file exists "${worksrcpath}/${module}/Makefile.in" ] } {
			reinplace "s|@PACKAGE@|${crossgcc-target}-@PACKAGE@|g" \
				"${worksrcpath}/${module}/Makefile.in"
		}
		if { [ file exists "${worksrcpath}/${module}/doc/Makefile.in" ] } {
			reinplace "s|@PACKAGE@|${crossgcc-target}-@PACKAGE@|g" \
				"${worksrcpath}/${module}/doc/Makefile.in"
		}
		if { [ file exists "${worksrcpath}/${module}/po/Make-in" ] } {
			reinplace "s|@PACKAGE@|${crossgcc-target}-@PACKAGE@|g" \
				"${worksrcpath}/${module}/po/Make-in"
		}
	}

	# gas/doc/as.texinfo
	crossgcc::fixinfo gas/doc/ gas/doc/Makefile.in as texinfo

	# bfd/doc/bfd.texinfo
	crossgcc::fixinfo bfd/doc/ bfd/doc/Makefile.in bfd texinfo

	# binutils/doc/binutils.texi
	crossgcc::fixinfo binutils/doc/ binutils/doc/Makefile.in binutils texi

	# gprof/gprof.texi
	crossgcc::fixinfo gprof/ gprof/Makefile.in gprof texi

	# ld/ld.texinfo
	crossgcc::fixinfo ld/ ld/Makefile.in ld texinfo
	
	# gettext stuff.
	crossgcc::fixgettext bfd
	crossgcc::fixgettext binutils
	crossgcc::fixgettext gas
	crossgcc::fixgettext gprof
	crossgcc::fixgettext ld
	crossgcc::fixgettext opcodes
}

post-destroot {
	# Installing (host) libiberty was a mistake.
	if {${configure.build_arch} == "x86_64"} {
		file delete "${destroot}${prefix}/lib/x86_64/libiberty.a"
	} else {
		file delete "${destroot}${prefix}/lib/libiberty.a"
	}
}

livecheck.type	freshmeat
livecheck.name	binutils
