# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

name                lm32-rtems-binutils
version             2.21.1

# Parameters for this port.
set crossgcc-target lm32-rtems
set rtemsversion    4.11

categories          cross devel
platforms           darwin
maintainers         gmail.com:yann.sionneau

description         FSF Binutils for lm32-rtems cross development
long_description    Free Software Foundation development toolchain ("binutils") for \
                    lm32-rtems cross development tools (for Milkymist & lm32_evr platforms).

homepage            http://www.gnu.org/software/binutils/binutils.html
master_sites        http://www.rtems.com/ftp/pub/rtems/SOURCES/${rtemsversion}/ \
                    ftp://ftp.rtems.com/pub/rtems/SOURCES/${rtemsversion}/

distname            binutils-${version}
use_bzip2           yes

checksums           binutils-${version}.tar.bz2 \
                        sha1    f188490772cc902ec44a76545887bff60dbfa22d \
                        rmd160  1b36120beda5efddc3194ee56c3099421ca5ca07

depends_lib         port:gettext port:zlib

# All cross ports violate the mtree layout.
destroot.violate_mtree  yes

# Download everything to binutils/
dist_subdir         binutils

# We don't want the etc module.
post-extract {
    delete ${worksrcpath}/etc
}

post-patch {
    namespace eval crossgcc {}

    # Fix the info pages and related stuff.
    #
    # path: path to the doc directory (e.g. gas/doc/)
    # makefile: path to Makefile.in (e.g. gas/doc/Makefile.in)
    # name: name of the info page (e.g. as)
    # suffix: suffix of the souce page (texinfo or texi)
    proc crossgcc::fixinfo { path makefile name suffix } {
        global crossgcc-target worksrcpath

        # Fix the source
        reinplace "s|setfilename ${name}.info|setfilename ${crossgcc-target}-${name}.info|g" \
            ${worksrcpath}/${path}/${name}.${suffix}
        reinplace "s|(${name})|(${crossgcc-target}-${name})|g" \
            ${worksrcpath}/${path}/${name}.${suffix}
        reinplace "s|@file{${name}}|@file{${crossgcc-target}-${name}}|g" \
            ${worksrcpath}/${path}/${name}.${suffix}
        
        # Fix the Makefile
        reinplace "s|${name}.info|${crossgcc-target}-${name}.info|g" \
            ${worksrcpath}/${makefile}
        reinplace "s|${name}.${suffix}|${crossgcc-target}-${name}.${suffix}|g" \
            ${worksrcpath}/${makefile}
        
        # Rename the source
        file rename ${worksrcpath}/${path}/${name}.${suffix} \
            ${worksrcpath}/${path}/${crossgcc-target}-${name}.${suffix}
        
        # Fix install-info's dir.
        # (note: this may be effectless if there was no info dir to be fixed)
        reinplace "s|--info-dir=\$(DESTDIR)\$(infodir)|--dir-file=\$(DESTDIR)\$(infodir)/${crossgcc-target}-dir|g" \
            "${worksrcpath}/${makefile}"
    }

    # Fix the gettext files and related stuff.
    #
    # module: name of the module (e.g. gas)
    proc crossgcc::fixgettext { module } {
        global crossgcc-target worksrcpath

        if { [ file exists "${worksrcpath}/${module}/Makefile.in" ] } {
            reinplace "s|@PACKAGE@|${crossgcc-target}-@PACKAGE@|g" \
                "${worksrcpath}/${module}/Makefile.in"
        }
        if { [ file exists "${worksrcpath}/${module}/doc/Makefile.in" ] } {
            reinplace "s|@PACKAGE@|${crossgcc-target}-@PACKAGE@|g" \
                "${worksrcpath}/${module}/doc/Makefile.in"
        }
        if { [ file exists "${worksrcpath}/${module}/po/Make-in" ] } {
            reinplace "s|@PACKAGE@|${crossgcc-target}-@PACKAGE@|g" \
                "${worksrcpath}/${module}/po/Make-in"
        }
    }

    # gas/doc/as.texinfo
    crossgcc::fixinfo gas/doc/ gas/doc/Makefile.in as texinfo

    # bfd/doc/bfd.texinfo
    crossgcc::fixinfo bfd/doc/ bfd/doc/Makefile.in bfd texinfo

    # binutils/doc/binutils.texi
    crossgcc::fixinfo binutils/doc/ binutils/doc/Makefile.in binutils texi

    # gprof/gprof.texi
    crossgcc::fixinfo gprof/ gprof/Makefile.in gprof texi

    # ld/ld.texinfo
    crossgcc::fixinfo ld/ ld/Makefile.in ld texinfo
    
    # gettext stuff.
    crossgcc::fixgettext bfd
    crossgcc::fixgettext binutils
    crossgcc::fixgettext gas
    crossgcc::fixgettext gprof
    crossgcc::fixgettext ld
    crossgcc::fixgettext opcodes
}

# Build in a different directory, as advised in the README file.
configure.dir       ${workpath}/build
pre-configure {
    file mkdir ${configure.dir}
}
configure.cmd       ${worksrcpath}/configure
configure.args      --infodir=${prefix}/share/info \
                    --target=${crossgcc-target}${rtemsversion} \
                    --mandir=${prefix}/share/man \
                    --disable-werror

build.dir           ${configure.dir}

post-destroot {
    # Installing (host) libiberty was a mistake.
    foreach f [glob -directory ${destroot}${prefix}/lib libiberty.a */libiberty.a] {
        file delete $f
    }
}

livecheck.type  freecode
livecheck.name  binutils
